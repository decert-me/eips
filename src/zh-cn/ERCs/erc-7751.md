---
eip: 7751
title: 包装气泡上升的回退
description: 使用带有附加上下文的自定义错误处理气泡上升的回退
author: Daniel Gretzke (@gretzke), Sara Reynolds (@snreynolds), Alice Henshaw (@hensha256), Marko Veniger <marko.veniger@tenderly.co>, Hadrien Croubois (@Amxx)
discussions-to: https://ethereum-magicians.org/t/erc-7751-wrapping-of-bubbled-up-reverts/20740
status: Draft
type: Standards Track
category: ERC
created: 2024-08-06
---

## 摘要

本 ERC 提出了一个标准，用于在以太坊智能合约中使用专用自定义错误处理气泡上升的回退。该标准旨在通过允许附加上下文与气泡上升的原始字节一起传递，从而提高回退原因的清晰度和可用性。`WrappedError` 自定义错误应包装来自被调用合约的回退，并为在 Etherscan 或 Tenderly 等工具中解析和处理回退提供一致的接口。

## 动机

目前，当一个智能合约调用另一个合约并且被调用合约回退时，回退原因通常会被气泡上升并原样抛出。这可能使得更难判断错误来自哪个上下文。通过标准化使用带有附加上下文的自定义错误，可以提供更有意义和信息量的回退原因。这将改善调试体验，并使开发人员和基础设施提供者（如 Etherscan）更容易显示准确的堆栈跟踪。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

为了包装回退，合约必须使用以下错误进行回退，该错误对应于以下签名 `0x90bfb865`：

```solidity
error WrappedError(address target, bytes4 selector, bytes reason, bytes details);
```

其中：

- `target` 是被调用合约的地址，该合约发生了回退。
- `selector` 是发生回退的被调用函数的选择器。如果调用是没有任何数据的 ETH 转账，则选择器必须为 `bytes4(0)`
- `reason` 是回退原因的原始字节。
- `details` 是关于回退的可选附加上下文。在不需要附加上下文的情况下，`details` 字节可以为空。在有附加上下文的情况下，`details` 字节必须是在发出 `WrappedError` 错误的合约中声明的 ABI 编码自定义错误。

## 理由

通过包含被调用合约和函数、原始回退字节和附加上下文，开发人员可以提供有关失败的更详细信息。此外，通过标准化气泡上升的回退方式，还可以实现嵌套的气泡上升回退，其中多个由不同合约抛出的回退可以递归跟踪。回退还可以被 Etherscan 和 Foundry 等工具解析和处理，以进一步增强智能合约交互的可读性和可调试性，并促进更好的错误处理实践。

## 向后兼容性

本 ERC 不引入任何向后不兼容性。现有合约可以逐步采用此标准。

## 测试用例

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

contract Token {
    mapping(address => uint256) public balanceOf;

    event  Transfer(address indexed sender, address indexed recipient, uint amount);

    function transfer(address to, uint256 amount) external returns (bool) {
        require(balanceOf[msg.sender] >= amount, "insufficient balance");
        balanceOf[msg.sender] -= amount;
        balanceOf[to] += amount;
        emit Transfer(msg.sender, to, amount);
        return true;
    }
}

contract Vault {
    Token token;

    error WrappedError(address target, bytes4 selector, bytes reason, bytes details);
    error ERC20TransferFailed(address recipient);


    constructor(Token token_) {
        token = token_;
    }

    function withdraw(address to, uint256 amount) external {
        // logic
        try token.transfer(to, amount) {} catch (bytes memory error) {
            revert WrappedError(address(token), token.transfer.selector, error, abi.encodeWithSelector(ERC20TransferFailed.selector, to));
        }
    }
}

contract Router {
    Vault vault;

    error WrappedError(address target, bytes4 selector, bytes reason, bytes details);

    constructor(Vault vault_) {
        vault = vault_;
    }

    function withdraw(uint256 amount) external {
        // logic
        try vault.withdraw(msg.sender, amount) {} catch (bytes memory error) {
            revert WrappedError(address(vault), vault.withdraw.selector, error, "");
        }
    }
}

contract Test {
    function test_BubbledNestedReverts(uint256 amount) external {
        Token token = new Token();
        Vault vault = new Vault(token);
        Router router = new Router(vault);

        try router.withdraw(amount) {} catch (bytes memory thrownError) {
            bytes memory expectedError = abi.encodeWithSelector(
                Router.WrappedError.selector, address(vault), vault.withdraw.selector, abi.encodeWithSelector(
                    Vault.WrappedError.selector,
                    address(token),
                    token.transfer.selector,
                    abi.encodeWithSignature("Error(string)", "insufficient balance"),
                    abi.encodeWithSelector(Vault.ERC20TransferFailed.selector, address(this))
                ), ""
            );
            assert(keccak256(thrownError) == keccak256(expectedError));
        }
    }
}
```

## 参考实现

在捕获来自被调用合约的回退时，调用合约应使用遵循上述约定的自定义错误进行回退。

```solidity
contract Foo {

    error WrappedError(address target, bytes4 selector, bytes reason, bytes details);
    error MyCustomError(uint256 x);

    function foo(address to, bytes memory data) external {
        // logic
        (bool success, bytes memory returnData) = to.call(data);
        if (!success) {
            revert WrappedError(to, bytes4(data), returnData, abi.encodeWithSelector(MyCustomError.selector, 42));
        }
    }
}
```

## 安全考虑

智能合约可能会在回退链上丢弃或故意抑制气泡上升的回退。此外，智能合约也可能会撒谎或错误报告包装的回退，因此信息不保证准确。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。