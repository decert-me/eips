---
eip: 1185
title: 在 ENS 中存储 DNS 记录
description: 一个在 ENS 合约中存储和检索 DNS 记录的系统。
author: Jim McDonald (@mcdee)
discussions-to: https://ethereum-magicians.org/t/eip1185-dns-resolver-profile-for-ens/1589
status: Review
type: Standards Track
category: ERC
created: 2018-06-26
requires: 137
---
  
## 摘要

本 EIP 定义了一个 ENS 的解析器配置文件，提供存储和查找 DNS 记录的功能。这使得 ENS 可以作为权威 DNS 信息的存储。

## 动机

ENS 是一个非常理想的 DNS 信息存储。它提供了 DNS 的分布式权威，而不混淆所有权和信息的权威提供。通过 ENS，域名的所有者对其 DNS 记录拥有完全的控制权。此外，ENS 通过智能合约能够将域名的子域名不可撤销地分配给其他实体。

## 规范

支持 ENS 上 DNS 的解析器配置文件遵循 [ERC-137](./eip-137.md) 中定义的解析器规范。

传统上，DNS 是一个基于区域的系统，所有区域的记录都保存在同一个文件中。这具有简单性和区域更新的原子性，但在转移到 ENS 时，可能会导致简单更改的高 gas 成本。因此，解析器基于记录集工作。记录集由元组 `(domain, name, resource record type)` 唯一定义，例如元组 `(example.com, www.example.com, A)` 定义了域 `example.com` 中名称为 `www.example.com` 的 `A` 记录集。记录集可以包含 0 个或多个值，例如如果 `www.example.com` 有 `A` 记录 `1.2.3.4` 和 `5.6.7.8`，那么上述元组将有两个值。

选择在记录集级别而不是区域级别工作意味着该规范无法完全支持 DNS 的某些功能，例如区域传输和 DNSSEC。可以构建一个在区域级别工作的不同解析器配置文件，但进行更新的成本将非常高，因此在本 EIP 中不再考虑。

DNS 解析器接口由两个设置 DNS 信息的函数和两个查询 DNS 信息的函数组成。

### setDNSRecords(bytes32 node, bytes data)

`setDNSRecords()` 设置、更新或清除给定节点的 1 个或多个 DNS 记录。它的函数签名为 `0x0af179d7`。

该函数的参数如下：

  - node: 要设置记录的 ENS 中完全合格域的名称哈希。名称哈希在 [ERC-137](./eip-137.md) 中定义
  - data: 1 个或多个以 DNS 线格式表示的 DNS 记录。任何未提供值的记录将被清除。请注意，同一 RRset 中的所有记录应在数据中是连续的；如果不是，则后面的 RRsets 将覆盖前面的记录

### clearDNSZone(bytes32 node)

`clearDNSZone()` 删除域的所有 DNS 记录。它的函数签名为 `0xad5780af`。

虽然可以通过上述 `setDNSRecords()` 单独清除记录，但这要求所有者知道已设置的所有记录（因为解析器没有方法遍历给定域的记录），并可能需要多次交易。`clearDNSZone()` 在一次操作中删除所有区域信息。

该函数的参数如下：

  - node: 要清除记录的 ENS 中完全合格域的名称哈希。名称哈希在 [ERC-137](./eip-137.md) 中定义

### dnsRecords(bytes32 node, bytes32 name, uint16 resource) view returns (bytes)

`dnsRecords()` 获取给定节点、名称和资源的 DNS 记录。它的函数签名为 `0x2461e851`。

该函数的参数如下：

  - node: 要设置记录的 ENS 中完全合格域的名称哈希。名称哈希在 [ERC-137](./eip-137.md) 中定义
  - name: DNS 线格式中记录名称的 `keccak256()` 哈希。
  - resource: 资源记录 ID。资源记录 ID 在 RFC1035 和后续 RFC 中定义。

该函数返回所有匹配的 DNS 线格式记录。如果没有记录存在，该函数将不返回任何内容。

### hasDNSRecords(bytes32 node, bytes32 name) view returns (bool)

`hasDNSRecords()` 报告提供的名称在域中是否有任何记录。它的函数签名为 `0x4cbf6ba4`。

当 DNS 解析器处理 RFC4592 中定义的通配符资源时，需要此函数。

该函数的参数如下：

  - node: 要设置记录的 ENS 中完全合格域的名称哈希。名称哈希在 [ERC-137](./eip-137.md) 中定义
  - name: DNS 线格式中记录名称的 `keccak256()` 哈希。

该函数返回 `true`，如果提供的节点和名称有任何记录，否则返回 `false`。

## 理由

DNS 是一个联邦命名系统，高级实体控制其下所有内容的可用性（例如 `.org` 控制 `ethereum.org` 的可用性）。去中心化版本的 DNS 不会有这种限制，并允许直接查找 ENS 中具有相关记录的任何域。

## 向后兼容性

不适用。

## 参考实现

DNS 解析器的参考实现如下：

```solidity
pragma solidity ^0.7.4;
import "../ResolverBase.sol";
import "@ensdomains/dnssec-oracle/contracts/RRUtils.sol";

abstract contract DNSResolver is ResolverBase {
    using RRUtils for *;
    using BytesUtils for bytes;

    bytes4 constant private DNS_RECORD_INTERFACE_ID = 0xa8fa5682;
    bytes4 constant private DNS_ZONE_INTERFACE_ID = 0x5c47637c;

    // DNSRecordChanged is emitted whenever a given node/name/resource's RRSET is updated.
    event DNSRecordChanged(bytes32 indexed node, bytes name, uint16 resource, bytes record);
    // DNSRecordDeleted is emitted whenever a given node/name/resource's RRSET is deleted.
    event DNSRecordDeleted(bytes32 indexed node, bytes name, uint16 resource);
    // DNSZoneCleared is emitted whenever a given node's zone information is cleared.
    event DNSZoneCleared(bytes32 indexed node);

    // DNSZonehashChanged is emitted whenever a given node's zone hash is updated.
    event DNSZonehashChanged(bytes32 indexed node, bytes lastzonehash, bytes zonehash);

    // Zone hashes for the domains.
    // A zone hash is an ERC-1577 content hash in binary format that should point to a
    // resource containing a single zonefile.
    // node => contenthash
    mapping(bytes32=>bytes) private zonehashes;

    // Version the mapping for each zone.  This allows users who have lost
    // track of their entries to effectively delete an entire zone by bumping
    // the version number.
    // node => version
    mapping(bytes32=>uint256) private versions;

    // The records themselves.  Stored as binary RRSETs
    // node => version => name => resource => data
    mapping(bytes32=>mapping(uint256=>mapping(bytes32=>mapping(uint16=>bytes)))) private records;

    // Count of number of entries for a given name.  Required for DNS resolvers
    // when resolving wildcards.
    // node => version => name => number of records
    mapping(bytes32=>mapping(uint256=>mapping(bytes32=>uint16))) private nameEntriesCount;

    /**
     * Set one or more DNS records.  Records are supplied in wire-format.
     * Records with the same node/name/resource must be supplied one after the
     * other to ensure the data is updated correctly. For example, if the data
     * was supplied:
     *     a.example.com IN A 1.2.3.4
     *     a.example.com IN A 5.6.7.8
     *     www.example.com IN CNAME a.example.com.
     * then this would store the two A records for a.example.com correctly as a
     * single RRSET, however if the data was supplied:
     *     a.example.com IN A 1.2.3.4
     *     www.example.com IN CNAME a.example.com.
     *     a.example.com IN A 5.6.7.8
     * then this would store the first A record, the CNAME, then the second A
     * record which would overwrite the first.
     *
     * @param node the namehash of the node for which to set the records
     * @param data the DNS wire format records to set
     */
    function setDNSRecords(bytes32 node, bytes calldata data) external authorised(node) {
        uint16 resource = 0;
        uint256 offset = 0;
        bytes memory name;
        bytes memory value;
        bytes32 nameHash;
        // Iterate over the data to add the resource records
        for (RRUtils.RRIterator memory iter = data.iterateRRs(0); !iter.done(); iter.next()) {
            if (resource == 0) {
                resource = iter.dnstype;
                name = iter.name();
                nameHash = keccak256(abi.encodePacked(name));
                value = bytes(iter.rdata());
            } else {
                bytes memory newName = iter.name();
                if (resource != iter.dnstype || !name.equals(newName)) {
                    setDNSRRSet(node, name, resource, data, offset, iter.offset - offset, value.length == 0);
                    resource = iter.dnstype;
                    offset = iter.offset;
                    name = newName;
                    nameHash = keccak256(name);
                    value = bytes(iter.rdata());
                }
            }
        }
        if (name.length > 0) {
            setDNSRRSet(node, name, resource, data, offset, data.length - offset, value.length == 0);
        }
    }

    /**
     * Obtain a DNS record.
     * @param node the namehash of the node for which to fetch the record
     * @param name the keccak-256 hash of the fully-qualified name for which to fetch the record
     * @param resource the ID of the resource as per https://en.wikipedia.org/wiki/List_of_DNS_record_types
     * @return the DNS record in wire format if present, otherwise empty
     */
    function dnsRecord(bytes32 node, bytes32 name, uint16 resource) public view returns (bytes memory) {
        return records[node][versions[node]][name][resource];
    }

    /**
     * Check if a given node has records.
     * @param node the namehash of the node for which to check the records
     * @param name the namehash of the node for which to check the records
     */
    function hasDNSRecords(bytes32 node, bytes32 name) public view returns (bool) {
        return (nameEntriesCount[node][versions[node]][name] != 0);
    }

    /**
     * Clear all information for a DNS zone.
     * @param node the namehash of the node for which to clear the zone
     */
    function clearDNSZone(bytes32 node) public authorised(node) {
        versions[node]++;
        emit DNSZoneCleared(node);
    }

    /**
     * setZonehash sets the hash for the zone.
     * May only be called by the owner of that node in the ENS registry.
     * @param node The node to update.
     * @param hash The zonehash to set
     */
    function setZonehash(bytes32 node, bytes calldata hash) external authorised(node) {
        bytes memory oldhash = zonehashes[node];
        zonehashes[node] = hash;
        emit DNSZonehashChanged(node, oldhash, hash);
    }

    /**
     * zonehash obtains the hash for the zone.
     * @param node The ENS node to query.
     * @return The associated contenthash.
     */
    function zonehash(bytes32 node) external view returns (bytes memory) {
        return zonehashes[node];
    }

    function supportsInterface(bytes4 interfaceID) virtual override public pure returns(bool) {
        return interfaceID == DNS_RECORD_INTERFACE_ID ||
               interfaceID == DNS_ZONE_INTERFACE_ID ||
               super.supportsInterface(interfaceID);
    }

    function setDNSRRSet(
        bytes32 node,
        bytes memory name,
        uint16 resource,
        bytes memory data,
        uint256 offset,
        uint256 size,
        bool deleteRecord) private
    {
        uint256 version = versions[node];
        bytes32 nameHash = keccak256(name);
        bytes memory rrData = data.substring(offset, size);
        if (deleteRecord) {
            if (records[node][version][nameHash][resource].length != 0) {
                nameEntriesCount[node][version][nameHash]--;
            }
            delete(records[node][version][nameHash][resource]);
            emit DNSRecordDeleted(node, name, resource);
        } else {
            if (records[node][version][nameHash][resource].length == 0) {
                nameEntriesCount[node][version][nameHash]++;
            }
            records[node][version][nameHash][resource] = rrData;
            emit DNSRecordChanged(node, name, resource, rrData);
        }
    }
}
```

## 安全考虑

该解决方案的安全性将取决于 ENS 域内记录的安全性。这归结为对具有该域权威的密钥的安全性。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。