---
eip: 3386
title: ERC-721 和 ERC-1155 到 ERC-20 包装器
author: Calvin Koder (@ashrowz)
discussions-to: https://github.com/ethereum/EIPs/issues/3384
status: Stagnant
type: Standards Track
category: ERC
created: 2021-03-12
requires: 165
---

## 简单总结
一个标准接口，用于创建从一池独特的 ERC-721/ERC-1155 代币派生的通用 ERC-20 代币的合约。

## 摘要

该标准概述了一个智能合约接口，用于将可识别的代币与可替代代币包装在一起。这允许通过将基础的 [ERC-721](./eip-721.md) 非同质化代币和 [ERC-1155](./eip-1155.md) 多代币锁定到一个池中来铸造衍生的 [ERC-20](./eip-20.md) 代币。衍生代币可以被销毁以从池中赎回基础代币。这些衍生代币与这些基础代币的唯一 ID 没有关联，并且应该与基础代币有一个成比例的兑换率。作为基础代币的代表，这些通用衍生代币可以根据 ERC-20 进行交易和其他用途，从而每个基础代币的唯一标识符变得无关紧要。

ERC-721 和 ERC-1155 代币被视为有效的基础代币，因为它们具有唯一标识符，并且根据类似的规则进行转移。这允许将 ERC-721 NFT 和 ERC-1155 多代币包装在一个共同的接口下。

## 动机

ERC-20 代币标准是以太坊上最广泛和流动性最高的代币标准。另一方面，ERC-721 和 ERC-1155 代币只能通过其各自的 ID 以整体数量进行转移。衍生代币允许在利用 ERC-20 代币的合约中获得基础资产的敞口。这使得基础代币可以被分割、交易并在 AMM 上通用池化、抵押，并用于任何其他 ERC-20 类型的合约。该提案的几种实现已经存在，但没有一个共同的标准。

在基础代币和衍生代币之间存在固定的兑换率时，衍生代币的价值与池中代币的底价成正比。随着衍生代币在 AMM 中的使用，衍生代币市场与基础 NFT 市场之间存在套利机会。通过指定可以被池化的基础代币子集，可以最小化池中最低和最高价值代币之间的差异。这允许在更大集合中池化更高价值的代币。此外，使用荷兰拍卖等方法进行价格计算，如 NFT20 实现的那样，允许基础代币子类的价格发现。这使得提供更高价值基础代币的用户能够获得比底价代币更多的衍生代币。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 [RFC 2119](https://www.ietf.org/rfc/rfc2119.txt) 中的描述进行解释。

**每个符合 IWrapper 的合约必须实现 `IWrapper` 和 `ERC165` 接口** :


```solidity
pragma solidity ^0.8.0;

/**
    @title IWrapper Identifiable Token Wrapper Standard
    @dev {Wrapper} refers to any contract implementing this interface.
    @dev {Base} refers to any ERC-721 or ERC-1155 contract. It MAY be the {Wrapper}.
    @dev {Pool} refers to the contract which holds the {Base} tokens. It MAY be the {Wrapper}.
    @dev {Derivative} refers to the ERC-20 contract which is minted/burned by the {Wrapper}. It MAY be the {Wrapper}.
    @dev All uses of "single", "batch" refer to the number of token ids. This includes individual ERC-721 tokens by id, and multiple ERC-1155 by id. An ERC-1155 `TransferSingle` event may emit with a `value` greater than `1`, but it is still considered a single token.
    @dev All parameters named `_amount`, `_amounts` refer to the `value` parameters in ERC-1155. When using this interface with ERC-721, `_amount` MUST be 1, and `_amounts` MUST be either an empty list or a list of 1 with the same length as `_ids`.
*/
interface IWrapper /* is ERC165 */ {
    /**
     * @dev MUST emit when a mint occurs where a single {Base} token is received by the {Pool}.
     * The `_from` argument MUST be the address of the account that sent the {Base} token.
     * The `_to` argument MUST be the address of the account that received the {Derivative} token(s).
     * The `_id` argument MUST be the id of the {Base} token transferred.
     * The `_amount` argument MUST be the number of {Base} tokens transferred.
     * The `_value` argument MUST be the number of {Derivative} tokens minted.
     */
    event MintSingle (address indexed _from, address indexed _to, uint256 _id, uint256 _amount, uint256 _value);

    /**
     * @dev MUST emit when a mint occurs where multiple {Base} tokens are received by the {Wrapper}.
     * The `_from` argument MUST be the address of the account that sent the {Base} tokens.
     * The `_to` argument MUST be the address of the account that received the {Derivative} token(s).
     * The `_ids` argument MUST be the list ids of the {Base} tokens transferred.
     * The `_amounts` argument MUST be the list of the numbers of {Base} tokens transferred.
     * The `_value` argument MUST be the number of {Derivative} tokens minted.
     */
    event MintBatch (address indexed _from, address indexed _to, uint256[] _ids, uint256[] _amounts, uint256 _value);

    /**
     * @dev MUST emit when a burn occurs where a single {Base} token is sent by the {Wrapper}.
     * The `_from` argument MUST be the address of the account that sent the {Derivative} token(s).
     * The `_to` argument MUST be the address of the account that received the {Base} token.
     * The `_id` argument MUST be the id of the {Base} token transferred.
     * The `_amount` argument MUST be the number of {Base} tokens transferred.
     * The `_value` argument MUST be the number of {Derivative} tokens burned.
     */
    event BurnSingle (address indexed _from, address indexed _to, uint256 _id, uint256 _amount, uint256 _value);

    /**
     * @dev MUST emit when a mint occurs where multiple {Base} tokens are sent by the {Wrapper}.
     * The `_from` argument MUST be the address of the account that sent the {Derivative} token(s).
     * The `_to` argument MUST be the address of the account that received the {Base} tokens.
     * The `_ids` argument MUST be the list of ids of the {Base} tokens transferred.
     * The `_amounts` argument MUST be the list of the numbers of {Base} tokens transferred.
     * The `_value` argument MUST be the number of {Derivative} tokens burned.
     */
    event BurnBatch (address indexed _from, address indexed _to, uint256[] _ids, uint256[] _amounts, uint256 _value);

    /**
     * @notice Transfers the {Base} token with `_id` from `msg.sender` to the {Pool} and mints {Derivative} token(s) to `_to`.
     * @param _to       Target address.
     * @param _id       Id of the {Base} token.
     * @param _amount   Amount of the {Base} token.
     *
     * Emits a {MintSingle} event.
     */
    function mint(
        address _to,
        uint256 _id,
        uint256 _amount
    ) external;

    /**
     * @notice Transfers `_amounts[i]` of the {Base} tokens with `_ids[i]` from `msg.sender` to the {Pool} and mints {Derivative} token(s) to `_to`.
     * @param _to       Target address.
     * @param _ids      Ids of the {Base} tokens.
     * @param _amounts  Amounts of the {Base} tokens.
     *
     * Emits a {MintBatch} event.
     */
    function batchMint(
        address _to,
        uint256[] calldata _ids,
        uint256[] calldata _amounts
    ) external;

    /**
     * @notice Burns {Derivative} token(s) from `_from` and transfers `_amounts` of some {Base} token from the {Pool} to `_to`. No guarantees are made as to what token is withdrawn.
     * @param _from     Source address.
     * @param _to       Target address.
     * @param _amount   Amount of the {Base} tokens.
     *
     * Emits either a {BurnSingle} or {BurnBatch} event.
     */
    function burn(
        address _from,
        address _to,
        uint256 _amount
    ) external;

    /**
     * @notice Burns {Derivative} token(s) from `_from` and transfers `_amounts` of some {Base} tokens from the {Pool} to `_to`. No guarantees are made as to what tokens are withdrawn.
     * @param _from     Source address.
     * @param _to       Target address.
     * @param _amounts  Amounts of the {Base} tokens.
     *
     * Emits either a {BurnSingle} or {BurnBatch} event.
     */
    function batchBurn(
        address _from,
        address _to,
        uint256[] calldata _amounts
    ) external;

    /**
     * @notice Burns {Derivative} token(s) from `_from` and transfers `_amounts[i]` of the {Base} tokens with `_ids[i]` from the {Pool} to `_to`.
     * @param _from     Source address.
     * @param _to       Target address.
     * @param _id       Id of the {Base} token.
     * @param _amount   Amount of the {Base} token.
     *
     * Emits either a {BurnSingle} or {BurnBatch} event.
     */
    function idBurn(
        address _from,
        address _to,
        uint256 _id,
        uint256 _amount
    ) external;

    /**
     * @notice Burns {Derivative} tokens from `_from` and transfers `_amounts[i]` of the {Base} tokens with `_ids[i]` from the {Pool} to `_to`.
     * @param _from     Source address.
     * @param _to       Target address.
     * @param _ids      Ids of the {Base} tokens.
     * @param _amounts   Amounts of the {Base} tokens.
     *
     * Emits either a {BurnSingle} or {BurnBatch} event.
     */
    function batchIdBurn(
        address _from,
        address _to,
        uint256[] calldata _ids,
        uint256[] calldata _amounts
    ) external;
}
```

## 理由

### 命名

被池化的 ERC-721/ERC-1155 代币称为 {Base} 代币。其他名称包括：
- 基础。
- NFT。然而，ERC-1155 代币可能被视为“半同质化”。

铸造/销毁的 ERC-20 代币称为 {Derivative} 代币。其他名称包括：
- 包装。
- 通用。

函数名称 `mint` 和 `burn` 借用自 ERC-20 的铸造和销毁扩展。其他名称包括：
- `mint`/`redeem` ([NFTX](https://nftx.org))
- `deposit`/`withdraw` ([WrappedKitties](https://wrappedkitties.com/))
- `wrap`/`unwrap` ([MoonCatsWrapped](https://etherscan.io/address/0x7c40c393dc0f283f318791d746d894ddd3693572))

函数名称 `*idBurn` 的选择是为了减少对正在被销毁的内容的混淆。也就是说，{Derivative} 代币被销毁以赎回 ID。

包装器/池本身可以根据 NFTX 被称为“指数基金”，或根据 [NFT20](https://nft20.io) 被称为“DEX”。然而，{NFT20Pair} 合约允许直接的 NFT-NFT 交换，这超出了该标准的范围。

### 铸造
铸造需要将 {Base} 代币转移到 {Pool} 中以换取 {Derivative} 代币。以这种方式存入的 {Base} 代币必须不得再次转移，除非通过销毁函数。这确保了 {Derivative} 代币的价值代表 {Base} 代币的价值。

除了将 {Base} 代币转移到 {Pool} 中外，代币也可以作为抵押品锁定以换取 {Derivative} 贷款，如 NFTX 轻论文中所提议的，类似于 Maker 金库。这仍然遵循铸造的通用模式，即在交换 {Derivative} 代币时移除 {Base} 代币的可转让性。

### 销毁
销毁需要将 {Base} 代币从 {Pool} 中转移以换取销毁 {Derivative} 代币。销毁函数通过赎回的 {Base} 代币的数量和质量进行区分。
- 对于不指定 `id` 的销毁：`burn`、`batchBurn`。
- 对于指定 `id`(s) 的销毁：`idBurn`、`batchIdBurn`。

通过允许针对特定 ID 进行操作，可以从池中选择更高价值的 {Base} 代币。NFTX 提出对这种有针对性的提取收取额外费用，以抵消从池中提取超过底价的 {Base} 代币的欲望。

### 定价
价格不一定是固定的。因此，铸造/销毁事件必须包括铸造/销毁的 ERC-20 `_value`。

现有的定价实现如下（以基础：衍生为单位）：
- 相等：每个 {Base} 代币成本 1 个 {Derivative}
    - NFTX
    - Wrapped Kitties
- 成比例
    - NFT20 设置每个 {Derivative} 代币固定为 100 个 {Base} 代币。
- 可变
    - NFT20 还允许在铸造时进行荷兰拍卖。
    - NFTX 提出在针对 {Base} 代币的 ID 时需支付额外费用。

由于定价实现的多样性，铸造*和销毁*事件必须包括铸造/销毁的 {Derivative} 代币数量。

### 继承
#### ERC-20
{Wrapper} 可以从 {ERC20} 继承，以便直接调用 `super.mint` 和 `super.burn`。
如果 {Wrapper} 不从 {ERC20} 继承，则 {Derivative} 合约必须限制，使得 {Wrapper} 具有唯一的 `mint`、`burn` 和其他更改代币供应的权力。

#### ERC721Receiver, ERC1155Receiver
如果不从 {ERC721Receiver} 和/或 {ERC1155Receiver} 继承，则池必须限制，使得基础代币只能通过包装器的 `mint`、`burn` 进行转移。

每个给定 (address, id) 对的 ERC-721 代币只有一个。然而，给定 (address, id) 的 ERC-1155 代币的数量可能大于 1。因此，“单个”和“批量”在每个标准中的含义有所不同。在这两个标准中，“单个”指的是单个 ID，而“批量”指的是多个 ID。在 ERC-1155 中，单个 ID 事件/函数可能涉及多个代币，具体取决于 `value` 字段。

在构建一组通用事件和函数时，我们必须意识到这些实现之间的差异。当前的实现将 ERC-721 代币视为特殊情况，在参考每个 {Base} 代币的数量时：
- 所有命名为 `_amount` 的参数，必须为 `1`。
- 所有命名为 `_amounts` 的参数必须是一个空列表或与 `_ids` 长度相同的 `1` 列表。
这保持了与 ERC-1155 一致的代币枚举。替代实现包括：
- 具有专用功能的通用接口。例如：`mintFromERC721`。
- 每种类型的单独接口。例如：`ERC721Wrapper`，`ERC1155Wrapper`。

#### ERC721, ERC1155
{Wrapper} 可以从 {ERC721} 和/或 {ERC1155} 继承，以便直接调用 `super.mint`。这不是强制性的，因为在此标准中不要求铸造 {Base} 代币。“初始 NFT 发行”可以利用此功能在合约内创建一组 {Base} 代币，并直接分发 {Derivative} 代币。

如果 {Wrapper} 不从 {ERC721} 或 {ERC1155} 继承，则必须包含对 {IERC721} 和 {IERC1155} 的调用，以便转移 {Base} 代币。

### 授权
所有底层转移方法与 {Wrapper} 无关，而是调用 ERC-20/721/1155 转移方法。此标准的实现必须：
- 要么实现 {Derivative} 转移授权以进行销毁，并实现 {Base} 转移授权以进行铸造。
- 要么在尝试执行之前通过 {IERC721} / {IERC1155} 检查 {Wrapper} 外部的授权。

## 向后兼容性
大多数现有实现继承自 ERC-20，使用 `mint` 和 `burn` 函数。
事件：
- 铸造
    - WK: DepositKittyAndMintToken
    - NFTX: Mint

- 销毁
    - WK: BurnTokenAndWithdrawKity
    - NFTX: Redeem

## 参考实现
[ERC-3386 参考实现](https://github.com/ashrowz/erc-3386)

## 安全考虑
建议 Wrapper 合约继承自可销毁的 ERC-20 代币。如果没有，则 {Derivative} 代币的供应必须由 Wrapper 控制。同样，价格实现必须确保 {Base} 代币的供应由 {Derivative} 代币反映。

通过函数 `idBurn`，`idBurns`，用户可以针对通用批次中最有价值的 NFT。如果不同 id 的代币价值之间存在显著差异，合约应考虑创建专用池（NFTX）或定价（NFT20）以考虑这一点。

## 版权
通过 [CC0](../LICENSE.md) 放弃版权及相关权利。