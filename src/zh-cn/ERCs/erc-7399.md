---
eip: 7399
title: ⚡ 闪电贷款 ⚡
description: 闪电贷款的接口和流程
author: Alberto Cuesta Cañada (@alcueca), Michael Amadi (@AmadiMichaels), Devtooligan (@devtooligan), Ultrasecr.eth (@ultrasecreth), Sam Bacha (@sambacha)
discussions-to: https://ethereum-magicians.org/t/erc7400-flash-loans/15211
status: 草案
type: 标准跟踪
category: ERC
created: 2023-07-25
requires: 20
---

## 摘要

闪电贷款是借款人和贷款人智能合约之间的贷款，必须在交易结束之前偿还，外加可选费用。该 ERC 指定了贷款人接受闪电贷款请求的接口，以及借款人在贷款人执行过程中暂时控制交易的接口。还指定了闪电贷款的安全执行流程。

## 动机

当前闪电贷款生态系统状态分散，缺乏标准化，给贷款人和借款人带来了若干挑战。缺乏通用接口导致集成工作量增加，因为每个闪电贷款提供者都实施了自己独特的方法。随着生态系统的增长，这种缺乏标准化的问题预计会变得更加严重，需要更多资源来维持兼容性。

对现有闪电贷款协议的全面分析揭示了它们在实现上的显著差异，包括：

- 不同平台启动闪电贷款的语法不一致。
- 贷款接收者与回调接收者之间的关系变化，一些协议允许每个角色使用不同地址，而另一些则不允许。
- 还款机制的差异，一些贷款人从贷款接收者提取本金和费用，而另一些则要求贷款接收者手动归还资金。
- 对闪电铸造的处理差异，一些贷款人允许在不收取费用的情况下创建任意数量的其本地资产，有效地允许受计算限制而非资产所有权限制的闪电贷款。

为了解决这些不一致并促进更高效和可访问的闪电贷款生态系统，该 ERC 指定了一个标准化接口，涵盖了贷款人和借款人所需的最大灵活性。通过将各种方法整合为统一标准，该提案旨在简化集成过程，使借款人能够在不需要代码修改的情况下无缝切换闪电贷款人。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

根据该标准，闪电贷款是从 `lender` 借出的 `asset` 的 `amount` 贷款。该贷款可以在 `lender` 的单个 `flash` 调用期间保持开放。

该 `amount` 加上 `lender` 在同一 `asset` 中定义的 `fee` 必须在 `flash` 调用结束之前偿还到 `lender` 定义的 `repayment receiver` 地址。

`flash` 函数由 `initiator` 调用，`initiator` 定义了 *loan receiver*、*callback receiver*、*callback function*、`asset` 和 `amount`。

当 `initiator` 在 `lender` 中调用 `flash` 时，`lender` 将把 `amount` 的 `asset` 转移到 *loan receiver*。

`lender` 在将 `amount` 的 `asset` 转移到 *loan receiver* 后，将在 *callback receiver* 上执行 *callback function*。`lender` 将在此 *callback function* 调用中包含与贷款相关的一些参数，如本标准所定义。

在 `flash` 调用结束之前，`amount` 和 `fee` 需要转移到 `repayment receiver`。`fee` 可以设置为零 `asset`。

*callback function* 可以返回任何任意数据，该数据将作为 `flash` 调用的返回值被 `initiator` 接收。

贷款人决定支持哪些 `assets`。贷款人可以决定支持所有可能的资产。

### 贷款人规范

`lender` 必须实现 [ERC-7399](./eip-7399.md) 接口。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity >=0.6.0 <0.9.0;

import { IERC20 } from "./IERC20.sol";

interface IERC7399 {
    /// @dev The amount of currency available to be lent.
    /// @param asset The loan currency.
    /// @return The amount of `asset` that can be borrowed.
    function maxFlashLoan(
        address asset
    ) external view returns (uint256);

    /// @dev The fee to be charged for a given loan. Returns type(uint256).max if the loan is not possible.
    /// @param asset The loan currency.
    /// @param amount The amount of assets lent.
    /// @return The amount of `asset` to be charged for the loan, on top of the returned principal.
    function flashFee(
        IERC20 asset,
        uint256 amount
    ) external view returns (uint256);

    /// @dev Initiate a flash loan.
    /// @param loanReceiver The address receiving the flash loan
    /// @param asset The asset to be loaned
    /// @param amount The amount to loaned
    /// @param data The ABI encoded user data
    /// @param callback The address and signature of the callback function
    /// @return result ABI encoded result of the callback
    function flash(
        address loanReceiver,
        ERC20 asset,
        uint256 amount,
        bytes calldata data,
        /// @dev callback. This is a combination of the callback receiver address, and the signature of callback
        /// function. It is encoded packed as 20 bytes + 4 bytes.
        /// @dev the return of the callback function is not encoded in the parameter, but must be `returns (bytes
        /// memory)` for compliance with the standard.
        /// @param initiator The address that called this function
        /// @param paymentReceiver The address that needs to receive the amount plus fee at the end of the callback
        /// @param asset The asset to be loaned
        /// @param amount The amount to loaned
        /// @param fee The fee to be paid
        /// @param data The ABI encoded data to be passed to the callback
        /// @return result ABI encoded result of the callback
        function(address, address, IERC20, uint256, uint256, bytes memory) external returns (bytes memory) callback
    )
        external
        returns (bytes memory);
}

```

`maxFlashLoan` 函数必须返回 `asset` 的最大可用贷款。`maxFlashLoan` 函数必须不返回错误。如果指定的 `asset` 不可能进行闪电贷款，则返回值必须为零。

`flashFee` 函数必须返回贷款 `amount` `asset` 的费用。`flashFee` 函数必须不返回错误。如果指定的 `asset` 和 `amount` 不可能进行闪电贷款，则返回值必须为 `type(uint256).max`。

`flash` 函数必须执行作为参数传递的回调。

```solidity
bytes memory result = callback(msg.sender, address(this), asset, amount, _fee, data);
```

`flash` 函数必须在执行回调之前将 `amount` 的 `asset` 转移到 *loan receiver*。

`flash` 函数必须在回调中包含 `msg.sender` 作为 `initiator`。

`flash` 函数必须不修改接收到的 `asset`、`amount` 和 `data` 参数，并且必须将它们传递给回调。

`flash` 函数必须在回调中包含一个 `fee` 参数，表示贷款的费用，加上本金，确保 `fee == flashFee(asset, amount)`。

在回调结束之前，`payment receiver` 的 `asset` 余额必须比回调开始时增加 `amount + fee`，否则将回滚。

`flash` 函数的返回值必须与回调的返回值相同。

### 接收者规范

闪电贷款的 *callback receiver* 必须实现一个或多个具有以下参数和返回值的外部函数：

```solidity
/// @dev This function can have any name and be overloaded.
/// @param initiator The address that called this function
/// @param paymentReceiver The address that needs to receive the amount plus fee at the end of the callback
/// @param asset The asset to be loaned
/// @param amount The amount to loaned
/// @param fee The fee to be paid
/// @param data The ABI encoded data to be passed to the callback
/// @return result ABI encoded result of the callback
function(address, address, IERC20, uint256, uint256, bytes memory) external returns (bytes memory) callback;
```

## 理由

本 ERC 中描述的接口已被选择以覆盖已知的闪电借贷用例，同时允许安全和高效的 gas 实现。

`maxFlashLoan` 和 `flashFee` 在不可能贷款时返回数值，以便在不处理回滚的情况下对贷款人进行排序。

当 `lender` 无法提供贷款时，`maxFlashLoan` 返回与不可能贷款一致的值。

当 `lender` 无法提供贷款时，`flashFee` 返回与不可能贷款一致的值。

`flash` 被选择为动词函数名，描述性足够，不太可能与 `lender` 中的其他函数冲突，并且包括了贷款资产被持有或铸造的两种用例。

现有的闪电贷款人都提供来自同一合约的多种资产类型的闪电贷款。在 `flash` 和回调函数中提供 `asset` 参数与观察到的功能密切匹配。

`bytes calldata data` 参数被包含以便 `initiator` 向 `receiver` 传递任意信息。`receiver` 可以使用 `bytes memory` 返回值将任意信息传回给 `initiator`。

在回调函数中通常需要一个 `initiator`，`lender` 知道它是 `msg.sender`。一种替代实现是通过调用者在 `data` 参数中嵌入 `initiator`，这将需要接收者验证其准确性的额外机制，因此不建议使用。

*loan receiver* 被作为参数引入，以允许单独的贷款发起者、贷款接收者和回调接收者的实现灵活性。该参数不会传递给 *callback receiver*，因为它通常与 *callback receiver* 相同，而在不相同的情况下，可以由 `initiator` 在 `data` 中编码。
`payment receiver` 允许与借款一样的灵活性进行还款。控制流和资产流是独立的。

在回调函数中将需要 `amount`，这是 `lender` 作为参数传入的。另一种实现方式是通过调用者将 `amount` 嵌入 `data` 参数，这需要接收者有额外的机制来验证其准确性，因此不建议使用。

`fee` 通常会在回调函数中计算，回调接收者必须了解这一点以便进行还款。将 `fee` 作为参数传递而不是附加到 `data` 中是简单有效的。

在回调接收者上使用任意回调函数可以实现不同的闪电贷行为，而无需使用 `data` 参数编码函数路由。函数调用类型为 24 字节，其中前 20 字节是目标地址，最后 4 字节是函数签名。

`amount + fee` 被推送到 `payment receiver` 以允许资产和控制流的分离。虽然“推送”架构更为普遍，但“拉取”架构也很常见。在 `lender` 无法实现“推送”架构的情况下，一个简单的包装合约可以提供本提案的外部接口，同时使用 `lender` 的流动性采用“拉取”架构。

## 向后兼容性

此 EIP 是 [ERC-3156](./eip-3156.md) 的继任者。虽然不是直接向后兼容，但提供本提案外部接口的包装合约，使用从 ERC-3156 闪电 `lender` 获得的流动性是微不足道的。

## 安全考虑

### 回调参数的验证

闪电贷回调的参数预计反映闪电贷的条件，但不能无条件信任。它们可以分为两组，需要在信任它们是真实之前进行不同的检查。

1. 不能假设任何参数是真实的，除非经过某种验证。`initiator`、`asset` 和 `amount` 指的是过去的交易，如果回调的调用者决定撒谎，这些交易可能并未发生。`fee` 可能是错误的或计算不正确。`data` 可能已被调用者操纵。
2. 为了信任 `initiator`、`asset`、`amount` 和 `fee` 的值是真实的，一个合理的模式是验证回调调用者是否在经过验证的闪电贷白名单中。由于通常 `flash` 的调用者也会接收回调，这将是微不足道的。在所有其他情况下，如果要信任回调中的参数，闪电贷提供者需要获得批准。
3. 为了信任 `data` 的值是真实的，除了第 1 点的检查外，建议验证 `initiator` 是否属于可信地址组。信任 `lender` 和 `initiator` 足以信任 `data` 的内容是真实的。

### 闪电贷安全考虑

#### 自动批准

任何偿还作为参数接收的 `amount` 和 `fee` 的 `receiver` 需要在回调中包含一个机制，以验证 `initiator` 和 `lender` 是否可信。

或者，回调接收者可以实现权限函数，设置状态变量，指示闪电贷已被发起以及期望的 `amount` 和 `fee`。

或者，回调接收者可以验证 `amount` 是否已被 `loanReceiver` 接收，并使用自己的启发式方法来确定 `fee` 是否公平以及贷款是否已偿还，或者交易是否被回滚。

### 闪电铸造外部安全考虑

闪电铸造交易中涉及的资产数量通常会产生新的创新攻击向量。

#### 现货预言机操控

闪电铸造资产的供应可以很容易被操控，因此考虑闪电铸造资产供应的预言机必须折扣闪电铸造的数量，生成随时间平均的数据，或找到其他解决方案以应对变化的供应。

#### 算术溢出和下溢

如果闪电铸造提供者没有对交易中的闪电铸造资产数量设置任何限制，那么任何人都可以闪电铸造 $2^256-1$ 数量的资产。

闪电铸造的接收方协议需要确保其合约能够处理这一点，方法是使用嵌入溢出保护的编译器，或设置显式检查。

### 闪电铸造内部安全考虑

将闪电铸造与同一平台上的特定业务功能耦合可能会导致意想不到的后果。

#### 财库耗尽

假设一个智能合约闪电借出其本地资产。该智能合约在用户销毁本地资产时从第三方借款。此模式将用于在智能合约中将多个用户的抵押债务聚合到第三方的单一账户中。闪电铸造可以用于使 `lender` 借款达到其限额，然后推动基础 `lender` 的利率，清算闪电 `lender`：

1. 从 `lender` 闪电铸造大量 FOO。
2. 兑换 FOO 为 BAR，导致 `lender` 从 `underwriter` 借款达到其借款限额。
3. 触发 `underwriter` 的债务利率上升，使 `lender` 变得不足抵押。
4. 清算 `lender` 以获取利润。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。