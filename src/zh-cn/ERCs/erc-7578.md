---
eip: 7578
title: 实体资产赎回
description: 为持有实体资产支持的 NFT 的用户提供可用的现实世界信息，关于其背后的实体资产。
author: Lee Vidor (@V1d0r), David Tan <david@emergentx.org>, Lee Smith <lee@emergentx.org>, Gabriel Stoica (@gabrielstoica)
discussions-to: https://ethereum-magicians.org/t/erc-7578-physical-asset-redemption/17556
status: 审核中
type: 标准跟踪
category: ERC
created: 2023-08-01
requires: 165, 721
---

## 摘要

本提案是 [ERC-721](./eip-721.md) 的扩展，实施了与 NFT 背后实体资产相关的附加功能和信息，通过捕获信息使持有实体资产支持的 NFT 的用户能够验证真实性并促进对基础实体资产的赎回。该提案主要旨在通过披露相关方的详细信息来提供透明度，并提供机会定义并使 NFT 持有者与相应基础实体资产的所有者/持有者之间的相关法律关系变得可用。该提案使代币发行人有责任嵌入关于基础实体资产和相关关键方的一组标准化信息的准确资料。

## 动机

NFT 的第一波用例主要包括数字资产所有权的表示。考虑到将任何现实世界资产代币化的预期趋势，NFT 的用例将迅速增长并扩展到实体资产。缺乏嵌入的关于基础实体资产的标准化信息集，加上相关关键方缺乏透明度，为 NFT 持有者和潜在用户创造了不必要的障碍，这可能会阻碍作为特定实体资产所有权表示的 NFT 的大规模采用。

为了解决缺乏可用信息的问题，并为代币化经济的大规模采用铺平道路，本提案要求每个铸造的代币包括一组定义的预定义变量，以便验证真实性并促进对基础实体资产的赎回。

## 规范

本文档中的关键词 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

当代币被铸造时，其属性应事先初始化，每个字段的定义如下：

- **代币发行人**：铸造 NFT 的个人或实体的识别
  <br> *代币发行人是连接实体资产和数字表示的关键人物。通过识别和披露代币发行人，NFT 持有者可以立即获得一个参考点，从而对 NFT 发行人进行尽职调查并评估其可信度。同时，这为代币发行人创造了责任，从而改善和标准化 NFT 铸造过程。代币发行人将竞争最佳实践和认可，以获得竞争优势。一个声誉良好的 NFT 发行人将例如在铸造基础实体资产之前保留有关实体资产法律所有者的信息，以满足任何 AML 和 KYC 的担忧。理想情况下，NFT 发行人通过名称进行识别，但也可以通过服务提供商颁发的唯一识别号码或网络 ID 进行识别，该服务提供商存储有关 NFT 发行人的相关信息。*

- **资产持有者**：基础实体资产法律所有者的识别
  <br> *考虑到对基础实体资产的赎回和法律权利的执行，从法律角度来看，NFT 持有者识别基础实体资产的法律所有者是至关重要的。这使得 NFT 持有者能够考虑法律对手方风险。不能假设 NFT 发行人是基础实体资产的法律所有者，因此 NFT 持有者必须能够立即访问此附加信息。与 NFT 发行人的身份一样，法律所有者理想上通过名称进行识别，但也可以通过服务提供商颁发的唯一识别号码或网络 ID 进行识别，该服务提供商存储有关法律所有者的相关信息。*

- **存储位置**：基础实体资产的存储位置的识别
  <br> *某些实体资产需要特定的存储条件。不当存储的实体资产的数字表示可能会显著影响 NFT 的价值。披露存储位置并使其直接可供 NFT 持有者访问，使他们能够评估基础实体资产的存储风险。此外，它为 NFT 持有者提供了第二个联系点，以便执行对基础实体资产的赎回。*

- **条款**：法律关系的识别
  <br> *披露和可访问 NFT 持有者与基础实体资产法律所有者之间关系的法律基础，促进代币发行人规定和定义相关关键方的法律权利。此外，它还允许 NFT 持有者进行法律风险和执行评估。理想情况下，通过嵌入指向实际法律文件（如协议或条款）的链接来提供信息。通过 NFT 可访问的信息越多，NFT 持有者就能越好地评估与执行基础实体资产赎回相关的法律风险。*

- **管辖权**：适用法律和管辖权
  <br> *适用法律是法律合同披露的扩展，使 NFT 持有者或智能合约能够立即了解在何种管辖权下进行执行，而无需查看法律合同的详细信息。这允许对管辖权风险进行即时评估。*

- **声明价值**：基础资产的价值
  <br> *某些辅助服务（如保险）与 NFT 和基础实体资产的价值相关。通过定义声明价值，NFT 可以以某种方式进行分类，而声明价值则提供了关于基础资产价值的指示。基础实体资产的声明价值不一定代表市场价值。*

`terms` 参数应为指向存储在 IPFS 上的文档的 HTTP 链接。这是为了确保文档是不可变的，并且可以由 NFT 持有者进行验证。

当要销毁具有有效属性的代币时，必须移除这些属性。

### 合约接口

```solidity
pragma solidity ^0.8.21;

/**
 * @notice Struct encapsulating fields required to by the ERC-7578 standard to represent the physical asset
 * @param tokenIssuer The network or entity minting the token
 * @param assetHolder The legal owner of the physical asset
 * @param storageLocation The physical location where the asset is stored
 * @param terms Link to IPFS contract, agreement or terms
 * @param jurisdiction The legal justification set out in the terms
 * @param declaredValue The declared value at time of token minting
 */
struct Properties {
    string tokenIssuer;
    string assetHolder;
    string storageLocation;
    string terms;
    string jurisdiction;
    Amount declaredValue;
}

/**
 * @notice Struct encapsulating fields describing the declared value of the physical asset
 * @param currency The currency of the amount
 * @param value The value of the amount
 */
struct Amount {
    string currency;
    uint256 value;
}

/**
 * @notice Required interface of an ERC-7578 compliant contract
 */
interface IERC7578 {
    /**
     * @notice Emitted when the properties of the `tokenId` token are set
     * @param tokenId The ID of the token
     * @param properties The properties of the token
     */
    event PropertiesSet(uint256 indexed tokenId, Properties properties);

    /**
     * @notice Emitted when the properties of the `tokenId` token are removed
     * @param tokenId The ID of the token
     */
    event PropertiesRemoved(uint256 indexed tokenId);

    /**
     * @notice Retrieves all properties of the `tokenId` token
     * @dev Does NOT revert if token doesn't exist
     * @param tokenId The token ID of the minted token
     */
    function getPropertiesOf(uint256 tokenId) external view returns (Properties memory properties);
}
```

当设置 `properties` 时，会发出 `PropertiesSet(uint256 indexed tokenId, Properties properties)` 事件。

当移除 `properties` 时，会发出 `PropertiesRemoved(uint256 indexed tokenId)` 事件。

`getPropertiesOf(uint256 tokenId)` 函数必须返回代币的唯一 `properties`。如果 ERC-721 代币被销毁或没有设置属性，则应返回一个空的 `Properties` 结构。

## 理由

如果在铸造之前不初始化代币的属性，则可能无法建立代币所代表的资产的来源。
合约级别的验证不适用于属性，因为我们认为声明数据的准确性是代币发行者的责任。这建立了对代币发行者和代币本身的信任。

## 向后兼容性

该标准与 ERC-721 兼容。

## 参考实现

一个包含此提案的 [ERC-721](./eip-721.md) 示例，使用 OpenZeppelin ERC-721 v5 库：

```solidity
pragma solidity ^0.8.21;

import { ERC721 } from "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import { IERC7578, Properties, Amount } from "./interfaces/IERC7578.sol";

/**
 * @title ERC7578
 * @author DESAT
 * @notice Implementation of the ERC-7578: Physical Asset Redemption standard
 **/
contract ERC7578 is ERC721, IERC7578 {
    /**
     * @notice Thrown when the properties of a token are not initialized
     */
    error PropertiesUninitialized();

    /**
     * @notice Retrieves the properties of the `tokenId` token
     */
    mapping(uint256 tokenId => Properties) private _properties;

    /**
     * @notice Initializes the name and symbol of the ERC-721 collection
     */
    constructor(string memory _name, string memory _symbol) ERC721(_name, _symbol) {}

    /**
     * @inheritdoc IERC7578
     */
    function getPropertiesOf(uint256 tokenId) public view override returns (Properties memory properties) {
        properties = _properties[tokenId];
    }

    /**
     * @notice Initializes the ERC-7578 properties of the `tokenId` token
     *
     * WARNING: This method should only be called within a function that has appropriate access control
     * It is recommended to restrict access to trusted Externally Owned Accounts (EOAs),
     * authorized contracts, or implement a Role-Based Access Control (RBAC) mechanism
     * Failure to properly secure this method could lead to unauthorized modification of token properties
     *
     * Emits a {PropertiesSet} event
     */
    function _setPropertiesOf(uint256 tokenId, Properties calldata properties) internal {
        _properties[tokenId] = Properties({
            tokenIssuer: properties.tokenIssuer,
            assetHolder: properties.assetHolder,
            storageLocation: properties.storageLocation,
            terms: properties.terms,
            jurisdiction: properties.jurisdiction,
            declaredValue: Amount({
                currency: properties.declaredValue.currency,
                value: properties.declaredValue.value
            })
        });

        emit PropertiesSet(tokenId, _properties[tokenId]);
    }

    /**
     * @notice Removes the properties of the `tokenId` token
     * @param tokenId The unique identifier of the token whose properties are to be removed
     *
     * Emits a {PropertiesRemoved} event
     */
    function _removePropertiesOf(uint256 tokenId) internal {
        delete _properties[tokenId];
        emit PropertiesRemoved(tokenId);
    }

    /**
     * @notice Override of the {_update} function to remove the properties of the `tokenId` token or
     * to check if they are set before minting
     * @param tokenId The unique identifier of the token being minted or burned
     */
    function _update(address to, uint256 tokenId, address auth) internal virtual override returns (address) {
        address from = _ownerOf(tokenId);
        if (to == address(0)) {
            _removePropertiesOf(tokenId);
        } else if (from == address(0)) {
            if (bytes(_properties[tokenId].tokenIssuer).length == 0) revert PropertiesUninitialized();
        }

        return super._update(to, tokenId, auth);
    }
}
```

## 安全考虑

为了确保代币属性的真实性，`_setPropertiesOf()` 方法应仅在受限于受信任的外部拥有账户 (EOA) 或合约的方法内部调用。该受信任实体必须验证属性是否准确反映代币的真实物理特征。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。