---
eip: 4799
title: 非同质化代币所有权指定标准
description: 用于指定 NFT 所有权的标准化接口
author: David Buckman (@davidbuckman), Isaac Buckman (@isaacbuckman)
discussions-to: https://ethereum-magicians.org/t/erc-4799-non-fungible-token-wrapping-standard/8396
status: 停滞
type: 标准跟踪
category: ERC
created: 2022-02-13
requires: 165
---

## 摘要

以下定义了一个标准接口，用于在 NFT 被智能合约托管时将其所有权指定给某人。该标准允许构建一个 NFT 的有向无环图，其中给定链中每个 NFT 的指定所有者是该链的终端地址。这使得在不放弃原始 NFT 的真实性的情况下，可以为现有 NFT 引入额外的功能。实际上，这意味着所有 NFT 都是可组合的，可以被租赁、用作抵押、进行分割等。

## 动机

许多 NFT 旨在为其持有者提供某种实用性——这种实用性可以有多种形式。这可以是居住公寓的权利、活动门票的获取、代币的空投，或其他无数潜在应用之一。然而，在当前形式下，NFT 的局限在于与 NFT 关联的唯一可验证钱包是所有者，因此希望分发实用性的客户被迫将其分发给 NFT 的列出所有者。这意味着任何复杂的所有权协议必须编码到原始 NFT 合约中——没有机制可以让所有者将其原始 NFT 的真实性链接到任何外部合约。

该标准的目标是允许用户和开发者在已经铸造的 NFT 上定义任意复杂的所有权协议。这样，可以部署具有创新所有权结构的新合约，但它们仍然可以利用由已建立的 NFT 合约提供的真实性——在过去，包装合约意味着全新的 NFT，没有建立的真实性。

在此标准之前，将 NFT 包装在另一个合约中是添加功能的唯一方法，但这意味着失去持有原始 NFT 的实用性。任何查询该 NFT 所有者的应用程序都会将包装智能合约视为所有者。使用该标准，应用程序将拥有与包装合约交互的标准化方法，以便即使 NFT 已被包装，它们仍然可以继续将其实用性指向用户。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

```solidity
import "@openzeppelin/contracts/utils/introspection/IERC165.sol";

interface IERC4799NFT is IERC165 {
    /// @dev This emits when ownership of any NFT changes by any mechanism.
    ///  This event emits when NFTs are created (`from` == 0) and destroyed
    ///  (`to` == 0). Exception: during contract creation, any number of NFTs
    ///  may be created and assigned without emitting Transfer. At the time of
    ///  any transfer, the approved address for that NFT (if any) is reset to none.
    event Transfer(
        address indexed from,
        address indexed to,
        uint256 indexed tokenId
    );

    /// @notice Find the owner of an NFT
    /// @dev NFTs assigned to zero address are considered invalid, and queries
    ///  about them throw
    /// @param tokenId The identifier for an NFT
    /// @return The address of the owner of the NFT
    function ownerOf(uint256 tokenId) external view returns (address);
}
```
```solidity
/// @title ERC-4799 Non-Fungible Token Ownership Designation Standard
/// @dev See https://eips.ethereum.org/EIPS/eip-4799
/// Note: the ERC-165 identifier for this interface is [TODO].

import "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import "./IERC4799NFT.sol";

interface IERC4799 is IERC165 {
    /// @dev Emitted when a source token designates its ownership to the owner of the target token
    event OwnershipDesignation(
        IERC4799NFT indexed sourceContract,
        uint256 sourceTokenId,
        IERC4799NFT indexed targetContract,
        uint256 targetTokenId
    );

    /// @notice Find the designated NFT
    /// @param sourceContract The contract address of the source NFT
    /// @param sourceTokenId The tokenId of the source NFT
    /// @return (targetContract, targetTokenId) contract address and tokenId of the parent NFT
    function designatedTokenOf(IERC4799NFT sourceContract, uint256 sourceTokenId)
        external
        view
        returns (IERC4799NFT, uint256);
}
```

指定 NFT 的所有权的真实性由指定的 ERC-4799 合约对原始 NFT 的所有权赋予。客户必须通过查询源合约来验证这一点。

遵循此规范的客户不得将任何实用性分发给 ERC-4799 合约的地址。相反，他们必须将其分发给 ERC-4799 合约指向的指定代币的所有者。

## 理由

为了最大化包装合约的未来兼容性，我们首先定义了一个规范的 NFT 接口。我们创建了 `IERC4799NFT`，这是一个几乎所有流行 NFT 合约隐式实现的接口，包括所有已部署的符合 [ERC-721](./eip-721.md) 的合约。该接口代表了 NFT 的本质：从代币标识符到单一所有者地址的映射，由 `ownerOf` 函数表示。

我们提案的核心是 `IERC4799` 接口，这是一个标准 NFT 所有权指定合约（ODC）的接口。ERC4799 要求实现一个 `designatedTokenOf` 函数，该函数将源 NFT 映射到一个目标 NFT。通过此函数，ODC 表达其对指定所有权的信念。只有当 ODC 被列为原始 NFT 的所有者时，这种指定所有权才是有效的，从而保持每个 NFT 只有一个指定所有者的恒等式。

## 向后兼容性

`IERC4799NFT` 接口与 `IERC721` 向后兼容，因为 `IERC721` 隐式扩展了 `IERC4799NFT`。这意味着包装实现 `ERC4799NFT` 的 NFT 的 ERC-4799 标准与 ERC-721 完全向后兼容。

## 参考实现

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity >=0.8.0 <0.9.0;

import "./IERC4799.sol";
import "./IERC4799NFT.sol";
import "./ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";

contract ERC721Composable is IERC4799, IERC721Receiver {
    mapping(IERC4799NFT => mapping(uint256 => IERC4799NFT)) private _targetContracts;
    mapping(IERC4799NFT => mapping(uint256 => uint256)) private _targetTokenIds;

    function designatedTokenOf(IERC4799NFT sourceContract, uint256 sourceTokenId)
        external
        view
        override
        returns (IERC4799NFT, uint256)
    {
        return (
            IERC4799NFT(_targetContracts[sourceContract][sourceTokenId]),
            _targetTokenIds[sourceContract][sourceTokenId]
        );
    }

    function designateToken(
        IERC4799NFT sourceContract,
        uint256 sourceTokenId,
        IERC4799NFT targetContract,
        uint256 targetTokenId
    ) external {
        require(
            ERC721(address(sourceContract)).ownerOf(sourceTokenId) == msg.sender ||
            ERC721(address(sourceContract)).getApproved(sourceTokenId) == msg.sender, 
            "ERC721Composable: Only owner or approved address can set a designate ownership");
        _targetContracts[sourceContract][sourceTokenId] = targetContract;
        _targetTokenIds[sourceContract][sourceTokenId] = targetTokenId;
        emit OwnershipDesignation(
            sourceContract, 
            sourceTokenId,  
            targetContract,
            targetTokenId
        );
    }

    function onERC721Received(
        address,
        address from,
        uint256 sourceTokenId,
        bytes calldata
    ) external override returns (bytes4) {
        ERC721(msg.sender).approve(from, sourceTokenId);
        return IERC721Receiver.onERC721Received.selector;
    }

        function supportsInterface(bytes4 interfaceId)
        public
        view
        virtual
        override
        returns (bool)
    {
        return
            (interfaceId == type(IERC4799).interfaceId ||
            interfaceId == type(IERC721Receiver).interfaceId);
    }
}
```
```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity >=0.8.0 <0.9.0;

import "./IERC4799.sol";
import "./IERC4799NFT.sol";
import "@openzeppelin/contracts/utils/introspection/ERC165Checker.sol";

contract DesignatedOwner {
    function designatedOwnerOf(
        IERC4799NFT tokenContract,
        uint256 tokenId,
        uint256 maxDepth
    ) public view returns (address owner) {
        owner = tokenContract.ownerOf(tokenId);
        if (ERC165Checker.supportsInterface(owner, type(IERC4799).interfaceId)) {
            require(maxDepth > 0, "designatedOwnerOf: depth limit exceeded");
            (tokenContract, tokenId) = IERC4799(owner).designatedTokenOf(
                tokenContract,
                tokenId
            );
            return designatedOwnerOf(tokenContract, tokenId, maxDepth - 1);
        }
    }
}
```

## 安全考虑

### 长/循环所有权链

主要的安全问题是恶意行为者创建过长或循环的所有权链，导致尝试查询给定代币的指定所有者的应用程序耗尽 gas 而无法正常工作。为了解决这个问题，客户应始终考虑 `maxDepth` 参数进行查询，在经过一定数量的链遍历后停止计算。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。