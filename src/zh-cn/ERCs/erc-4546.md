---
eip: 4546
title: 包装存款
description: 一个用于管理资产存款的单例合约。
author: Justice Hudson (@jchancehud)
discussions-to: https://ethereum-magicians.org/t/wrapped-deposit-contract-eip/7740
status: 停滞
type: 标准跟踪
category: ERC
created: 2021-12-11
---

## 摘要
包装存款合约代表用户处理资产（以太币、[ERC-20](./erc-20.md)、[ERC-721](./eip-721.md)）的存款。用户只需批准一次支出限额，然后资产可以存入任何支持来自该合约存款的不同应用程序。

## 动机
当前在去中心化应用（dapps）中存款资产的用户流程不必要地昂贵且不安全。要存入 ERC-20 资产，用户必须：

  - 在存款之前发送一个确切金额的批准交易，然后对每个后续存款重复此过程。
  - 在存款之前发送一个无限支出金额的批准交易。

第一个选项不方便且昂贵。第二个选项不安全。此外，向新用户或非技术用户解释批准过程令人困惑。这必须在支持 ERC20 存款的*每个* dapp 中完成。

## 规范
包装存款合约应部署在一个可识别的地址（例如 `0x1111119a9e30bceadf9f939390293ffacef93fe9`）。该合约必须是不可升级的，且没有更改状态变量的能力。

包装存款合约必须具有以下公共函数：

```js
depositERC20(address to, address token, uint amount) external;
depositERC721(address to, address token, uint tokenId) external;
safeDepositERC721(address to, address token, uint tokenId, bytes memory data) external;
safeDepositERC1155(address to, address token, uint tokenId, uint value, bytes calldata data) external;
batchDepositERC1155(address to, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) external;
depositEther(address to) external payable;
```

如果 `to` 是一个代码大小为零的地址，则这些函数必须回退。每个函数必须尝试调用 `to` 地址上的一个方法，以确认其愿意并能够接受存款。如果此函数调用未返回 true 值，则执行必须回退。如果资产转移不成功，则执行必须回退。

希望接受存款的合约应存在以下接口：

```ts
interface ERC20Receiver {
  function acceptERC20Deposit(address depositor, address token, uint amount) external returns (bool);
}

interface ERC721Receiver {
  function acceptERC721Deposit(address depositor, address token, uint tokenId) external returns (bool);
}

interface ERC1155Receiver {
  function acceptERC1155Deposit(address depositor, address token, uint tokenId, uint value, bytes calldata data) external returns (bool);
  function acceptERC1155BatchDeposit(address depositor, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) external returns (bool);
}

interface EtherReceiver {
  function acceptEtherDeposit(address depositor, uint amount) external returns (bool);
}
```

接收合约可以根据需要实现这些函数中的任何一个。如果某个函数未实现，则不得为该资产类型发送存款。

## 理由
拥有一个处理所有代币转移的单一合约允许用户为每个代币提交一次批准，以便存入任意数量的合约。用户不必信任接收合约的代币支出批准，接收合约的复杂性也因不必自己实现代币转移而降低。

用户体验得到了改善，因为可以实现一个简单的全球 dapp，传达的信息是：“启用代币以便在其他应用中使用”。

## 向后兼容性

此 EIP 不向后兼容。任何计划使用此存款系统的合约必须实现特定函数以接受存款。现有的可升级合约可以通过实现一个或多个接受存款函数来追溯性地添加对该 EIP 的支持。

升级后的合约可以同时使用旧系统（批准合约本身）和提议的存款系统来允许存款，以保留现有的批准。新用户应被提示使用提议的存款系统。

## 参考实现
```ts
pragma solidity ^0.7.0;

interface ERC20Receiver {
  function acceptERC20Deposit(address depositor, address token, uint amount) external returns (bool);
}

interface ERC721Receiver {
  function acceptERC721Deposit(address depositor, address token, uint tokenId) external returns (bool);
}

interface ERC1155Receiver {
  function acceptERC1155Deposit(address depositor, address token, uint tokenId, uint value, bytes calldata data) external returns (bool);
  function acceptERC1155BatchDeposit(address depositor, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) external returns (bool);
}

interface EtherReceiver {
  function acceptEtherDeposit(address depositor, uint amount) external returns (bool);
}

interface IERC20 {
  function transferFrom(address sender, address recipient, uint amount) external returns (bool);
}

interface IERC721 {
  function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
  function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes memory data) external payable;
}

interface IERC1155 {
  function safeTransferFrom(address _from, address _to, uint _id, uint _value, bytes calldata _data) external;
  function safeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external;
}

contract WrappedDeposit {
  function depositERC20(address to, address token, uint amount) public {
    _assertContract(to);
    require(ERC20Receiver(to).acceptERC20Deposit(msg.sender, token, amount));
    bytes memory data = abi.encodeWithSelector(
      IERC20(token).transferFrom.selector,
      msg.sender,
      to,
      amount
    );
    (bool success, bytes memory returndata) = token.call(data);
    require(success);
    // backward compat for tokens incorrectly implementing the transfer function
    if (returndata.length > 0) {
      require(abi.decode(returndata, (bool)), "ERC20 operation did not succeed");
    }
  }

  function depositERC721(address to, address token, uint tokenId) public {
    _assertContract(to);
    require(ERC721Receiver(to).acceptERC721Deposit(msg.sender, token, tokenId));
    IERC721(token).transferFrom(msg.sender, to, tokenId);
  }

  function safeDepositERC721(address to, address token, uint tokenId, bytes memory data) public {
    _assertContract(to);
    require(ERC721Receiver(to).acceptERC721Deposit(msg.sender, token, tokenId));
    IERC721(token).safeTransferFrom(msg.sender, to, tokenId, data);
  }

  function safeDepositERC1155(address to, address token, uint tokenId, uint value, bytes calldata data) public {
    _assertContract(to);
    require(ERC1155Receiver(to).acceptERC1155Deposit(msg.sender, to, tokenId, value, data));
    IERC1155(token).safeTransferFrom(msg.sender, to, tokenId, value, data);
  }

  function batchDepositERC1155(address to, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) public {
    _assertContract(to);
    require(ERC1155Receiver(to).acceptERC1155BatchDeposit(msg.sender, to, tokenIds, values, data));
    IERC1155(token).safeBatchTransferFrom(msg.sender, to, tokenIds, values, data);
  }

  function depositEther(address to) public payable {
    _assertContract(to);
    require(EtherReceiver(to).acceptEtherDeposit(msg.sender, msg.value));
    (bool success, ) = to.call{value: msg.value}('');
    require(success, "nonpayable");
  }

  function _assertContract(address c) private view {
    uint size;
    assembly {
      size := extcodesize(c)
    }
    require(size > 0, "noncontract");
  }
}
```
## 安全考虑
包装存款的实现应尽可能小，以降低出现错误的风险。合约应足够小，以便工程师可以在几分钟内阅读和理解。

接收合约必须验证 `msg.sender` 等于包装存款合约。未能做到这一点将允许任何人模拟存款。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。