---
eip: 7564
title: 合约钱包管理 NFT
description: 专注于智能合约钱包中的 NFT 管理，提供增强的交易灵活性和安全性
author: Xiang (@wenzhenxiang), Ben77 (@ben2077), Mingshi S. (@newnewsms)
discussions-to: https://ethereum-magicians.org/t/erc-draft-contract-wallet-management-nft/16702
status: 草案
type: 标准跟踪
category: ERC
created: 2023-11-21
requires: 165
---

## 摘要

本提案引入了一种基于智能合约钱包的 NFT 管理方法，专注于利用智能合约钱包的可编程特性进行 NFT 资产管理。此外，它引入了 `nftApprove`、`nftSetApprovalForOneAll`、`nftSetApprovalForAllAll`、`nftGetApproved`、`nftIsApprovedForOneAll`、`nftIsApprovedForAllAll` 和 `nftTransfer` 等功能，提供对 NFT 交易的增强控制。该方法旨在通过利用智能合约钱包的内置功能来增强 NFT 管理，从而提供一种更灵活、安全和高效的代币交易管理方法。

## 动机

外部拥有账户 (EOA) 钱包没有状态和代码存储，而智能合约钱包则有。

账户抽象 (AA) 是智能合约钱包的一个方向，它围绕抽象账户运作。该 ERC 也可以是基于 [ERC-4337](./eip-4337) 的扩展或作为钱包的插件。

智能合约钱包允许用户自己的账户具有状态和代码，为钱包带来了可编程性。我们认为还有更多的扩展方向。例如，NFT 资产管理、NFT 交易的功能扩展等。

该 ERC 的智能合约钱包接口用于 NFT 资产管理和 NFT 资产批准。它支持 simplenft <!-- TODO --> ERC-X，并且 [ERC-721](./eip-721) 向后兼容 <!-- TODO --> ERC-X，因此可以兼容现有市场中所有 NFT 的管理。

该提案旨在实现以下目标：

1. NFT 资产由钱包本身分配和管理，例如批准功能，由用户的合约钱包配置，而不是由 NFT 资产合约控制，以避免一些现有 ERC-721 合约的风险。
2. 添加 `nftTransfer` 功能，由非智能钱包本身发起交易。
3. 添加 `nftApprove`、`nftSetApprovalForOneAll`、`nftSetApprovalForAllAll`、`nftGetApproved`、`nftIsApprovedForOneAll`、`nftIsApprovedForAllAll` 功能。用户钱包本身支持批准并提供对一个 NFT、一个 NFT 智能合约的所有 NFT、所有 NFT 资产的批准。
4. 用户钱包可以选择批量批准和批量转移。
5. 用户可以选择在其 `nftTransfer` 之前和之后添加钩子函数，以增加用户的可玩性。
6. 用户可以选择实现 `nftReceive` 功能。

## 规范

文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 的描述进行解释。

**合规合约必须实现 [ERC-165](./eip-165) 接口**
```solidity

/// @title ERC-7564
/// @dev See https://eips.ethereum.org/EIPS/eip-7564
/// @dev Note: the ERC-165 identifier for this interface is 
pragma solidity ^0.8.20;

interface IERC7564{

    /**
     * @notice Used to notify listeners that owner has granted approval to the user to manage one nft.
     * @param _asset Address of the nft
     * @param _owner Address of the account that has granted the approval for nft‘s assets
     * @param _operator Address of the operator
     * @param _tokenId The unique identifier of the NFT
     */
    event NftApproval(
        address indexed _asset,
        address indexed _owner, 
        address indexed _operator, 
        uint256 _tokenId
    );

    /**
     * @notice Used to notify listeners that owner has granted approval to the operator to manage all nft of one asset contract.
     * @param _asset Address of the nft
     * @param _owner Address of the account that has granted the approval for nft‘s assets
     * @param _operator Address of the operator
     * @param _approved approve all nft of one asset contract
     */
    event NftApprovalForOneAll(
        address indexed _asset,
        address indexed _owner, 
        address indexed _operator,
        bool _approved
    );

    /**
     * @notice Used to notify listeners that owner has granted approval to the operator to manage all nft .
     * @param _owner Address of the account that has granted the approval for nft‘s assets
     * @param _operator Address of the operator
     * @param _approved approve all nft
     */
    event NftApprovalForAllAll(
        address indexed _owner, 
        address indexed _operator,
        bool _approved
    );

    /**
     * @notice Approve nft
     * @dev Allows operator address to withdraw from your wallet one nft.
     * @dev Emits an {nftApproval} event.
     * @param _asset Address of the nft
     * @param _operator Address of the operator
     * @param _tokenId The unique identifier of the NFT
     */
    function nftApprove(address _asset, address _operator, uint256 _tokenId) external;

   

    /**
     * @notice Approve all nft of one asset
     * @dev Allows operator address to withdraw from your wallet all nft.
     * @dev Emits an {nftApprovalForOneAll} event.
    * @param _asset Address of the nft
     * @param _operator Address of the operator
     * @param _approved Approved all nfts of one asset
     */
    function nftSetApprovalForOneAll(address _asset, address _operator, bool _approved) external;


     /**
     * @notice Approve all nft
     * @dev Allows operator address to withdraw from your wallet all nft.
     * @dev Emits an {nftApprovalForAllAll} event.
     * @param _operator Address of the operator
     * @param _approved Approved all nfts
     */
    function nftSetApprovalForAllAll(address _operator, bool _approved) external;

    /**
     * @notice read operator approved
     * @param _asset Address of the nft
     * @param _operator Address of the operator
     * @param _tokenId The unique identifier of the NFT
     * @return _approved Whether to approved operator one nft
     */
    function nftGetApproved(address _asset, address _operator, uint256 _tokenId) 
        external
        view
        returns (bool _approved);

    /**
     * @notice read operator approved
     * @param _asset Address of the nft
     * @param _operator Address of the operator
     * @return _approved Whether to approved operator all nfts of this one asset
     */
    function nftIsApprovedForOneAll(address _asset, address _operator) 
        external
        view
        returns (bool _approved);

    /**
     * @notice read operator approved
     * @param _operator Address of the operator
     * @return _approved Whether to approved operator all nfts
     */
    function nftIsApprovedForAllAll(address _operator) 
        external
        view
        returns (bool _approved);

    /**
     * @notice Transfer nft
     * @dev must call nft asset transfer() inside the function
     * @dev If the caller is not wallet self, must verify the approve and update
     * @param _asset Address of the nft
     * @param _to Address of the receive
     * @param _tokenId The transaction amount
     * @return _success The bool value returns whether the transfer is successful
     */
    function nftTransfer(address _asset, address _to, uint256 _tokenId) 
        external 
        returns (bool _success); 


}
```

## 理由

本提案中的关键技术决策是：

**改进的批准机制**
- **当前与提议**：在现有的 ERC-721 系统中，外部拥有账户 (EOA) 直接与 NFT 合约交互以进行 `approve`。提议中的新 `nftApprove`、`nftSetApprovalForOneAll`、`nftSetApprovalForAllAll`、`nftGetApproved`、`nftIsApprovedForOneAll`、`nftIsApprovedForAllAll` 功能使得在钱包合约内对 NFT 使用进行更精确的控制，相较于传统方法有显著改进。
- **增强的安全性**：该机制通过将批准控制转移到用户的智能合约钱包，减轻了 NFT 过度批准的风险。
- **可编程性**：用户获得设置高级批准策略的能力，例如条件或时间限制的批准，`nftSetApprovalForAllAll` 功能特别允许对所有 NFT 进行通用设置。这在传统的 ERC-721 NFT 中是无法实现的。

**优化的转移过程**
- **效率和安全性**：`nftTransfer` 功能简化了 NFT 转移过程，使交易更加高效和安全。
- **灵活性**：允许在转移之前和之后集成自定义逻辑（钩子），以便进行额外的安全检查或根据用户需求执行特定操作。

**支持批量操作**
- **提高效率**：用户可以同时处理多个 `approve` 或 `transfer` 操作，显著提高交易效率。
- **增强用户体验**：简化了对众多资产的管理，改善了拥有大额投资组合用户的整体体验。

## 向后兼容性

该 ERC 可以作为 [ERC-4337](./eip-4337.md) 的扩展，并与 ERC-4337 向后兼容。

## 安全考虑

未发现安全考虑。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。