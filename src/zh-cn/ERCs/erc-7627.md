---
eip: 7627
title: 安全消息传递协议
description: 用户之间发送消息的端到端加密。
author: 陈辽源 (@chenly) <cly@kip.pro>
discussions-to: https://ethereum-magicians.org/t/erc-7627-secure-messaging-protocol/18761
status: 审核中
type: 标准跟踪
category: ERC
created: 2024-02-19
---

## 摘要

本提案实现了在链上安全交换加密消息的能力。用户可以通过注册其公钥和加密算法，随后使用其地址向其他用户发送加密消息。该接口还包括公钥算法的枚举和用户信息的结构，以支持各种加密算法和用户信息管理。

## 动机

随着具有亚秒区块时间的第二层链的出现以及账户抽象的引入，端到端加密通信的使用促进了实时通信和在线聊天去中心化应用的普及。提供统一的接口使加密通信能够轻松集成到智能合约中，从而促进创新。标准化促进了互操作性，便于跨平台的无缝通信。

## 规范

### 目标

- 提供一个标准化的接口，用于在智能合约中实现消息系统，包括用户注册和消息发送功能。
- 通过定义公钥算法的枚举和用户信息的结构，增强消息系统的灵活性和可扩展性。
- 定义事件以跟踪消息发送，增强合约的可观察性和可审计性。
- 使用自定义的 sessionId 允许将消息组织成对话。
- 在消息传输过程中使用接收者的公钥加密消息内容。

### 接口

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

该标准的实现者 **MUST** 具备以下所有功能：

``` solidity
pragma solidity ^0.8.0;

interface IERC7627 {

    enum PublicKeyAlgorithm { ECDSA, ED25519, X25519 }

    struct PublicKey {
        bytes public_key; 
        uint64 valid_before;
        PublicKeyAlgorithm algorithm; 
    }
    
    // Events
	/**
     * @notice Event emitted when a message is sent between users.
     * @param from The address of the sender
     * @param to The address of the recipient
     * @param keyIndex The index of the public key used to encrypt the message
     * @param sessionId The session ID for the communication
     * @param encryptedMessage The encrypted message in bytes
	*/
    event MessageSent(address indexed from, address indexed to, bytes32 indexed keyIndex, bytes32 sessionId, bytes encryptedMessage);

	/**
     * @notice Event emitted when a user's public key is updated.
     * @param user The address of the user whose public key is updated
     * @param keyIndex The index of the public key being updated
     * @param newPublicKey The new public key data
	*/
    event PublicKeyUpdated(address indexed user, bytes32 indexed keyIndex, PublicKey newPublicKey);

    // Functions

	/**
     * @notice Updates the public key for the sender.
     * @param _keyIndex The index of the key to be updated
     * @param _publicKey The new public key data
	*/
    function updatePublicKey(bytes32 _keyIndex, PublicKey memory _publicKey) external;

	/**
     * @notice Sends an encrypted message to a specified address.
     * @param _to The recipient's address
     * @param _keyIndex The index of the public key used to encrypt the message
     * @param _sessionId The session ID for the communication
     * @param _encryptedMessage The encrypted message in bytes
	*/
    function sendMessage(address _to, bytes32 _keyIndex, bytes32 _sessionId, bytes calldata _encryptedMessage) external;

	/**
     * @notice Retrieves a public key for a specific user and key index.
     * @param _user The address of the user
     * @param _keyIndex The index of the key to retrieve
     * @return The public key data associated with the user and key index
	*/
    function getUserPublicKey(address _user, bytes32 _keyIndex) external view returns (PublicKey memory);
}
```

## 理由

### 事件发射以便于链外集成
通过在消息发送或公钥更新时发射事件，实施方案促进了与链外去中心化应用的无缝集成。这使得这些去中心化应用能够轻松跟踪和显示最新的消息和更新，确保实时响应并增强用户互动。

### 端到端加密安全性
设计确保只有地址的所有者可以更新其公钥。这一限制保持了端到端加密的完整性，确保只有预期的接收者可以解密和阅读消息，从而保障通信安全。

### 会话 ID 用于对话组织
在消息交易中使用会话 ID 允许将多个消息归类到特定的对话中。此功能对于组织和管理去中心化应用中的讨论至关重要，为用户提供一致且结构化的消息体验。

## 参考实现

```solidity
pragma solidity ^0.8.0;

contract ERC7627 {

    /// @dev Enum to specify the algorithm used for the public key.
    enum PublicKeyAlgorithm { ECDSA, ED25519, X25519 }

    /// @dev Structure to represent a user's public key.
    struct PublicKey {
        bytes public_key; 
        uint64 valid_before;
        PublicKeyAlgorithm algorithm; 
    }

    /// @dev Mapping to store public keys for each address. The mapping is by user address and a unique key index.
    mapping(address => mapping(bytes32 => PublicKey)) public pk;

    event MessageSent(address indexed from, address indexed to, bytes32 indexed keyIndex, bytes32 sessionId, bytes encryptedMessage);

    event PublicKeyUpdated(address indexed user, bytes32 indexed keyIndex, PublicKey newPublicKey);

    function updatePublicKey(bytes32 _keyIndex, PublicKey memory _publicKey) external {
        pk[msg.sender][_keyIndex] = _publicKey;
        emit PublicKeyUpdated(msg.sender, _keyIndex, _publicKey);
    }

    function sendMessage(address _to, bytes32 _keyIndex, bytes32 _sessionId, bytes calldata _encryptedMessage) external {
        emit MessageSent(msg.sender, _to, _keyIndex, _sessionId, _encryptedMessage);
    }

    function getUserPublicKey(address _user, bytes32 _keyIndex) external view returns (PublicKey memory) {
        return pk[_user][_keyIndex];
    }
}
```

## 安全考虑

#### 利用最新的安全加密算法
在选择加密算法时，保持对最新安全新闻和建议的了解至关重要。避免使用已知漏洞的非对称加密算法或不推荐使用的算法，以确保消息的机密性和完整性。定期更新加密算法以应对不断演变的安全威胁。

#### 使用公钥严格加密消息内容
为了维护消息的机密性，发送消息的内容必须严格使用接收者的公钥加密。任何传输的明文信息都可能导致信息泄露和安全风险。在传输和存储过程中始终加密消息内容，以防止对敏感信息的未经授权访问。

#### 密钥管理和保护
对用户公钥和私钥实施强有力的密钥管理和保护措施。确保密钥的安全存储和传输，以防止泄露和篡改。采用多因素认证和密钥轮换策略以增强密钥安全性，并定期评估密钥管理流程以降低潜在的安全风险。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃