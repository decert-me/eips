<!-- ---
eip: 1822
title: 通用可升级代理标准 (UUPS)
author: Gabriel Barros <gabriel@terminal.co>, Patrick Gallagher <blockchainbuddha@gmail.com>
discussions-to: https://ethereum-magicians.org/t/eip-1822-universal-upgradeable-proxy-standard-uups
status: Stagnant
type: 标准跟踪
category: ERC
created: 2019-03-04
--- -->

## 简要概述

标准可升级代理合约。

## 摘要

以下描述了一种代理合约标准，该标准与所有合约通用兼容，并且不会在代理和业务逻辑合约之间创建不兼容性。这是通过利用代理合约中的唯一存储位置来存储逻辑合约的地址来实现的。兼容性检查确保升级成功。升级可以无限次执行，或者根据自定义逻辑确定。此外，提供了一种从多个构造函数中进行选择的方法，这不会妨碍验证字节码的能力。

## 动机

- 改进现有代理实现，以提高部署和维护代理和逻辑合约的开发人员体验。

- 标准化和改进用于验证代理合约使用的字节码的方法。

## 术语

- `delegatecall()` - 合约 **A** 中的函数，允许外部合约 **B**（委托方）修改 **A** 的存储（请参阅下面的图表，[Solidity 文档](https://solidity.readthedocs.io/en/v0.5.3/introduction-to-smart-contracts.html#delegatecall-callcode-and-libraries) ）
- **代理合约** - 存储数据的合约 **A**，但通过 `delegatecall()` 使用外部合约 **B** 的逻辑。
- **逻辑合约** - 包含代理合约 **A** 使用的逻辑的合约 **B**
- **可代理合约** - 继承于逻辑合约 **B**，以提供升级功能

![](../assets/eip-1822/proxy-diagram.png)

## 规范

此处提出的代理合约应按原样部署，并用作替换任何现有合约生命周期管理方法的可插拔替代品。除了代理合约，我们提议 Proxiable 合约接口/基础，该接口/基础为升级建立了一种模式，不会干扰现有的业务规则。允许升级的逻辑可以根据需要实现。

### 代理合约

#### 函数

##### `fallback`

建议的回退函数遵循其他代理合约实现（如 [Zeppelin][1] 或 [Gnosis][2]）中常见的模式。

但是，与其强制使用变量不同，逻辑合约的地址存储在定义的存储位置 `keccak256("PROXIABLE")`。这消除了代理和逻辑合约之间变量冲突的可能性，从而与任何逻辑合约提供了“通用”兼容性。

```javascript
function() external payable {
    assembly { // solium-disable-line
        let contractLogic := sload(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7)
        calldatacopy(0x0, 0x0, calldatasize)
        let success := delegatecall(sub(gas, 10000), contractLogic, 0x0, calldatasize, 0, 0)
        let retSz := returndatasize
        returndatacopy(0, 0, retSz)
        switch success
        case 0 {
            revert(0, retSz)
        }
        default {
            return(0, retSz)
        }
    }
}
```

#### `constructor`

建议的构造函数接受任意数量和任何类型的参数，因此与任何逻辑合约构造函数兼容。

此外，代理合约构造函数的任意性提供了从逻辑合约源代码中选择一个或多个构造函数的能力（例如，`constructor1`，`constructor2`，等等）。请注意，如果逻辑合约中包含多个构造函数，则应包括检查以防止在初始化后再次调用构造函数。

值得注意的是，支持多个构造函数的附加功能不会妨碍验证代理合约的字节码，因为初始化交易调用数据（输入）可以通过首先使用代理合约 ABI，然后使用逻辑合约 ABI 进行解码。

下面的合约显示了代理合约的建议实现。

```javascript
contract Proxy {
    // Code position in storage is keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"
    constructor(bytes memory constructData, address contractLogic) public {
        // save the code address
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, contractLogic)
        }
        (bool success, bytes memory _ ) = contractLogic.delegatecall(constructData); // solium-disable-line
        require(success, "Construction failed");
    }

    function() external payable {
        assembly { // solium-disable-line
            let contractLogic := sload(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7)
            calldatacopy(0x0, 0x0, calldatasize)
            let success := delegatecall(sub(gas, 10000), contractLogic, 0x0, calldatasize, 0, 0)
            let retSz := returndatasize
            returndatacopy(0, 0, retSz)
            switch success
            case 0 {
                revert(0, retSz)
            }
            default {
                return(0, retSz)
            }
        }
    }
}
```

### Proxiable 合约

Proxiable 合约包含在逻辑合约中，并提供执行升级所需的函数。兼容性检查 `proxiable` 防止在升级期间进行不可修复的更新。

> :warning: 警告：逻辑合约中必须包含 `updateCodeAddress` 和 `proxiable`。未包含这些内容可能会阻止升级，并可能导致代理合约变得完全无法使用。请参见下面的 [限制危险函数](#restricting-dangerous-functions)

#### 函数

##### `proxiable`

兼容性检查，以确保新的逻辑合约实现了通用可升级代理标准。请注意，为了支持未来的实现，`bytes32` 比较可以更改，例如，`keccak256("PROXIABLE-ERC1822-v1")`。

##### `updateCodeAddress`

在代理合约的存储 `keccak256("PROXIABLE")` 中存储逻辑合约的地址。

下面的合约显示了 Proxiable 合约的建议实现。

```javascript
contract Proxiable {
    // Code position in storage is keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"

    function updateCodeAddress(address newAddress) internal {
        require(
            bytes32(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7) == Proxiable(newAddress).proxiableUUID(),
            "Not compatible"
        );
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, newAddress)
        }
    }
    function proxiableUUID() public pure returns (bytes32) {
        return 0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7;
    }
}
```

## 使用代理时的陷阱

在使用代理合约时，应遵循以下常见最佳实践，适用于所有逻辑合约。

### 将变量与逻辑分离

在设计新的逻辑合约时，应仔细考虑，以防止在升级后与代理合约的现有存储不兼容。具体来说，应该不修改新合约中实例化变量的顺序，并且任何新变量都应该添加到先前逻辑合约的所有现有变量之后。

为了促进这种实践，我们建议使用一个包含所有变量的单个“基础”合约，并在后续逻辑合约中继承该合约。这种做法极大地减少了意外重新排序变量或在存储中覆盖它们的机会。

### 限制危险函数

Proxiable 合约中的兼容性检查是一种安全机制，可防止升级到未实现通用可升级代理标准的逻辑合约。然而，就像发生在 parity 钱包黑客事件中一样，仍然有可能对逻辑合约造成无法弥补的损害。

为了防止对逻辑合约造成损害，我们建议将任何可能具有破坏性的函数的权限限制为 `onlyOwner`，并在部署后立即将逻辑合约的所有权转移给一个空地址（例如，address(1)）。可能具有破坏性的函数包括诸如 `SELFDESTRUCT` 这样的本机函数，以及其代码可能来自外部的函数，如 `CALLCODE` 和 `delegatecall()`。在下面的 [ERC-20 代币](#erc-20-token) 示例中，使用 `LibraryLock` 合约来防止逻辑合约的销毁。

## 示例

### 拥有者

在此示例中，我们展示了标准拥有者示例，并将 `updateCodeAddress` 限制为仅由所有者调用。

```javascript
contract Owned is Proxiable {
    // ensures no one can manipulate this contract once it is deployed
    address public owner = address(1);

    function constructor1() public{
        // ensures this can be called only once per *proxy* contract deployed
        require(owner == address(0));
        owner = msg.sender;
    }

    function updateCode(address newCode) onlyOwner public {
        updateCodeAddress(newCode);
    }

    modifier onlyOwner() {
        require(msg.sender == owner, "Only owner is allowed to perform this action");
        _;
    }
}
```

### ERC-20 代币

#### 代理合约

```javascript
pragma solidity ^0.5.1;

contract Proxy {
    // Code position in storage is keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"
    constructor(bytes memory constructData, address contractLogic) public {
        // save the code address
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, contractLogic)
        }
        (bool success, bytes memory _ ) = contractLogic.delegatecall(constructData); // solium-disable-line
        require(success, "Construction failed");
    }

    function() external payable {
        assembly { // solium-disable-line
            let contractLogic := sload(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7)
            calldatacopy(0x0, 0x0, calldatasize)
            let success := delegatecall(sub(gas, 10000), contractLogic, 0x0, calldatasize, 0, 0)
            let retSz := returndatasize
            returndatacopy(0, 0, retSz)
            switch success
            case 0 {
                revert(0, retSz)
            }
            default {
                return(0, retSz)
            }
        }
    }
}
```

#### 代币逻辑合约

``` javascript

contract Proxiable {
    // Code position in storage is keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"

    function updateCodeAddress(address newAddress) internal {
        require(
            bytes32(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7) == Proxiable(newAddress).proxiableUUID(),
            "Not compatible"
        );
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, newAddress)
        }
    }
    function proxiableUUID() public pure returns (bytes32) {
        return 0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7;
    }
}


contract Owned {

    address owner;

    function setOwner(address _owner) internal {
        owner = _owner;
    }
    modifier onlyOwner() {
        require(msg.sender == owner, "Only owner is allowed to perform this action");
        _;
    }
}

contract LibraryLockDataLayout {
  bool public initialized = false;
}

contract LibraryLock is LibraryLockDataLayout {
    // Ensures no one can manipulate the Logic Contract once it is deployed.
    // PARITY WALLET HACK PREVENTION

    modifier delegatedOnly() {
        require(initialized == true, "The library is locked. No direct 'call' is allowed");
        _;
    }
    function initialize() internal {
        initialized = true;
    }
}

contract ERC20DataLayout is LibraryLockDataLayout {
  uint256 public totalSupply;
  mapping(address=>uint256) public tokens;
}

contract ERC20 {
    //  ...
    function transfer(address to, uint256 amount) public {
        require(tokens[msg.sender] >= amount, "Not enough funds for transfer");
        tokens[to] += amount;
        tokens[msg.sender] -= amount;
    }
}

contract MyToken is ERC20DataLayout, ERC20, Owned, Proxiable, LibraryLock {

    function constructor1(uint256 _initialSupply) public {
        totalSupply = _initialSupply;
        tokens[msg.sender] = _initialSupply;
        initialize();
        setOwner(msg.sender);
    }
    function updateCode(address newCode) public onlyOwner delegatedOnly  {
        updateCodeAddress(newCode);
    }
    function transfer(address to, uint256 amount) public delegatedOnly {
        ERC20.transfer(to, amount);
    }
}
```

## 参考

- ["scape-hatch" 代理 Medium 文章](https://medium.com/terminaldotco/escape-hatch-proxy-efb681de108d)

## 版权

通过 [CC0](../LICENSE.md) 放弃版权和相关权利。

[1]: https://github.com/maraoz/solidity-proxy/blob/master/contracts/Dispatcher.sol
[2]: https://blog.gnosis.pm/solidity-delegateproxy-contracts-e09957d0f201