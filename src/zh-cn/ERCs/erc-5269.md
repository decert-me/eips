---
eip: 5269
title: ERC 检测与发现
description: 一个接口，用于识别给定调用者是否支持 ERC 中指定的主要行为或可选行为。
author: Zainan Victor Zhou (@xinbenlv)
discussions-to: https://ethereum-magicians.org/t/erc5269-human-readable-interface-detection/9957
status: 审核中
type: 标准跟踪
category: ERC
created: 2022-07-15
requires: 5750
---

## 摘要

一个用于更好地通过数字识别和检测 ERC 的接口。
它指定了一个字段，称为 `majorERCIdentifier`，通常被称为“ERC 编号”。例如，`ERC-721` 也就是 [ERC-721](./eip-721.md) 的 `majorERCIdentifier = 721`。该 ERC 的 `majorERCIdentifier = 5269`。

将其称为 `majorERCIdentifier` 而不是 `ERCNumber` 使其具有未来适应性：预见到未来可能会出现未编号的 ERC，或者我们希望纳入其他类型的标准。

它还提出了一个新的概念 `minorERCIdentifier`，由各个 ERC 的作者定义。例如，ERC-721 的作者可以将 `ERC721Metadata` 接口定义为 `minorERCIdentifier= keccak256("ERC721Metadata")`。

它还提议一个事件，允许智能合约可选地声明它们支持的 ERC。

## 动机

该 ERC 是作为 [ERC-165](./eip-165.md) 的竞争标准创建的。

以下是该 ERC 与 [ERC-165](./eip-165.md) 之间的主要区别。

1. [ERC-165](./eip-165.md) 使用方法签名的哈希来声明一个或多个方法的存在，因此它至少需要一个方法 *存在*。在某些情况下，一些 ERC 接口没有方法，例如与数据格式和签名方案相关的一些 ERC，或“灵魂绑定”即 SBT，它可以在不需要任何特定方法的情况下直接拒绝转账调用。
1. [ERC-165](./eip-165.md) 不提供基于调用者的查询能力。
该 ERC 的合规合约将根据给定调用者响应是否支持某个 ERC。

以下是该 ERC 的动机，因为 ERC-165 已经存在：

1. 使用 ERC 编号提高了人类可读性，并使与命名合约（如 ENS）一起工作变得更容易。

2. 我们看到越来越多的兴趣使用 ERC 编号作为识别或指定 ERC 的方式，而不是使用 ERC-165 标识符。例如

- [ERC-5267](./eip-5267.md) 指定 `extensions` 为 ERC 编号的列表。
- [ERC-600](./eip-600.md) 和 [ERC-601](./eip-601.md) 在 `m / purpose' / subpurpose' / ERC' / wallet'` 路径中指定一个 `ERC` 编号。
- [ERC-5568](./eip-5568.md) 指定 `由 ERC 定义的指令的 instruction_id 必须是其 ERC 编号，除非有特殊情况（要合理）`
- [ERC-6120](./eip-6120.md) 指定 `struct Token { uint eip; ..., }` 其中 `uint eip` 是用于识别 ERC 的 ERC 编号。
- `ERC-867`（停滞）提议创建 `erpId: 该 ERP 的字符串标识符（可能是相关的 ERC 编号，例如“ERC-1234”）。`

3. 拥有一个 ERC/ERC 编号检测接口减少了在智能合约中查找表的需求，以将函数方法或任何 ERC 中的整个接口在 bytes4 ERC-165 标识符中转换为其各自的 ERC 编号，并大大简化了指定 ERC 以扩展行为的方式。

4. 我们还认识到，智能合约可能会根据不同的调用者账户具有不同的行为。一个最显著的用例是，当使用透明可升级模式时，代理合约在管理员账户和非管理员账户调用时给予不同的处理。

## 规范

在以下描述中，我们将 ERC 和 ERC 互换使用。这是因为虽然大多数情况下描述适用于 ERC 标准跟踪类别，但 ERC 编号空间是 ERC 编号空间的一个子空间，我们有时可能会遇到未被识别为 ERC 的 ERC，但其行为值得查询。

1. 任何合规的智能合约必须实现以下接口

```solidity
// DRAFTv1
pragma solidity ^0.8.9;

interface IERC5269 {
  event OnSupportERC(
      address indexed caller, // when emitted with `address(0x0)` means all callers.
      uint256 indexed majorERCIdentifier,
      bytes32 indexed minorERCIdentifier, // 0 means the entire ERC
      bytes32 ercStatus,
      bytes extraData
  );

  /// @dev The core method of ERC Interface Detection
  /// @param caller, a `address` value of the address of a caller being queried whether the given ERC is supported.
  /// @param majorERCIdentifier, a `uint256` value and SHOULD BE the ERC number being queried. Unless superseded by future ERC, such ERC number SHOULD BE less or equal to (0, 2^32-1]. For a function call to `supportERC`, any value outside of this range is deemed unspecified and open to implementation's choice or for future ERCs to specify.
  /// @param minorERCIdentifier, a `bytes32` value reserved for authors of individual ERC to specify. For example the author of [ERC-721](/ERCS/eip-721) MAY specify `keccak256("ERC721Metadata")` or `keccak256("ERC721Metadata.tokenURI")` as `minorERCIdentifier` to be quired for support. Author could also use this minorERCIdentifier to specify different versions, such as ERC-712 has its V1-V4 with different behavior.
  /// @param extraData, a `bytes` for [ERC-5750](/ERCS/eip-5750) for future extensions.
  /// @return ercStatus, a `bytes32` indicating the status of ERC the contract supports.
  ///                    - For FINAL ERCs, it MUST return `keccak256("FINAL")`.
  ///                    - For non-FINAL ERCs, it SHOULD return `keccak256("DRAFT")`.
  ///                      During ERC procedure, ERC authors are allowed to specify their own
  ///                      ercStatus other than `FINAL` or `DRAFT` at their discretion such as `keccak256("DRAFTv1")`
  ///                      or `keccak256("DRAFT-option1")`and such value of ercStatus MUST be documented in the ERC body
  function supportERC(
    address caller,
    uint256 majorERCIdentifier,
    bytes32 minorERCIdentifier,
    bytes calldata extraData)
  external view returns (bytes32 ercStatus);
}
```

在以下描述中，`ERC_5269_STATUS` 被设置为 `keccak256("DRAFTv1")`。

除了 `IERC5269` 注释中指定的行为外：

1. 任何 `minorERCIdentifier=0` 被保留以指代被查询的 ERC 的主要行为。
2. 合规 ERC 的作者建议声明一个 `minorERCIdentifier` 列表，以便为其可选接口、行为和未来扩展的值范围提供支持。
3. 当该 ERC 为最终状态时，任何合规合约必须返回 `ERC_5269_STATUS` 以响应 `supportERC((any caller), 5269, 0, [])` 的调用。

*注意*: 在当前快照中，`supportERC((any caller), 5269, 0, [])` 必须返回 `ERC_5269_STATUS`。

4. 任何合规合约应在构造或升级时发出 `OnSupportERC(address(0), 5269, 0, ERC_5269_STATUS, [])` 事件。
5. 任何合规合约可以通过发出 `OnSupportERC` 事件，声明任何 ERC 主要行为或子行为，以便于发现，并在合规合约更改是否支持某个 ERC 或某种行为时，针对特定调用者或所有调用者发出相关值。
6. 对于任何不处于最终状态的 `ERC-XXX`，在查询 `supportERC((any caller), xxx, (any minor identifier), [])` 时，必须不返回 `keccak256("FINAL")`。在这种情况下，建议返回 `0`，但其他 `ercStatus` 值也是允许的。调用者必须将任何返回值视为非最终状态，且必须将 `0` 严格视为“不支持”。
7. 函数 `supportERC` 必须是可变性 `view`，即它必须不改变 EVM 的任何全局状态。

## 理由

1. 当数据类型为 `uint256 majorERCIdentifier` 时，还有其他替代选项，例如：

- (1) 使用 ERC 编号的哈希版本，
- (2) 使用原始数字，或
- (3) 使用 ERC-165 标识符。

(1) 的优点在于它自动支持未来 ERC 编号/命名约定的任何演变。
但缺点是它不具备向后可读性：看到 `hash(ERC-number)` 时，通常无法轻易猜测其 ERC 编号。

我们选择了 (2)，在动机中阐述了理由。

2. 我们在设计决策中有一个 `bytes32 minorERCIdentifier`。另外，它可以是 (1) 一个数字，强制所有 ERC 作者为其子行为定义编号，因此我们选择了 `bytes32`，并要求 ERC 作者使用字符串名称的哈希作为其子行为，这正是他们在规范中通过接口名称或方法名称所做的。

3. 另外，可能我们添加额外数据作为返回值或所有支持的 ERC 的数组，但我们不确定这种复杂性带来的价值以及额外的开销是否合理。

4. 与 [ERC-165](./eip-165.md) 相比，我们还增加了一个输入 `address caller`，考虑到代理模式（如 [ERC-1967](./eip-1967.md) 所启用）的日益普及。有人可能会问：为什么不简单地使用 `msg.sender`？这是因为我们希望允许在没有交易或代理合约的情况下查询，了解接口 ERC-`number` 是否对特定发送者可用。

1. 我们保留输入 `majorERCIdentifier` 大于或等于 `2^32` 的情况，以防我们需要支持其他不属于 ERC/ERC 的标准集合。
## 测试用例

```typescript

describe("ERC5269", function () {
  async function deployFixture() {
    // ...
  }

  describe("Deployment", function () {
    // ...
    it("Should emit proper OnSupportERC events", async function () {
      let { txDeployErc721 } = await loadFixture(deployFixture);
      let events = txDeployErc721.events?.filter(event => event.event === 'OnSupportERC');
      expect(events).to.have.lengthOf(4);

      let ev5269 = events!.filter(
        (event) => event.args!.majorERCIdentifier.eq(5269));
      expect(ev5269).to.have.lengthOf(1);
      expect(ev5269[0].args!.caller).to.equal(BigNumber.from(0));
      expect(ev5269[0].args!.minorERCIdentifier).to.equal(BigNumber.from(0));
      expect(ev5269[0].args!.ercStatus).to.equal(ethers.utils.id("DRAFTv1"));

      let ev721 = events!.filter(
        (event) => event.args!.majorERCIdentifier.eq(721));
      expect(ev721).to.have.lengthOf(3);
      expect(ev721[0].args!.caller).to.equal(BigNumber.from(0));
      expect(ev721[0].args!.minorERCIdentifier).to.equal(BigNumber.from(0));
      expect(ev721[0].args!.ercStatus).to.equal(ethers.utils.id("FINAL"));

      expect(ev721[1].args!.caller).to.equal(BigNumber.from(0));
      expect(ev721[1].args!.minorERCIdentifier).to.equal(ethers.utils.id("ERC721Metadata"));
      expect(ev721[1].args!.ercStatus).to.equal(ethers.utils.id("FINAL"));

      // ...
    });

    it("Should return proper ercStatus value when called supportERC() for declared supported ERC/features", async function () {
      let { erc721ForTesting, owner } = await loadFixture(deployFixture);
      expect(await erc721ForTesting.supportERC(owner.address, 5269, ethers.utils.hexZeroPad("0x00", 32), [])).to.equal(ethers.utils.id("DRAFTv1"));
      expect(await erc721ForTesting.supportERC(owner.address, 721, ethers.utils.hexZeroPad("0x00", 32), [])).to.equal(ethers.utils.id("FINAL"));
      expect(await erc721ForTesting.supportERC(owner.address, 721, ethers.utils.id("ERC721Metadata"), [])).to.equal(ethers.utils.id("FINAL"));
      // ...

      expect(await erc721ForTesting.supportERC(owner.address, 721, ethers.utils.id("WRONG FEATURE"), [])).to.equal(BigNumber.from(0));
      expect(await erc721ForTesting.supportERC(owner.address, 9999, ethers.utils.hexZeroPad("0x00", 32), [])).to.equal(BigNumber.from(0));
    });

    it("Should return zero as ercStatus value when called supportERC() for non declared ERC/features", async function () {
      let { erc721ForTesting, owner } = await loadFixture(deployFixture);
      expect(await erc721ForTesting.supportERC(owner.address, 721, ethers.utils.id("WRONG FEATURE"), [])).to.equal(BigNumber.from(0));
      expect(await erc721ForTesting.supportERC(owner.address, 9999, ethers.utils.hexZeroPad("0x00", 32), [])).to.equal(BigNumber.from(0));
    });
  });
});
```

请参见 [`TestERC5269.ts`](../assets/eip-5269/test/TestERC5269.ts)。

## 参考实现

这是该 ERC 的参考实现：

```solidity
contract ERC5269 is IERC5269 {
    bytes32 constant public ERC_STATUS = keccak256("DRAFTv1");
    constructor () {
        emit OnSupportERC(address(0x0), 5269, bytes32(0), ERC_STATUS, "");
    }

    function _supportERC(
        address /*caller*/,
        uint256 majorERCIdentifier,
        bytes32 minorERCIdentifier,
        bytes calldata /*extraData*/)
    internal virtual view returns (bytes32 ercStatus) {
        if (majorERCIdentifier == 5269) {
            if (minorERCIdentifier == bytes32(0)) {
                return ERC_STATUS;
            }
        }
        return bytes32(0);
    }

    function supportERC(
        address caller,
        uint256 majorERCIdentifier,
        bytes32 minorERCIdentifier,
        bytes calldata extraData)
    external virtual view returns (bytes32 ercStatus) {
        return _supportERC(caller, majorERCIdentifier, minorERCIdentifier, extraData);
    }
}
```

请参见 [`ERC5269.sol`](../assets/eip-5269/contracts/ERC5269.sol)。

这是一个示例，其中一个 [ERC-721](./eip-721.md) 合约也实现了该 ERC，以便于检测和发现：

```solidity
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "../ERC5269.sol";
contract ERC721ForTesting is ERC721, ERC5269 {

    bytes32 constant public ERC_FINAL = keccak256("FINAL");
    constructor() ERC721("ERC721ForTesting", "E721FT") ERC5269() {
        _mint(msg.sender, 0);
        emit OnSupportERC(address(0x0), 721, bytes32(0), ERC_FINAL, "");
        emit OnSupportERC(address(0x0), 721, keccak256("ERC721Metadata"), ERC_FINAL, "");
        emit OnSupportERC(address(0x0), 721, keccak256("ERC721Enumerable"), ERC_FINAL, "");
    }

  function supportERC(
    address caller,
    uint256 majorERCIdentifier,
    bytes32 minorERCIdentifier,
    bytes calldata extraData)
  external
  override
  view
  returns (bytes32 ercStatus) {
    if (majorERCIdentifier == 721) {
      if (minorERCIdentifier == 0) {
        return keccak256("FINAL");
      } else if (minorERCIdentifier == keccak256("ERC721Metadata")) {
        return keccak256("FINAL");
      } else if (minorERCIdentifier == keccak256("ERC721Enumerable")) {
        return keccak256("FINAL");
      }
    }
    return super._supportERC(caller, majorERCIdentifier, minorERCIdentifier, extraData);
  }
}

```

请参见 [`ERC721ForTesting.sol`](../assets/eip-5269/contracts/testing/ERC721ForTesting.sol)。

## 安全考虑

与 [ERC-165](./eip-165.md) 类似，接口的调用者必须假设声明支持此类 ERC 接口的智能合约不一定正确支持它们。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。