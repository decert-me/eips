---
eip: 6900
title: 模块化智能合约账户和插件
description: 可组合合约账户的接口，支持可选的可升级性和自省
author: Adam Egyed (@adamegyed), Fangting Liu (@trinity-0111), Jay Paik (@jaypaik), Yoav Weiss (@yoavw), Huawei Gu (@huaweigu), Daniel Lim (@dlim-circle), Zhiyu Zhang (@ZhiyuCircle), Ruben Koch (@0xrubes), David Philipson (@dphilipson), Howy Ho (@howydev)
discussions-to: https://ethereum-magicians.org/t/eip-modular-smart-contract-accounts-and-plugins/13885
status: 草案
type: 标准跟踪
category: ERC
created: 2023-04-18
requires: 165, 4337
---

## 摘要

本提案标准化智能合约账户和账户插件，这些插件是允许在智能合约账户内实现可组合逻辑的智能合约接口。本提案符合 [ERC-4337](./eip-4337.md)，并在定义更新和查询模块化功能实现的接口时借鉴了 [ERC-2535](./eip-2535.md)。

这种模块化方法将账户功能分为三类，在外部合约中实现，并定义了账户的预期执行流程。

## 动机

ERC-4337 实现的目标之一是将执行和验证的逻辑抽象到每个智能合约账户中。

通过自定义进入验证和执行步骤的逻辑，可以构建许多新账户功能。这些功能的示例包括会话密钥、订阅、支出限制和基于角色的访问控制。目前，这些功能中的一些由特定智能合约账户原生实现，其他则可以通过插件系统实现。专有插件系统的示例包括 Safe 模块和 ZeroDev 插件。

然而，管理多个账户实例会导致更糟糕的用户体验，使账户在支持的功能和安全配置之间碎片化。此外，这要求插件开发者选择支持哪些平台，导致平台锁定或重复开发工作。

我们提议一个标准，协调插件开发者和钱包开发者之间的实现工作。该标准定义了一个能够支持所有符合标准插件的模块化智能合约账户。这使用户的数据具有更大的可移植性，并且插件开发者不必选择特定的账户实现进行支持。

![展示账户与插件之间关系的图表](../assets/eip-6900/MSCA_Shared_Components_Diagram.svg)

我们借鉴了 ERC-2535 的钻石模式，根据函数选择器路由执行，并创建了一个类似的可组合账户。然而，该标准并不要求多面代理模式。

这些插件可以包含执行逻辑、验证方案和钩子。验证方案定义了智能合约账户在何种情况下会批准代表其采取的行动，而钩子则允许进行执行前和/或执行后的控制。

采用该标准的账户将支持模块化、可升级的执行和验证逻辑。将其定义为智能合约账户的标准将使插件更容易安全开发，并允许更大的互操作性。

目标：

- 提供智能合约账户的验证、执行和钩子函数的编写标准。
- 提供符合标准的账户如何添加、更新、移除和检查插件的标准。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 术语

- **账户**（或 **智能合约账户，SCA**）是一个可以用于发送交易和持有数字资产的智能合约。它实现了 ERC-4337 的 `IAccount` 接口。
- **模块化账户**（或 **模块化智能合约账户，MSCA**）是一个支持模块化功能的账户。模块化功能有三种类型：
  - **验证函数** 验证调用者的真实性和对账户的权限。
  - **执行函数** 执行账户允许的任何自定义逻辑。
  - **钩子** 在执行函数或验证函数之前和/或之后执行自定义逻辑和检查。
- **验证函数** 是一个验证调用者对账户的身份验证和授权的函数。验证函数有两种类型：
  - **用户操作验证** 函数处理对 `validateUserOp` 的调用，并检查 ERC-4337 用户操作的有效性。
  - **运行时验证** 函数在未通过用户操作调用的情况下，在执行函数之前运行，并强制执行检查。常见检查包括仅允许所有者执行。
- **执行函数** 是定义模块化账户的主要执行步骤的智能合约函数。
- **标准执行** 函数是由模块化账户原生实现的两个特定执行函数，而不是在插件上实现的。这些函数允许开放式执行。
- **钩子** 是在另一个函数之前或之后执行的智能合约函数，能够修改状态或导致整个调用回滚。钩子有四种类型：
  - **用户操作验证前钩子** 函数在用户操作验证函数之前运行。这些可以强制验证函数通过用户操作执行的权限。
  - **运行时验证前钩子** 函数在运行时验证函数之前运行。这些可以强制验证函数通过直接调用执行的权限。
  - **执行前钩子** 函数在执行函数之前运行。它们可以选择性地返回数据供其相关的执行后钩子函数使用。
  - **执行后钩子** 函数在执行函数之后运行。它们可以选择性地从其相关的执行前钩子函数获取返回的数据。
- **关联函数** 指的是验证函数或钩子。
- **原生函数** 指的是由模块化账户原生实现的函数，而不是由插件添加的函数。
- **插件** 是一个部署的智能合约，承载上述三种模块化函数中的任意数量：执行函数、验证函数或钩子。
- 插件 **清单** 负责描述在安装期间将在 MSCA 上配置的执行函数、验证函数和钩子，以及插件的元数据、依赖要求和权限。

### 概述

模块化账户处理两种调用：通过 ERC-4337 的 `Entrypoint` 或通过来自外部拥有账户（EOA）和其他智能合约的直接调用。该标准支持这两种用例。

对智能合约账户的调用可以分解为如下图所示的步骤。验证步骤验证调用者是否被允许执行该调用。执行前钩子步骤可用于进行任何执行前检查或更新。它还可以与执行后钩子步骤一起使用，以执行额外的操作或验证。执行步骤执行定义的任务或任务集合。
![图示显示模块化账户内的调用流程](../assets/eip-6900/Modular_Account_Call_Flow.svg)

以下图示显示了允许的插件执行流程。在上述图示中插件的执行步骤中，插件可以执行“插件执行函数”，使用 `executeFromPlugin` 或 `executeFromPluginExternal`。这些可以被插件用来在账户的上下文中执行。

- `executeFromPlugin` 处理对模块化账户上其他已安装插件的执行函数的调用。
- `executeFromPluginExternal` 处理对外部地址的调用。

![图示显示插件执行流程](../assets/eip-6900/Plugin_Execution_Flow.svg)

每个步骤都是模块化的，支持每个执行函数的不同实现，并且是可组合的，支持通过钩子进行多个步骤的组合。结合起来，这些允许开放式可编程账户。

### 接口

**模块化智能合约账户** **必须** 实现

- `IAccount.sol` 来自 [ERC-4337](./eip-4337.md)。
- `IPluginManager.sol` 以支持安装和卸载插件。
- `IStandardExecutor.sol` 以支持开放式执行。**通过此调用插件的操作应当回退。**
- `IPluginExecutor.sol` 以支持来自插件的执行。**通过 `executeFromPluginExternal` 调用插件的操作应当回退。**

**模块化智能合约账户** **可以** 实现

- `IAccountLoupe.sol` 以支持在链上查看插件配置。

**插件** **必须** 实现

- `IPlugin.sol` 如下所述，并实现 [ERC-165](./eip-165.md) 的 `IPlugin`。

#### `IPluginManager.sol`

插件管理器接口。模块化智能合约账户 **必须** 实现此接口以支持安装和卸载插件。

```solidity
// Treats the first 20 bytes as an address, and the last byte as a function identifier.
type FunctionReference is bytes21;

interface IPluginManager {
    event PluginInstalled(address indexed plugin, bytes32 manifestHash, FunctionReference[] dependencies);

    event PluginUninstalled(address indexed plugin, bool indexed onUninstallSucceeded);

    /// @notice Install a plugin to the modular account.
    /// @param plugin The plugin to install.
    /// @param manifestHash The hash of the plugin manifest.
    /// @param pluginInstallData Optional data to be decoded and used by the plugin to setup initial plugin data
    /// for the modular account.
    /// @param dependencies The dependencies of the plugin, as described in the manifest. Each FunctionReference
    /// MUST be composed of an installed plugin's address and a function ID of its validation function.
    function installPlugin(
        address plugin,
        bytes32 manifestHash,
        bytes calldata pluginInstallData,
        FunctionReference[] calldata dependencies
    ) external;

    /// @notice Uninstall a plugin from the modular account.
    /// @param plugin The plugin to uninstall.
    /// @param config An optional, implementation-specific field that accounts may use to ensure consistency
    /// guarantees.
    /// @param pluginUninstallData Optional data to be decoded and used by the plugin to clear plugin data for the
    /// modular account.
    function uninstallPlugin(address plugin, bytes calldata config, bytes calldata pluginUninstallData) external;
}

```

#### `IStandardExecutor.sol`

标准执行接口。模块化智能合约账户 **必须** 实现此接口以支持开放式执行。

标准执行函数应当检查调用的目标是否通过 ERC-165 实现了 `IPlugin` 接口。

**如果目标是插件，调用应当回退。** 这可以防止插件（无论是已安装还是未安装）的意外错误配置或误用。

```solidity
struct Call {
    // The target address for the account to call.
    address target;
    // The value to send with the call.
    uint256 value;
    // The calldata for the call.
    bytes data;
}

interface IStandardExecutor {
    /// @notice Standard execute method.
    /// @dev If the target is a plugin, the call SHOULD revert.
    /// @param target The target address for account to call.
    /// @param value The value to send with the call.
    /// @param data The calldata for the call.
    /// @return The return data from the call.
    function execute(address target, uint256 value, bytes calldata data) external payable returns (bytes memory);

    /// @notice Standard executeBatch method.
    /// @dev If the target is a plugin, the call SHOULD revert. If any of the calls revert, the entire batch MUST
    /// revert.
    /// @param calls The array of calls.
    /// @return An array containing the return data from the calls.
    function executeBatch(Call[] calldata calls) external payable returns (bytes[] memory);
}
```

#### `IPluginExecutor.sol`

来自插件的调用执行接口。模块化智能合约账户 **必须** 实现此接口以支持来自插件的执行。

`executeFromPluginExternal` 函数应当检查调用的目标是否通过 ERC-165 实现了 `IPlugin` 接口。

**如果 `executeFromPluginExternal` 函数的目标是插件，调用应当回退。**

这可以防止插件（无论是已安装还是未安装）的意外错误配置或误用。已安装的插件可以通过 `executeFromPlugin` 函数与其他已安装的插件进行交互。

```solidity
interface IPluginExecutor {
    /// @notice Execute a call from a plugin through the account.
    /// @dev Permissions must be granted to the calling plugin for the call to go through.
    /// @param data The calldata to send to the account.
    /// @return The return data from the call.
    function executeFromPlugin(bytes calldata data) external payable returns (bytes memory);

    /// @notice Execute a call from a plugin to a non-plugin address.
    /// @dev If the target is a plugin, the call SHOULD revert. Permissions must be granted to the calling plugin
    /// for the call to go through.
    /// @param target The address to be called.
    /// @param value The value to send with the call.
    /// @param data The calldata to send to the target.
    /// @return The return data from the call.
    function executeFromPluginExternal(address target, uint256 value, bytes calldata data)
        external
        payable
        returns (bytes memory);
}
```

#### `IAccountLoupe.sol`

插件检查接口。模块化智能合约账户 **可以** 实现此接口以支持在链上查看插件配置。

```solidity
interface IAccountLoupe {
    /// @notice Config for an execution function, given a selector.
    struct ExecutionFunctionConfig {
        address plugin;
        FunctionReference userOpValidationFunction;
        FunctionReference runtimeValidationFunction;
    }

    /// @notice Pre and post hooks for a given selector.
    /// @dev It's possible for one of either `preExecHook` or `postExecHook` to be empty.
    struct ExecutionHooks {
        FunctionReference preExecHook;
        FunctionReference postExecHook;
    }

    /// @notice Get the validation functions and plugin address for a selector.
    /// @dev If the selector is a native function, the plugin address will be the address of the account.
    /// @param selector The selector to get the configuration for.
    /// @return The configuration for this selector.
    function getExecutionFunctionConfig(bytes4 selector) external view returns (ExecutionFunctionConfig memory);

    /// @notice Get the pre and post execution hooks for a selector.
    /// @param selector The selector to get the hooks for.
    /// @return The pre and post execution hooks for this selector.
    function getExecutionHooks(bytes4 selector) external view returns (ExecutionHooks[] memory);

    /// @notice Get the pre user op and runtime validation hooks associated with a selector.
    /// @param selector The selector to get the hooks for.
    /// @return preUserOpValidationHooks The pre user op validation hooks for this selector.
    /// @return preRuntimeValidationHooks The pre runtime validation hooks for this selector.
    function getPreValidationHooks(bytes4 selector)
        external
        view
        returns (
            FunctionReference[] memory preUserOpValidationHooks,
            FunctionReference[] memory preRuntimeValidationHooks
        );

    /// @notice Get an array of all installed plugins.
    /// @return The addresses of all installed plugins.
    function getInstalledPlugins() external view returns (address[] memory);
}
```

#### `IPlugin.sol`

插件接口。插件 **必须** 实现此接口以支持插件管理和与 MSCAs 的交互。

```solidity
interface IPlugin {
    /// @notice Initialize plugin data for the modular account.
    /// @dev Called by the modular account during `installPlugin`.
    /// @param data Optional bytes array to be decoded and used by the plugin to setup initial plugin data for the modular account.
    function onInstall(bytes calldata data) external;

    /// @notice Clear plugin data for the modular account.
    /// @dev Called by the modular account during `uninstallPlugin`.
    /// @param data Optional bytes array to be decoded and used by the plugin to clear plugin data for the modular account.
    function onUninstall(bytes calldata data) external;

    /// @notice Run the pre user operation validation hook specified by the `functionId`.
    /// @dev Pre user operation validation hooks MUST NOT return an authorizer value other than 0 or 1.
    /// @param functionId An identifier that routes the call to different internal implementations, should there be more than one.
    /// @param userOp The user operation.
    /// @param userOpHash The user operation hash.
    /// @return Packed validation data for validAfter (6 bytes), validUntil (6 bytes), and authorizer (20 bytes).
    function preUserOpValidationHook(uint8 functionId, UserOperation memory userOp, bytes32 userOpHash) external returns (uint256);

    /// @notice Run the user operation validationFunction specified by the `functionId`.
    /// @param functionId An identifier that routes the call to different internal implementations, should there be
    /// more than one.
    /// @param userOp The user operation.
    /// @param userOpHash The user operation hash.
    /// @return Packed validation data for validAfter (6 bytes), validUntil (6 bytes), and authorizer (20 bytes).
    function userOpValidationFunction(uint8 functionId, UserOperation calldata userOp, bytes32 userOpHash)
        external
        returns (uint256);

    /// @notice Run the pre runtime validation hook specified by the `functionId`.
    /// @dev To indicate the entire call should revert, the function MUST revert.
    /// @param functionId An identifier that routes the call to different internal implementations, should there be more than one.
    /// @param sender The caller address.
    /// @param value The call value.
    /// @param data The calldata sent.
    function preRuntimeValidationHook(uint8 functionId, address sender, uint256 value, bytes calldata data) external;

    /// @notice Run the runtime validationFunction specified by the `functionId`.
    /// @dev To indicate the entire call should revert, the function MUST revert.
    /// @param functionId An identifier that routes the call to different internal implementations, should there be
    /// more than one.
    /// @param sender The caller address.
    /// @param value The call value.
    /// @param data The calldata sent.
    function runtimeValidationFunction(uint8 functionId, address sender, uint256 value, bytes calldata data)
        external;

    /// @notice Run the pre execution hook specified by the `functionId`.
    /// @dev To indicate the entire call should revert, the function MUST revert.
    /// @param functionId An identifier that routes the call to different internal implementations, should there be more than one.
    /// @param sender The caller address.
    /// @param value The call value.
    /// @param data The calldata sent.
    /// @return Context to pass to a post execution hook, if present. An empty bytes array MAY be returned.
    function preExecutionHook(uint8 functionId, address sender, uint256 value, bytes calldata data) external returns (bytes memory);

    /// @notice Run the post execution hook specified by the `functionId`.
    /// @dev To indicate the entire call should revert, the function MUST revert.
    /// @param functionId An identifier that routes the call to different internal implementations, should there be more than one.
    /// @param preExecHookData The context returned by its associated pre execution hook.
    function postExecutionHook(uint8 functionId, bytes calldata preExecHookData) external;

    /// @notice Describe the contents and intended configuration of the plugin.
    /// @dev This manifest MUST stay constant over time.
    /// @return A manifest describing the contents and intended configuration of the plugin.
    function pluginManifest() external pure returns (PluginManifest memory);

    /// @notice Describe the metadata of the plugin.
    /// @dev This metadata MUST stay constant over time.
    /// @return A metadata struct describing the plugin.
    function pluginMetadata() external pure returns (PluginMetadata memory);
}
```

### 插件清单

插件清单负责描述将在安装期间配置在 MSCA 上的执行函数、验证函数和钩子，以及插件的元数据、依赖关系和权限。

```solidity
enum ManifestAssociatedFunctionType {
    // Function is not defined.
    NONE,
    // Function belongs to this plugin.
    SELF,
    // Function belongs to an external plugin provided as a dependency during plugin installation. Plugins MAY depend
    // on external validation functions. It MUST NOT depend on external hooks, or installation will fail.
    DEPENDENCY,
    // Resolves to a magic value to always bypass runtime validation for a given function.
    // This is only assignable on runtime validation functions. If it were to be used on a user op validationFunction,
    // it would risk burning gas from the account. When used as a hook in any hook location, it is equivalent to not
    // setting a hook and is therefore disallowed.
    RUNTIME_VALIDATION_ALWAYS_ALLOW,
    // Resolves to a magic value to always fail in a hook for a given function.
    // This is only assignable to pre hooks (pre validation and pre execution). It should not be used on
    // validation functions themselves, because this is equivalent to leaving the validation functions unset.
    // It should not be used in post-exec hooks, because if it is known to always revert, that should happen
    // as early as possible to save gas.
    PRE_HOOK_ALWAYS_DENY
}

/// @dev For functions of type `ManifestAssociatedFunctionType.DEPENDENCY`, the MSCA MUST find the plugin address
/// of the function at `dependencies[dependencyIndex]` during the call to `installPlugin(config)`.
struct ManifestFunction {
    ManifestAssociatedFunctionType functionType;
    uint8 functionId;
    uint256 dependencyIndex;
}

struct ManifestAssociatedFunction {
    bytes4 executionSelector;
    ManifestFunction associatedFunction;
}

struct ManifestExecutionHook {
    bytes4 selector;
    ManifestFunction preExecHook;
    ManifestFunction postExecHook;
}

struct ManifestExternalCallPermission {
    address externalAddress;
    bool permitAnySelector;
    bytes4[] selectors;
}

struct SelectorPermission {
    bytes4 functionSelector;
    string permissionDescription;
}

/// @dev A struct holding fields to describe the plugin in a purely view context. Intended for front end clients.
struct PluginMetadata {
    // A human-readable name of the plugin.
    string name;
    // The version of the plugin, following the semantic versioning scheme.
    string version;
    // The author field SHOULD be a username representing the identity of the user or organization
    // that created this plugin.
    string author;
    // String descriptions of the relative sensitivity of specific functions. The selectors MUST be selectors for
    // functions implemented by this plugin.
    SelectorPermission[] permissionDescriptors;
}

/// @dev A struct describing how the plugin should be installed on a modular account.
struct PluginManifest {
    // List of ERC-165 interface IDs to add to account to support introspection checks. This MUST NOT include
    // IPlugin's interface ID.
    bytes4[] interfaceIds;
    // If this plugin depends on other plugins' validation functions, the interface IDs of those plugins MUST be
    // provided here, with its position in the array matching the `dependencyIndex` members of `ManifestFunction`
    // structs used in the manifest.
    bytes4[] dependencyInterfaceIds;
    // Execution functions defined in this plugin to be installed on the MSCA.
    bytes4[] executionFunctions;
    // Plugin execution functions already installed on the MSCA that this plugin will be able to call.
    bytes4[] permittedExecutionSelectors;
    // Boolean to indicate whether the plugin can call any external address.
    bool permitAnyExternalAddress;
    // Boolean to indicate whether the plugin needs access to spend native tokens of the account. If false, the
    // plugin MUST still be able to spend up to the balance that it sends to the account in the same call.
    bool canSpendNativeToken;
    ManifestExternalCallPermission[] permittedExternalCalls;
    ManifestAssociatedFunction[] userOpValidationFunctions;
    ManifestAssociatedFunction[] runtimeValidationFunctions;
    ManifestAssociatedFunction[] preUserOpValidationHooks;
    ManifestAssociatedFunction[] preRuntimeValidationHooks;
    ManifestExecutionHook[] executionHooks;
}

```

### 预期行为

#### `StandardExecutor` 和 `PluginExecutor` 的职责

`StandardExecutor` 函数用于对外部地址的开放式调用。

`PluginExecutor` 函数专门用于插件请求账户在账户上下文中执行。插件使用 `PluginExecutor` 需要明确的权限。

以下行为必须遵循：

- `StandardExecutor` 不能调用插件执行函数和/或 `PluginExecutor`。这通过检查调用的目标是否通过 ERC-165 实现了 `IPlugin` 接口来保证。
- `StandardExecutor` 不能被插件执行函数和/或 `PluginExecutor` 调用。
- 插件执行函数不得请求对 `StandardExecutor` 的访问，它们可以请求对 `PluginExecutor` 的访问。

#### 调用 `installPlugin`

函数 `installPlugin` 接受 4 个参数：要安装的插件地址、插件清单的 Keccak-256 哈希、传递给插件的 `onInstall` 回调的 ABI 编码数据，以及表示插件安装依赖关系的函数引用数组。

该函数必须通过调用 `pluginManifest()` 使用 `staticcall` 来检索插件的清单。

该函数必须执行以下初步检查：

- 如果插件已经在模块化账户上安装，则回退。
- 如果插件未实现 ERC-165 或不支持 `IPlugin` 接口，则回退。
- 如果 `manifestHash` 与插件返回的清单的计算 Keccak-256 哈希不匹配，则回退。这可以防止安装试图安装与客户端批准的不同插件配置的插件。
- 如果 `dependencies` 中的任何地址不支持其在清单的 `dependencyInterfaceIds` 中匹配索引的接口，或者两个数组的长度不匹配，或者任何依赖项尚未在模块化账户上安装，则回退。

该函数必须记录用于插件安装的清单哈希和依赖项。每个依赖项的记录也必须更新，以反映它有一个新的依赖项。这些记录必须用于确保对 `uninstallPlugin` 的调用是全面的，并撤销安装时的所有编辑配置状态。存储和验证这些记录的机制由实现决定。

该函数必须存储插件的允许函数选择器、允许的外部调用，以及它是否可以花费账户的原生代币，以便能够验证对 `executeFromPlugin` 和 `executeFromPluginExternal` 的调用。

该函数必须解析清单中的执行函数、验证函数和钩子，并在解析每个 `ManifestFunction` 类型后将它们添加到模块化账户中。

- 每个执行函数选择器必须作为有效的执行函数添加到模块化账户。如果函数选择器已经添加或与原生函数的选择器匹配，则该函数应当回退。
- 如果要添加的验证函数与已经具有该类型验证函数的选择器相同，则该函数应当回退。

该函数可以存储清单中提供的接口 ID，并相应地更新其 `supportsInterface` 行为。

接下来，该函数必须调用插件的 `onInstall` 回调，并使用 `pluginInstallData` 参数中提供的数据。这用于初始化模块化账户的插件状态。如果 `onInstall` 回退，则 `installPlugin` 函数必须回退。

最后，该函数必须发出事件 `PluginInstalled`，包含插件的地址、其清单的哈希和所使用的依赖项。

> **⚠️ 安装和卸载插件的能力是非常强大的。这些函数的安全性决定了账户的安全性。模块化账户的实现者必须确保 `IPluginManager` 中函数的实现具有适当的安全考虑和访问控制。**
#### 调用 `uninstallPlugin`

函数 `uninstallPlugin` 接受 3 个参数：要卸载的插件地址、一个可能具有自定义要求或由实现账户使用的字节字段，以及传递给插件的 `onUninstall` 回调的 ABI 编码数据。

如果插件未安装在模块化账户上，函数必须回退。

函数应执行以下检查：

- 如果安装时使用的清单的哈希与插件当前清单的计算 Keccak-256 哈希不匹配，则回退。这可以防止不干净地移除试图强制移除与客户端最初批准安装的不同插件配置的插件。为了允许移除此类插件，模块化账户可以实现将清单编码为配置字段中的参数的能力。
- 如果至少有 1 个其他已安装插件依赖于此插件添加的验证函数，则回退。在依赖插件存在时，不应卸载作为依赖的插件。

函数应更新账户存储以反映通过检查函数的卸载，例如由 `IAccountLoupe` 定义的那些。每个依赖项的记录也应更新，以反映它不再将此插件作为依赖。

函数必须删除插件的清单哈希、依赖项、允许的函数选择器、允许的外部调用以及是否可以花费账户的本地代币的记录。

函数必须解析清单中的执行函数、验证函数和钩子，并在解析每个 `ManifestFunction` 类型后将其从模块化账户中移除。如果多个插件添加了相同的钩子，则必须在最后一个插件卸载之前保留该钩子。

如果账户在安装期间存储了清单的 `interfaceIds` 中提供的接口 ID，则必须将其移除并相应地更新其 `supportsInterface` 行为。如果多个插件添加了相同的接口 ID，则必须在最后一个插件卸载之前保留该接口 ID。

接下来，函数必须调用插件的 `onUninstall` 回调，并使用 `pluginUninstallData` 参数中提供的数据。这用于清除模块化账户的插件状态。如果 `onUninstall` 回退，执行应继续以允许卸载完成。

最后，函数必须发出事件 `PluginUninstalled`，包括插件的地址以及 `onUninstall` 回调是否成功。

> **⚠️ 不正确卸载的插件可能会阻止其依赖项的卸载。因此，需要某种形式的验证，以确保卸载步骤完全且正确地移除插件及其对依赖项的使用。**

#### 调用 `validateUserOp`

当 `EntryPoint` 在模块化账户上调用函数 `validateUserOp` 时，必须找到与 `userOp.callData` 前四个字节的函数选择器关联的用户操作验证函数。如果未为选择器定义函数，或者 `userOp.callData.length < 4`，则执行必须回退。

如果函数选择器有关联的预用户操作验证钩子，则这些钩子必须按顺序运行。如果任何钩子回退，外部调用必须回退。如果任何钩子设置为 `PRE_HOOK_ALWAYS_DENY`，则调用必须回退。如果任何钩子返回的 `authorizer` 值不是 0 或 1，则执行必须回退。如果任何钩子返回的 `authorizer` 值为 1，表示签名无效，则外部调用返回的验证数据也必须为 1。如果任何钩子通过指定 `validUntil` 或 `validBefore` 值返回时间限制验证，则结果验证数据必须是所有提供的时间限制的交集。

然后，模块化账户必须使用 `call` 操作码执行验证函数，参数为用户操作及其哈希。用户操作验证函数返回的验证数据必须根据任何预用户操作验证钩子的返回值进行更新（如有必要），然后由 `validateUserOp` 返回。

#### 调用执行函数

当在模块化账户上调用非原生函数时，必须找到通过插件安装添加的相应选择器的插件配置。如果未找到相应的插件，模块化账户必须回退。否则，必须执行以下步骤。

此外，当模块化账户原生实现 `IPluginManager` 和 `IStandardExecutor` 中的函数时，必须对这些函数执行相同的步骤。其他原生函数可以执行这些步骤。

要执行的步骤是：

- 如果调用不是来自 `EntryPoint`，则查找关联的运行时验证函数。如果不存在，则执行必须回退。模块化账户必须执行所有预运行时验证钩子，然后使用 `call` 操作码执行运行时验证函数。所有这些函数必须接收调用者、值和执行函数的 calldata 作为参数。如果任何这些函数回退，执行必须回退。如果任何预运行时验证钩子设置为 `PRE_HOOK_ALWAYS_DENY`，执行必须回退。如果运行时验证函数设置为 `RUNTIME_VALIDATION_ALWAYS_ALLOW`，则必须绕过验证函数。
- 如果为执行函数定义了预执行钩子，则使用调用者、值和执行函数的 calldata 作为参数执行这些钩子。如果任何这些钩子返回数据，则必须在调用后执行钩子之前保留该数据。操作必须使用 `call` 操作码完成。如果存在重复的预执行钩子（即具有相同 `FunctionReference` 的钩子），则仅运行一次钩子。如果任何这些函数回退，执行必须回退。
- 运行执行函数。
- 如果定义了后执行钩子，则运行这些函数。如果预执行钩子返回数据给账户，则该数据必须作为参数传递给关联的后执行钩子。操作必须使用 `call` 操作码完成。如果存在重复的后执行钩子，则对每个唯一的关联预执行钩子运行一次。如果没有关联的预执行钩子的后执行钩子，则仅运行一次。如果任何这些函数回退，执行必须回退。

对于给定执行函数运行的钩子集必须是执行阶段开始时账户状态指定的钩子。这与像 `installPlugin` 和 `uninstallPlugin` 这样的函数相关，这些函数修改账户状态，可能还有其他执行或原生函数。

#### 从插件发出的调用

插件可以通过模块化账户使用 `IPluginExecutor` 接口中定义的函数与其他插件和外部地址进行交互。这些函数可以在没有定义验证函数的情况下调用，但模块化账户必须强制执行这些检查和行为：

`executeFromPlugin` 函数必须允许插件调用由插件在模块化账户上安装的执行函数。必须调用与 `data` 中提供的函数选择器匹配的钩子。如果调用插件的清单在安装时未包含提供的函数选择器，则执行必须回退。

`executeFromPluginExternal` 函数必须允许插件代表模块化账户调用其参数指定的外部地址。如果调用插件的清单在安装时未明确允许外部调用，则执行必须回退。
## 理由

与 ERC-4337 兼容的账户必须实现 `IAccount` 接口，该接口仅包含一个将验证与执行捆绑在一起的方法：`validateUserOp`。该提案的主要设计理由是通过将这些和其他功能解耦，扩展智能合约账户的可能功能，而不仅限于这个单一的方法，同时保留账户抽象的好处。

ERC-2535 的函数路由模式是实现多功能账户扩展的逻辑起点。它还满足我们另一个主要设计理由，即在多个实现合约之间概括执行调用。然而，严格的钻石模式受到其无法在 `validateUserOp` 的上下文中定制特定执行函数的验证方案的限制，以及其对 `delegatecall` 的要求。

该提案包括几个基于 ERC-4337 并受到 ERC-2535 启发的接口。首先，我们标准化了一组模块化函数，使智能合约开发者在捆绑验证、执行和钩子逻辑方面具有更大的灵活性。我们还提议了一些接口，借鉴钻石标准，提供查询模块账户上的执行函数、验证函数和钩子的方法。其余接口描述了插件的方法，用于公开其模块化函数和所需配置，以及模块账户的方法，用于安装和移除插件，并允许跨插件和外部地址执行。

## 向后兼容性

未发现向后兼容性问题。

## 参考实现

请参见 `https://github.com/erc6900/reference-implementation`

## 安全考虑

模块化智能合约账户本身是受信任的组件。已安装的插件在不同程度上是受信任的，因为插件可以与账户上的任意大或小的资源集进行交互。例如，一个广泛的恶意插件可能会向原生函数选择器添加回退钩子，从而使账户无法使用，或者添加可能耗尽账户资金的执行函数。然而，也可以安装一个具有非常狭窄域的插件，并依赖账户行为的正确性来强制执行其有限的访问权限。因此，用户在向其账户添加插件时应谨慎。

用户在安装插件之前应进行仔细的尽职调查，并应注意插件可能是危险的。插件的清单可以让用户了解插件的域，即请求的权限，以在某些执行选择器上安装某些验证函数和/或钩子。通常，包含原生函数选择器的插件，例如，向原生 `uninstallPlugin()` 函数添加验证钩子的插件，可能会带来比仅向插件自己添加到账户的函数选择器添加验证钩子的插件更大的危害。

插件还可以向其他插件安装的函数选择器添加验证钩子。虽然通常这样的插件会，例如，添加额外的预验证钩子，但它也可能导致先前安装的插件在意外的上下文中被执行。例如，如果一个插件仅打算在用户操作上下文中运行，其插件清单可能仅定义用户操作验证函数。然而，另一个插件可能会向该函数选择器添加一个通过的运行时验证函数，导致例如会话密钥插件突然在运行时验证上下文中被执行，绕过所有在用户操作验证期间会发生的参数验证，并授予对所有会话密钥的无限制访问。因此，强烈建议始终在插件不打算执行的上下文中添加回退验证钩子。这个建议可能会在标准的下一个版本中改变。

值得一提的是，执行钩子对在同一函数选择器执行设置中执行的其他执行钩子没有意识。由于执行钩子可以执行状态更改，这揭示了一个重要的安全考虑：执行钩子只能确保在其自身执行时满足某些条件，但这不能推广到潜在多个预执行钩子的整个预执行上下文。例如，预执行钩子不能确保它所执行验证的存储在后续的预执行钩子中不会进一步更新。即使一个相关的后执行钩子可能重复验证，也不能确保存储保持未修改，因为先前的后执行钩子可能已重置状态。只要插件作为执行钩子的一部分检查的要求仅由插件本身可修改，这可以被视为安全的。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。