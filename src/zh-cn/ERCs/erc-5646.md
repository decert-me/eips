---
eip: 5646
title: 代币状态指纹
description: 明确的代币状态标识符
author: Naim Ashhab (@ashhanai)
discussions-to: https://ethereum-magicians.org/t/eip-5646-discussion-token-state-fingerprint/10808
status: Final
type: Standards Track
category: ERC
created: 2022-09-11
requires: 165
---

## 摘要

本规范定义了在不需要实现细节知识的情况下，明确识别可变代币状态所需的最小接口。

## 动机

目前，协议需要了解代币的状态属性以创建明确的标识符。不幸的是，这导致了一个明显的瓶颈，协议需要专门支持每个新代币。

![](../assets/eip-5646/support-per-abi.png)

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"SHOULD"、"SHOULD NOT" 和 "MAY" 应按 RFC 2119 中的描述进行解释。

```solidity
pragma solidity ^0.8.0;

interface ERC5646 is ERC165 {

    /// @notice Function to return current token state fingerprint.
    /// @param tokenId Id of a token state in question.
    /// @return Current token state fingerprint.
    function getStateFingerprint(uint256 tokenId) external view returns (bytes32);

}
```

- `getStateFingerprint` 在代币状态变化时 MUST 返回不同的值。
- `getStateFingerprint` 在代币状态保持不变时 MUST NOT 返回不同的值。
- `getStateFingerprint` MUST 包含在代币生命周期中可能变化的所有状态属性（不是不可变的）。
- `getStateFingerprint` MAY 包含计算值，例如基于当前时间戳的值（例如，过期、到期）。
- `getStateFingerprint` MAY 包含代币元数据 URI。
- `supportsInterface(0xf5112315)` MUST 返回 `true`。

## 理由

协议可以将状态指纹作为代币标识符的一部分使用，并在不知道任何状态实现细节的情况下支持可变代币。

![](../assets/eip-5646/support-per-eip.png)

状态指纹不必考虑不可变的状态属性，因为它们可以通过代币 ID 安全识别。

该标准不适用于需要代币状态属性知识的用例，因为这些情况无法逃脱前面描述的瓶颈问题。

## 向后兼容性

此 EIP 不引入任何向后不兼容性。

## 参考实现

```solidity
pragma solidity ^0.8.0;

/// @title Example of a mutable token implementing state fingerprint.
contract LPToken is ERC721, ERC5646 {

    /// @dev Stored token states (token id => state).
    mapping (uint256 => State) internal states;

    struct State {
        address asset1;
        address asset2;
        uint256 amount1;
        uint256 amount2;
        uint256 fee; // Immutable
        address operator; // Immutable
        uint256 expiration; // Parameter dependent on a block.timestamp
    }


    /// @dev State fingerprint getter.
    /// @param tokenId Id of a token state in question.
    /// @return Current token state fingerprint.
    function getStateFingerprint(uint256 tokenId) override public view returns (bytes32) {
        State storage state = states[tokenId];

        return keccak256(
            abi.encode(
                state.asset1,
                state.asset2,
                state.amount1,
                state.amount2,
                // state.fee don't need to be part of the fingerprint computation as it is immutable
                // state.operator don't need to be part of the fingerprint computation as it is immutable
                block.timestamp >= state.expiration
            )
        );
    }

    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return super.supportsInterface(interfaceId) ||
            interfaceId == type(ERC5646).interfaceId;
    }

}
```

## 安全考虑

来自两个不同合约的代币状态指纹可能会发生冲突。因此，它们应仅在一个代币合约的上下文中进行比较。

如果 `getStateFingerprint` 实现未包含所有可能改变代币状态的参数，则代币所有者将能够在不改变代币指纹的情况下更改代币状态。这可能会破坏多个协议的无信任假设，例如为代币创建购买报价。代币所有者将能够在接受报价之前更改代币的状态。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。