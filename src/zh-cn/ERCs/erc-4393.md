---
eip: 4393
title: NFT 和多代币的微支付
description: 一个允许向 NFT 和多代币持有者打赏的代币接口
author: Jules Lai (@julesl23)
discussions-to: https://ethereum-magicians.org/t/eip-proposal-micropayments-standard-for-nfts-and-multi-tokens/7366
status: Stagnant
type: Standards Track
category: ERC
created: 2021-10-24
requires: 165, 721, 1155
---

## 摘要

本标准概述了一个用于向非同质化代币和多代币打赏的智能合约接口。代币持有者能够将打赏作为 [EIP-20](./erc-20.md) 奖励提取。

在本 EIP 中，微支付被定义为涉及通常小额资金的金融交易，这些资金被称为“打赏”，发送给特定的 [EIP-721](./eip-721.md) NFT 和 [EIP-1155](./eip-1155.md) 多代币，作为对其持有者的奖励。持有者（也称为控制者）是对所有者的更通用的称呼，因为 NFT 可能代表非数字资产，例如服务。

## 动机

以低成本向任何类型的 NFT 或多代币发送打赏。这可以通过优化打赏代币合约的 gas 并使用接口中的 `tipBatch` 函数批量发送打赏来实现。

为了便于将打赏服务集成到 dapp 中，以奖励 NFT 和多代币持有者。允许用户社区向 NFT 持有者更公平地分配收入。

使接口尽可能简洁，以便能够适应多种不同的用例。

一些用例包括：

- 游戏内购买和其他虚拟商品

- 对消息、帖子、音乐和视频内容的打赏

- 捐赠/众筹

- 版税分配

- 按点击付费广告

- 激励使用服务

- 奖励卡和优惠券

这些都可以利用区块链的安全性、即时性和透明性。

## 规范

本标准提案概述了一种通过实现 `ITipToken` 接口来允许打赏的通用方法。该接口故意保持最小化，以便允许最大化的用例。

实现本 EIP 标准的智能合约必须实现本 EIP 接口中的所有函数。还必须发出接口中指定的事件，以便仅通过发出的事件即可推导出打赏代币合约的完整状态。

实现本 EIP 标准的智能合约必须实现 [EIP-165](./eip-165.md) supportsInterface 函数，并且如果通过 interfaceID 参数传递 0xE47A7022，则必须返回常量值 true。请注意，本文档中的 revert 可能意味着 require、throw（不推荐，因为已弃用）或带有或不带错误消息的 revert solidity 语句。

请注意，代码中提到的 nft（或大写的 NFT）也可能指代 EIP-1155 可替代代币。

```solidity
interface ITipToken {
    /**
        @dev This emits when the tip token implementation approves the address
        of an NFT for tipping.
        The holders of the 'nft' are approved to receive rewards.
        When an NFT Transfer event emits, this also indicates that the approved
        addresses for that NFT (if any) is reset to none.
        Note: the ERC-165 identifier for this interface is 0x985A3267.
    */
    event ApprovalForNFT(
        address[] holders,
        address indexed nft,
        uint256 indexed id,
        bool approved
    );

    /**
        @dev This emits when a user has deposited an ERC-20 compatible token to
        the tip token's contract address or to an external address.
        This also indicates that the deposit has been exchanged for an
        amount of tip tokens
    */
    event Deposit(
        address indexed user,
        address indexed rewardToken,
        uint256 amount,
        uint256 tipTokenAmount
    );

    /**
        @dev This emits when a holder withdraws an amount of ERC-20 compatible
        reward. This reward comes from the tip token's contract address or from
        an external address, depending on the tip token implementation
    */
    event WithdrawReward(
        address indexed holder,
        address indexed rewardToken,
        uint256 amount
    );

    /**
        @dev This emits when the tip token constructor or initialize method is
        executed.
        Importantly the ERC-20 compatible token 'rewardToken_' to use as reward
        to NFT holders is set at this time and remains the same throughout the
        lifetime of the tip token contract.
        The 'rewardToken_' and 'tipToken_' MAY be the same.
    */
    event InitializeTipToken(
        address indexed tipToken_,
        address indexed rewardToken_,
        address owner_
    );

    /**
        @dev This emits every time a user tips an NFT holder.
        Also includes the reward token address and the reward token amount that
        will be held pending until the holder withdraws the reward tokens.
    */
    event Tip(
        address indexed user,
        address[] holder,
        address indexed nft,
        uint256 id,
        uint256 amount,
        address rewardToken,
        uint256[] rewardTokenAmount
    );

    /**
        @notice Enable or disable approval for tipping for a single NFT held
        by a holder or a multi token shared by holders
        @dev MUST revert if calling nft's supportsInterface does not return
        true for either IERC721 or IERC1155.
        MUST revert if any of the 'holders' is the zero address.
        MUST revert if 'nft' has not approved the tip token contract address as operator.
        MUST emit the 'ApprovalForNFT' event to reflect approval or not approval.
        @param holders The holders of the NFT (NFT controllers)
        @param nft The NFT contract address
        @param id The NFT token id
        @param approved True if the 'holder' is approved, false to revoke approval
    */
    function setApprovalForNFT(
        address[] calldata holders,
        address nft,
        uint256 id,
        bool approved
    ) external;

    /**
        @notice Checks if 'holder' and 'nft' with token 'id' have been approved
        by setApprovalForNFT
        @dev This does not check that the holder of the NFT has changed. That is
        left to the implementer to detect events for change of ownership and to
        take appropriate action
        @param holder The holder of the NFT (NFT controller)
        @param nft The NFT contract address
        @param id The NFT token id
        @return True if 'holder' and 'nft' with token 'id' have previously been
        approved by the tip token contract
    */
    function isApprovalForNFT(
        address holder,
        address nft,
        uint256 id
    ) external returns (bool);

    /**
        @notice Sends tip from msg.sender to holder of a single NFT or
        to shared holders of a multi token
        @dev If 'nft' has not been approved for tipping, MUST revert
        MUST revert if 'nft' is zero address.
        MUST burn the tip 'amount' to the 'holder' and send the reward to
        an account pending for the holder(s).
        If 'nft' is a multi token that has multiple holders then each holder
        MUST receive tip amount in proportion of their balance of multi tokens
        MUST emit the 'Tip' event to reflect the amounts that msg.sender tipped
        to holder(s) of 'nft'.
        @param nft The NFT contract address
        @param id The NFT token id
        @param amount Amount of tip tokens to send to the holder of the NFT
    */
    function tip(
        address nft,
        uint256 id,
        uint256 amount
    ) external;

    /**
        @notice Sends a batch of tips to holders of 'nfts' for gas efficiency
        @dev If NFT has not been approved for tipping, revert
        MUST revert if the input arguments lengths are not all the same
        MUST revert if any of the user addresses are zero
        MUST revert the whole batch if there are any errors
        MUST emit the 'Tip' events so that the state of the amounts sent to
        each holder and for which nft and from whom, can be reconstructed.
        @param users User accounts to tip from
        @param nfts The NFT contract addresses whose holders to tip to
        @param ids The NFT token ids that uniquely identifies the 'nfts'
        @param amounts Amount of tip tokens to send to the holders of the NFTs
    */
    function tipBatch(
        address[] calldata users,
        address[] calldata nfts,
        uint256[] calldata ids,
        uint256[] calldata amounts
    ) external;

    /**
        @notice Deposit an ERC-20 compatible token in exchange for tip tokens
        @dev The price of tip tokens can be different for each deposit as
        the amount of reward token sent ultimately is a ratio of the
        amount of tip tokens to tip over the user's tip tokens balance available
        multiplied by the user's deposit balance.
        The deposited tokens can be held in the tip tokens contract account or
        in an external escrow. This will depend on the tip token implementation.
        Each tip token contract MUST handle only one type of ERC-20 compatible
        reward for deposits.
        This token address SHOULD be passed in to the tip token constructor or
        initialize method. SHOULD revert if ERC-20 reward for deposits is
        zero address.
        MUST emit the 'Deposit' event that shows the user, deposited token details
        and amount of tip tokens minted in exchange
        @param user The user account
        @param amount Amount of ERC-20 token to deposit in exchange for tip tokens.
        This deposit is to be used later as the reward token
    */
    function deposit(address user, uint256 amount) external payable;

    /**
        @notice An NFT holder can withdraw their tips as an ERC-20 compatible
        reward at a time of their choosing
        @dev MUST revert if not enough balance pending available to withdraw.
        MUST send 'amount' to msg.sender account (the holder)
        MUST reduce the balance of reward tokens pending by the 'amount' withdrawn.
        MUST emit the 'WithdrawReward' event to show the holder who withdrew, the reward
        token address and 'amount'
        @param amount Amount of ERC-20 token to withdraw as a reward
    */
    function withdrawReward(uint256 amount) external payable;

    /**
        @notice MUST have identical behaviour to ERC-20 balanceOf and is the amount
        of tip tokens held by 'user'
        @param user The user account
        @return The balance of tip tokens held by user
    */
    function balanceOf(address user) external view returns (uint256);

    /**
        @notice The balance of deposit available to become rewards when
        user sends the tips
        @param user The user account
        @return The remaining balance of the ERC-20 compatible token deposited
    */
    function balanceDepositOf(address user) external view returns (uint256);

    /**
        @notice The amount of reward token owed to 'holder'
        @dev The pending tokens can come from the tip token contract account
        or from an external escrow, depending on tip token implementation
        @param holder The holder of NFT(s) (NFT controller)
        @return The amount of reward tokens owed to the holder from tipping
    */
    function rewardPendingOf(address holder) external view returns (uint256);
}
```

### 向持有者打赏和奖励

用户首先向打赏代币合约存入兼容的 EIP-20，然后在托管中持有（扣除任何约定费用），以换取打赏代币。这些打赏代币可以由用户发送给 NFT 和多代币（已被打赏代币合约批准打赏），以便在持有者提取时兑换为原始 EIP-20 存款作为奖励。

### 打赏代币转移和价值计算

打赏代币的价值与 EIP-20 存款进行交换，反之亦然。由打赏代币实现者决定打赏代币的价格，从而决定为存入的 EIP-20 铸造多少打赏代币。一种可能性是根据地理区域设定固定的转换率，以便来自较贫困国家的用户能够以相同的数量发送打赏，与来自较富裕国家的用户对内容/资产等的相同欣赏水平。因此，在分析中不会因平均财富而偏斜，从而发现哪些 NFT 实际上受欢迎，使创作者能够拥有公平的竞争环境。

每当用户发送打赏时，必须将等值的存入 EIP-20 转移到 NFT 或多代币持有者的待处理账户，并且发送的打赏代币必须被销毁。这个等值是使用简单公式计算的：

_total user balance of EIP-20 deposit _ tip amount / total user balance of tip tokens*

因此，例如，简单地将 *免费* 打赏添加到用户的打赏余额，只是稀释了该用户每个打赏的整体价值，因为它们共同仍然指代相同数量的 EIP-20 存款。

请注意，如果打赏代币合约继承自 EIP-20，则打赏可以直接从一个用户转移到另一个用户。存款金额已经在打赏代币合约（或外部托管账户）中，因此只需更新打赏代币合约的用户账户到存款余额的内部映射。建议从用户 A 销毁打赏金额，然后以保持用户 B 每个打赏的平均 EIP-20 存款值相同的金额铸造回用户 B，以便在打赏过程中不会波动打赏的价值。

如果不继承自 EIP-20，则铸造打赏代币必须发出 `event Transfer(address indexed from, address indexed to, uint256 value)`，其中发送者为零地址表示铸造，接收者为零地址表示销毁。Transfer 事件必须与 `IERC20` 接口中的 Transfer 函数具有相同的签名。

### 版税分配

EIP-1155 允许一个代币 ID 的共享持有者。想象一个场景，其中由多个贡献者撰写的文章由 NFT 表示。在这里，每个贡献者都是持有者，持有的 EIP-1155 代币 ID 的余额可以表示他们之间的分成比例。因此，对于 EIP-1155 代币 1 的两个持有者 A 和 B，如果持有者 A 的余额为 25，持有者 B 的余额为 75，则发送给代币 1 的任何打赏将分配 25% 的待处理奖励给持有者 A，剩余的 75% 待处理奖励给持有者 B。

以下是 ITipToken 合约数据结构的示例实现：

```solidity
    /// Mapping from NFT/multi token to token id to holder(s)
    mapping(address => mapping(uint256 => address[])) private _tokenIdToHolders;

    /// Mapping from user to user's deposit balance
    mapping(address => uint256) private _depositBalances;

    /// Mapping from holder to holder's reward pending amount
    mapping(address => uint256) private _rewardsPending;
```

这适用于必须具有唯一代币 ID 和单一持有者的 EIP-721 合约（以符合标准），以及可以具有多个代币 ID 和每个实例多个持有者的 EIP-1155 合约。`tip` 函数的实现将通过 NFT/多代币地址和代币 ID 的索引访问 `_tokenIdToHolders`，以分配给持有者或持有者的 `_rewardsPending`。

对于需要直接向持有者分配版税的场景，`ITipToken` 合约的 `tip` 方法可以将版税金额直接从用户账户发送到单个 NFT 的持有者或多代币的共享持有者，扣除可选的约定费用。在这种情况下，打赏代币类型就是奖励代币。

### 注意事项

为了保持 `ITipToken` 接口的简单性和通用性，每个打赏代币合约必须同时使用一种 EIP-20 兼容的存款类型。如果打赏需要支持多种 EIP-20 存款，则每个打赏代币合约必须为所需的每种 EIP-20 兼容类型单独部署。因此，如果需要同时从 ETH 和 BTC 包装的 EIP-20 存款进行打赏，则打赏代币合约需要部署两次。打赏代币合约的构造函数要求传入特定打赏代币合约所支持的 EIP-20 代币的地址。或者在可升级的打赏代币合约的情况下，初始化方法要求传入 EIP-20 代币地址。
此 EIP 并未提供 EIP-20 奖励存款的具体存放位置。它必须在持有者提取他们应得的奖励时可用。推荐的实现方式是将存款锁定在小费代币合约地址中。通过保持一个记录待持有者余额的映射结构，当用户给予小费时，存款可以保持在原地，只有在持有者提取作为奖励时才转移到持有者的地址。

该标准未指定允许的 EIP-20 兼容存款类型。实际上，可以是小费代币本身。但建议在转账后检查存款的余额，以找出确切的存款金额，以保持内部会计的一致性。例如，如果 EIP-20 合约收取费用，从而减少实际存款金额。

该标准未指定存款或发送的小费代币的退款功能，留给实现者将其添加到他们的智能合约中。这样做的原因是保持接口简洁，而不强制实现者需要退款，而是将其作为选择。

### 最小化 Gas 成本

通过在链外缓存小费，然后批量调用 ITipToken 接口的 `tipBatch` 方法，基本上初始化交易的成本只需支付一次，而不是每次小费都支付一次。此外，如果同一用户向同一 NFT 代币发送多个小费并将其合并为批次中的一个条目，则可以在链外进一步节省 Gas。

通过将向同一 NFT 发送的小费的用户分组，可以进一步节省 Gas，这样就可以对每个组执行一次 NFT 的有效性检查，以及它是 EIP-721 还是 EIP-1155。

巧妙地最小化每个用户的存款余额和每个持有者的奖励余额的链上状态更新，可以进一步帮助在批量发送时减少 Gas 成本，如果批次事先已排序。例如，如果批次中的下一个 NFT 相同，则可以避免检查。这留给小费代币合约的实现者。无论应用何种优化，仍然必须允许从发出的 Tip 和 TipBatch 事件中重建哪个账户给哪个账户小费以及针对哪个 NFT 的信息。

## 理由

### 简单性

ITipToken 接口使用最少的函数，以保持其尽可能通用，同时提供足够的指导以实现其对 NFT 持有者的微支付目的。

### 使用 NFT

每个 NFT 是一个独特的非同质化代币数字资产，存储在区块链上，通过其地址和代币 ID 唯一标识。它的真实性通过在安全区块链上使用加密哈希进行验证，这意味着它作为链接唯一数字资产、服务或其他合同协议的锚。这样的用例可能包括（但仅限于想象力和接受度）：

- 数字艺术、收藏品、音乐、视频、许可证和证书、活动门票、ENS 名称、游戏物品、元宇宙中的物体、物理物品的真实性证明、服务协议等。

该机制为 NFT 的消费者提供了一种安全的方式，轻松地给予小费和奖励 NFT 持有者。

### 新商业模式

以音乐用例为例。传统上，自从行业从通过 CD 等物理介质分发音频转向通过流媒体的在线数字分发模型以来，音乐行业一直受到寡头的控制，这些寡头在过渡中发挥了作用。他们运营固定的订阅模型，并从中设定内容创作者的版税分配金额；例如，歌手、音乐家等。使用小费代币代表了一种额外的方式，让音乐爱好者奖励内容创作者。每首歌或曲目都由一个 NFT 表示，粉丝可以给他们喜欢的歌曲（因此是 NFT）小费，反过来，NFT 的内容创作者能够获得为小费购买的 EIP-20 奖励。一个由粉丝主导的音乐行业，去中心化和代币化，预计将带来新的收入，并使粉丝与内容创作者更紧密地联系在一起。

在其他行业中，可以应用类似的理念，让第三方控制者转向更具促进作用的角色，而不是今天存在的货币控制角色。

### 保证审计轨迹

随着以太坊生态系统的不断发展，许多 dapp 依赖传统数据库和探索者 API 服务来检索和分类数据。该 EIP 标准保证智能合约发出的事件日志必须提供足够的数据，以创建所有当前小费代币和 EIP-20 奖励余额的准确记录。数据库或探索者可以提供对从任何实现此标准的小费代币合约发出的事件中发送给 NFT 持有者的每个小费代币和奖励的索引和分类搜索。因此，小费代币合约的状态可以仅通过发出的事件重建。

## 向后兼容性

小费代币合约可以完全兼容 EIP-20 规范，并继承一些函数，例如如果代币允许直接发送给其他用户，则可以继承 transfer。请注意，balanceOf 已被采用，必须是用户地址持有的小费数量。如果从例如 OpenZeppelin 的 EIP-20 代币实现中继承，则他们的合约负责维护小费代币的余额。因此，小费代币的 balanceOf 函数应该直接调用父合约的 balanceOf 函数。

未被转移到小费代币标准的是其他用户小费的支出者的能力。目前，该标准未预见到对此的需求。

该 EIP 并未强调小费代币二级市场或其他用例的必要性，在这些用例中，使用名称而不是地址识别小费代币类型可能会很有用，因此这些功能被排除在 ITipToken 接口之外，留给实现者。

## 安全考虑

尽管建议将用户的存款锁定在小费代币合约或外部托管账户中，并且不应仅用于持有者的奖励，但这无法强制执行。该标准规定，奖励必须在持有者从存款池中提取奖励时可用。

在任何用户可以给 NFT 小费之前，NFT 的持有者必须批准从小费代币合约进行小费。该标准规定，NFT 的持有者应收到奖励。小费代币合约代码中应清楚表明这一点，而不应模糊奖励的去向。任何费用收取应在接受存款之前向用户明确说明。存在恶意实现者可能试图*劫持*潜在小费收入流以谋取私利的风险。此外，小费过程的交易数量和频率应使这种类型的欺诈更快被发现。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。