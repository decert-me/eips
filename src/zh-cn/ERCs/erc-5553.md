---
eip: 5553
title: 表示知识产权及其版权结构
description: 一种在链上表示知识产权及其相应版权结构的方法
author: Roy Osherove (@royosherove)
discussions-to: https://ethereum-magicians.org/t/eip-5553-representing-intellectual-property-on-chain-with-royalty-rights/10551
status: 停滞
type: 标准跟踪
category: ERC
created: 2022-08-17
requires: 20, 721
---

## 摘要
本提案介绍了一种在链上表示知识产权的通用方法，以及一个精细化的版权表示机制和相关的元数据链接。该标准与特定类型的知识产权无关，可以表示多种类型的知识产权，例如音乐知识产权、视频、书籍、图像等。
该标准保持非常通用，以允许行业发展出可以依赖于相同基本标准的新生态系统。

该标准允许市场参与者：
1) 观察知识产权的规范链上表示
2) 发现其附加的元数据
3) 发现其相关的版权结构
4) 这将使未来能够建立知识产权资产的注册、许可和支付机制。

## 动机

目前没有公认的标准机制来许可知识产权或表示知识产权，除了使用传统的 NFT。然而，常规 NFT 仅表示收藏品用例，无法轻松表示更复杂的知识产权许可用例。
如果我们能够：

1) 声明知识产权存在，独立于其购买能力
2) 声明可能有多个利益相关方为该知识产权获得报酬

对于 1，目前没有标准。

对于 2，传统的分成标准基于 NFT 购买或通过像 0xsplits 这样的机制存在。虽然这些解决了主要问题，但它们不包含命名多种类型的合作参与者的能力。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

**希望在链上表示知识产权的合约必须实现 [EIP-721](./eip-721.md) 和本提案**

该标准扩展了 [EIP-721](./eip-721.md)，并具有以下`IIPRepresentation`（简称 IPR）接口。
该标准的实现者**必须**具有以下所有功能：

### royaltyPortionTokens() 函数
该函数必须返回一个与 [EIP-20](./eip-20.md) 代币相关的地址数组，这些代币必须表示不同类型利益相关方的版权份额。这些版权份额代币代表了一种更细化和简化的方式，以声明多个合作参与者在知识产权创作中的版权分成。

例如，对于音乐知识产权，我们可能有两个代币，分别代表创作/写作/出版版权份额和录音/母带版权份额。这些版权份额代币分配给合作参与者，稍后可以由各个持有者查询以分配版权。即，如果某人持有 10%的版权份额代币，该持有者将获得与该类型版权相关的 10%的财务分配。

### metadataURI() 函数
该函数必须返回一个指向包含知识产权所需元数据的元数据文件的 URI，或返回空字符串。每种知识产权类型可以实现其元数据标准，单独定义。该文件必须托管在 IPFS、Arweave 或其他去中心化内容可寻址系统中，其中文件的内容在不更改 URI 的情况下不可更改。

### changeMetadataURI() 函数
该函数允许更改元数据 URI，以指向元数据文件的新版本。调用此函数必须在成功时触发事件`MetadataChanged`。

### ledger() 函数
该函数必须返回注册或登记合约地址或初始化知识产权及相关版权代币的 EOA 账户。知识产权表示可以由不同的参与者在不同的地方注册以用于不同的目的。该函数使市场参与者能够发现哪个注册机制是知识产权的父级，并可能具有管理知识产权的特殊访问权限。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.9;
import '@openzeppelin/contracts/interfaces/IERC165.sol';


///
/// @dev Interface for Intellectual Property Representation
///
interface IIPRepresentation is IERC165 {
    
    /// @notice Called with the new URI to an updated metadata file
    /// @param _newUri - the URI pointing to a metadata file (file standard is up to the implementer)
    /// @param _newFileHash - The hash of the new metadata file for future reference and verification
    function changeMetadataURI(string memory _newUri, string memory _newFileHash) external ;

    /// @return array of addresses of ERC20 tokens representing royalty portion in the IP
    /// @dev i.e implementing ERC5501 (IRoyaltyInterestToken interface)
    function royaltyPortionTokens() external view returns (address[] memory) ;

    /// @return the address of the contract or EOA that initialized the IP registration
    /// @dev i.e., a registry or registrar, to be implemented in the future
    function ledger() external view returns (address) ;

    /// @return the URI of the current metadata file for the II P
    function metadataURI() external view returns (string memory) ;

    /// @dev event to be triggered whenever metadata URI is changed
    /// @param byAddress the addresses that triggered this operation
    /// @param oldURI the URI to the old metadata file before the change
    /// @param oldFileHash the hash of the old metadata file before the change
    /// @param newURI the URI to the new metadata file 
    /// @param newFileHash the hash of the new metadata file 
    event MetadaDataChanged(address byAddress, string oldURI, string oldFileHash, string newURI, string newFileHash);
}
```

## 理由

### 返回 EIP-20 代币数组提供了更强大的版权份额结构

当前的版权实现仅处理单一类型的版权支付：NFT 销售。它们也仅允许单一类型的版权——即，音乐 NFT 无法在不同场景中支付不同的人。
换句话说，目前，版权分成的工作方式在所有参与方的购买或许可交易中都是相同的。

通过本提案，允许多种**类型**的版权场景。一个经典的案例是音乐行业，其中我们有创作/作曲版权和录音/母带版权。不同的许可类型将根据上下文向不同的参与方支付不同的百分比。

在歌曲翻唱的情况下，可以创建一个许可支付公式，以便
a) 原始知识产权的作家因使用歌词或歌曲的创作而获得报酬
b) 原始歌曲的录音艺术家不获得报酬，因为他们的录音未被使用
c) 新知识产权的录音艺术家将获得报酬
d) 翻唱创作者没有写作版权。

此外，该 EIP 具有一个单一结构，连接所有类型的版权类型，并允许更容易地找到它们。
最后，移动 EIP-20 代币比管理 0xsplits 合约要容易得多。

### 将知识产权合约与收藏品和许可 NFT 分开，能够扩展许可类型
通过将知识产权的规范版本与其各种许可用途（NFT 购买、流媒体、艺术使用等）分开，该 EIP 为各种许可类型和支付分配的生态系统发展引入了一条路径。
换句话说，当人们使用此方案时，他们不会首先创建音乐 NFT 或艺术 NFT；他们首先创建知识产权表示，然后为其创建许可或收藏品类型，每种类型作为其自己的可销售 NFT。

### 指向知识产权元数据的单一指针
IPR 指向存储在 IPFS 或 Arweave 中的元数据，并允许以简单和标准的方式更改它并跟踪更改。今天唯一的元数据标准是 NFT 元数据扩展，但无法知道文档遵循哪个标准。对于不同的知识产权类型，可以制定不同的元数据标准，并有一个简单、易于发现附加元数据的地方。

## 参考实现

#### 基于 IIPRepresentation 实现音乐知识产权表示（简称 MIPR）
```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.9;
import '@openzeppelin/contracts/token/ERC721/ERC721.sol';
import "./interfaces/IIPRepresentation.sol";
import "./interfaces/Structs.sol";


contract MusicalIP is ERC721, IIPRepresentation {
    address public songLedger;
    address public compToken;
    address public recToken;
    string public metadataURI;
    string public fileHash;
    uint256 public tokenId;
    bool public activated =false;

    function supportsInterface(bytes4 interfaceId) public view virtual override( ERC721, IERC165) returns (bool) {
        return
            interfaceId == type(IIPRepresentation).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    function getInterfaceId() public pure returns (bytes4){
        return type(IIPRepresentation).interfaceId;
    }

    constructor (
        uint256 _tokenId,
        address _songLedger,
        SongMintingParams memory _params,
        address _compAddress,
        address _recAddress
        )
    ERC721(_params.shortName, _params.symbol){

        songLedger = _songLedger;
        compToken = _compAddress;
        recToken = _recAddress;
        metadataURI = _params.metadataUri;
        fileHash = _params.fileHash;
        tokenId = _tokenId;
        
        _safeMint(_songLedger, _tokenId);
        emit Minted(_params.shortName,_songLedger,_compAddress,_recAddress,_msgSender(),tokenId,_params.metadataUri);
    }

    function changeMetadataURI(string memory _newURI,string memory _newFileHash) public 
     {
        string memory oldURI = metadataURI;
        string memory oldHash = fileHash;
        metadataURI = _newURI; 
        fileHash = _newFileHash;
        
        emit MetadataChanged(oldURI, oldHash,_newURI,_newFileHash);
    }
    
    function royaltyPortionTokens() external view returns (address[] memory) {
        address[] memory items = new address[](2); 
        items[0] = compToken;
        items[1] = recToken;
        return items;
    }
    function ledger() external view returns (address) {
         return songLedger;
    }

    event MetadataChanged(
        string  oldUri, string oldFileHash,
        string  newUri, string newFileHash
        );
    event Minted(
        string  abbvName,
        address ledger,
        address compToken,
        address recToken,
        address creator,
        uint256 tokenId,
        string metadataUri
        );
}



```

#### 使用简单的歌曲注册合约部署新的音乐知识产权

```solidity  
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.9;
import "@openzeppelin/contracts/utils/Counters.sol";
import "./MusicalIP.sol";
import "./CompositionRoyaltyToken.sol";
import "./RecordingRoyaltyToken.sol";


contract SimpleSongLedger is IERC721Receiver {
    using Counters for Counters.Counter;
    Counters.Counter private mipIds;
      function onERC721Received(address, address, uint256, bytes calldata) external pure returns (bytes4) {
        return IERC721Receiver.onERC721Received.selector;
    }

    function mintSong(SongMintingParams memory _params) public {
        CompositionRoyaltyToken comp = new CompositionRoyaltyToken(address(this),"SONGCOMP","COMP");
        RecordingRoyaltyToken rec = new RecordingRoyaltyToken(address(this),"SONGREC","REC");
        mipIds.increment();

        MusicalIP mip = new MusicalIP(
                                        mipIds.current(),
                                        address(this),
                                        _params,
                                        address(comp),
                                        address(rec)
                                    );
    }
}


```
## 安全考虑

可能存在攻击者说服版权份额代币持有者将其代币发送给他们并获得各种 IPR 的版权份额的潜在安全挑战。然而，这些并不是特定于版权的，而是 EIP-20 代币的常见问题。

在知识产权注册所有权的情况下，建议注册合约拥有知识产权注册，该注册将是不可转让的（与创建它的注册机构绑定的账户）。
## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。