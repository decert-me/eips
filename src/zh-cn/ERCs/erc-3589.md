---
eip: 3589
title: 将资产组装成 NFT
author: Zhenyu Sun (@Ungigdu), Xinqi Yang (@xinqiyang)
discussions-to: https://github.com/ethereum/EIPs/issues/3590
status: Stagnant
type: Standards Track
category: ERC
created: 2021-05-24
requires: 721
---

## 简要总结
该标准定义了一种称为组装代币的 ERC-721 代币，可以表示资产的组合。

## 摘要
ERC-1155 多代币合约定义了一种批量转移代币的方法，但这些代币必须由 ERC-1155 合约本身铸造。此 EIP 是 ERC-721 的扩展，具有将以太、ERC-20 代币、ERC-721 代币和 ERC-1155 代币组装成一个 ERC-721 代币的能力，其代币 ID 也是资产的签名。随着资产被组装成一个，可以非常容易地实现批量转移或交换。

## 动机
随着 NFT 艺术和收藏品的快速增加，一些收藏家对传统交易方式感到不满。当两个收藏家想要交换他们的一些藏品时，目前他们可以在市场上列出他们的 NFT 并通知另一方购买，但这效率低下且消耗 gas。相反，一些收藏家转向社交媒体或聊天群组，寻找可信赖的第三方为他们交换 NFT。第三方从收藏家 A 和 B 那里获取 NFT，并将 A 的藏品转移给 B，将 B 的转移给 A。这是非常危险的。

进行批量交换的最安全方法是将批量交换转变为原子交换，即一对一交换。但首先我们应该将这些以太、ERC-20 代币、ERC-721 代币和 ERC-1155 代币“组装”在一起，这也是此 EIP 的主要目的。

## 规范
本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

符合 ERC-721 的合约可以实现此 ERC，以提供组装资产的标准方法。

`mint`和`safeMint`将资产组装成一个 ERC-721 代币。`mint`应为正常的 ERC-20 代币实现，其`_transfer`是无损的。`safeMint`必须处理如 PIG 代币等有损代币，其`_transfer`函数是有税的。

`hash`函数的`_salt`可以以其他方式实现，甚至可以作为用户输入提供。但代币 ID 必须由`hash`函数生成。

该标准的实现可以支持不同的资产集。

该标准的实现者必须具有以下所有功能：

```
pragma solidity ^0.8.0;

interface AssemblyNFTInterface {

  event AssemblyAsset(address indexed firstHolder,
                    uint256 indexed tokenId,
                    uint256 salt,
                    address[] addresses,
                    uint256[] numbers);

  /**
  * @dev hash function assigns the combination of assets with salt to bytes32 signature that is also the token id.
  * @param `_salt` prevents hash collision, can be chosen by user input or increasing nonce from contract.
  * @param `_addresses` concat assets addresses, e.g. [ERC-20_address1, ERC-20_address2, ERC-721_address_1, ERC-1155_address_1, ERC-1155_address_2]
  * @param `_numbers` describes how many eth, ERC-20 token addresses length, ERC-721 token addresses length, ERC-1155 token addresses length,
  * ERC-20 token amounts, ERC-721 token ids, ERC-1155 token ids and amounts.
  */
  function hash(uint256 _salt, address[] memory _addresses, uint256[] memory _numbers) external pure returns (uint256 tokenId);

  /// @dev to assemble lossless assets
  /// @param `_to` the receiver of the assembly token
  function mint(address _to, address[] memory _addresses, uint256[] memory _numbers) payable external returns(uint256 tokenId);

  /// @dev mint with additional logic that calculates the actual received value for tokens.
  function safeMint(address _to, address[] memory _addresses, uint256[] memory _numbers) payable external returns(uint256 tokenId);

  /// @dev burn this token and releases assembled assets
  /// @param `_to` to which address the assets is released
  function burn(address _to, uint256 _tokenId, uint256 _salt, address[] calldata _addresses, uint256[] calldata _numbers) external;

}

```

## 理由
人们想要将他们的 NFT 打包在一起有很多原因。例如，一个收藏家想要将一组足球运动员打包成一个足球队；一个收藏家有数百个没有类别的 NFT 来管理；一个收藏家想要购买完整的 NFT 收藏或不购买它们。他们都需要一种将这些 NFT 组合在一起的方法。

选择 ERC-721 标准作为包装的原因是 ERC-721 代币已经被广泛使用，并且得到了 NFT 钱包的良好支持。组装代币本身也可以再次被组装。组装代币在批量交易、批量交换或收藏品交换等场景中比一批资产更容易被智能合约使用。

该标准具有 AssemblyAsset 事件，记录组装代币所代表的确切种类和数量的资产。钱包可以通过代币 ID 轻松向用户显示这些 NFT。

## 向后兼容性
该提案结合了已经可用的 721 扩展，并与 ERC-721 标准向后兼容。

## 实现
```
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/utils/ERC721Holder.sol";
import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";
import "@openzeppelin/contracts/token/ERC1155/utils/ERC1155Holder.sol";
import "./AssemblyNFTInterface.sol";

abstract contract AssemblyNFT is ERC721, ERC721Holder, ERC1155Holder, AssemblyNFTInterface{
  using SafeERC20 for IERC20;

  function supportsInterface(bytes4 interfaceId) public view virtual override(ERC721, ERC1155Receiver) returns (bool) {
        return ERC721.supportsInterface(interfaceId) || ERC1155Receiver.supportsInterface(interfaceId);
  }

  uint256 nonce;

  /**
  * layout of _addresses:
  *     erc20 addresses | erc721 addresses | erc1155 addresses
  * layout of _numbers:
  *     eth | erc20.length | erc721.length | erc1155.length | erc20 amounts | erc721 ids | erc1155 ids | erc1155 amounts
   */

  function hash(uint256 _salt, address[] memory _addresses, uint256[] memory _numbers) public pure override returns (uint256 tokenId){
      bytes32 signature = keccak256(abi.encodePacked(_salt));
      for(uint256 i=0; i< _addresses.length; i++){
        signature = keccak256(abi.encodePacked(signature, _addresses[i]));
      }
      for(uint256 j=0; j<_numbers.length; j++){
        signature = keccak256(abi.encodePacked(signature, _numbers[j]));
      }
      assembly {
        tokenId := signature
      }
  }

  function mint(address _to, address[] memory _addresses, uint256[] memory _numbers) payable external override returns(uint256 tokenId){
      require(_to != address(0), "can't mint to address(0)");
      require(msg.value == _numbers[0], "value not match");
      require(_addresses.length == _numbers[1] + _numbers[2] + _numbers[3], "2 array length not match");
      require(_addresses.length == _numbers.length -4 - _numbers[3], "numbers length not match");
      uint256 pointerA; //points to first erc20 address, if there is any
      uint256 pointerB =4; //points to first erc20 amount, if there is any
      for(uint256 i = 0; i< _numbers[1]; i++){
        require(_numbers[pointerB] > 0, "transfer erc20 0 amount");
        IERC20(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB++]);
      }
      for(uint256 j = 0; j< _numbers[2]; j++){
        IERC721(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB++]);
      }
      for(uint256 k =0; k< _numbers[3]; k++){
        IERC1155(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB], _numbers[_numbers[3] + pointerB++], "");
      }
      tokenId = hash(nonce, _addresses, _numbers);
      super._mint(_to, tokenId);
      emit AssemblyAsset(_to, tokenId, nonce, _addresses, _numbers);
      nonce ++;
  }

  function safeMint(address _to, address[] memory _addresses, uint256[] memory _numbers) payable external override returns(uint256 tokenId){
      require(_to != address(0), "can't mint to address(0)");
      require(msg.value == _numbers[0], "value not match");
      require(_addresses.length == _numbers[1] + _numbers[2] + _numbers[3], "2 array length not match");
      require(_addresses.length == _numbers.length -4 - _numbers[3], "numbers length not match");
      uint256 pointerA; //points to first erc20 address, if there is any
      uint256 pointerB =4; //points to first erc20 amount, if there is any
      for(uint256 i = 0; i< _numbers[1]; i++){
        require(_numbers[pointerB] > 0, "transfer erc20 0 amount");
        IERC20 token = IERC20(_addresses[pointerA++]);
        uint256 orgBalance = token.balanceOf(address(this));
        token.safeTransferFrom(_msgSender(), address(this), _numbers[pointerB]);
        _numbers[pointerB++] = token.balanceOf(address(this)) - orgBalance;
      }
      for(uint256 j = 0; j< _numbers[2]; j++){
        IERC721(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB++]);
      }
      for(uint256 k =0; k< _numbers[3]; k++){
        IERC1155(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB], _numbers[_numbers[3] + pointerB++], "");
      }
      tokenId = hash(nonce, _addresses, _numbers);
      super._mint(_to, tokenId);
      emit AssemblyAsset(_to, tokenId, nonce, _addresses, _numbers);
      nonce ++;
  }

  function burn(address _to, uint256 _tokenId, uint256 _salt, address[] calldata _addresses, uint256[] calldata _numbers) override external {
      require(_msgSender() == ownerOf(_tokenId), "not owned");
      require(_tokenId == hash(_salt, _addresses, _numbers));
      super._burn(_tokenId);
      payable(_to).transfer(_numbers[0]);
      uint256 pointerA; //points to first erc20 address, if there is any
      uint256 pointerB =4; //points to first erc20 amount, if there is any
      for(uint256 i = 0; i< _numbers[1]; i++){
        require(_numbers[pointerB] > 0, "transfer erc20 0 amount");
        IERC20(_addresses[pointerA++]).safeTransfer(_to, _numbers[pointerB++]);
      }
      for(uint256 j = 0; j< _numbers[2]; j++){
        IERC721(_addresses[pointerA++]).safeTransferFrom(address(this), _to, _numbers[pointerB++]);
      }
      for(uint256 k =0; k< _numbers[3]; k++){
        IERC1155(_addresses[pointerA++]).safeTransferFrom(address(this), _to, _numbers[pointerB], _numbers[_numbers[3] + pointerB++], "");
      }
  }

}
```

## 安全考虑
在使用`mint`或`safeMint`函数之前，用户应注意某些代币的实现是可暂停的。如果在组装成一个 NFT 后其中一个资产被暂停，则`burn`函数可能无法成功执行。使用此标准的平台应制定支持列表或阻止列表以避免这种情况。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。