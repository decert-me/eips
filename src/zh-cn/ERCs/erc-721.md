---
eip: 721
title: 非同质化代币标准
author: William Entriken (@fulldecent), Dieter Shirley <dete@axiomzen.co>, Jacob Evans <jacob@dekz.net>, Nastassia Sachs <nastassia.sachs@protonmail.com>
discussions-to: https://github.com/ethereum/eips/issues/721
type: 标准跟踪
category: ERC
status: 最终
created: 2018-01-24
requires: 165
---

## 简要总结

非同质化代币的标准接口，也称为契约。

## 摘要

以下标准允许在智能合约中实现非同质化代币（NFT）的标准 API。该标准提供基本功能以跟踪和转移 NFT。

我们考虑了个人拥有和交易 NFT 的用例，以及委托给第三方经纪人/钱包/拍卖师（“操作员”）。NFT 可以代表对数字或实物资产的所有权。我们考虑了多种资产的多样性，我们知道你会想出更多：

- 实物财产 — 房屋、独特艺术品
- 虚拟收藏品 — 独特的小猫图片、收藏卡
- “负价值”资产 — 贷款、负担和其他责任

一般来说，所有房屋都是独特的，没有两只小猫是相同的。NFT 是 *可区分的*，你必须单独跟踪每一个的所有权。

## 动机

标准接口允许钱包/经纪人/拍卖应用程序与以太坊上的任何 NFT 一起工作。我们提供简单的 ERC-721 智能合约以及跟踪 *任意数量* NFT 的合约。下面讨论了其他应用。

该标准受到 ERC-20 代币标准的启发，并基于自 EIP-20 创建以来的两年经验。EIP-20 对于跟踪 NFT 是不够的，因为每个资产都是独特的（非同质化），而每个代币的数量是相同的（同质化）。

下面检查了该标准与 EIP-20 之间的差异。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

**每个符合 ERC-721 的合约必须实现 `ERC721` 和 `ERC165` 接口**（受以下“注意事项”约束）：

```solidity
pragma solidity ^0.4.20;

/// @title ERC-721 Non-Fungible Token Standard
/// @dev See https://eips.ethereum.org/EIPS/eip-721
///  Note: the ERC-165 identifier for this interface is 0x80ac58cd.
interface ERC721 /* is ERC165 */ {
    /// @dev This emits when ownership of any NFT changes by any mechanism.
    ///  This event emits when NFTs are created (`from` == 0) and destroyed
    ///  (`to` == 0). Exception: during contract creation, any number of NFTs
    ///  may be created and assigned without emitting Transfer. At the time of
    ///  any transfer, the approved address for that NFT (if any) is reset to none.
    event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);

    /// @dev This emits when the approved address for an NFT is changed or
    ///  reaffirmed. The zero address indicates there is no approved address.
    ///  When a Transfer event emits, this also indicates that the approved
    ///  address for that NFT (if any) is reset to none.
    event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

    /// @dev This emits when an operator is enabled or disabled for an owner.
    ///  The operator can manage all NFTs of the owner.
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

    /// @notice Count all NFTs assigned to an owner
    /// @dev NFTs assigned to the zero address are considered invalid, and this
    ///  function throws for queries about the zero address.
    /// @param _owner An address for whom to query the balance
    /// @return The number of NFTs owned by `_owner`, possibly zero
    function balanceOf(address _owner) external view returns (uint256);

    /// @notice Find the owner of an NFT
    /// @dev NFTs assigned to zero address are considered invalid, and queries
    ///  about them do throw.
    /// @param _tokenId The identifier for an NFT
    /// @return The address of the owner of the NFT
    function ownerOf(uint256 _tokenId) external view returns (address);

    /// @notice Transfers the ownership of an NFT from one address to another address
    /// @dev Throws unless `msg.sender` is the current owner, an authorized
    ///  operator, or the approved address for this NFT. Throws if `_from` is
    ///  not the current owner. Throws if `_to` is the zero address. Throws if
    ///  `_tokenId` is not a valid NFT. When transfer is complete, this function
    ///  checks if `_to` is a smart contract (code size > 0). If so, it calls
    ///  `onERC721Received` on `_to` and throws if the return value is not
    ///  `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`.
    /// @param _from The current owner of the NFT
    /// @param _to The new owner
    /// @param _tokenId The NFT to transfer
    /// @param data Additional data with no specified format, sent in call to `_to`
    function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;

    /// @notice Transfers the ownership of an NFT from one address to another address
    /// @dev This works identically to the other function with an extra data parameter,
    ///  except this function just sets data to "".
    /// @param _from The current owner of the NFT
    /// @param _to The new owner
    /// @param _tokenId The NFT to transfer
    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice Transfer ownership of an NFT -- THE CALLER IS RESPONSIBLE
    ///  TO CONFIRM THAT `_to` IS CAPABLE OF RECEIVING NFTS OR ELSE
    ///  THEY MAY BE PERMANENTLY LOST
    /// @dev Throws unless `msg.sender` is the current owner, an authorized
    ///  operator, or the approved address for this NFT. Throws if `_from` is
    ///  not the current owner. Throws if `_to` is the zero address. Throws if
    ///  `_tokenId` is not a valid NFT.
    /// @param _from The current owner of the NFT
    /// @param _to The new owner
    /// @param _tokenId The NFT to transfer
    function transferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice Change or reaffirm the approved address for an NFT
    /// @dev The zero address indicates there is no approved address.
    ///  Throws unless `msg.sender` is the current NFT owner, or an authorized
    ///  operator of the current owner.
    /// @param _approved The new approved NFT controller
    /// @param _tokenId The NFT to approve
    function approve(address _approved, uint256 _tokenId) external payable;

    /// @notice Enable or disable approval for a third party ("operator") to manage
    ///  all of `msg.sender`'s assets
    /// @dev Emits the ApprovalForAll event. The contract MUST allow
    ///  multiple operators per owner.
    /// @param _operator Address to add to the set of authorized operators
    /// @param _approved True if the operator is approved, false to revoke approval
    function setApprovalForAll(address _operator, bool _approved) external;

    /// @notice Get the approved address for a single NFT
    /// @dev Throws if `_tokenId` is not a valid NFT.
    /// @param _tokenId The NFT to find the approved address for
    /// @return The approved address for this NFT, or the zero address if there is none
    function getApproved(uint256 _tokenId) external view returns (address);

    /// @notice Query if an address is an authorized operator for another address
    /// @param _owner The address that owns the NFTs
    /// @param _operator The address that acts on behalf of the owner
    /// @return True if `_operator` is an approved operator for `_owner`, false otherwise
    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}

interface ERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///  uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

如果钱包/经纪人/拍卖应用程序将接受安全转移，则必须实现 **钱包接口**。

```solidity
/// @dev Note: the ERC-165 identifier for this interface is 0x150b7a02.
interface ERC721TokenReceiver {
    /// @notice Handle the receipt of an NFT
    /// @dev The ERC721 smart contract calls this function on the recipient
    ///  after a `transfer`. This function MAY throw to revert and reject the
    ///  transfer. Return of other than the magic value MUST result in the
    ///  transaction being reverted.
    ///  Note: the contract address is always the message sender.
    /// @param _operator The address which called `safeTransferFrom` function
    /// @param _from The address which previously owned the token
    /// @param _tokenId The NFT identifier which is being transferred
    /// @param _data Additional data with no specified format
    /// @return `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`
    ///  unless throwing
    function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);
}
```

**元数据扩展** 对于 ERC-721 智能合约是可选的（见下面的“注意事项”）。这允许你的智能合约被查询其名称和关于你的 NFT 代表的资产的详细信息。

```solidity
/// @title ERC-721 Non-Fungible Token Standard, optional metadata extension
/// @dev See https://eips.ethereum.org/EIPS/eip-721
///  Note: the ERC-165 identifier for this interface is 0x5b5e139f.
interface ERC721Metadata /* is ERC721 */ {
    /// @notice A descriptive name for a collection of NFTs in this contract
    function name() external view returns (string _name);

    /// @notice An abbreviated name for NFTs in this contract
    function symbol() external view returns (string _symbol);

    /// @notice A distinct Uniform Resource Identifier (URI) for a given asset.
    /// @dev Throws if `_tokenId` is not a valid NFT. URIs are defined in RFC
    ///  3986. The URI may point to a JSON file that conforms to the "ERC721
    ///  Metadata JSON Schema".
    function tokenURI(uint256 _tokenId) external view returns (string);
}
```

这是上述提到的“ERC721 元数据 JSON 架构”。

```json
{
    "title": "Asset Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "Identifies the asset to which this NFT represents"
        },
        "description": {
            "type": "string",
            "description": "Describes the asset to which this NFT represents"
        },
        "image": {
            "type": "string",
            "description": "A URI pointing to a resource with mime type image/* representing the asset to which this NFT represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
        }
    }
}
```

**枚举扩展** 对于 ERC-721 智能合约是可选的（见下面的“注意事项”）。这允许你的合约发布其所有 NFT 的完整列表并使其可发现。

```solidity
/// @title ERC-721 Non-Fungible Token Standard, optional enumeration extension
/// @dev See https://eips.ethereum.org/EIPS/eip-721
///  Note: the ERC-165 identifier for this interface is 0x780e9d63.
interface ERC721Enumerable /* is ERC721 */ {
    /// @notice Count NFTs tracked by this contract
    /// @return A count of valid NFTs tracked by this contract, where each one of
    ///  them has an assigned and queryable owner not equal to the zero address
    function totalSupply() external view returns (uint256);

    /// @notice Enumerate valid NFTs
    /// @dev Throws if `_index` >= `totalSupply()`.
    /// @param _index A counter less than `totalSupply()`
    /// @return The token identifier for the `_index`th NFT,
    ///  (sort order not specified)
    function tokenByIndex(uint256 _index) external view returns (uint256);

    /// @notice Enumerate NFTs assigned to an owner
    /// @dev Throws if `_index` >= `balanceOf(_owner)` or if
    ///  `_owner` is the zero address, representing invalid NFTs.
    /// @param _owner An address where we are interested in NFTs owned by them
    /// @param _index A counter less than `balanceOf(_owner)`
    /// @return The token identifier for the `_index`th NFT assigned to `_owner`,
    ///   (sort order not specified)
    function tokenOfOwnerByIndex(address _owner, uint256 _index) external view returns (uint256);
}
```

### 注意事项

0.4.20 版本的 Solidity 接口语法不足以记录 ERC-721 标准。符合 ERC-721 的合约还必须遵守以下规定：

- Solidity 问题 #3412：上述接口为每个函数包含明确的可变性保证。可变性保证的顺序从弱到强为：`payable`、隐式非支付、`view` 和 `pure`。你的实现必须满足该接口中的可变性保证，并且可以满足更强的保证。例如，该接口中的 `payable` 函数可以在你的合约中实现为非支付（未指定状态可变性）。我们预计后续的 Solidity 版本将允许你的严格合约从该接口继承，但对于 0.4.20 版本的解决方法是，你可以在继承你的合约之前编辑该接口以添加更严格的可变性。
- Solidity 问题 #3419：实现 `ERC721Metadata` 或 `ERC721Enumerable` 的合约也必须实现 `ERC721`。ERC-721 实现了接口 ERC-165 的要求。
- Solidity 问题 #2330：如果在本规范中显示某个函数为 `external`，则如果合约使用 `public` 可见性，则合约将符合要求。作为 0.4.20 版本的解决方法，你可以在继承你的合约之前编辑该接口以切换为 `public`。
- Solidity 问题 #3494、#3544：使用 `this.*.selector` 被 Solidity 标记为警告，未来版本的 Solidity 将不再将其标记为错误。

*如果更新版本的 Solidity 允许在代码中表达注意事项，则该 EIP 可以更新并删除注意事项，这将等同于原始规范。*

## 理由

有许多提议的以太坊智能合约用途依赖于跟踪可区分的资产。现有或计划中的 NFT 示例包括 Decentraland 中的 LAND、CryptoPunks 中的同名朋克，以及使用 DMarket 或 EnjinCoin 等系统的游戏内物品。未来的用途包括跟踪现实世界资产，如房地产（如 Ubitquity 或 Propy 等公司设想的那样）。在每种情况下，至关重要的是，这些项目不能作为账本中的数字“合并在一起”，而是每个资产必须单独和原子地跟踪其所有权。无论这些资产的性质如何，如果我们有一个标准化的接口，允许跨功能的资产管理和销售平台，生态系统将会更强大。

**“NFT” 词汇选择**

“NFT” 对几乎所有被调查者来说都是令人满意的，并且广泛适用于可区分的数字资产的广泛宇宙。我们认识到“契约”对于该标准的某些应用（尤其是实物财产）非常具有描述性。

*考虑的替代方案：可区分资产、所有权、代币、资产、股权、票据*

**NFT 标识符**

每个 NFT 由 ERC-721 智能合约中的唯一 `uint256` ID 标识。该标识号码在合约的生命周期内不得更改。对 `(合约地址, uint256 tokenId)` 的组合将是以太坊链上特定资产的全球唯一和完全合格的标识符。虽然一些 ERC-721 智能合约可能会发现从 ID 0 开始并简单地为每个新 NFT 增加一是方便的，但调用者不得假设 ID 号码有任何特定模式，并且必须将 ID 视为“黑箱”。还要注意，NFT 可能会变得无效（被销毁）。请参见枚举函数以获取支持的枚举接口。

选择 `uint256` 允许广泛的应用，因为 UUID 和 sha3 哈希可以直接转换为 `uint256`。

**转移机制**

ERC-721 标准化了安全转移函数 `safeTransferFrom`（重载有和没有 `bytes` 参数）和不安全函数 `transferFrom`。转移可以由以下人员发起：

- NFT 的所有者
- NFT 的批准地址
- 当前 NFT 所有者的授权操作员

此外，授权操作员可以设置 NFT 的批准地址。这为钱包、经纪人和拍卖应用程序提供了一套强大的工具，以快速使用 *大量* NFT。

转移和接受函数的文档仅指定交易必须抛出时的条件。你的实现也可以在其他情况下抛出。这允许实现达到有趣的结果：

- **如果合约被暂停，则不允许转移** — 先前的案例，CryptoKitties 部署合约，第 611 行
- **阻止某些地址接收 NFT** — 先前的案例，CryptoKitties 部署合约，第 565、566 行
- **不允许不安全转移** — `transferFrom` 抛出，除非 `_to` 等于 `msg.sender` 或 `countOf(_to)` 非零或之前非零（因为这种情况是安全的）
- **对交易的双方收取费用** — 在调用 `approve` 时，如果之前是零地址，则要求支付非零 `_approved`，如果之前是非零地址，则在调用 `approve` 时退款，要求在调用任何转移函数时支付，要求转移参数 `_to` 等于 `msg.sender`，要求转移参数 `_to` 为 NFT 的批准地址
- **只读 NFT 注册表** — 从 `safeTransferFrom`、`transferFrom`、`approve` 和 `setApprovalForAll` 始终抛出
失败的交易将抛出异常，这是在 ERC-223、ERC-677、ERC-827 和 OpenZeppelin 的 SafeERC20.sol 实现中识别出的最佳实践。ERC-20 定义了一个 `allowance` 特性，这在调用后再修改为不同的金额时造成了问题，如 OpenZeppelin 问题 \#438 所示。在 ERC-721 中，没有 allowance，因为每个 NFT 是独一无二的，数量为零或一。因此，我们可以享受 ERC-20 原始设计的好处，而没有后续发现的问题。

NFT 的创建（“铸造”）和销毁（“燃烧”）不包含在规范中。您的合约可以通过其他方式实现这些功能。请参阅 `event` 文档以了解您在创建或销毁 NFT 时的责任。

我们质疑 `onERC721Received` 中的 `operator` 参数是否必要。在我们能想象的所有情况下，如果 operator 重要，那么该 operator 可以将代币转移给自己，然后再发送——这样他们将是 `from` 地址。这似乎是牵强的，因为我们认为 operator 是代币的临时拥有者（将代币转移给自己是多余的）。当 operator 发送代币时，是 operator 自主行动，而不是代表代币持有者行动。这就是为什么 operator 和之前的代币拥有者对代币接收者都很重要。

*考虑的替代方案：仅允许两步 ERC-20 风格的交易，要求转移函数永远不抛出异常，要求所有函数返回一个布尔值以指示操作的成功。*

**ERC-165 接口**

我们选择标准接口检测（ERC-165）来公开 ERC-721 智能合约支持的接口。

未来的 EIP 可能会创建一个合约接口的全球注册表。我们强烈支持这样的 EIP，这将允许您的 ERC-721 实现通过委托给一个单独的合约来实现 `ERC721Enumerable`、`ERC721Metadata` 或其他接口。

**Gas 和复杂性**（关于枚举扩展）

本规范考虑了管理少量和 *任意大量* NFT 的实现。如果您的应用能够增长，则应避免在代码中使用 for/while 循环（参见 CryptoKitties 奖励问题 \#4）。这些表明您的合约可能无法扩展，且 gas 成本将随着时间的推移而无上限地上升。

我们已在测试网部署了一个合约 XXXXERC721，该合约实例化并跟踪 340282366920938463463374607431768211456 个不同的契约（2^128）。这足以将每个 IPV6 地址分配给一个以太坊账户所有者，或跟踪几微米大小的纳米机器人，整体总量达到地球的一半。您可以从区块链查询它。并且每个函数的 gas 消耗都低于查询 ENS。

这个例子清楚地表明：ERC-721 标准是可扩展的。

*考虑的替代方案：如果需要 for 循环，则移除资产枚举功能，从枚举函数返回 Solidity 数组类型。*

**隐私**

在动机部分中识别的钱包/经纪人/拍卖商有强烈的需求来识别所有者拥有的 NFT。

考虑一个 NFT 不可枚举的用例可能很有趣，例如私有财产所有权注册，或部分私有注册。然而，隐私无法实现，因为攻击者可以简单地调用 `ownerOf` 查询每个可能的 `tokenId`。

**元数据选择**（元数据扩展）

我们在元数据扩展中要求 `name` 和 `symbol` 函数。我们审查的每个代币 EIP 和草案（ERC-20、ERC-223、ERC-677、ERC-777、ERC-827）都包含这些函数。

我们提醒实现作者，如果您反对使用此机制，空字符串是 `name` 和 `symbol` 的有效响应。我们还提醒大家，任何智能合约都可以使用与 *您的* 合约相同的名称和符号。客户如何确定哪些 ERC-721 智能合约是知名的（规范的）超出了本标准的范围。

提供了一种将 NFT 与 URI 关联的机制。我们预计许多实现将利用这一点为每个 NFT 提供元数据。图像大小建议来自 Instagram，他们可能对图像可用性了解很多。URI 可以是可变的（即，时不时会更改）。我们考虑了一个代表房屋所有权的 NFT，在这种情况下，关于房屋的元数据（图像、居住者等）自然会发生变化。

元数据作为字符串值返回。目前，这仅可通过 `web3` 调用，而不能通过其他合约调用。这是可以接受的，因为我们没有考虑到区块链上的应用程序会查询此类信息的用例。

*考虑的替代方案：将每个资产的所有元数据放在区块链上（成本太高），使用 URL 模板查询元数据部分（URL 模板不适用于所有 URL 方案，尤其是 P2P URL），multiaddr 网络地址（尚不成熟）*

**社区共识**

在原始 ERC-721 问题上进行了大量讨论，此外我们在 Gitter 上举行了第一次现场会议，参与者良好且宣传充分（在 Reddit、Gitter #ERC 频道和原始 ERC-721 问题中）。感谢参与者：

- [@ImAllInNow](https://github.com/imallinnow) Rob 来自 DEC Gaming / 在密歇根以太坊聚会上演讲 2 月 7 日
- [@Arachnid](https://github.com/arachnid) Nick Johnson
- [@jadhavajay](https://github.com/jadhavajay) Ajay Jadhav 来自 AyanWorks
- [@superphly](https://github.com/superphly) Cody Marx Bailey - XRAM Capital / 在黑客马拉松上分享 1 月 20 日 / 联合国金融未来黑客马拉松。
- [@fulldecent](https://github.com/fulldecent) William Entriken

2018 年 ETHDenver 举办了第二次活动，讨论可区分资产标准（笔记将发布）。

在这个过程中，我们非常包容，邀请任何有问题或贡献的人参与我们的讨论。然而，本标准仅旨在支持此处列出的已识别用例。

## 向后兼容性

我们采用了 ERC-20 规范中的 `balanceOf`、`totalSupply`、`name` 和 `symbol` 语义。实现还可以包括一个返回 `uint8(0)` 的 `decimals` 函数，如果其目标是与 ERC-20 更兼容，同时支持本标准。然而，我们认为要求所有 ERC-721 实现支持 `decimals` 函数是牵强的。

截至 2018 年 2 月的示例 NFT 实现：

- CryptoKitties -- 与该标准的早期版本兼容。
- CryptoPunks -- 部分 ERC-20 兼容，但不易于推广，因为它直接在合约中包含拍卖功能，并使用明确指代资产为“punks”的函数名称。
- Auctionhouse 资产接口 -- 作者需要一个通用接口用于 Auctionhouse ÐApp（目前被冰箱封存）。他的“资产”合约非常简单，但缺少 ERC-20 兼容性、`approve()` 功能和元数据。该工作在 EIP-173 的讨论中被提及。

注意：“限量版、收藏代币”如 Curio Cards 和 Rare Pepe *不是* 可区分资产。它们实际上是一组单独的可替代代币，每个代币由其自己的智能合约跟踪，具有自己的总供应量（在极端情况下可能为 `1`）。

`onERC721Received` 函数专门处理旧部署合约，这些合约在某些情况下可能会无意中返回 1（`true`），即使它们没有实现该函数（参见 Solidity DelegateCallReturnValue bug）。通过返回并检查一个魔法值，我们能够区分实际的肯定响应与这些空洞的 `true`。
## 测试用例

0xcert ERC-721 代币包括使用 Truffle 编写的测试用例。

## 实现

0xcert ERC721 -- 参考实现

- MIT 许可，因此您可以自由地将其用于您的项目
- 包含测试用例
- 活跃的漏洞赏金计划，如果您发现错误将获得报酬

Su Squares -- 一个广告平台，您可以租用空间并放置图像

- 完成 Su Squares 漏洞赏金计划以寻找此标准或其实现中的问题
- 实现完整的标准和所有可选接口

ERC721ExampleDeed -- 一个示例实现

- 使用 OpenZeppelin 项目格式实现

XXXXERC721，由 William Entriken 提供 -- 一个可扩展的示例实现

- 在测试网部署，拥有 10 亿资产，并支持所有带有元数据扩展的查找。这证明了扩展不是问题。

## 参考

**标准**

1. [ERC-20](./erc-20.md) 代币标准。
1. [ERC-165](./eip-165.md) 标准接口检测。
1. [ERC-173](./eip-173.md) 所有权标准。
1. [ERC-223](https://github.com/ethereum/EIPs/issues/223) 代币标准。
1. [ERC-677](https://github.com/ethereum/EIPs/issues/677) `transferAndCall` 代币标准。
1. [ERC-827](https://github.com/ethereum/EIPs/issues/827) 代币标准。
1. 以太坊名称服务 (ENS)。 https://ens.domains
1. Instagram -- 图像分辨率是多少？ https://help.instagram.com/1631821640426723
1. JSON Schema. https://json-schema.org/
1. Multiaddr. https://github.com/multiformats/multiaddr
1. RFC 2119 用于指示要求级别的 RFC 关键字。 https://www.ietf.org/rfc/rfc2119.txt

**问题**

1. 原始 ERC-721 问题。 https://github.com/ethereum/eips/issues/721
1. Solidity 问题 \#2330 -- 接口函数是外部的。 https://github.com/ethereum/solidity/issues/2330
1. Solidity 问题 \#3412 -- 实现接口：允许更严格的可变性。 https://github.com/ethereum/solidity/issues/3412
1. Solidity 问题 \#3419 -- 接口不能继承。 https://github.com/ethereum/solidity/issues/3419
1. Solidity 问题 \#3494 -- 编译器错误地推理 `selector` 函数。 https://github.com/ethereum/solidity/issues/3494
1. Solidity 问题 \#3544 -- 无法计算名为 `transfer` 的函数的选择器。 https://github.com/ethereum/solidity/issues/3544
1. CryptoKitties 悬赏问题 \#4 -- 列出用户拥有的所有小猫是 `O(n^2)`。 https://github.com/axiomzen/cryptokitties-bounty/issues/4
1. OpenZeppelin 问题 \#438 -- `approve` 方法的实现违反 ERC20 标准。 https://github.com/OpenZeppelin/zeppelin-solidity/issues/438
1. Solidity DelegateCallReturnValue 错误。 https://solidity.readthedocs.io/en/develop/bugs.html#DelegateCallReturnValue

**讨论**

1. Reddit（首次实时讨论的公告）。 https://www.reddit.com/r/ethereum/comments/7r2ena/friday_119_live_discussion_on_erc_nonfungible/
1. Gitter #EIPs（首次实时讨论的公告）。 https://gitter.im/ethereum/EIPs?at=5a5f823fb48e8c3566f0a5e7
1. ERC-721（首次实时讨论的公告）。 https://github.com/ethereum/eips/issues/721#issuecomment-358369377
1. ETHDenver 2018。 https://ethdenver.com

**NFT 实现和其他项目**

1. CryptoKitties。 https://www.cryptokitties.co
1. 0xcert ERC-721 代币。 https://github.com/0xcert/ethereum-erc721
1. Su Squares。 https://tenthousandsu.com
1. Decentraland。 https://decentraland.org
1. CryptoPunks。 https://www.larvalabs.com/cryptopunks
1. DMarket。 https://www.dmarket.io
1. Enjin Coin。 https://enjincoin.io
1. Ubitquity。 https://www.ubitquity.io
1. Propy。 https://tokensale.propy.com
1. CryptoKitties 部署的合约。 https://etherscan.io/address/0x06012c8cf97bead5deae237070f9587f8e7a266d#code
1. Su Squares 漏洞赏金计划。 https://github.com/fulldecent/su-squares-bounty
1. XXXXERC721。 https://github.com/fulldecent/erc721-example
1. ERC721ExampleDeed。 https://github.com/nastassiasachs/ERC721ExampleDeed
1. Curio Cards。 https://mycuriocards.com
1. Rare Pepe。 https://rarepepewallet.com
1. Auctionhouse 资产接口。 https://github.com/dob/auctionhouse/blob/master/contracts/Asset.sol
1. OpenZeppelin SafeERC20.sol 实现。 https://github.com/OpenZeppelin/zeppelin-solidity/blob/master/contracts/token/ERC20/SafeERC20.sol

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。