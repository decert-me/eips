---
eip: 4931
title: 通用代币升级标准
description: 创建一个用于升级 ERC20 代币合约的标准接口。
author: John Peterson (@John-peterson-coinbase), Roberto Bayardo (@roberto-bayardo), David Núñez (@cygnusv)
discussions-to: https://ethereum-magicians.org/t/eip-4931-generic-token-upgrade-standard/8687
status: Stagnant
type: Standards Track
category: ERC
created: 2021-11-02
requires: 20
---


## 摘要

以下标准允许实现用于 [ERC-20](./erc-20.md) 代币升级的标准 API。该标准指定了一个接口，支持将代币从一个合约（称为“源代币”）转换为另一个合约（称为“目标代币”），以及几个辅助方法以提供有关代币升级的基本信息（即源和目标代币合约的地址、源代币升级为目标代币的比例等）。

## 动机

代币合约升级通常要求每个资产持有者使用开发者提供的定制接口将其旧代币兑换为新代币。该标准接口将允许资产持有者以及中心化和去中心化交易所更高效地进行代币升级，因为代币合约升级脚本本质上是可重用的。标准化将减少验证升级合约功能所涉及的安全开销。它还将为资产发行者提供明确的指导，以有效实施代币升级。

## 规范
本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

请注意：标记为（Optional Ext.）的方法是可选扩展的一部分，用于降级功能，如果不需要降级功能，则可以不实现。
### 代币升级接口合约
``` solidity
interface IEIP4931 {
```
#### 方法

##### upgradeSource

返回将要升级的原始（源）代币的地址。

``` solidity
/// @dev A getter to determine the contract that is being upgraded from ("source contract")
/// @return The address of the source token contract
function upgradeSource() external view returns(address)
```

##### upgradeDestination

返回正在升级到的代币合约的地址。

``` solidity
/// @dev A getter to determine the contract that is being upgraded to ("destination contract")
/// @return The address of the destination token contract
function upgradeDestination() external view returns(address)
```

##### isUpgradeActive

返回升级功能的当前状态。当升级合约正常工作并提供升级时，状态必须返回 `true`。当升级合约当前不提供升级时，必须返回 `false`。

``` solidity
/// @dev The method will return true when the contract is serving upgrades and otherwise false
/// @return The status of the upgrade as a boolean
function isUpgradeActive() external view returns(bool)
```
##### isDowngradeActive

返回降级功能的当前状态。当升级合约正常工作并提供降级时，状态必须返回 `true`。当升级合约当前不提供降级时，必须返回 `false`。如果未实现降级可选扩展，则此方法将始终返回 `false`，以表示不提供降级。

``` solidity
/// @dev The method will return true when the contract is serving downgrades and otherwise false
/// @return The status of the downgrade as a boolean
function isDowngradeActive() external view returns(bool)
```
##### ratio

返回目标代币与源代币的比例，以 2 元组的形式表示，升级将使用该比例。例如，`(3, 1)` 意味着每 1 个源代币将提供 3 个目标代币。

``` solidity
/// @dev A getter for the ratio of destination tokens to source tokens received when conducting an upgrade
/// @return Two uint256, the first represents the numerator while the second represents
/// the denominator of the ratio of destination tokens to source tokens allotted during the upgrade
function ratio() external view returns(uint256, uint256)
```

##### totalUpgraded

返回从源代币升级到目标代币的总数量。如果实现了降级可选扩展，对 `downgrade` 的调用将减少 `totalUpgraded` 返回值，使得该值在调用之间可能会减少。如果未实现降级，则返回值将严格递增。

``` solidity
/// @dev A getter for the total amount of source tokens that have been upgraded to destination tokens.
/// The value may not be strictly increasing if the downgrade Optional Ext. is implemented.
/// @return The number of source tokens that have been upgraded to destination tokens
function totalUpgraded() external view returns(uint256)
```
##### computeUpgrade

计算与给定的 `sourceAmount` 源代币对应的目标代币 `destinationAmount`，根据预定义的转换比例，以及无法升级的源代币 `sourceRemainder` 数量。例如，考虑一个 (3, 2) 的比例，这意味着每 2 个源代币提供 3 个目标代币；那么，对于 5 个源代币，`computeUpgrade(5)` 必须返回 `(6, 1)`，这意味着预计会有 6 个目标代币（在这种情况下，来自 4 个源代币），并且剩下 1 个源代币作为余数。
``` solidity
/// @dev A method to mock the upgrade call determining the amount of destination tokens received from an upgrade
/// as well as the amount of source tokens that are left over as remainder
/// @param sourceAmount The amount of source tokens that will be upgraded
/// @return destinationAmount A uint256 representing the amount of destination tokens received if upgrade is called
/// @return sourceRemainder A uint256 representing the amount of source tokens left over as remainder if upgrade is called
function computeUpgrade(uint256 sourceAmount) external view
        returns (uint256 destinationAmount, uint256 sourceRemainder)
```

##### computeDowngrade (Optional Ext.)

计算与给定的 `destinationAmount` 目标代币对应的源代币 `sourceAmount`，根据预定义的转换比例，以及无法降级的目标代币 `destinationRemainder` 数量。例如，考虑一个 (3, 2) 的比例，这意味着每 2 个源代币提供 3 个目标代币；对于 13 个目标代币，`computeDowngrade(13)` 必须返回 `(4, 1)`，这意味着预计会有 4 个源代币（在这种情况下，来自 12 个目标代币），并且剩下 1 个目标代币作为余数。
``` solidity
/// @dev A method to mock the downgrade call determining the amount of source tokens received from a downgrade
/// as well as the amount of destination tokens that are left over as remainder
/// @param destinationAmount The amount of destination tokens that will be downgraded
/// @return sourceAmount A uint256 representing the amount of source tokens received if downgrade is called
/// @return destinationRemainder A uint256 representing the amount of destination tokens left over as remainder if upgrade is called
function computeDowngrade(uint256 destinationAmount) external view
        returns (uint256 sourceAmount, uint256 destinationRemainder)
```


##### upgrade

将指定比例的 `amount` 源代币升级为目标代币。目标代币将发送到 `_to` 地址。该函数必须在升级合约中锁定源代币或销毁它们。如果实现了降级可选扩展，则源代币必须被锁定而不是销毁。如果调用者的地址没有足够的源代币进行升级，或者 `isUpgradeActive` 返回 `false`，则该函数必须 `throw`。该函数还必须触发 `Upgrade` 事件。必须首先在源合约上调用 `approve`。
``` solidity
/// @dev A method to conduct an upgrade from source token to destination token.
/// The call will fail if upgrade status is not true, if approve has not been called
/// on the source contract, or if sourceAmount is larger than the amount of source tokens at the msg.sender address.
/// If the ratio would cause an amount of tokens to be destroyed by rounding/truncation, the upgrade call will
/// only upgrade the nearest whole amount of source tokens returning the excess to the msg.sender address. 
/// Emits the Upgrade event
/// @param _to The address the destination tokens will be sent to upon completion of the upgrade
/// @param sourceAmount The amount of source tokens that will be upgraded 
function upgrade(address _to, uint256 sourceAmount) external
```


##### downgrade (Optional Ext.)
将指定比例的 `amount` 目标代币降级为源代币。源代币将发送到 `_to` 地址。该函数必须将目标代币解包回源代币。如果调用者的地址没有足够的目标代币进行降级，或者 `isDowngradeActive` 返回 `false`，则该函数必须 `throw`。该函数还必须触发 `Downgrade` 事件。必须首先在目标合约上调用 `approve`。
``` solidity
/// @dev A method to conduct a downgrade from destination token to source token.
/// The call will fail if downgrade status is not true, if approve has not been called
/// on the destination contract, or if destinationAmount is larger than the amount of destination tokens at the msg.sender address.
/// If the ratio would cause an amount of tokens to be destroyed by rounding/truncation, the downgrade call will only downgrade
/// the nearest whole amount of destination tokens returning the excess to the msg.sender address. 
///  Emits the Downgrade event
/// @param _to The address the source tokens will be sent to upon completion of the downgrade
/// @param destinationAmount The amount of destination tokens that will be downgraded 
function downgrade(address _to, uint256 destinationAmount) external
```

#### 事件

##### Upgrade

在代币升级时必须触发。

``` solidity
/// @param _from Address that called upgrade
/// @param _to Address that destination tokens were sent to upon completion of the upgrade
/// @param sourceAmount Amount of source tokens that were upgraded
/// @param destinationAmount Amount of destination tokens sent to the _to address
event Upgrade(address indexed _from, address indexed _to, uint256 sourceAmount, uint256 destinationAmount)
```

##### Downgrade (Optional Ext.)

在代币降级时必须触发。

``` solidity
/// @param _from Address that called downgrade
/// @param _to Address that source tokens were sent to upon completion of the downgrade
/// @param sourceAmount Amount of source tokens sent to the _to address
/// @param destinationAmount Amount of destination tokens that were downgraded
event Downgrade(address indexed _from, address indexed _to, uint256 sourceAmount, uint256 destinationAmount)
}
```

## 理由
已经有几个显著的 ERC20 升级（例如 Golem: GNT -> GLM），其中升级功能直接写入代币合约。我们认为这种将升级与现有代币紧密耦合的方式是次优的。该 EIP 提倡使用第三方合约来促进代币升级，以将升级功能与代币合约的功能解耦。标准化升级功能将允许资产持有者和交易所编写简化的可重用脚本来进行升级，从而减少未来进行升级的开销。该接口旨在故意广泛，留给实现者许多升级的具体细节，以便代币合约的实现不会干扰升级过程。最后，我们希望通过强制在升级过程中严格处理源代币，来增强对代币升级的安全感和有效性。这是通过对 `upgrade` 方法的规范来实现的。约定的规范是可销毁的代币应被销毁。否则，代币应通过发送到 `0x00` 地址的方式有效销毁。当实现降级可选扩展时，默认情况下应将源代币锁定在升级合约中，以避免连续调用 `upgrade` 和 `downgrade` 导致任一代币（源代币或目标代币）供应量的虚假膨胀。
## 向后兼容性
没有破坏性的向后兼容性问题。之前实施的代币升级可能不符合此标准。在这些情况下，资产发行者可能需要沟通他们的升级不符合 EIP-4931。

## 参考实现
``` solidity
//SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.9;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "./IEIP4931.sol";

contract SourceUpgrade is  IEIP4931 {
	using SafeERC20  for IERC20;

	uint256 constant RATIO_SCALE = 10**18;
    
	IERC20 private source;
	IERC20 private destination;
	bool private upgradeStatus;
	bool private downgradeStatus;
	uint256 private numeratorRatio;
	uint256 private denominatorRatio;
	uint256 private sourceUpgradedTotal;

	mapping(address => uint256) public upgradedBalance;

	constructor(address _source, address _destination, bool _upgradeStatus, bool _downgradeStatus, uint256 _numeratorRatio, uint256 _denominatorRatio) {
		require(_source != _destination, "SourceUpgrade: source and destination addresses are the same");
		require(_source != address(0), "SourceUpgrade: source address cannot be zero address");
		require(_destination != address(0), "SourceUpgrade: destination address cannot be zero address");
		require(_numeratorRatio > 0, "SourceUpgrade: numerator of ratio cannot be zero");
		require(_denominatorRatio > 0, "SourceUpgrade: denominator of ratio cannot be zero");

		source = IERC20(_source);
		destination = IERC20(_destination);
		upgradeStatus = _upgradeStatus;
		downgradeStatus = _downgradeStatus;
		numeratorRatio = _numeratorRatio;
		denominatorRatio = _denominatorRatio;
	}

	/// @dev A getter to determine the contract that is being upgraded from ("source contract")
	/// @return The address of the source token contract
	function upgradeSource() external view returns(address) {
		return address(source);
	}

	/// @dev A getter to determine the contract that is being upgraded to ("destination contract")
	/// @return The address of the destination token contract
	function upgradeDestination() external view returns(address) {
		return address(destination);
	}

	/// @dev The method will return true when the contract is serving upgrades and otherwise false
	/// @return The status of the upgrade as a boolean
	function isUpgradeActive() external view returns(bool) {
		return upgradeStatus;
	}

	/// @dev The method will return true when the contract is serving downgrades and otherwise false
	/// @return The status of the downgrade as a boolean
	function isDowngradeActive() external view returns(bool) {
		return downgradeStatus;
	}

	/// @dev A getter for the ratio of destination tokens to source tokens received when conducting an upgrade
	/// @return Two uint256, the first represents the numerator while the second represents
	/// the denominator of the ratio of destination tokens to source tokens allotted during the upgrade
	function ratio() external view returns(uint256, uint256) {
		return (numeratorRatio, denominatorRatio);
	}

	/// @dev A getter for the total amount of source tokens that have been upgraded to destination tokens.
	/// The value may not be strictly increasing if the downgrade Optional Ext. is implemented.
	/// @return The number of source tokens that have been upgraded to destination tokens
	function totalUpgraded() external view returns(uint256) {
		return sourceUpgradedTotal;
	}

	/// @dev A method to mock the upgrade call determining the amount of destination tokens received from an upgrade
	/// as well as the amount of source tokens that are left over as remainder
	/// @param sourceAmount The amount of source tokens that will be upgraded
	/// @return destinationAmount A uint256 representing the amount of destination tokens received if upgrade is called
	/// @return sourceRemainder A uint256 representing the amount of source tokens left over as remainder if upgrade is called
	function computeUpgrade(uint256 sourceAmount)
		public
		view
		returns (uint256 destinationAmount, uint256 sourceRemainder)
	{
		sourceRemainder = sourceAmount % (numeratorRatio / denominatorRatio);
		uint256 upgradeableAmount = sourceAmount - (sourceRemainder * RATIO_SCALE);
		destinationAmount = upgradeableAmount * (numeratorRatio / denominatorRatio);
	}

	/// @dev A method to mock the downgrade call determining the amount of source tokens received from a downgrade
	/// as well as the amount of destination tokens that are left over as remainder
	/// @param destinationAmount The amount of destination tokens that will be downgraded
	/// @return sourceAmount A uint256 representing the amount of source tokens received if downgrade is called
	/// @return destinationRemainder A uint256 representing the amount of destination tokens left over as remainder if upgrade is called
	function computeDowngrade(uint256 destinationAmount)
		public
		view
		returns (uint256 sourceAmount, uint256 destinationRemainder)
	{
		destinationRemainder = destinationAmount % (denominatorRatio / numeratorRatio);
		uint256 upgradeableAmount = destinationAmount - (destinationRemainder * RATIO_SCALE);
		sourceAmount = upgradeableAmount / (denominatorRatio / numeratorRatio);
	}

	/// @dev A method to conduct an upgrade from source token to destination token.
	/// The call will fail if upgrade status is not true, if approve has not been called
	/// on the source contract, or if sourceAmount is larger than the amount of source tokens at the msg.sender address.
	/// If the ratio would cause an amount of tokens to be destroyed by rounding/truncation, the upgrade call will
	/// only upgrade the nearest whole amount of source tokens returning the excess to the msg.sender address.
	/// Emits the Upgrade event
	/// @param _to The address the destination tokens will be sent to upon completion of the upgrade
	/// @param sourceAmount The amount of source tokens that will be upgraded
	function upgrade(address _to, uint256 sourceAmount) external {
		require(upgradeStatus == true, "SourceUpgrade: upgrade status is not active");
		(uint256 destinationAmount, uint256 sourceRemainder) = computeUpgrade(sourceAmount);
		sourceAmount -= sourceRemainder;
		require(sourceAmount > 0, "SourceUpgrade: disallow conversions of zero value");

		upgradedBalance[msg.sender] += sourceAmount;
		source.safeTransferFrom(
			msg.sender,
			address(this),
			sourceAmount
			);
		destination.safeTransfer(_to, destinationAmount);
		sourceUpgradedTotal += sourceAmount;
		emit Upgrade(msg.sender, _to, sourceAmount, destinationAmount);
	}

	/// @dev A method to conduct a downgrade from destination token to source token.
	/// The call will fail if downgrade status is not true, if approve has not been called
	/// on the destination contract, or if destinationAmount is larger than the amount of destination tokens at the msg.sender address.
	/// If the ratio would cause an amount of tokens to be destroyed by rounding/truncation, the downgrade call will only downgrade
	/// the nearest whole amount of destination tokens returning the excess to the msg.sender address.
	///  Emits the Downgrade event
	/// @param _to The address the source tokens will be sent to upon completion of the downgrade
	/// @param destinationAmount The amount of destination tokens that will be downgraded
	function downgrade(address _to, uint256 destinationAmount) external {
		require(upgradeStatus == true, "SourceUpgrade: upgrade status is not active");
		(uint256 sourceAmount, uint256 destinationRemainder) = computeDowngrade(destinationAmount);
		destinationAmount -= destinationRemainder;
		require(destinationAmount > 0, "SourceUpgrade: disallow conversions of zero value");
		require(upgradedBalance[msg.sender] >= sourceAmount,
			"SourceUpgrade: can not downgrade more than previously upgraded"
			);

		upgradedBalance[msg.sender] -= sourceAmount;
		destination.safeTransferFrom(
			msg.sender,
			address(this),
			destinationAmount
			);
		source.safeTransfer(_to, sourceAmount);
		sourceUpgradedTotal -= sourceAmount;
		emit Downgrade(msg.sender, _to, sourceAmount, destinationAmount);
	}
}
```

## 安全考虑
主要的安全考虑是确保接口的实现能够在升级过程中处理源代币，使其不再可访问。如果处理不当，升级的有效性可能会受到质疑，因为源代币可能会被多次升级。这就是为什么 EIP-4931 将严格要求对可销毁的源代币使用 `burn`。对于不可销毁的代币，接受的方法是将源代币发送到 `0x00` 地址。当降级可选扩展被实施时，约束将会放宽，以便源代币可以由升级合约持有。

## 版权
通过 [CC0](https://creativecommons.org/publicdomain/zero/1.0/) 放弃版权及相关权利。