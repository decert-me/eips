---
eip: 7758
title: 带授权的转账
description: 通过签名授权转移可替代资产。
author: Peter Jihoon Kim (@petejkim), Kevin Britz (@kbrizzle), David Knott (@DavidLKnott), Dongri Jin (@dongri)
discussions-to: https://ethereum-magicians.org/t/erc-7758-transfer-with-authorization/20859
status: 审核中
type: 标准跟踪
category: ERC
created: 2020-09-28
requires: 20, 712
---

## 摘要

一组函数，用于通过符合 [EIP-712](./eip-712.md) 类型消息签名规范的签名，启用元交易和与 [ERC-20](./erc-20.md) 代币合约的原子交互。

这使用户能够：

- 将 gas 费用委托给其他人，
- 用代币本身支付 gas，而不是用 ETH，
- 在单个原子交易中执行一个或多个代币转移和其他操作，
- 将 ERC-20 代币转移到另一个地址，并让接收者提交交易，
- 批量处理多个交易，减少开销，并且
- 创建和执行多个交易，而不必担心由于意外的 nonce 重用或矿工的不当排序而导致交易失败。

## 动机

现有的规范 [EIP-2612](./eip-2612) 也允许元交易，建议合约同时实现这两者以获得最大兼容性。这一规范与 EIP-2612 之间的两个主要区别是：

- EIP-2612 使用顺序 nonce，而本规范使用随机 32 字节 nonce，并且
- EIP-2612 依赖于 ERC-20 的`approve`/`transferFrom`（“ERC-20 授权”）模式。

使用顺序 nonce 的最大问题在于，它不允许用户在不冒着交易失败风险的情况下同时执行多个交易，因为：

- DApp 可能会无意中重用尚未在区块链上处理的 nonce。
- 矿工可能会以错误的顺序处理交易。

如果 gas 价格非常高，交易经常被排队并长时间未确认，这可能尤其成问题。非顺序 nonce 允许用户同时创建任意数量的交易。

ERC-20 授权机制容易受到多次提取攻击的影响，并鼓励使用“无限”授权等反模式。可升级合约的广泛存在使得这些攻击在现实中发生的条件变得有利。

ERC-20 授权模式的缺陷促使了替代代币标准的开发，例如 [ERC-777](./eip-777)。然而，由于兼容性和潜在的安全问题，它们未能获得广泛采用。

## 规范

### 事件

```solidity
event AuthorizationUsed(
    address indexed authorizer,
    bytes32 indexed nonce
);

// keccak256("TransferWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)")
bytes32 public constant TRANSFER_WITH_AUTHORIZATION_TYPEHASH = 0x7c7c6cdb67a18743f49ec6fa9b35f50d52ed05cbed4cc592e13b44501c1a2267;

// keccak256("ReceiveWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)")
bytes32 public constant RECEIVE_WITH_AUTHORIZATION_TYPEHASH = 0xd099cc98ef71107a616c4f0f941f04c322d8e254fe26b3c6668db87aae413de8;

/**
 * @notice Returns the state of an authorization
 * @dev Nonces are randomly generated 32-byte data unique to the authorizer's
 * address
 * @param authorizer    Authorizer's address
 * @param nonce         Nonce of the authorization
 * @return True if the nonce is used
 */
function authorizationState(
    address authorizer,
    bytes32 nonce
) external view returns (bool);

/**
 * @notice Execute a transfer with a signed authorization
 * @param from          Payer's address (Authorizer)
 * @param to            Payee's address
 * @param value         Amount to be transferred
 * @param validAfter    The time after which this is valid (unix time)
 * @param validBefore   The time before which this is valid (unix time)
 * @param nonce         Unique nonce
 * @param v             v of the signature
 * @param r             r of the signature
 * @param s             s of the signature
 */
function transferWithAuthorization(
    address from,
    address to,
    uint256 value,
    uint256 validAfter,
    uint256 validBefore,
    bytes32 nonce,
    uint8 v,
    bytes32 r,
    bytes32 s
) external;

/**
 * @notice Receive a transfer with a signed authorization from the payer
 * @dev This has an additional check to ensure that the payee's address matches
 * the caller of this function to prevent front-running attacks. (See security
 * considerations)
 * @param from          Payer's address (Authorizer)
 * @param to            Payee's address
 * @param value         Amount to be transferred
 * @param validAfter    The time after which this is valid (unix time)
 * @param validBefore   The time before which this is valid (unix time)
 * @param nonce         Unique nonce
 * @param v             v of the signature
 * @param r             r of the signature
 * @param s             s of the signature
 */
function receiveWithAuthorization(
    address from,
    address to,
    uint256 value,
    uint256 validAfter,
    uint256 validBefore,
    bytes32 nonce,
    uint8 v,
    bytes32 r,
    bytes32 s
) external;
```

**可选：**

```
event AuthorizationCanceled(
    address indexed authorizer,
    bytes32 indexed nonce
);

// keccak256("CancelAuthorization(address authorizer,bytes32 nonce)")
bytes32 public constant CANCEL_AUTHORIZATION_TYPEHASH = 0x158b0a9edf7a828aad02f63cd515c68ef2f50ba807396f6d12842833a1597429;

/**
 * @notice Attempt to cancel an authorization
 * @param authorizer    Authorizer's address
 * @param nonce         Nonce of the authorization
 * @param v             v of the signature
 * @param r             r of the signature
 * @param s             s of the signature
 */
function cancelAuthorization(
    address authorizer,
    bytes32 nonce,
    uint8 v,
    bytes32 r,
    bytes32 s
) external;
```

参数`v`、`r`和`s`必须使用 [EIP-712](./eip-712.md) 类型消息签名规范获得。

**示例：**

```
DomainSeparator := Keccak256(ABIEncode(
  Keccak256(
    "EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"
  ),
  Keccak256("USD Coin"),                      // name
  Keccak256("2"),                             // version
  1,                                          // chainId
  0xa0b86991c6218b36c1d19d4a2e9eb0ce3606eb48  // verifyingContract
))
```

通过域分隔符、类型哈希（用于识别所使用的 EIP-712 消息类型）和参数值，您可以推导出一个 Keccak-256 哈希摘要，然后使用代币持有者的私钥进行签名。

**示例：**

```
// Transfer With Authorization
TypeHash := Keccak256(
  "TransferWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)"
)
Params := { From, To, Value, ValidAfter, ValidBefore, Nonce }

// ReceiveWithAuthorization
TypeHash := Keccak256(
  "ReceiveWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)"
)
Params := { From, To, Value, ValidAfter, ValidBefore, Nonce }

// CancelAuthorization
TypeHash := Keccak256(
  "CancelAuthorization(address authorizer,bytes32 nonce)"
)
Params := { Authorizer, Nonce }
```

```
// "‖" denotes concatenation.
Digest := Keecak256(
  0x1901 ‖ DomainSeparator ‖ Keccak256(ABIEncode(TypeHash, Params...))
)

{ v, r, s } := Sign(Digest, PrivateKey)
```

包装`receiveWithAuthorization`调用的智能合约函数可以选择通过接受`receiveWithAuthorization`调用的完整 ABI 编码参数集作为单个`bytes`类型的参数来减少参数数量。

**示例：**

```solidity
// keccak256("receiveWithAuthorization(address,address,uint256,uint256,uint256,bytes32,uint8,bytes32,bytes32)")[0:4]
bytes4 private constant _RECEIVE_WITH_AUTHORIZATION_SELECTOR = 0xef55bec6;

function deposit(address token, bytes calldata receiveAuthorization)
    external
    nonReentrant
{
    (address from, address to, uint256 amount) = abi.decode(
        receiveAuthorization[0:96],
        (address, address, uint256)
    );
    require(to == address(this), "Recipient is not this contract");

    (bool success, ) = token.call(
        abi.encodePacked(
            _RECEIVE_WITH_AUTHORIZATION_SELECTOR,
            receiveAuthorization
        )
    );
    require(success, "Failed to transfer tokens");

    ...
}
```

### 与 web3 提供者一起使用

可以使用 web3 提供者通过`eth_signTypedData{_v4}`方法获得授权的签名。

**示例：**

```javascript
const data = {
  types: {
    EIP712Domain: [
      { name: "name", type: "string" },
      { name: "version", type: "string" },
      { name: "chainId", type: "uint256" },
      { name: "verifyingContract", type: "address" },
    ],
    TransferWithAuthorization: [
      { name: "from", type: "address" },
      { name: "to", type: "address" },
      { name: "value", type: "uint256" },
      { name: "validAfter", type: "uint256" },
      { name: "validBefore", type: "uint256" },
      { name: "nonce", type: "bytes32" },
    ],
  },
  domain: {
    name: tokenName,
    version: tokenVersion,
    chainId: selectedChainId,
    verifyingContract: tokenAddress,
  },
  primaryType: "TransferWithAuthorization",
  message: {
    from: userAddress,
    to: recipientAddress,
    value: amountBN.toString(10),
    validAfter: 0,
    validBefore: Math.floor(Date.now() / 1000) + 3600, // Valid for an hour
    nonce: Web3.utils.randomHex(32),
  },
};

const signature = await ethereum.request({
  method: "eth_signTypedData_v4",
  params: [userAddress, JSON.stringify(data)],
});

const v = "0x" + signature.slice(130, 132);
const r = signature.slice(0, 66);
const s = "0x" + signature.slice(66, 130);
```

## 理由

### 唯一随机 Nonce，而不是顺序 Nonce

有人可能会说，交易排序是顺序 nonce 被优先选择的一个原因。然而，顺序 nonce 实际上并没有帮助实现元交易的交易排序：

- 对于原生以太坊交易，当提交一个 nonce 值过高的交易到网络时，它将保持待处理状态，直到消耗较低未使用 nonce 的交易被确认。
- 然而，对于元交易，当提交一个包含顺序 nonce 值过高的交易时，它不会保持待处理状态，而是会立即回滚并失败，导致 gas 浪费。
- 矿工也可以重新排序交易并按他们想要的顺序将其包含在区块中（假设每个交易是由不同的元交易中继者提交到网络的），这也使得即使使用的 nonce 是正确的，元交易也可能失败。（例如，用户提交 nonce 3、4 和 5，但矿工最终将它们按 4、5、3 的顺序包含在区块中，导致只有 3 成功）
- 最后，当同时使用不同的应用程序时，在没有某种离线 nonce 跟踪器的情况下，如果存在已使用但尚未提交和确认的 nonce，就无法确定正确的下一个 nonce 值。
- 在高 gas 价格条件下，交易往往会在池中“卡住”很长时间。在这种情况下，意外重用相同 nonce 的可能性更大。例如，如果您从一个应用程序发起一个使用顺序 nonce 的元交易，并在前一个交易确认之前切换到另一个应用程序发起另一个元交易，如果该应用程序完全依赖于链上可用的数据，则将使用相同的 nonce，导致其中一个交易失败。
- 总之，保证交易排序的唯一方法是让中继者一次提交一个交易，在每次提交之间等待确认（它们应该提交的顺序可以是某些离线元数据的一部分），这使得顺序 nonce 变得无关紧要。

### 有效期后和有效期前

- 依赖中继者为您提交交易意味着您可能无法准确控制交易提交的时间。
- 这些参数允许用户安排交易仅在未来有效或在特定截止日期之前有效，保护用户免受由于提交过晚或过早而可能造成的潜在不良影响。

### EIP-712

- EIP-712 确保生成的签名仅对该特定实例的代币合约有效，并且不能在具有不同链 ID 的不同网络上重放。
- 这是通过将合约地址和链 ID 纳入一个称为域分隔符的 Keccak-256 哈希摘要来实现的。用于推导域分隔符的实际参数集由实现合约决定，但强烈建议包括`verifyingContract`和`chainId`字段。

## 向后兼容性

新合约可以直接利用此提案来创建原子交易，但现有合约仍可能依赖于传统的 [ERC-20](./erc-20.md) 授权模式（`approve`/`transferFrom`）。

为了向使用 ERC-20 授权模式的现有合约（“父合约”）添加对该提案的支持，可以构建一个转发合约（“转发器”），该合约接受授权并执行以下操作：
1. 从授权中提取用户和存款金额
2. 调用 `receiveWithAuthorization` 将指定资金从用户转移到转发器
3. 批准父合约从转发器支出资金
4. 调用父合约上支出从转发器设置的授权的方法
5. 将任何结果代币的所有权转回给用户

**示例：**

```solidity
interface IDeFiToken {
    function deposit(uint256 amount) external returns (uint256);

    function transfer(address account, uint256 amount)
        external
        returns (bool);
}

contract DepositForwarder {
    bytes4 private constant _RECEIVE_WITH_AUTHORIZATION_SELECTOR = 0xef55bec6;

    IDeFiToken private _parent;
    IERC20 private _token;

    constructor(IDeFiToken parent, IERC20 token) public {
        _parent = parent;
        _token = token;
    }

    function deposit(bytes calldata receiveAuthorization)
        external
        nonReentrant
        returns (uint256)
    {
        (address from, address to, uint256 amount) = abi.decode(
            receiveAuthorization[0:96],
            (address, address, uint256)
        );
        require(to == address(this), "Recipient is not this contract");

        (bool success, ) = address(_token).call(
            abi.encodePacked(
                _RECEIVE_WITH_AUTHORIZATION_SELECTOR,
                receiveAuthorization
            )
        );
        require(success, "Failed to transfer to the forwarder");

        require(
            _token.approve(address(_parent), amount),
            "Failed to set the allowance"
        );

        uint256 tokensMinted = _parent.deposit(amount);
        require(
            _parent.transfer(from, tokensMinted),
            "Failed to transfer the minted tokens"
        );

        uint256 remainder = _token.balanceOf(address(this);
        if (remainder > 0) {
            require(
                _token.transfer(from, remainder),
                "Failed to refund the remainder"
            );
        }

        return tokensMinted;
    }
}
```

## 参考实现

### `EIP7758.sol`

```solidity
abstract contract EIP7758 is IERC20Transfer, EIP712Domain {
    // keccak256("TransferWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)")
    bytes32 public constant TRANSFER_WITH_AUTHORIZATION_TYPEHASH = 0x7c7c6cdb67a18743f49ec6fa9b35f50d52ed05cbed4cc592e13b44501c1a2267;

    // keccak256("ReceiveWithAuthorization(address from,address to,uint256 value,uint256 validAfter,uint256 validBefore,bytes32 nonce)")
    bytes32 public constant RECEIVE_WITH_AUTHORIZATION_TYPEHASH = 0xd099cc98ef71107a616c4f0f941f04c322d8e254fe26b3c6668db87aae413de8;

    mapping(address => mapping(bytes32 => bool)) internal _authorizationStates;

    event AuthorizationUsed(address indexed authorizer, bytes32 indexed nonce);

    string internal constant _INVALID_SIGNATURE_ERROR = "EIP7758: invalid signature";

    function authorizationState(address authorizer, bytes32 nonce)
        external
        view
        returns (bool)
    {
        return _authorizationStates[authorizer][nonce];
    }

    function transferWithAuthorization(
        address from,
        address to,
        uint256 value,
        uint256 validAfter,
        uint256 validBefore,
        bytes32 nonce,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external {
        require(now > validAfter, "EIP7758: authorization is not yet valid");
        require(now < validBefore, "EIP7758: authorization is expired");
        require(
            !_authorizationStates[from][nonce],
            "EIP7758: authorization is used"
        );

        bytes memory data = abi.encode(
            TRANSFER_WITH_AUTHORIZATION_TYPEHASH,
            from,
            to,
            value,
            validAfter,
            validBefore,
            nonce
        );
        require(
            EIP712.recover(DOMAIN_SEPARATOR, v, r, s, data) == from,
            "EIP7758: invalid signature"
        );

        _authorizationStates[from][nonce] = true;
        emit AuthorizationUsed(from, nonce);

        _transfer(from, to, value);
    }
}
```

### `IERC20Transfer.sol`

```solidity
abstract contract IERC20Transfer {
    function _transfer(
        address sender,
        address recipient,
        uint256 amount
    ) internal virtual;
}
```

### `EIP712Domain.sol`
```solidity
abstract contract EIP712Domain {
    bytes32 public DOMAIN_SEPARATOR;
}
```

### `EIP712.sol`

```solidity
library EIP712 {
    // keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)")
    bytes32 public constant EIP712_DOMAIN_TYPEHASH = 0x8b73c3c69bb8fe3d512ecc4cf759cc79239f7b179b0ffacaa9a75d522b39400f;

    function makeDomainSeparator(string memory name, string memory version)
        internal
        view
        returns (bytes32)
    {
        uint256 chainId;
        assembly {
            chainId := chainid()
        }

        return
            keccak256(
                abi.encode(
                    EIP712_DOMAIN_TYPEHASH,
                    keccak256(bytes(name)),
                    keccak256(bytes(version)),
                    bytes32(chainId),
                    address(this)
                )
            );
    }

    function recover(
        bytes32 domainSeparator,
        uint8 v,
        bytes32 r,
        bytes32 s,
        bytes memory typeHashAndData
    ) internal pure returns (address) {
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                domainSeparator,
                keccak256(typeHashAndData)
            )
        );
        address recovered = ecrecover(digest, v, r, s);
        require(recovered != address(0), "EIP712: invalid signature");
        return recovered;
    }
}
```

## 安全考虑

在从其他智能合约调用时，使用 `receiveWithAuthorization` 而不是 `transferWithAuthorization`。攻击者可能会监视交易池以提取转账授权，并在 `transferWithAuthorization` 调用之前进行前置交易，从而在不调用包装函数的情况下执行转账。这可能导致未处理的、被锁定的存款。`receiveWithAuthorization` 通过执行额外检查来防止这种情况，确保调用者是收款人。此外，如果有多个合约函数接受接收授权，应用开发者可以将 nonce 的一些前导字节作为标识符，以防止交叉使用。

在同时提交多个转账时，请注意中继者和矿工将决定它们被处理的顺序。如果交易之间没有相互依赖，这通常不是问题，但对于高度依赖的交易，建议一次提交一个签名授权。

在使用 `ecrecover` 时，必须拒绝零地址，以防止从零地址进行未经授权的转账和批准资金。内置的 `ecrecover` 在提供格式错误的签名时返回零地址。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。