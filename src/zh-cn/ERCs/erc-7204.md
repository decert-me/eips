---
eip: 7204
title: 合约钱包管理代币
description: 专注于智能合约钱包中的可替代代币管理，提供增强的交易灵活性和安全性
author: Xiang (@wenzhenxiang), Ben77 (@ben2077), Mingshi S. (@newnewsms)
discussions-to: https://ethereum-magicians.org/t/token-asset-management-interface-with-smart-contract-wallet/14759
status: 草案
type: 标准跟踪
category: ERC
created: 2023-06-21
requires: 165
---

## 摘要

本提案介绍了一种基于智能合约钱包的代币管理方法，专注于利用智能合约钱包的可编程特性进行资产管理。 
此外，它引入了 `tokenTransfer`、`tokenApprove`、`tokenApproveForAll`、`tokenIsApproveForAll` 和 `tokenAllowance` 等功能，提供对代币交易的增强控制。该方法旨在通过利用智能合约钱包的内置功能来增强代币管理，从而提供一种更灵活、安全和高效的代币交易管理方法。


## 动机

外部拥有账户（EOA）钱包没有状态和代码存储，而智能合约钱包则有。

账户抽象（AA）是智能合约钱包的一个方向，它围绕抽象账户运作。该 ERC 也可以是基于 [ERC-4337](./eip-4337.md) 的扩展或作为钱包的插件。

智能合约钱包允许用户自己的账户具有状态和代码，为钱包带来了可编程性。我们认为还有更多的扩展方向。例如，代币资产管理、代币交易的功能扩展等。

该 ERC 的智能合约钱包接口用于资产管理和资产批准。它支持简单代币 <!-- TODO --> ERC-X，并且 [ERC-20](./eip-20.md) 向后兼容 <!-- TODO --> ERC-X，因此可以兼容管理现有市场中的所有可替代代币。

该提案旨在实现以下目标：

1. 资产由钱包本身分配和管理，例如 `approve` 和 `allowance`，由用户的合约钱包配置，而不是由代币资产合约控制，以避免一些现有 ERC-20 合约的风险。
2. 添加 `tokenTransfer` 功能，交易由非智能钱包本身发起或将验证允许金额。
3. 添加 `tokenApprove`、`tokenAllowance`、`tokenApproveForAll`、`tokenIsApproveForAll` 功能。用户钱包本身支持批准并提供单个代币资产和所有代币资产的批准。
4. 用户钱包可以选择批量批准和批量转账。
5. 用户可以选择在其 `tokenTransfer` 之前和之后添加钩子函数，以增加用户的可玩性。
6. 用户可以选择实现 `tokenReceive` 函数。


## 规范

文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 的描述进行解释。

** 合规合约必须实现 [ERC-165](./eip-165.md) 接口**

```solidity
/// @title ERC-7204 
/// @dev See https://eips.ethereum.org/EIPS/eip-7204
/// @dev Note: the ERC-165 identifier for this interface is 0xf73edcda
pragma solidity ^0.8.20;

interface IERC7204 /* is ERC165 */ {

    /**
     * @notice Used to notify listeners that owner has granted approval to the user to manage assets tokens.
     * @param asset Address of the token
     * @param owner Address of the account that has granted the approval for token‘s assets
     * @param spender Address of the spender
     * @param value The amount allowed to spend
     */
    event TokenApproval(
        address indexed asset,
        address indexed owner, 
        address indexed spender, 
        uint256 value
    );

    /**
     * @notice Used to notify listeners that owner has granted approval to the spender to manage all token .
     * @param asset Address of the token
     * @param owner Address of the account that has granted the approval for token‘s assets
     * @param approved approve all token
     */
    event TokenApprovalForAll(
        address indexed owner, 
        address indexed spender,
        bool approved
    );

    /**
     * @notice Approve token
     * @dev Allows spender address to withdraw from your account multiple times, up to the value amount.
     * @dev If this function is called again it overwrites the current allowance with value.
     * @dev Emits an {TokenApproval} event.
     * @param asset Address of the token
     * @param spender Address of the spender
     * @param value The amount allowed to spend
     * @return success The bool value returns whether the approve is successful
     */
    function tokenApprove(address asset, address spender, uint256 value) 
        external 
        returns (bool success);

    /**
     * @notice read token allowance value
     * @param asset Address of the token
     * @param spender Address of the spender
     * @return remaining The asset amount which spender is still allowed to withdraw from owner.
     */
    function tokenAllowance(address asset, address spender) 
        external
        view
        returns (uint256 remaining);

    /**
     * @notice Approve all token
     * @dev Allows spender address to withdraw from your wallet all token.
     * @dev Emits an {TokenApprovalForAll} event.
     * @param spender Address of the spender
     * @param approved Approved all tokens
     * @return success The bool value returns whether the approve is successful
     */
    function tokenApproveForAll(address spender, bool approved) 
        external 
        returns (bool success);

    /**
     * @notice read spender approved value
     * @param spender Address of the spender
     * @return approved Whether to approved spender all tokens
     */
    function tokenIsApproveForAll(address spender) 
        external
        view
        returns (bool approved);

    /**
     * @notice Transfer token
     * @dev must call asset.transfer() inside the function
     * @dev If the caller is not wallet self, must verify the allowance and update the allowance value
     * @param asset Address of the token
     * @param to Address of the receive
     * @param value The transaction amount
     * @return success The bool value returns whether the transfer is successful
     */
    function tokenTransfer(address asset, address to, uint256 value) 
        external 
        returns (bool success); 
}
```


## 理由

本提案中的关键技术决策包括：

**改进的批准机制**
- **当前与提议**：在现有的 ERC-20 系统中，外部拥有账户（EOA）直接与代币合约交互以进行 `approve`。本提案中的新 `tokenApprove` 和 `tokenApproveForAll` 函数使得在钱包合约内对代币使用进行更精确的控制，相较于传统方法有显著改进。
- **增强的安全性**：该机制通过将批准控制转移到用户的智能合约钱包，减轻了代币过度批准等风险。
- **可编程性**：用户可以设置高级批准策略，例如条件或时间限制的批准，`tokenApproveForAll` 函数特别允许对所有代币进行通用设置。这在传统的 ERC-20 代币中是无法实现的。

**优化的转账流程**
- **效率和安全性**：`tokenTransfer` 函数简化了代币转账流程，使交易更加高效和安全。
- **灵活性**：允许在转账之前和之后集成自定义逻辑（钩子），以便进行额外的安全检查或根据用户需求执行特定操作。

**支持批量操作**
- **提高效率**：用户可以同时处理多个 `approve` 或 `transfer` 操作，显著提升交易效率。
- **增强用户体验**：简化了对众多资产的管理，改善了拥有大额投资组合用户的整体体验。



## 向后兼容性

该 ERC 可作为 [ERC-4337](./eip-4337.md) 的扩展，并与 ERC-4337 向后兼容。

## 安全考虑

未发现安全考虑。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。