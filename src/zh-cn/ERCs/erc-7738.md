---
eip: 7738
title: 无权限脚本注册表
description: 用于获取合约可执行脚本的无权限注册表
author: Victor Zhang (@zhangzhongnan928), James Brown (@JamesSmartCell)
discussions-to: https://ethereum-magicians.org/t/erc-7738-permissionless-script-registry/20503
status: 草案
type: 标准跟踪
category: ERC
created: 2024-07-01
requires: 173
---
## 摘要

本 EIP 提供了一种创建标准注册表的方法，用于定位与代币相关的可执行脚本。

## 动机

[ERC-5169](./eip-5169.md) 为合约提供了客户端脚本查找方法。这要求合约在构造时实现 `ERC-5169` 接口（或允许升级路径）。

本提案概述了一个可以提供原型和认证脚本的合约。该合约将是一个多链单例实例，将在支持的链上以相同地址部署。

### 概述

注册表合约将为给定合约地址提供一组 URI 链接。这些 URI 链接指向可以被钱包、查看器或迷你 DApp 获取的脚本程序。

指针可以通过注册表合约中的设置器无权限地设置。

## 规范

本文件中的关键词“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“不应该”、“推荐”、“可以”和“可选”应按 RFC 2119 中的描述进行解释。

合约必须实现 `IERC7738` 接口。
合约必须在脚本更新时发出 `ScriptUpdate` 事件。
合约应该按顺序返回 `scriptURI`，以便合约脚本条目的 `ERC-173` `owner()` 首先返回（在简单实现的情况下，钱包将选择返回的第一个 `scriptURI`）。
如果有大量的 `scriptURI` 条目，合约应该提供分页浏览条目的方法。

```solidity
interface IERC7738 {
    /// @dev This event emits when the scriptURI is updated, 
    /// so wallets implementing this interface can update a cached script
    event ScriptUpdate(address indexed contractAddress, string[] newScriptURI);

    /// @notice Get the scriptURI for the contract
    /// @return The scriptURI
    function scriptURI(address contractAddress) external view returns (string[] memory);

    /// @notice Update the scriptURI 
    /// emits event ScriptUpdate(address indexed contractAddress, scriptURI memory newScriptURI);
    function setScriptURI(address contractAddress, string[] memory scriptURIList) external;
}
```

本文件中的关键词“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“不应该”、“推荐”、“不推荐”、“可以”和“可选”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

## 理由

此方法允许未实现 [ERC-5169](./eip-5169.md) 接口的合约将脚本与自身关联，并避免了对集中在线服务器的需求，从而避免了安全性问题，并且不需要组织成为数据库的守门人。

## 测试用例

测试工具和部署的说明可以在 [资产文件夹](../assets/eip-7738/tests.md) 中找到。

## 参考实现

```solidity
import "@openzeppelin/contracts/access/Ownable.sol";

contract DecentralisedRegistry is IERC7738 {
    struct ScriptEntry {
        mapping(address => string[]) scriptURIs;
        address[] addrList;
    }

    mapping(address => ScriptEntry) private _scriptURIs;

    function setScriptURI(
        address contractAddress,
        string[] memory scriptURIList
    ) public {
        require (scriptURIList.length > 0, "> 0 entries required in scriptURIList");
        bool isOwnerOrExistingEntry = Ownable(contractAddress).owner() == msg.sender 
            || _scriptURIs[contractAddress].scriptURIs[msg.sender].length > 0;
        _scriptURIs[contractAddress].scriptURIs[msg.sender] = scriptURIList;
        if (!isOwnerOrExistingEntry) {
            _scriptURIs[contractAddress].addrList.push(msg.sender);
        }
        
        emit ScriptUpdate(contractAddress, msg.sender, scriptURIList);
    }

    // Return the list of scriptURI for this contract.
    // Order the return list so `Owner()` assigned scripts are first in the list
    function scriptURI(
        address contractAddress
    ) public view returns (string[] memory) {
        //build scriptURI return list, owner first
        address contractOwner = Ownable(contractAddress).owner();
        address[] memory addrList = _scriptURIs[contractAddress].addrList;
        uint256 i;

        //now calculate list length
        uint256 listLen = _scriptURIs[contractAddress].scriptURIs[contractOwner].length;
        for (i = 0; i < addrList.length; i++) {
            listLen += _scriptURIs[contractAddress].scriptURIs[addrList[i]].length;
        }

        string[] memory ownerScripts = new string[](listLen);

        // Add owner scripts
        uint256 scriptIndex = _addScriptURIs(contractOwner, contractAddress, ownerScripts, 0);

        // Add remainder scripts
        for (uint256 i = 0; i < addrList.length; i++) {
            scriptIndex = _addScriptURIs(addrList[i], contractAddress, ownerScripts, scriptIndex);
        }

        return ownerScripts;
    }

    function _addScriptURIs(
        address user,
        address contractAddress,
        string[] memory ownerScripts,
        uint256 scriptIndex
    ) internal view returns (uint256) {
        for (uint256 j = 0; j < _scriptURIs[contractAddress].scriptURIs[user].length; j++) {
            string memory thisScriptURI = _scriptURIs[contractAddress].scriptURIs[user][j];
            if (bytes(thisScriptURI).length > 0) {
                ownerScripts[scriptIndex++] = thisScriptURI;
            }
        }
        return scriptIndex;
    }
}
```

## 安全考虑

提供的脚本可以通过多种方式进行身份验证：

1. 目标合约由设置器指定，且实现了 [ERC-173](./eip-173.md) `Ownable` 接口。一旦脚本被获取，可以验证签名以匹配 Owner()。在 TokenScript 的情况下，可以通过 DApp 或钱包使用 TokenScript SDK、TokenScript 在线验证服务，或通过从 XML 中提取签名，计算脚本的 keccak256 并使用 ecrecover 签名密钥地址进行检查。
2. 如果合约未实现 Ownable，可以采取进一步措施：
 a. 托管应用/钱包可以使用第三方 API 或区块浏览器确定部署密钥。实现钱包、DApp 或查看器将检查签名是否与此部署密钥匹配。
 b. 签名密钥可以通过托管应用进行预认证，使用嵌入式密钥链。
 c. 治理代币可以允许脚本委员会认证请求以设置和验证密钥。

如果未满足这些标准：
- 对于主网实现，实现钱包应谨慎使用脚本 - 这将由应用和/或用户自行决定。
- 对于测试网，允许脚本功能是可以接受的，由钱包提供者自行决定。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。