---
eip: 1155
title: 多代币标准
author: Witek Radomski <witek@enjin.io>, Andrew Cooke <ac0dem0nk3y@gmail.com>, Philippe Castonguay (@phabc) <pc@horizongames.net>, James Therien <james@turing-complete.com>, Eric Binet <eric@enjin.io>, Ronan Sandford (@wighawag) <wighawag@gmail.com>
type: 标准跟踪
category: ERC
status: 最终
created: 2018-06-17
discussions-to: https://github.com/ethereum/EIPs/issues/1155
requires: 165
---

## 简要总结

一个管理多种代币类型的标准接口。一个已部署的合约可以包含任何组合的可替代代币、不可替代代币或其他配置（例如半可替代代币）。

## 摘要

该标准概述了一个智能合约接口，可以表示任意数量的可替代和不可替代代币类型。现有标准如 ERC-20 需要为每种代币类型部署单独的合约。ERC-721 标准的代币 ID 是一个单一的不可替代索引，这些不可替代代币的组作为一个单一合约部署，设置适用于整个集合。相比之下，ERC-1155 多代币标准允许每个代币 ID 代表一个新的可配置代币类型，该类型可以具有自己的元数据、供应量和其他属性。

每个函数的参数集中包含的 `_id` 参数表示交易中的特定代币或代币类型。

## 动机

代币标准如 ERC-20 和 ERC-721 需要为每种代币类型或集合部署单独的合约。这在以太坊区块链上放置了大量冗余字节码，并通过将每个代币合约分隔到其自己的权限地址中限制了某些功能。随着区块链游戏和像 Enjin Coin 这样的平台的兴起，游戏开发者可能会创建成千上万的代币类型，因此需要一种新的代币标准来支持它们。然而，ERC-1155 并不特定于游戏，许多其他应用程序也可以从这种灵活性中受益。

这种设计使得新功能成为可能，例如一次转移多种代币，从而节省交易成本。多种代币的交易（托管/原子交换）可以建立在此标准之上，并且不再需要单独“批准”每个代币合约。描述和混合多种可替代或不可替代代币类型在一个合约中也变得简单。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

**实现 ERC-1155 标准的智能合约 MUST 实现 `ERC1155` 接口中的所有函数。**

**实现 ERC-1155 标准的智能合约 MUST 实现 ERC-165 `supportsInterface` 函数，并且如果通过 `interfaceID` 参数传递 `0xd9b67a26`，则 MUST 返回常量值 `true`。**

```solidity
pragma solidity ^0.5.9;

/**
    @title ERC-1155 Multi Token Standard
    @dev See https://eips.ethereum.org/EIPS/eip-1155
    Note: The ERC-165 identifier for this interface is 0xd9b67a26.
 */
interface ERC1155 /* is ERC165 */ {
    /**
        @dev Either `TransferSingle` or `TransferBatch` MUST emit when tokens are transferred, including zero value transfers as well as minting or burning (see "Safe Transfer Rules" section of the standard).
        The `_operator` argument MUST be the address of an account/contract that is approved to make the transfer (SHOULD be msg.sender).
        The `_from` argument MUST be the address of the holder whose balance is decreased.
        The `_to` argument MUST be the address of the recipient whose balance is increased.
        The `_id` argument MUST be the token type being transferred.
        The `_value` argument MUST be the number of tokens the holder balance is decreased by and match what the recipient balance is increased by.
        When minting/creating tokens, the `_from` argument MUST be set to `0x0` (i.e. zero address).
        When burning/destroying tokens, the `_to` argument MUST be set to `0x0` (i.e. zero address).        
    */
    event TransferSingle(address indexed _operator, address indexed _from, address indexed _to, uint256 _id, uint256 _value);

    /**
        @dev Either `TransferSingle` or `TransferBatch` MUST emit when tokens are transferred, including zero value transfers as well as minting or burning (see "Safe Transfer Rules" section of the standard).      
        The `_operator` argument MUST be the address of an account/contract that is approved to make the transfer (SHOULD be msg.sender).
        The `_from` argument MUST be the address of the holder whose balance is decreased.
        The `_to` argument MUST be the address of the recipient whose balance is increased.
        The `_ids` argument MUST be the list of tokens being transferred.
        The `_values` argument MUST be the list of number of tokens (matching the list and order of tokens specified in _ids) the holder balance is decreased by and match what the recipient balance is increased by.
        When minting/creating tokens, the `_from` argument MUST be set to `0x0` (i.e. zero address).
        When burning/destroying tokens, the `_to` argument MUST be set to `0x0` (i.e. zero address).                
    */
    event TransferBatch(address indexed _operator, address indexed _from, address indexed _to, uint256[] _ids, uint256[] _values);

    /**
        @dev MUST emit when approval for a second party/operator address to manage all tokens for an owner address is enabled or disabled (absence of an event assumes disabled).        
    */
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

    /**
        @dev MUST emit when the URI is updated for a token ID.
        URIs are defined in RFC 3986.
        The URI MUST point to a JSON file that conforms to the "ERC-1155 Metadata URI JSON Schema".
    */
    event URI(string _value, uint256 indexed _id);

    /**
        @notice Transfers `_value` amount of an `_id` from the `_from` address to the `_to` address specified (with safety call).
        @dev Caller must be approved to manage the tokens being transferred out of the `_from` account (see "Approval" section of the standard).
        MUST revert if `_to` is the zero address.
        MUST revert if balance of holder for token `_id` is lower than the `_value` sent.
        MUST revert on any other error.
        MUST emit the `TransferSingle` event to reflect the balance change (see "Safe Transfer Rules" section of the standard).
        After the above conditions are met, this function MUST check if `_to` is a smart contract (e.g. code size > 0). If so, it MUST call `onERC1155Received` on `_to` and act appropriately (see "Safe Transfer Rules" section of the standard).        
        @param _from    Source address
        @param _to      Target address
        @param _id      ID of the token type
        @param _value   Transfer amount
        @param _data    Additional data with no specified format, MUST be sent unaltered in call to `onERC1155Received` on `_to`
    */
    function safeTransferFrom(address _from, address _to, uint256 _id, uint256 _value, bytes calldata _data) external;

    /**
        @notice Transfers `_values` amount(s) of `_ids` from the `_from` address to the `_to` address specified (with safety call).
        @dev Caller must be approved to manage the tokens being transferred out of the `_from` account (see "Approval" section of the standard).
        MUST revert if `_to` is the zero address.
        MUST revert if length of `_ids` is not the same as length of `_values`.
        MUST revert if any of the balance(s) of the holder(s) for token(s) in `_ids` is lower than the respective amount(s) in `_values` sent to the recipient.
        MUST revert on any other error.        
        MUST emit `TransferSingle` or `TransferBatch` event(s) such that all the balance changes are reflected (see "Safe Transfer Rules" section of the standard).
        Balance changes and events MUST follow the ordering of the arrays (_ids[0]/_values[0] before _ids[1]/_values[1], etc).
        After the above conditions for the transfer(s) in the batch are met, this function MUST check if `_to` is a smart contract (e.g. code size > 0). If so, it MUST call the relevant `ERC1155TokenReceiver` hook(s) on `_to` and act appropriately (see "Safe Transfer Rules" section of the standard).                      
        @param _from    Source address
        @param _to      Target address
        @param _ids     IDs of each token type (order and length must match _values array)
        @param _values  Transfer amounts per token type (order and length must match _ids array)
        @param _data    Additional data with no specified format, MUST be sent unaltered in call to the `ERC1155TokenReceiver` hook(s) on `_to`
    */
    function safeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external;

    /**
        @notice Get the balance of an account's tokens.
        @param _owner  The address of the token holder
        @param _id     ID of the token
        @return        The _owner's balance of the token type requested
     */
    function balanceOf(address _owner, uint256 _id) external view returns (uint256);

    /**
        @notice Get the balance of multiple account/token pairs
        @param _owners The addresses of the token holders
        @param _ids    ID of the tokens
        @return        The _owner's balance of the token types requested (i.e. balance for each (owner, id) pair)
     */
    function balanceOfBatch(address[] calldata _owners, uint256[] calldata _ids) external view returns (uint256[] memory);

    /**
        @notice Enable or disable approval for a third party ("operator") to manage all of the caller's tokens.
        @dev MUST emit the ApprovalForAll event on success.
        @param _operator  Address to add to the set of authorized operators
        @param _approved  True if the operator is approved, false to revoke approval
    */
    function setApprovalForAll(address _operator, bool _approved) external;

    /**
        @notice Queries the approval status of an operator for a given owner.
        @param _owner     The owner of the tokens
        @param _operator  Address of authorized operator
        @return           True if the operator is approved, false if not
    */
    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}
```

### ERC-1155 代币接收者

**智能合约 MUST 实现 `ERC1155TokenReceiver` 接口中的所有函数以接受转账。有关详细信息，请参见“安全转账规则”。**

**智能合约 MUST 实现 ERC-165 `supportsInterface` 函数，并表示支持 `ERC1155TokenReceiver` 接口以接受转账。有关详细信息，请参见“ERC1155TokenReceiver ERC-165 规则”。**

```solidity
pragma solidity ^0.5.9;

/**
    Note: The ERC-165 identifier for this interface is 0x4e2312e0.
*/
interface ERC1155TokenReceiver {
    /**
        @notice Handle the receipt of a single ERC1155 token type.
        @dev An ERC1155-compliant smart contract MUST call this function on the token recipient contract, at the end of a `safeTransferFrom` after the balance has been updated.        
        This function MUST return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` (i.e. 0xf23a6e61) if it accepts the transfer.
        This function MUST revert if it rejects the transfer.
        Return of any other value than the prescribed keccak256 generated value MUST result in the transaction being reverted by the caller.
        @param _operator  The address which initiated the transfer (i.e. msg.sender)
        @param _from      The address which previously owned the token
        @param _id        The ID of the token being transferred
        @param _value     The amount of tokens being transferred
        @param _data      Additional data with no specified format
        @return           `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
    */
    function onERC1155Received(address _operator, address _from, uint256 _id, uint256 _value, bytes calldata _data) external returns(bytes4);

    /**
        @notice Handle the receipt of multiple ERC1155 token types.
        @dev An ERC1155-compliant smart contract MUST call this function on the token recipient contract, at the end of a `safeBatchTransferFrom` after the balances have been updated.        
        This function MUST return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` (i.e. 0xbc197c81) if it accepts the transfer(s).
        This function MUST revert if it rejects the transfer(s).
        Return of any other value than the prescribed keccak256 generated value MUST result in the transaction being reverted by the caller.
        @param _operator  The address which initiated the batch transfer (i.e. msg.sender)
        @param _from      The address which previously owned the token
        @param _ids       An array containing ids of each token being transferred (order and length must match _values array)
        @param _values    An array containing amounts of each token being transferred (order and length must match _ids array)
        @param _data      Additional data with no specified format
        @return           `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
    */
    function onERC1155BatchReceived(address _operator, address _from, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external returns(bytes4);       
}
```

### 安全转账规则

为了更明确地说明标准的 `safeTransferFrom` 和 `safeBatchTransferFrom` 函数在与 `ERC1155TokenReceiver` 钩子函数的关系中如何操作，以下是一些场景和规则。

#### 场景

**_场景#1 :_** 接收者不是合约。
* `onERC1155Received` 和 `onERC1155BatchReceived` MUST NOT 在 EOA（外部拥有账户）上被调用。

**_场景#2 :_** 交易不是代币的铸造/转移。
* `onERC1155Received` 和 `onERC1155BatchReceived` MUST NOT 在铸造或转移过程之外被调用。

**_场景#3 :_** 接收者未实现必要的 `ERC1155TokenReceiver` 接口函数。
* 转账 MUST 被撤销，唯一的例外如下。
    - 如果发送的代币是另一个标准的混合实现的一部分，则可以遵循该标准在发送到合约时的规则。请参见“向后兼容性”部分。

**_场景#4 :_** 接收者实现了必要的 `ERC1155TokenReceiver` 接口函数，但返回了未知值。
* 转账 MUST 被撤销。

**_场景#5 :_** 接收者实现了必要的 `ERC1155TokenReceiver` 接口函数，但抛出错误。
* 转账 MUST 被撤销。

**_场景#6 :_** 接收者实现了 `ERC1155TokenReceiver` 接口，并且是一个且仅一个余额变更的接收者（例如调用 `safeTransferFrom`）。
* 转账的余额 MUST 在调用接收者合约上的 `ERC1155TokenReceiver` 钩子之前更新。
* 转账事件 MUST 在调用接收者合约上的 `ERC1155TokenReceiver` 钩子之前被发出，以反映余额变更。
* `onERC1155Received` 或 `onERC1155BatchReceived` MUST 在接收者合约上被调用。
* `onERC1155Received` 钩子 SHOULD 在接收者合约上被调用并遵循其规则。
    - 请参见“onERC1155Received 规则”以获取必须遵循的进一步规则。
* `onERC1155BatchReceived` 钩子 MAY 在接收者合约上被调用并遵循其规则。
    - 请参见“onERC1155BatchReceived 规则”以获取必须遵循的进一步规则。

**_场景#7 :_** 接收者实现了 `ERC1155TokenReceiver` 接口，并且是多个余额变更的接收者（例如调用 `safeBatchTransferFrom`）。
* 在调用接收者合约上的 `ERC1155TokenReceiver` 钩子之前，所有在调用 `ERC1155TokenReceiver` 钩子中引用的余额转账 MUST 被更新。
* 在调用接收者合约上的 `ERC1155TokenReceiver` 钩子之前，所有转账事件 MUST 被发出以反映当前余额变更。
* `onERC1155Received` 或 `onERC1155BatchReceived` MUST 在接收者上被调用，次数应足以确保场景中每个余额变更都被考虑。
    - 每个钩子调用的返回魔法值 MUST 被检查并根据“onERC1155Received 规则”和“onERC1155BatchReceived 规则”进行处理。
* `onERC1155BatchReceived` 钩子 SHOULD 在接收者合约上被调用并遵循其规则。    
    - 请参见“onERC1155BatchReceived 规则”以获取必须遵循的进一步规则。
* `onERC1155Received` 钩子 MAY 在接收者合约上被调用并遵循其规则。    
    - 请参见“onERC1155Received 规则”以获取必须遵循的进一步规则。

**_场景#8 :_** 你是实现了 `ERC1155TokenReceiver` 接口的合约的创建者，并且你在 `onERC1155Received` 和 `onERC1155BatchReceived` 中将代币转发到另一个地址。
* 转发应被视为接受，然后在新上下文中发起新的 `safeTransferFrom` 或 `safeBatchTransferFrom`。
    - 在转发成功后，必须返回接收者钩子的规定 keccak256 接受值魔法。
* `_data` 参数 MAY 被重新用于新上下文。
* 如果转发失败，交易 MAY 被撤销。
    - 如果合约逻辑希望在这种情况下保留代币的所有权，则可以这样做。
    
**_场景#9 :_** 您正在通过非标准 API 调用转移代币，即特定实现的 API，而不是 `safeTransferFrom` 或 `safeBatchTransferFrom`。
* 在这种情况下，所有余额更新和事件输出规则与调用标准转移函数时相同。
    - 即，外部查看者仍然必须能够通过标准函数查询余额，并且必须与仅通过 `TransferSingle` 和 `TransferBatch` 事件确定的余额相同。
* 如果接收者是一个合约，则仍需在其上调用 `ERC1155TokenReceiver` 钩子，并且返回值应与调用标准转移函数时相同。
    - 然而，虽然 `safeTransferFrom` 或 `safeBatchTransferFrom` 函数必须在接收合约未实现 `ERC1155TokenReceiver` 接口时回退，但非标准函数可以继续进行转移。
    - 请参见“特定实现转移 API 规则”。

#### 规则

**_safeTransferFrom 规则:_**
* 调用者必须获得批准以管理从 `_from` 账户转出的代币（请参见“批准”部分）。
* 如果 `_to` 是零地址，则必须回退。
* 如果持有者的 `_id` 代币余额低于发送给接收者的 `_value`，则必须回退。
* 在任何其他错误情况下必须回退。
* 必须发出 `TransferSingle` 事件以反映余额变化（请参见“TransferSingle 和 TransferBatch 事件规则”部分）。
* 在满足上述条件后，此函数必须检查 `_to` 是否为智能合约（例如，代码大小 > 0）。如果是，则必须在 `_to` 上调用 `onERC1155Received` 并采取适当的行动（请参见“onERC1155Received 规则”部分）。
    - 发送者为转移提供的 `_data` 参数必须以其内容不变的方式传递给 `onERC1155Received` 钩子函数，通过其 `_data` 参数。

**_safeBatchTransferFrom 规则:_**
* 调用者必须获得批准以管理从 `_from` 账户转出的所有代币（请参见“批准”部分）。
* 如果 `_to` 是零地址，则必须回退。
* 如果 `_ids` 的长度与 `_values` 的长度不相同，则必须回退。
* 如果 `_ids` 中的持有者的任何代币余额低于发送给接收者的相应 `_values` 数量，则必须回退。
* 在任何其他错误情况下必须回退。
* 必须发出 `TransferSingle` 或 `TransferBatch` 事件，以便所有余额变化都得到反映（请参见“TransferSingle 和 TransferBatch 事件规则”部分）。
* 余额变化和事件必须按照提交的数组顺序发生（_ids[0]/_values[0] 在 _ids[1]/_values[1] 之前，等等）。
* 在满足上述条件后，此函数必须检查 `_to` 是否为智能合约（例如，代码大小 > 0）。如果是，则必须在 `_to` 上调用 `onERC1155Received` 或 `onERC1155BatchReceived` 并采取适当的行动（请参见“onERC1155Received 和 onERC1155BatchReceived 规则”部分）。
    - 发送者为转移提供的 `_data` 参数必须以其内容不变的方式传递给 `ERC1155TokenReceiver` 钩子函数，通过其 `_data` 参数。

**_TransferSingle 和 TransferBatch 事件规则:_**
* `TransferSingle` 应该用于指示在 `_from` 和 `_to` 之间发生了单个余额转移。
    - 它可以多次发出以指示交易中的多个余额变化，但请注意，`TransferBatch` 是为此设计的，以减少 gas 消耗。
    - `_operator` 参数必须是获得批准进行转移的账户/合约的地址（应该是 msg.sender）。
    - `_from` 参数必须是余额减少的持有者的地址。
    - `_to` 参数必须是余额增加的接收者的地址。
    - `_id` 参数必须是被转移的代币类型。
    - `_value` 参数必须是持有者余额减少的代币数量，并与接收者余额增加的数量相匹配。
    - 在铸造/创建代币时，`_from` 参数必须设置为 `0x0`（即零地址）。请参见“铸造/创建和销毁/销毁规则”。
    - 在销毁/销毁代币时，`_to` 参数必须设置为 `0x0`（即零地址）。请参见“铸造/创建和销毁/销毁规则”。
* `TransferBatch` 应该用于指示在 `_from` 和 `_to` 之间发生了多个余额转移。
    - 它可以在列表中发出单个元素以指示交易中的单一余额变化，但请注意，`TransferSingle` 是为此设计的，以减少 gas 消耗。
    - `_operator` 参数必须是获得批准进行转移的账户/合约的地址（应该是 msg.sender）。
    - `_from` 参数必须是每个 `_ids` 和 `_values` 条目对中余额减少的持有者的地址。
    - `_to` 参数必须是每个 `_ids` 和 `_values` 条目对中余额增加的接收者的地址。
    - `_ids` 数组参数必须包含被转移的代币的 ID。
    - `_values` 数组参数必须包含每个对应的 `_ids` 条目要转移的代币数量。
    - `_ids` 和 `_values` 必须具有相同的长度。
    - 在铸造/创建代币时，`_from` 参数必须设置为 `0x0`（即零地址）。请参见“铸造/创建和销毁/销毁规则”。
    - 在销毁/销毁代币时，`_to` 参数必须设置为 `0x0`（即零地址）。请参见“铸造/创建和销毁/销毁规则”。
* 从地址 `0x0` 转移的总值减去通过 `TransferSingle` 和 `TransferBatch` 事件观察到的转移到 `0x0` 的总值，可以被客户端和交易所用来确定给定代币 ID 的“流通供应”。
* 为了广播一个初始余额为零的代币 ID 的存在，合约应该从 `0x0` 发出 `TransferSingle` 事件到 `0x0`，以代币创建者作为 `_operator`，并且 `_value` 为 0。
* 所有 `TransferSingle` 和 `TransferBatch` 事件必须被发出，以反映在调用任何 `onERC1155Received` 或 `onERC1155BatchReceived` 之前发生的所有余额变化。
    - 为了确保在有效重入的情况下事件顺序正确（例如，如果接收合约在接收时转发代币），状态余额和事件余额必须在调用外部合约之前匹配。

**_onERC1155Received 规则:_**
- `_operator` 参数必须是获得批准进行转移的账户/合约的地址（应该是 msg.sender）。
* `_from` 参数必须是余额减少的持有者的地址。
    - `_from` 在铸造时必须为 0x0。
* `_id` 参数必须是被转移的代币类型。
* `_value` 参数必须是持有者余额减少的代币数量，并与接收者余额增加的数量相匹配。
* `_data` 参数必须包含发送者为转移提供的信息，其内容不变。
    - 即，必须传递通过 `safeTransferFrom` 或 `safeBatchTransferFrom` 调用发送的未更改的 `_data` 参数。
* 接收合约可以通过返回接受魔法值 `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` 来接受其余额的增加。
    - 如果返回值是 `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`，则转移必须完成，或者如果未满足任何其他成功条件，则必须回退。
* 接收合约可以通过调用回退来拒绝其余额的增加。
    - 如果接收合约抛出/回退，则交易必须回退。
* 如果返回值不是 `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`，则交易必须回退。
* `onERC1155Received`（和/或 `onERC1155BatchReceived`）可以在单个交易中多次调用，必须满足以下要求：
    - 所有回调表示互斥的余额变化。
    - 所有调用 `onERC1155Received` 和 `onERC1155BatchReceived` 的集合描述了在交易中按提交顺序发生的所有余额变化。
* 如果转移操作是将代币转移到自身，则合约可以跳过调用 `onERC1155Received` 钩子函数。
**_onERC1155BatchReceived 规则:_**
- `_operator` 参数必须是被批准进行转移的账户/合约地址（应为 msg.sender）。
* `_from` 参数必须是余额减少的持有者地址。
    - `_from` 在铸造时必须为 0x0。    
* `_ids` 参数必须是被转移的代币列表。
* `_values` 参数必须是持有者余额减少的代币数量列表（与 `_ids` 中指定的代币列表和顺序相匹配），并与接收者余额增加的数量相匹配。
* `_data` 参数必须包含发送者为转移提供的信息，其内容不得更改。
    - 即，它必须传递通过 `safeBatchTransferFrom` 调用发送的未更改的 `_data` 参数。
* 接收合约可以通过返回接受魔法值 `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` 来接受其余额的增加。
    - 如果返回值是 `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`，则转移必须完成，或者如果未满足任何其他成功条件，则必须回滚。
* 接收合约可以通过调用 revert 来拒绝其余额的增加。
    - 如果接收合约抛出/回滚，则交易必须回滚。
* 如果返回值不是 `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`，则交易必须回滚。
* `onERC1155BatchReceived`（和/或 `onERC1155Received`）可以在单个交易中被多次调用，且必须满足以下要求：
    - 所有回调表示互斥的余额变化。
    - 所有对 `onERC1155Received` 和 `onERC1155BatchReceived` 的调用集合描述了在交易过程中发生的所有余额变化，按提交顺序排列。
* 如果转移操作是将代币转移到自身，则合约可以跳过调用 `onERC1155BatchReceived` 钩子函数。
    
**_ERC1155TokenReceiver ERC-165 规则:_**
* ERC-165 `supportsInterface` 函数的实现应如下所示：
    ```solidity
    function supportsInterface(bytes4 interfaceID) external view returns (bool) {
        return  interfaceID == 0x01ffc9a7 ||    // ERC-165 support (i.e. `bytes4(keccak256('supportsInterface(bytes4)'))`).
                interfaceID == 0x4e2312e0;      // ERC-1155 `ERC1155TokenReceiver` support (i.e. `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)")) ^ bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`).
    }
    ```
* 实现可以与上述不同，但：
  - 如果 `0x01ffc9a7` 通过 `interfaceID` 参数传递，则必须返回常量值 `true`。这表示支持 ERC-165。
  - 如果 `0x4e2312e0` 通过 `interfaceID` 参数传递，则必须返回常量值 `true`。这表示支持 ERC-1155 `ERC1155TokenReceiver`。
  - 不得消耗超过 10,000 gas。
    - 这使其低于 ERC-165 要求的 30,000 gas，减少了 gas 储备需求，并最小化了在调用期间 gas 耗尽的可能副作用。

**_特定实现转移 API 规则:_**
* 如果使用特定实现的 API 函数将 ERC-1155 代币转移到合约，则如果接收者实现了 `ERC1155TokenReceiver` 接口，则仍必须遵循 `safeTransferFrom` 或 `safeBatchTransferFrom`（视情况而定）规则。如果没有，则非标准实现应回滚，但可以继续。    
* 示例：
    1. 被批准的用户调用一个函数，例如 `function myTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values);`。
    2. `myTransferFrom` 更新 `_from` 和 `_to` 地址的所有 `_ids` 和 `_values` 的余额。
    3. `myTransferFrom` 触发 `TransferBatch` 事件，包含从地址 `_from` 转移到地址 `_to` 的详细信息。
    4. `myTransferFrom` 检查 `_to` 是否为合约地址，并确定它是（如果不是，则转移可以视为成功）。
    5. `myTransferFrom` 在 `_to` 上调用 `onERC1155BatchReceived`，并且它回滚或返回未知值（如果返回 `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`，则转移可以视为成功）。    
    6. 此时，`myTransferFrom` 应立即回滚交易，因为 `onERC1155BatchReceived` 函数未明确接受代币的接收。            
    7. 如果 `myTransferFrom` 希望继续，则必须在 `_to` 上调用 `supportsInterface(0x4e2312e0)`，如果返回常量值 `true`，则交易必须回滚，因为现在已知它是有效接收者，而先前的接受步骤失败。 
        - 注意：如果您希望更早地收集和处理该信息，可以在先前的步骤中调用 `supportsInterface(0x4e2312e0)`。
    8. 如果对 `_to` 的上述 `supportsInterface(0x4e2312e0)` 调用回滚或返回的值不是常量值 `true`，则 `myTransferFrom` 函数可以视为此转移成功。
        - __注意__: 如果发送到不期望接收 ERC-1155 代币的地址，这可能会导致无法恢复的代币。
* 上述示例并不详尽，但说明了主要要点（并显示大多数与 `safeTransferFrom` 和 `safeBatchTransferFrom` 共享）：
    - 更新的余额必须触发相应的转移事件。
    - 必须检查接收地址是否为合约，如果是，则必须在其上调用相关的 `ERC1155TokenReceiver` 钩子函数。 
    - 在调用 `ERC1155TokenReceiver` 钩子之前，必须更新（并触发）在调用中引用的余额（和相关事件）。
    - 调用的 `ERC1155TokenReceiver` 钩子函数的返回值必须得到尊重（如果已实现）。    
    - 只有非标准转移函数可以允许代币发送到不实现必要的 `ERC1155TokenReceiver` 钩子函数的接收合约。在这种情况下，`safeTransferFrom` 和 `safeBatchTransferFrom` 必须回滚（除非是混合标准实现，见“向后兼容性”）。

**_铸造/创建和销毁/销毁规则:_**
* 铸造/创建操作本质上是一个特殊的转移，必须遵循以下规则：
    - 为了广播没有初始余额的代币 ID 的存在，合约应从 `0x0` 发出 `TransferSingle` 事件到 `0x0`，以代币创建者作为 `_operator`，并且 `_value` 为 0。
    - 必须根据铸造（即单个或批量）适当遵循“TransferSingle 和 TransferBatch 事件规则”，但是 `_from` 参数必须设置为 `0x0`（即零地址），以标记转移为铸造给合约观察者。
        - __注意:__ 这包括在合约中给予初始余额的代币。合约的余额也必须能够仅通过事件来确定，这意味着初始合约余额（例如，在构造中）也必须发出事件以反映这些余额。            
* 销毁/销毁操作本质上是一个特殊的转移，必须遵循以下规则：
    - 必须根据销毁（即单个或批量）适当遵循“TransferSingle 和 TransferBatch 事件规则”，但是 `_to` 参数必须设置为 `0x0`（即零地址），以标记转移为销毁给合约观察者。           
    - 在销毁/销毁时，您不必实际转移到 `0x0`（这取决于实现），仅事件中的 `_to` 参数必须设置为 `0x0`，如上所述。
* 从地址 `0x0` 转移的总值减去通过 `TransferSingle` 和 `TransferBatch` 事件观察到的转移到 `0x0` 的总值，可以被客户端和交易所用来确定给定代币 ID 的“流通供应”。
* 如上所述，铸造/创建和销毁/销毁操作是特殊的转移，因此可能会通过自定义转移函数而不是 `safeTransferFrom` 或 `safeBatchTransferFrom` 来完成。如果是这样，“特定实现转移 API 规则”部分将是适当的。   
    - 即使在非安全 API 和/或混合标准情况下，铸造/创建或销毁/销毁时仍必须遵循上述事件规则。
* 如果铸造操作是将代币转移到自身，则合约可以跳过调用 `ERC1155TokenReceiver` 钩子函数。在所有其他情况下，必须根据实现适当遵循 `ERC1155TokenReceiver` 规则（即安全、自定义和/或混合）。 


##### Solidity 示例的 keccak256 生成常量（这些可以被实现使用）：

```solidity
bytes4 constant public ERC1155_ERC165 = 0xd9b67a26; // ERC-165 identifier for the main token standard.
bytes4 constant public ERC1155_ERC165_TOKENRECEIVER = 0x4e2312e0; // ERC-165 identifier for the `ERC1155TokenReceiver` support (i.e. `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)")) ^ bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`).
bytes4 constant public ERC1155_ACCEPTED = 0xf23a6e61; // Return value from `onERC1155Received` call if a contract accepts receipt (i.e `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`).
bytes4 constant public ERC1155_BATCH_ACCEPTED = 0xbc197c81; // Return value from `onERC1155BatchReceived` call if a contract accepts receipt (i.e `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`).
```

### 元数据

URI 值允许客户端进行 ID 替换。如果字符串 `{id}` 存在于任何 URI 中，客户端必须将其替换为实际的代币 ID 的十六进制形式。这允许大量代币使用相同的链上字符串，通过一次定义 URI 来实现。

* 替换的十六进制 ID 的字符串格式必须是小写字母数字：[0-9a-f]，且没有 0x 前缀。
* 替换的十六进制 ID 的字符串格式必须在必要时用零填充到 64 个十六进制字符长度。

这样的 URI 示例：`https://token-cdn-domain/{id}.json` 如果客户端引用代币 ID 314592/0x4CCE0，将被替换为 `https://token-cdn-domain/000000000000000000000000000000000000000000000000000000000004cce0.json`。

#### 元数据扩展

可选的 `ERC1155Metadata_URI` 扩展可以通过 [ERC-165 标准接口检测](./eip-165.md) 来识别。

如果包含可选的 `ERC1155Metadata_URI` 扩展：
* 如果 `0x0e89341c` 通过 `interfaceID` 参数传递，则 ERC-165 `supportsInterface` 函数必须返回常量值 `true`。
* 对 URI 的_更改_ 必须在可以通过事件表达的情况下触发 `URI` 事件（即它不是动态/程序化的）。
    - 实现可以在铸造操作期间发出 `URI` 事件，但这不是强制性的。如果未发出事件，观察者可以在铸造时从 `uri` 函数获取元数据 URI。    
* 如果未发出事件，则应使用 `uri` 函数检索值。 
* 如果发出了事件，则 `uri` 函数必须返回与最新事件相同的值。
* `uri` 函数不得用于检查代币的存在，因为实现可能返回有效字符串，即使代币不存在。

```solidity
pragma solidity ^0.5.9;

/**
    Note: The ERC-165 identifier for this interface is 0x0e89341c.
*/
interface ERC1155Metadata_URI {
    /**
        @notice A distinct Uniform Resource Identifier (URI) for a given token.
        @dev URIs are defined in RFC 3986.
        The URI MUST point to a JSON file that conforms to the "ERC-1155 Metadata URI JSON Schema".        
        @return URI string
    */
    function uri(uint256 _id) external view returns (string memory);
}
```

#### ERC-1155 元数据 URI JSON 架构

此 JSON 架构大致基于“ERC721 元数据 JSON 架构”，但包括可选格式以允许客户端进行 ID 替换。如果字符串 `{id}` 存在于任何 JSON 值中，所有遵循此标准的客户端软件必须将其替换为实际的代币 ID。

* 替换的十六进制 ID 的字符串格式必须是小写字母数字：[0-9a-f]，且没有 0x 前缀。
* 替换的十六进制 ID 的字符串格式必须在必要时用零填充到 64 个十六进制字符长度。

```json
{
    "title": "Token Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "Identifies the asset to which this token represents"
        },
        "decimals": {
            "type": "integer",
            "description": "The number of decimal places that the token amount should display - e.g. 18, means to divide the token amount by 1000000000000000000 to get its user representation."
        },
        "description": {
            "type": "string",
            "description": "Describes the asset to which this token represents"
        },
        "image": {
            "type": "string",
            "description": "A URI pointing to a resource with mime type image/* representing the asset to which this token represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
        },
        "properties": {
            "type": "object",
            "description": "Arbitrary properties. Values may be strings, numbers, object or arrays."
        }
    }
}
```

ERC-1155 元数据 JSON 文件的示例如下。属性数组建议了一些用于代币特定显示属性和元数据的格式。

```json
{
	"name": "Asset Name",
	"description": "Lorem ipsum...",
	"image": "https:\/\/s3.amazonaws.com\/your-bucket\/images\/{id}.png",
	"properties": {
		"simple_property": "example value",
		"rich_property": {
			"name": "Name",
			"value": "123",
			"display_value": "123 Example Value",
			"class": "emphasis",
			"css": {
				"color": "#ffffff",
				"font-weight": "bold",
				"text-decoration": "underline"
			}
		},
		"array_property": {
			"name": "Name",
			"value": [1,2,3,4],
			"class": "emphasis"
		}
	}
}
```

##### 本地化

元数据本地化应标准化，以提高所有语言的展示统一性。因此，提出了一种简单的覆盖方法来启用本地化。如果元数据 JSON 文件包含 `localization` 属性，则其内容可以用于提供需要本地化的字段的本地化值。`localization` 属性应是一个子对象，包含三个属性：`uri`、`default` 和 `locales`。如果字符串 `{locale}` 存在于任何 URI 中，所有客户端软件必须将其替换为所选的语言环境。

##### JSON 架构

```json
{
    "title": "Token Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "Identifies the asset to which this token represents",
        },
        "decimals": {
            "type": "integer",
            "description": "The number of decimal places that the token amount should display - e.g. 18, means to divide the token amount by 1000000000000000000 to get its user representation."
        },
        "description": {
            "type": "string",
            "description": "Describes the asset to which this token represents"
        },
        "image": {
            "type": "string",
            "description": "A URI pointing to a resource with mime type image/* representing the asset to which this token represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
        },
        "properties": {
            "type": "object",
            "description": "Arbitrary properties. Values may be strings, numbers, object or arrays.",
        },
        "localization": {
            "type": "object",
            "required": ["uri", "default", "locales"],
            "properties": {
                "uri": {
                    "type": "string",
                    "description": "The URI pattern to fetch localized data from. This URI should contain the substring `{locale}` which will be replaced with the appropriate locale value before sending the request."
                },
                "default": {
                    "type": "string",
                    "description": "The locale of the default data within the base JSON"
                },
                "locales": {
                    "type": "array",
                    "description": "The list of locales for which data is available. These locales should conform to those defined in the Unicode Common Locale Data Repository (http://cldr.unicode.org/)."
                }
            }
        }
    }
}
```

##### 本地化示例

基础 URI：
```json
{
  "name": "Advertising Space",
  "description": "Each token represents a unique Ad space in the city.",
  "localization": {
    "uri": "ipfs://QmWS1VAdMD353A6SDk9wNyvkT14kyCiZrNDYAad4w1tKqT/{locale}.json",
    "default": "en",
    "locales": ["en", "es", "fr"]
  }
}
```

es.json:
```json
{
  "name": "Espacio Publicitario",
  "description": "Cada token representa un espacio publicitario único en la ciudad."
}
```

fr.json:
```json
{
  "name": "Espace Publicitaire",
  "description": "Chaque jeton représente un espace publicitaire unique dans la ville."
}
```

### 授权

函数 `setApprovalForAll` 允许操作员代表批准者管理其整个代币集。为了允许对代币 ID 子集的批准，建议使用诸如 [ERC-1761 Scoped Approval Interface](./eip-1761.md) 的接口。
对应的 `isApprovedForAll` 提供了对 `setApprovalForAll` 设置的任何状态的内省。

应假定所有者始终能够操作自己的代币，而不论批准状态如何，因此不应在操作之前调用 `setApprovalForAll` 来批准自己作为操作员。  

## 理由

### 元数据选择

`symbol` 函数（在 ERC-20 和 ERC-721 标准中找到）未被包含，因为我们认为这不是一个全球通用的数据来识别通用虚拟物品/资产，并且也容易发生冲突。简写符号用于行情和货币交易，但在该领域之外并不那么有用。

`name` 函数（用于人类可读的资产名称，链上）从标准中删除，以允许元数据 JSON 成为最终的资产名称，并减少数据的重复。这也允许名称的本地化，如果每种语言字符串存储在链上，将是不可承受的成本，更不用说膨胀标准接口。虽然这个决定可能会给实现者带来小负担，需要托管一个包含元数据的 JSON 文件，但我们相信任何严肃的 ERC-1155 实现都将已经利用 JSON 元数据。

### 升级

发出 `TransferSingle` 或 `TransferBatch` 以更改余额的要求意味着有效的 ERC-1155 实现重新部署到新合约地址时，必须从新合约地址发出事件，以复制已弃用合约的最终状态。有效地只发出最少数量的事件以反映最终余额，并省略导致该状态的所有交易是有效的。事件发出要求是为了确保合约的当前状态始终可以仅通过事件追踪。为了减轻在更改合约地址时发出事件的需要，可以考虑使用代理模式，如 [EIP-2535](./eip-2535.md) 中所述。这还将带来为用户提供稳定合约地址的额外好处。

### 设计决策：支持非批量

该标准支持 `safeTransferFrom` 和 `onERC1155Received` 函数，因为它们在单个代币类型转移时显著更便宜，这可以说是一个常见的用例。

### 设计决策：仅安全转移

该标准仅支持安全风格的转移，使接收合约能够依赖于 `onERC1155Received` 或 `onERC1155BatchReceived` 函数在转移结束时始终被调用。

### 保证日志追踪

随着以太坊生态系统的不断发展，许多 DApp 依赖传统数据库和浏览器 API 服务来检索和分类数据。ERC-1155 标准保证智能合约发出的事件日志将提供足够的数据，以创建所有当前代币余额的准确记录。数据库或浏览器可以监听事件，并能够提供每个 ERC-1155 代币的索引和分类搜索。

### 授权

函数 `setApprovalForAll` 允许操作员代表批准者管理其整个代币集。它使与交易所和交易合约的交互无缝。

将批准限制为某一特定代币 ID、数量或其他规则可以通过附加接口或外部合约来完成。其理由是保持 ERC-1155 标准尽可能通用，以适应所有用例，而不对可能不需要的实现施加特定的批准方案。可以使用标准代币批准接口，例如建议的 [ERC-1761 Scoped Approval Interface](./eip-1761.md)，该接口与 ERC-1155 兼容。

## 向后兼容性

在设计讨论中，有要求使该标准在向合约地址发送时与现有标准兼容，特别是在撰写时的 ERC-721。
为了满足这种情况，回滚逻辑有一些余地，应该合约未按照上述“安全转移规则”部分实现 `ERC1155TokenReceiver`，特别是“场景#3：接收者未实现必要的 `ERC1155TokenReceiver` 接口函数”。

因此，在混合 ERC-1155 合约实现中，必须在任何钩子调用 `onERC1155Received` 或 `onERC1155BatchReceived` 之前，对接收合约进行额外调用并进行检查。
操作顺序必须因此为：
1. 实现必须在接收合约上调用函数 `supportsInterface(0x4e2312e0)`，提供至少 10,000 gas。
2. 如果函数调用成功且返回值为常量值 `true`，则实现按常规 ERC-1155 实现进行，调用 `onERC1155Received` 或 `onERC1155BatchReceived` 钩子及相关规则。
3. 如果函数调用失败或返回值不是常量值 `true`，则实现可以假定接收合约不是 `ERC1155TokenReceiver`，并遵循其其他标准的转移规则。 
   
*__注意，建议纯实现单一标准__* 而不是混合解决方案，但在实现部分的参考中链接了混合 ERC-1155/ERC-721 合约的示例。

一个重要的考虑是，即使代币是按照其他标准的规则发送的，__ERC-1155 转移事件仍必须被发出。__ 这样可以根据 ERC-1155 标准规则仅通过事件确定余额。

## 用法

该标准可用于表示整个域的多种代币类型。可在同一智能合约中存储可替代和不可替代的代币。

### 批量转移

`safeBatchTransferFrom` 函数允许批量转移多个代币 ID 和数量。ERC-1155 的设计使得批量转移成为可能，而无需像现有代币标准那样使用包装合约。这在包含多种代币类型的批量转移时减少了 gas 成本，相较于多次交易的单次转移。

标准化批量转移的另一个优势是智能合约可以在单个操作中响应批量转移，使用 `onERC1155BatchReceived`。

建议客户端和钱包在发布批量转移时对代币 ID 和相关值（按升序）进行排序，因为某些 ERC-1155 实现提供了显著的 gas 成本节省，当 ID 被排序时。请参见 [Horizon Games - Multi-Token Standard](https://github.com/horizon-games/multi-token-standard) 中的“打包余额”实现示例。

### 批量余额

`balanceOfBatch` 函数允许客户端通过单个调用检索多个所有者和代币 ID 的余额。

### 从事件中枚举

为了保持实现 ERC-1155 的合约的存储需求轻，枚举（发现代币的 ID 和数量）必须使用事件日志进行。建议客户端（如交易所和区块链浏览器）维护一个本地数据库，至少包含代币 ID、供应和 URI。这可以从每个 TransferSingle、TransferBatch 和 URI 事件构建，从智能合约部署的区块开始，直到最新区块。

因此，ERC-1155 合约必须在创建、铸造、转移或销毁代币的任何情况下仔细发出 `TransferSingle` 或 `TransferBatch` 事件。

### 不可替代代币

以下策略是如何在同一合约中混合可替代和不可替代代币的示例。该标准并不强制实现必须这样做。 

##### 拆分 ID 位

任何 ERC-1155 函数中的 uint256 `_id` 参数的前 128 位可以表示基础代币 ID，而后 128 位可以表示不可替代代币的索引，以使其唯一。

不可替代代币可以使用基于索引的访问器与合约/代币数据集进行交互。因此，要访问混合数据合约中的特定代币集和该集合中的特定不可替代代币，可以将 `_id` 作为 `<uint128: 基础代币 ID><uint128: 不可替代代币索引>` 传递。

要将不可替代代币集/类别整体（或可替代代币）识别为整体，您可以通过 `_id` 参数将基础 ID 作为 `<uint128: 基础代币 ID><uint128: 零>` 传递。如果您的实现使用此技术，这自然意味着不可替代代币的索引应为 1 基。

在合约代码中，提取访问单个不可替代代币所需的两个数据部分可以使用 uint128(~0) 和相同的掩码右移 128。

```solidity
uint256 baseTokenNFT = 12345 << 128;
uint128 indexNFT = 50;

uint256 baseTokenFT = 54321 << 128;

balanceOf(msg.sender, baseTokenNFT); // Get balance of the base token for non-fungible set 12345 (this MAY be used to get balance of the user for all of this token set if the implementation wishes as a convenience).
balanceOf(msg.sender, baseTokenNFT + indexNFT); // Get balance of the token at index 50 for non-fungible set 12345 (should be 1 if user owns the individual non-fungible token or 0 if they do not).
balanceOf(msg.sender, baseTokenFT); // Get balance of the fungible base token 54321.
```

请注意，128 是一个任意数字，实施可以选择如何将其拆分为适合其用例的方式。合约的观察者将仅看到显示余额转移和铸造的事件，并可以仅使用该信息跟踪余额。
为了使观察者能够仅通过 ID 确定类型（不可替代或可替代），他们必须知道每个实现的拆分 ID 位格式。

[ERC-1155 参考实现](https://github.com/enjin/erc-1155) 是拆分 ID 位策略的示例。

##### 自然不可替代代币

表示不可替代代币的另一种简单方法是允许每个不可替代代币的最大值为 1。这自然反映了现实世界中，独特物品的数量为 1，而可替代物品的数量大于 1。

## 参考

**标准**
- [ERC-721 非可替代代币标准](./eip-721.md)
- [ERC-165 标准接口检测](./eip-165.md)
- [ERC-1538 透明合约标准](./eip-1538.md)
- [JSON 架构](https://json-schema.org/)
- [RFC 2119 用于指示要求级别的 RFC 中使用的关键字](https://www.ietf.org/rfc/rfc2119.txt)

**实现**
- [ERC-1155 参考实现](https://github.com/enjin/erc-1155)
- [Horizon Games - 多代币标准](https://github.com/horizon-games/multi-token-standard)
- [Enjin Coin](https://enjincoin.io) ([GitHub](https://github.com/enjin))
- [The Sandbox - 双 ERC-1155/721 合约](https://github.com/pixowl/thesandbox-contracts/tree/master/src/Asset)

**文章与讨论**
- [GitHub - 原始讨论线程](https://github.com/ethereum/EIPs/issues/1155)
- [ERC-1155 - 加密物品标准](https://blog.enjincoin.io/erc-1155-the-crypto-item-standard-ac9cf1c5a226)
- [这里有龙 - 超越 ERC-20 和 ERC-721 以减少约 80% 的 gas 成本](https://medium.com/horizongames/going-beyond-erc20-and-erc721-9acebd4ff6ef)
- [Blockonomi - 以太坊 ERC-1155 代币非常适合在线游戏，可能更多](https://blockonomi.com/erc1155-gaming-token/)
- [超越游戏 - 探索 ERC-1155 代币标准的实用性！](https://blockgeeks.com/erc-1155-token/)
- [ERC-1155：The Sandbox 的新标准](https://medium.com/sandbox-game/erc-1155-a-new-standard-for-the-sandbox-c95ee1e45072)

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。