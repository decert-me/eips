---
eip: 7432
title: 非同质化代币角色
description: NFT 的角色管理。允许账户通过可过期的角色分配共享 NFT 的实用性。
author: Ernani São Thiago (@ernanirst), Daniel Lima (@karacurt)
discussions-to: https://ethereum-magicians.org/t/eip-7432-non-fungible-token-roles/15298
status: 最终
type: 标准跟踪
category: ERC
created: 2023-07-14
requires: 165, 721
---

## 摘要

该标准引入了 NFT 的角色管理。每个角色分配与单个 NFT 相关联，并在给定时间戳自动过期。角色被定义为 `bytes32`，并具有一个自定义的 `data` 字段，大小任意，以允许自定义。

## 动机

NFT 角色接口旨在建立 NFT 中角色管理的标准。在链上跟踪角色使去中心化应用程序（dApps）能够实现对特权操作的访问控制，例如，使用角色铸造代币（空投索赔权）。

NFT 角色可以与 dApps 深度集成，以创建共享实用性的机制。一个好的例子是在数字房地产中。用户可以创建一个数字财产 NFT，并将 `keccak256("PropertyManager()")` 角色授予另一个用户，允许他们在不妨碍所有权的情况下委托特定的实用性。同一用户还可以将 `keccak256("PropertyTenant(uint256)")` 角色授予其他用户，允许接收者访问和与数字财产进行交互。

在去中心化金融（DeFi）中也有有趣的用例。保险单可以作为 NFT 发行，受益人、被保险人和保险人都可以是使用该标准跟踪的链上角色。

## 规范

本文档中的关键词 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC-2119 和 RFC-8174 中的描述进行解释。

合规的合约必须实现以下接口：

```solidity
/// @title ERC-7432 Non-Fungible Token Roles
/// @dev See https://eips.ethereum.org/EIPS/eip-7432
/// Note: the ERC-165 identifier for this interface is 0xd00ca5cf.
interface IERC7432 /* is ERC165 */ {
  struct Role {
    bytes32 roleId;
    address tokenAddress;
    uint256 tokenId;
    address recipient;
    uint64 expirationDate;
    bool revocable;
    bytes data;
  }

  /** Events **/

  /// @notice Emitted when an NFT is locked (deposited or frozen).
  /// @param _owner The owner of the NFT.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  event TokenLocked(address indexed _owner, address indexed _tokenAddress, uint256 _tokenId);

  /// @notice Emitted when a role is granted.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  /// @param _roleId The role identifier.
  /// @param _owner The user assigning the role.
  /// @param _recipient The user receiving the role.
  /// @param _expirationDate The expiration date of the role.
  /// @param _revocable Whether the role is revocable or not.
  /// @param _data Any additional data about the role.
  event RoleGranted(
    address indexed _tokenAddress,
    uint256 indexed _tokenId,
    bytes32 indexed _roleId,
    address _owner,
    address _recipient,
    uint64 _expirationDate,
    bool _revocable,
    bytes _data
  );

  /// @notice Emitted when a role is revoked.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  /// @param _roleId The role identifier.
  event RoleRevoked(address indexed _tokenAddress, uint256 indexed _tokenId, bytes32 indexed _roleId);

  /// @notice Emitted when an NFT is unlocked (withdrawn or unfrozen).
  /// @param _owner The original owner of the NFT.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  event TokenUnlocked(address indexed _owner, address indexed _tokenAddress, uint256 indexed _tokenId);

  /// @notice Emitted when a user is approved to manage roles on behalf of another user.
  /// @param _tokenAddress The token address.
  /// @param _operator The user approved to grant and revoke roles.
  /// @param _isApproved The approval status.
  event RoleApprovalForAll(address indexed _tokenAddress, address indexed _operator, bool indexed _isApproved);

  /** External Functions **/

  /// @notice Grants a role to a user.
  /// @dev Reverts if sender is not approved or the NFT owner.
  /// @param _role The role attributes.
  function grantRole(Role calldata _role) external;

  /// @notice Revokes a role from a user.
  /// @dev Reverts if sender is not approved or the original owner.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  /// @param _roleId The role identifier.
  function revokeRole(address _tokenAddress, uint256 _tokenId, bytes32 _roleId) external;

  /// @notice Unlocks NFT (transfer back to original owner or unfreeze it).
  /// @dev Reverts if sender is not approved or the original owner.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  function unlockToken(address _tokenAddress, uint256 _tokenId) external;

  /// @notice Approves operator to grant and revoke roles on behalf of another user.
  /// @param _tokenAddress The token address.
  /// @param _operator The user approved to grant and revoke roles.
  /// @param _approved The approval status.
  function setRoleApprovalForAll(address _tokenAddress, address _operator, bool _approved) external;

  /** View Functions **/

  /// @notice Retrieves the original owner of the NFT.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  /// @return owner_ The owner of the token.
  function ownerOf(address _tokenAddress, uint256 _tokenId) external view returns (address owner_);

  /// @notice Retrieves the recipient of an NFT role.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  /// @param _roleId The role identifier.
  /// @return recipient_ The user that received the role.
  function recipientOf(
    address _tokenAddress,
    uint256 _tokenId,
    bytes32 _roleId
  ) external view returns (address recipient_);

  /// @notice Retrieves the custom data of a role assignment.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  /// @param _roleId The role identifier.
  /// @return data_ The custom data of the role.
  function roleData(
    address _tokenAddress,
    uint256 _tokenId,
    bytes32 _roleId
  ) external view returns (bytes memory data_);

  /// @notice Retrieves the expiration date of a role assignment.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  /// @param _roleId The role identifier.
  /// @return expirationDate_ The expiration date of the role.
  function roleExpirationDate(
    address _tokenAddress,
    uint256 _tokenId,
    bytes32 _roleId
  ) external view returns (uint64 expirationDate_);

  /// @notice Verifies whether the role is revocable.
  /// @param _tokenAddress The token address.
  /// @param _tokenId The token identifier.
  /// @param _roleId The role identifier.
  /// @return revocable_ Whether the role is revocable.
  function isRoleRevocable(
    address _tokenAddress,
    uint256 _tokenId,
    bytes32 _roleId
  ) external view returns (bool revocable_);

  /// @notice Verifies if the owner approved the operator.
  /// @param _tokenAddress The token address.
  /// @param _owner The user that approved the operator.
  /// @param _operator The user that can grant and revoke roles.
  /// @return Whether the operator is approved.
  function isRoleApprovedForAll(
    address _tokenAddress,
    address _owner,
    address _operator
  ) external view returns (bool);
}
```

### 元数据扩展

角色元数据扩展扩展了 NFT 的传统基于 JSON 的元数据架构。因此，支持此功能的 DApps 也必须实现 [ERC-721](./eip-721.md) 的元数据扩展。此扩展是 **可选** 的，允许开发者提供角色的额外信息。

更新的元数据架构：

```js
{
  
  /** Existing NFT Metadata **/

  "title": "Asset Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "Identifies the asset to which this NFT represents"
    },
    "description": {
      "type": "string",
      "description": "Describes the asset to which this NFT represents"
    },
    "image": {
      "type": "string",
      "description": "A URI pointing to a resource with mime type image/* representing the asset to which this NFT represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive"
    }
  },
  
  /** Additional fields for Roles **/

  "roles": [
    {
      "id": {
        "type": "bytes32",
        "description": "Identifies the role"
      },
      "name": {
        "type": "string",
        "description": "Human-readable name of the role"
      },
      "description": {
        "type": "string",
        "description": "Describes the role"
      },
      "inputs": [
        {
          "name": {
            "type": "string",
            "description": "Human-readable name of the argument"
          },
          "type": {
            "type": "string",
            "description": "Solidity type, e.g., uint256 or address"
          }
        }
      ]
    }
  ]
  
}
```

以下 JSON 是 [ERC-7432](./eip-7432.md) 元数据的示例：

```js
{
  // ... Existing NFT Metadata
  
  "roles": [
    {
      // keccak256("PropertyManager()")
      "id": "0x76be0ffb73d8cd9e8fa76c28632ebbc3865a8ec7a0b6acab6ac589a1c88dd301",
      "name": "Property Manager",
      "description": "The manager of the property is responsible for furnishing it and ensuring its good condition.",
      "inputs": []
    },
    {
      // keccak256("PropertyTenant(uint256)")
      "id": "0x17dfc8ea82661b71bd62ce0bd9db3858dd8f3e8ab9799d6ab468ec64f1be21a5",
      "name": "Property Tenant",
      "description": "The tenant of the property is responsible for paying the rent and keeping the property in good condition.",
      "inputs": [
        {
          "name": "rent",
          "type": "uint256"
        }
      ]
    }
  ]
  
}
```

`roles` 数组属性是建议的，开发者应根据需要添加任何其他相关信息（例如，角色的图像）。还需要强调 `inputs` 属性的重要性。该字段描述应编码并传递给 `grantRole` 函数的参数。建议使用 Solidity ABI 规范中定义的 `type` 和 `components` 属性，其中 `type` 是参数的规范类型，`components` 用于复杂元组类型。

### 注意事项

* 合规的合约必须实现 `IERC7432` 接口。
* 角色由 `bytes32` 表示，建议使用角色名称及其输入的 `keccak256`：`bytes32 roleId = keccak256("RoleName(input_type)")`。
* 如果 `expirationDate` 在过去，或者 `msg.sender` 未被批准代表 NFT 所有者授予角色，则 `grantRole` 函数必须回退。它可以实现为 `public` 或 `external`。
* 除了发出 `RoleGranted` 事件外，如果代币被冻结或转移到托管账户，`grantRole` 函数必须发出 `TokenLocked` 事件。
* 如果 `msg.sender` 未被批准代表原始 NFT 所有者或 `recipient` 撤销角色，则 `revokeRole` 函数必须回退。它可以实现为 `public` 或 `external`。
* 如果 `revocable` 为 false，只有 `recipient` 可以撤销角色。如果 `revocable` 为 true，`recipient` 和原始 NFT 所有者都可以撤销角色。
* 如果 `msg.sender` 未被批准，或者至少有一个未过期的不可撤销角色，则 `unlockToken` 函数必须回退。它可以实现为 `public` 或 `external`。
* `setRoleApprovalForAll` 函数可以实现为 `public` 或 `external`。
* `ownerOf` 函数可以实现为 `pure` 或 `view`，并必须返回 NFT 原始所有者的地址。
* `recipientOf` 函数可以实现为 `pure` 或 `view`，并必须返回接收角色的账户地址。
* `roleData` 函数可以实现为 `pure` 或 `view`，并必须返回传递给 `grantRole` 函数的编码数据。
* `roleExpirationDate` 函数可以实现为 `pure` 或 `view`，并必须返回给定角色的过期日期。
* `isRoleRevocable` 函数可以实现为 `pure` 或 `view`，并必须返回角色是否可撤销。
* `isRoleApprovedForAll` 函数可以实现为 `pure` 或 `view`，并应仅在 `_operator` 被批准代表原始 NFT 所有者授予和撤销角色时返回 `true`。
* 合规的合约应实现 [ERC-165](./eip-165.md)。

## 理由

[ERC-7432](./eip-7432.md) 不是 [ERC-721](./eip-721.md) 的扩展。做出这一决定的主要原因是使其能够在 NFT 的同一合约或外部实现，允许 dApps 使用不可变资产实现角色。该标准涵盖了许多关键特性，例如自动过期和自定义数据，但也许最重要的是其在实现上的灵活性。ERC-7432 可以以多种方式实现，因此使用了中立术语 "lock"。该术语可以指 NFT 被冻结（防止在角色过期之前转移）或存入托管合约。开发者应根据其用例决定使用哪种实现。

### 自动过期

自动过期通过 `grantRole` 和 `roleExpirationDate` 函数实现。`grantRole` 负责设置过期日期，而 `roleExpirationDate` 允许开发者检查角色是否过期。由于 `uint256` 在大多数编程语言中不被原生支持，因此在该标准中日期表示为 `uint64`。`uint64` 表示的最大 UNIX 时间戳约为 `584,942,417,355` 年，这应该足够被视为“永久”。因此，建议使用 `type(uint64).max` 来支持需要角色永不过期的用例。

### 可撤销角色

在某些情况下，NFT 的原始所有者可能需要在角色过期之前撤销角色，而在其他情况下，接收者可能需要确保角色无法被撤销。`revocable` 参数被引入到 `grantRole` 函数中，以指定角色是否可以被提前撤销，从而使标准支持这两种用例。

无论 `revocable` 的值如何，始终建议允许 `recipient` 撤销角色，以便他们能够消除不希望的分配。

### 自定义数据

DApps 可以使用 `grantRole` 函数的 `data` 参数自定义角色。`data` 使用通用类型 `bytes` 实现，以使 dApps 在授予角色时能够编码任何角色特定的信息。自定义数据可以通过 `roleData` 函数检索，并在 `RoleGranted` 事件中发出。通过这种方式，开发者可以将这些信息集成到他们的应用程序中，无论是链上还是链下。
### 角色批准

类似于 [ERC-721](./eip-721.md)，该标准使其他账户能够代表 NFT 拥有者管理角色。此功能的引入是为了允许第三方在不需要 NFT 所有权的情况下与 ERC-7432 进行交互。合规的合约必须实现 `setRoleApprovalForAll` 和 `isRoleApprovedForAll` 函数以提供此功能。

## 向后兼容性

在所有函数和事件中，标准要求提供 `tokenAddress` 和 `tokenId`。此要求使 dApp 能够使用独立的 [ERC-7432](./eip-7432.md) 合约作为不可变 NFT 角色的权威来源。

## 参考实现

请参见 [ERC-7432.sol](../assets/eip-7432/ERC7432.sol)。

## 安全考虑

集成非同质化代币角色接口的开发者应在其实现中考虑以下事项：

* 确保适当的访问控制，以防止未经授权的角色分配或撤销。
* 考虑潜在的攻击向量，如重入攻击，并确保采取适当的保护措施。
* 被批准的账户应能够代表其他用户管理角色。然而，确保 NFT 只能转移到托管合约，并返回其原始拥有者（而不是转移到被批准的账户）。
* 在允许用户访问 NFT 的功能之前，始终检查到期日期。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。