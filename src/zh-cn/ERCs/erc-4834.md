---
eip: 4834
title: 层级域
description: 极其通用的名称解析
author: Gavin John (@Pandapip1)
discussions-to: https://ethereum-magicians.org/t/erc-4834-hierarchical-domains-standard/8388
status: Final
type: Standards Track
category: ERC
created: 2022-02-22
---

## 摘要

这是一个用于通用名称解析的标准，具有任意复杂的访问控制和解析功能。它允许实现此 EIP 的合约（以下简称为“域”）使用更人性化的名称进行寻址，目的与 [ERC-137](./eip-137.md)（也称为“ENS”）类似。

## 动机

此 EIP 相较于现有标准的优势在于，它提供了一个支持名称解析的最小接口，增加了标准化的访问控制，并具有简单的架构。虽然 ENS 有用，但其架构相对复杂，并且没有标准的访问控制。

此外，所有域（包括子域、顶级域和根域本身）实际上都被实现为域，这意味着名称解析是一个简单的迭代算法，与 DNS 本身并无不同。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

### 合约接口

```solidity
interface IDomain {
    /// @notice     Query if a domain has a subdomain with a given name
    /// @param      name The subdomain to query, in right to left order
    /// @return     `true` if the domain has a subdomain with the given name, `false` otherwise
    function hasDomain(string[] memory name) external view returns (bool);

    /// @notice     Fetch the subdomain with a given name
    /// @dev        This should revert if `hasDomain(name)` is `false`
    /// @param      name The subdomain to fetch, in right to left order
    /// @return     The subdomain with the given name
    function getDomain(string[] memory name) external view returns (address);
}
```

### 名称解析

要解析一个名称（如 `"a.b.c"`），按分隔符拆分它（结果类似于 `["a", "b", "c"]`）。将 `domain` 初始设置为根域，将 `path` 设置为空列表。

弹出数组的最后一个元素（`"c"`）并将其添加到路径中，然后调用 `domain.hasDomain(path)`。如果返回 `false`，则域解析失败。否则，将域设置为 `domain.getDomain(path)`。重复此过程，直到拆分的段列表为空。

嵌套的数量没有限制。例如，如果根域包含 `z`，而 `z` 包含 `y`，依此类推，`0.1.2.3.4.5.6.7.8.9.a.b.c.d.e.f.g.h.i.j.k.l.m.n.o.p.q.r.s.t.u.v.w.x.y.z` 将是有效的。

以下是一个解析名称的 Solidity 函数：

```solidity
function resolve(string[] calldata splitName, IDomain root) public view returns (address) {
    IDomain current = root;
    string[] memory path = [];
    for (uint i = splitName.length - 1; i >= 0; i--) {
        // Append to back of list
        path.push(splitName[i]);
        // Require that the current domain has a domain
        require(current.hasDomain(path), "Name resolution failed");
        // Resolve subdomain
        current = current.getDomain(path);
    }
    return current;
}
```

### 可选扩展：可注册

```solidity
interface IDomainRegisterable is IDomain {
    //// Events
    
    /// @notice     Must be emitted when a new subdomain is created (e.g. through `createDomain`)
    /// @param      sender msg.sender for createDomain
    /// @param      name name for createDomain
    /// @param      subdomain subdomain in createDomain
    event SubdomainCreate(address indexed sender, string name, address subdomain);

    /// @notice     Must be emitted when the resolved address for a domain is changed (e.g. with `setDomain`)
    /// @param      sender msg.sender for setDomain
    /// @param      name name for setDomain
    /// @param      subdomain subdomain in setDomain
    /// @param      oldSubdomain the old subdomain
    event SubdomainUpdate(address indexed sender, string name, address subdomain, address oldSubdomain);

    /// @notice     Must be emitted when a domain is unmapped (e.g. with `deleteDomain`)
    /// @param      sender msg.sender for deleteDomain
    /// @param      name name for deleteDomain
    /// @param      subdomain the old subdomain
    event SubdomainDelete(address indexed sender, string name, address subdomain);

    //// CRUD
    
    /// @notice     Create a subdomain with a given name
    /// @dev        This should revert if `canCreateDomain(msg.sender, name, pointer)` is `false` or if the domain exists
    /// @param      name The subdomain name to be created
    /// @param      subdomain The subdomain to create
    function createDomain(string memory name, address subdomain) external payable;

    /// @notice     Update a subdomain with a given name
    /// @dev        This should revert if `canSetDomain(msg.sender, name, pointer)` is `false` of if the domain doesn't exist
    /// @param      name The subdomain name to be updated
    /// @param      subdomain The subdomain to set
    function setDomain(string memory name, address subdomain) external;

    /// @notice     Delete the subdomain with a given name
    /// @dev        This should revert if the domain doesn't exist or if `canDeleteDomain(msg.sender, name)` is `false`
    /// @param      name The subdomain to delete
    function deleteDomain(string memory name) external;


    //// Parent Domain Access Control

    /// @notice     Get if an account can create a subdomain with a given name
    /// @dev        This must return `false` if `hasDomain(name)` is `true`.
    /// @param      updater The account that may or may not be able to create/update a subdomain
    /// @param      name The subdomain name that would be created/updated
    /// @param      subdomain The subdomain that would be set
    /// @return     Whether an account can update or create the subdomain
    function canCreateDomain(address updater, string memory name, address subdomain) external view returns (bool);

    /// @notice     Get if an account can update or create a subdomain with a given name
    /// @dev        This must return `false` if `hasDomain(name)` is `false`.
    ///             If `getDomain(name)` is also a domain implementing the subdomain access control extension, this should return `false` if `getDomain(name).canMoveSubdomain(msg.sender, this, subdomain)` is `false`.
    /// @param      updater The account that may or may not be able to create/update a subdomain
    /// @param      name The subdomain name that would be created/updated
    /// @param      subdomain The subdomain that would be set
    /// @return     Whether an account can update or create the subdomain
    function canSetDomain(address updater, string memory name, address subdomain) external view returns (bool);

    /// @notice     Get if an account can delete the subdomain with a given name
    /// @dev        This must return `false` if `hasDomain(name)` is `false`.
    ///             If `getDomain(name)` is a domain implementing the subdomain access control extension, this should return `false` if `getDomain(name).canDeleteSubdomain(msg.sender, this, subdomain)` is `false`.
    /// @param      updater The account that may or may not be able to delete a subdomain
    /// @param      name The subdomain to delete
    /// @return     Whether an account can delete the subdomain
    function canDeleteDomain(address updater, string memory name) external view returns (bool);
}
```

### 可选扩展：可枚举

```solidity
interface IDomainEnumerable is IDomain {
    /// @notice     Query all subdomains. Must revert if the number of domains is unknown or infinite.
    /// @return     The subdomain with the given index.
    function subdomainByIndex(uint256 index) external view returns (string memory);
    
    /// @notice     Get the total number of subdomains. Must revert if the number of domains is unknown or infinite.
    /// @return     The total number of subdomains.
    function totalSubdomains() external view returns (uint256);
}
```

### 可选扩展：访问控制

```solidity
interface IDomainAccessControl is IDomain {
    /// @notice     Get if an account can move the subdomain away from the current domain
    /// @dev        May be called by `canSetDomain` of the parent domain - implement access control here!!!
    /// @param      updater The account that may be moving the subdomain
    /// @param      name The subdomain name
    /// @param      parent The parent domain
    /// @param      newSubdomain The domain that will be set next
    /// @return     Whether an account can update the subdomain
    function canMoveSubdomain(address updater, string memory name, IDomain parent, address newSubdomain) external view returns (bool);

    /// @notice     Get if an account can unset this domain as a subdomain
    /// @dev        May be called by `canDeleteDomain` of the parent domain - implement access control here!!!
    /// @param      updater The account that may or may not be able to delete a subdomain
    /// @param      name The subdomain to delete
    /// @param      parent The parent domain
    /// @return     Whether an account can delete the subdomain
    function canDeleteSubdomain(address updater, string memory name, IDomain parent) external view returns (bool);
}
```

## 理由

如摘要中所述，此 EIP 的目标是提供一个简单的名称解析接口。以下是一些设计决策及其原因：

- 名称解析算法
  - 与 ENS 的解析算法不同，此 EIP 的名称解析完全由解析路径上的合约控制。
  - 这种行为对用户更直观。
  - 这种行为允许更大的灵活性——例如，一个合约可以根据一天中的时间改变其解析内容。
- 父域访问控制
  - 没有使用简单的“可拥有”接口，因为此规范旨在尽可能通用。如果需要可拥有的实现，可以进行实现。
  - 这也使得父域能够调用子域的访问控制方法，以便子域也可以选择其所需的任何访问控制机制。
- 子域访问控制
  - 包含这些方法是为了使子域不总是受限于其父域的访问控制。
  - 根域可以由一个具有相等股份的不可转让代币的 DAO 控制，顶级域可以由一个代表股份的代币的 DAO 控制，该顶级域的域可以由单一所有者控制，该域的子域可以由与 NFT 关联的单一所有者控制，依此类推。
  - 子域访问控制函数是建议：一个可拥有的域可能会实现一个所有者覆盖，以便在密钥丢失时可能恢复子域。

## 向后兼容性

此 EIP 足够通用以支持 ENS，但 ENS 不够通用以支持此 EIP。

## 安全考虑

### 恶意 canMoveSubdomain（黑洞）

#### 描述：恶意 `canMoveSubdomain`

使用 `setDomain` 移动子域是一个潜在危险的操作。

根据父域的实现，如果恶意的新子域在 `canMoveSubdomain` 上意外返回 `false`，则该子域可以有效地锁定域的所有权。

或者，它可能在不被预期的情况下返回 `true`（即后门），允许合约所有者接管该域。

#### 缓解：恶意 `canMoveSubdomain`

客户端应通过警告来帮助，如果新子域的 `canMoveSubdomain` 或 `canDeleteSubdomain` 变为 `false`。然而，重要的是要注意，由于这些是函数，因此值可能会根据是否已经链接而变化。它也仍然可能意外返回 true。因此，建议在调用 `setDomain` 之前**始终**审核新子域的源代码。

### 父域解析

#### 描述：父域解析

父域对其子域的名称解析拥有完全控制权。如果特定域链接到 `a.b.c`，则 `b.c` 可以根据其代码将 `a.b.c` 设置为任何域，而 `c` 可以将 `b.c` 本身设置为任何域。

#### 缓解：父域解析

在获取已预链接的域之前，建议始终审核合约**和**所有父域直到根域。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。