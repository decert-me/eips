<!-- ---
eip: 1967
title: 代理存储槽
description: 代理合约存储逻辑合约地址以及其他代理特定信息的一致位置，这些代理广泛用于升级和节省 gas。
author: Santiago Palladino (@spalladino), Francisco Giordano (@frangio), Hadrien Croubois (@Amxx)
discussions-to: https://ethereum-magicians.org/t/eip-1967-standard-proxy-storage-slots/3185
status: Final
type: 标准跟踪
category: ERC
created: 2019-04-24
--- -->

## 摘要
委托**代理合约**被广泛用于升级和节省 gas。这些代理依赖于一个称为**逻辑合约**（也称为实现合约或主副本）的合约，通过 `delegatecall` 调用该合约。这使得代理可以在代码委托给逻辑合约时保持持久状态（存储和余额）。

为了避免代理和逻辑合约之间存储使用的冲突，逻辑合约的地址通常保存在一个特定的存储槽中（例如在 OpenZeppelin 合约中为 `0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc`），该存储槽保证编译器永远不会分配。该 EIP 提议了一组标准存储槽来存储代理信息。这使得像区块浏览器这样的客户端可以正确提取和展示这些信息给最终用户，逻辑合约也可以选择性地对其进行操作。

## 动机
委托代理被广泛使用，旨在支持升级和减少部署的 gas 成本。这些代理的示例可在 OpenZeppelin Contracts、Gnosis、AragonOS、Melonport、Limechain、WindingTree、Decentraland 等项目中找到。

然而，缺乏获取代理的逻辑地址的通用接口使得无法构建可以操作此信息的通用工具。

一个经典的例子是区块浏览器。在这里，最终用户希望与底层逻辑合约交互，而不是代理本身。有一种通用方式从代理中检索逻辑合约地址允许区块浏览器显示逻辑合约的 ABI，而不是代理的 ABI。区块浏览器检查合约的存储以确定是否确实是代理，如果是，则显示有关代理和逻辑合约的信息。例如，这是 Etherscan 上显示的 `0xa0b86991c6218b36c1d19d4a2e9eb0ce3606eb48` 的方式：

![Etherscan 上的示例代理](https://raw.githubusercontent.com/ethereum/ERCs/7dc164427e0c146ea5c58f731adbecc941daf73c/assets/erc-1967/Sample-proxy-on-etherscan.png)

另一个例子是明确根据被代理的事实进行操作的逻辑合约。这使得它们有可能在其逻辑的一部分触发代码更新。一个通用的存储槽允许这些用例独立于所使用的特定代理实现。

## 规范
监视代理对许多应用程序的安全性至关重要。因此，跟踪实现和管理员存储槽的更改是至关重要的。不幸的是，跟踪存储槽的更改并不容易。因此，建议任何更改这些存储槽中任何一个的函数应该也发出相应的事件。这包括初始化，从 `0x0` 到第一个非零值。

用于代理特定信息的建议存储槽如下。根据需要，可以在随后的 ERC 中添加更多存储槽以存储额外信息。

### 逻辑合约地址

存储槽 `0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc`
（通过 `bytes32(uint256(keccak256('eip1967.proxy.implementation')) - 1)` 获得）。

保存此代理委托给的逻辑合约的地址。如果使用 beacon，则应为空。对此存储槽的更改应通过以下事件通知：

```solidity
event Upgraded(address indexed implementation);
```

### Beacon 合约地址

存储槽 `0xa3f0ad74e5423aebfd80d3ef4346578335a9a72aeaee59ff6cb3582b35133d50`（通过 `bytes32(uint256(keccak256('eip1967.proxy.beacon')) - 1)` 获得）。

保存此代理依赖的 beacon 合约的地址（回退）。如果直接使用逻辑地址，则应为空，并且仅在逻辑合约槽为空时考虑。对此存储槽的更改应通过以下事件通知：

```solidity
event BeaconUpgraded(address indexed beacon);
```

Beacon 用于将多个代理的逻辑地址保存在单个位置，从而通过修改单个存储槽来升级多个代理。beacon 合约必须实现以下函数：

```
function implementation() returns (address)
```

基于 Beacon 的代理合约不使用逻辑合约槽。相反，它们使用 beacon 合约槽来存储它们附加到的 beacon 的地址。为了知道 beacon 代理使用的逻辑合约，客户端应：

- 读取 beacon 逻辑存储槽的 beacon 地址；
- 调用 beacon 合约的 `implementation()` 函数。

beacon 合约上的 `implementation()` 函数的结果不应取决于调用者（`msg.sender`）。

### 管理员地址

存储槽 `0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103`
（通过 `bytes32(uint256(keccak256('eip1967.proxy.admin')) - 1)` 获得）。

该地址保存了允许升级代理合约的逻辑合约地址（可选）。对此存储槽的更改应通过以下事件通知：

```solidity
event AdminChanged(address previousAdmin, address newAdmin);
```

## 理由

该 EIP 将逻辑合约地址的**存储槽**标准化，而不是在代理合约上公开一个公共方法。这样做的理由是代理不应该向最终用户公开可能与逻辑合约的函数发生冲突的函数。

请注意，即使在具有不同名称的函数之间也可能发生冲突，因为 ABI 仅依赖于函数选择器的四个字节。这可能导致意外错误，甚至利用，其中对代理合约的调用返回与预期不同的值，因为代理拦截调用并用自己的值回答。

来自 Nomic Labs 的*以太坊代理中的恶意后门*：

> 与实现合约中的选择器匹配的代理合约中的任何函数将直接调用，完全跳过实现代码。
>
> 因为函数选择器使用固定数量的字节，所以总会存在冲突的可能性。这对于日常开发并不是问题，因为 Solidity 编译器将检测到合约内的选择器冲突，但是当选择器用于跨合约交互时，这就可以被利用。冲突可以被滥用以创建一个看似行为良好的合约，实际上隐藏着一个后门。

代理公共函数有潜在的可利用性，这使得有必要以不同的方式标准化逻辑合约地址。

所选存储槽的主要要求是编译器永远不会选择它们来存储任何合约状态变量。否则，当逻辑合约写入自己的变量时，逻辑合约可能无意中覆盖代理上的这些信息。

Solidity 将变量映射到存储器中，基于它们声明的顺序，继承链是线性化的：第一个变量分配第一个槽，依此类推。唯一的例外是动态数组和映射中的值，它们存储在键和存储槽的连接哈希中。Solidity 开发团队已确认存储布局将在新版本中得以保留：

> 存储器中状态变量的布局被视为 Solidity 的外部接口的一部分，因为存储指针可以传递给库。这意味着对本节中概述的规则的任何更改都被视为语言的破坏性更改，由于其关键性质，应在执行之前非常谨慎地考虑。在发生这种破坏性更改的情况下，我们希望发布一个兼容模式，其中编译器将生成支持旧布局的字节码。

Vyper 似乎遵循与 Solidity 相同的策略。请注意，用其他语言编写的合约，或直接使用汇编语言编写的合约，可能会发生冲突。

它们被选择为不会与编译器分配的状态变量发生冲突，因为它们依赖于不以存储索引开头的字符串的哈希。此外，还添加了一个`-1`的偏移量，以便哈希的原像不可知，进一步降低可能发生攻击的几率。

## 参考实现

```solidity
/**
 * @dev This contract implements an upgradeable proxy. It is upgradeable because calls are delegated to an
 * implementation address that can be changed. This address is stored in storage in the location specified by
 * https://eips.ethereum.org/EIPS/eip-1967[EIP1967], so that it doesn't conflict with the storage layout of the
 * implementation behind the proxy.
 */
contract ERC1967Proxy is Proxy, ERC1967Upgrade {
    /**
     * @dev Initializes the upgradeable proxy with an initial implementation specified by `_logic`.
     *
     * If `_data` is nonempty, it's used as data in a delegate call to `_logic`. This will typically be an encoded
     * function call, and allows initializing the storage of the proxy like a Solidity constructor.
     */
    constructor(address _logic, bytes memory _data) payable {
        assert(_IMPLEMENTATION_SLOT == bytes32(uint256(keccak256("eip1967.proxy.implementation")) - 1));
        _upgradeToAndCall(_logic, _data, false);
    }

    /**
     * @dev Returns the current implementation address.
     */
    function _implementation() internal view virtual override returns (address impl) {
        return ERC1967Upgrade._getImplementation();
    }
}

/**
 * @dev This abstract contract provides getters and event emitting update functions for
 * https://eips.ethereum.org/EIPS/eip-1967[EIP1967] slots.
 */
abstract contract ERC1967Upgrade {
    // This is the keccak-256 hash of "eip1967.proxy.rollback" subtracted by 1
    bytes32 private constant _ROLLBACK_SLOT = 0x4910fdfa16fed3260ed0e7147f7cc6da11a60208b5b9406d12a635614ffd9143;

    /**
     * @dev Storage slot with the address of the current implementation.
     * This is the keccak-256 hash of "eip1967.proxy.implementation" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;

    /**
     * @dev Emitted when the implementation is upgraded.
     */
    event Upgraded(address indexed implementation);

    /**
     * @dev Returns the current implementation address.
     */
    function _getImplementation() internal view returns (address) {
        return StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value;
    }

    /**
     * @dev Stores a new address in the EIP1967 implementation slot.
     */
    function _setImplementation(address newImplementation) private {
        require(Address.isContract(newImplementation), "ERC1967: new implementation is not a contract");
        StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value = newImplementation;
    }

    /**
     * @dev Perform implementation upgrade
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeTo(address newImplementation) internal {
        _setImplementation(newImplementation);
        emit Upgraded(newImplementation);
    }

    /**
     * @dev Perform implementation upgrade with additional setup call.
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeToAndCall(
        address newImplementation,
        bytes memory data,
        bool forceCall
    ) internal {
        _upgradeTo(newImplementation);
        if (data.length > 0 || forceCall) {
            Address.functionDelegateCall(newImplementation, data);
        }
    }

    /**
     * @dev Perform implementation upgrade with security checks for UUPS proxies, and additional setup call.
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeToAndCallSecure(
        address newImplementation,
        bytes memory data,
        bool forceCall
    ) internal {
        address oldImplementation = _getImplementation();

        // Initial upgrade and setup call
        _setImplementation(newImplementation);
        if (data.length > 0 || forceCall) {
            Address.functionDelegateCall(newImplementation, data);
        }

        // Perform rollback test if not already in progress
        StorageSlot.BooleanSlot storage rollbackTesting = StorageSlot.getBooleanSlot(_ROLLBACK_SLOT);
        if (!rollbackTesting.value) {
            // Trigger rollback using upgradeTo from the new implementation
            rollbackTesting.value = true;
            Address.functionDelegateCall(
                newImplementation,
                abi.encodeWithSignature("upgradeTo(address)", oldImplementation)
            );
            rollbackTesting.value = false;
            // Check rollback was effective
            require(oldImplementation == _getImplementation(), "ERC1967Upgrade: upgrade breaks further upgrades");
            // Finally reset to the new implementation and log the upgrade
            _upgradeTo(newImplementation);
        }
    }

    /**
     * @dev Storage slot with the admin of the contract.
     * This is the keccak-256 hash of "eip1967.proxy.admin" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant _ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;

    /**
     * @dev Emitted when the admin account has changed.
     */
    event AdminChanged(address previousAdmin, address newAdmin);

    /**
     * @dev Returns the current admin.
     */
    function _getAdmin() internal view returns (address) {
        return StorageSlot.getAddressSlot(_ADMIN_SLOT).value;
    }

    /**
     * @dev Stores a new address in the EIP1967 admin slot.
     */
    function _setAdmin(address newAdmin) private {
        require(newAdmin != address(0), "ERC1967: new admin is the zero address");
        StorageSlot.getAddressSlot(_ADMIN_SLOT).value = newAdmin;
    }

    /**
     * @dev Changes the admin of the proxy.
     *
     * Emits an {AdminChanged} event.
     */
    function _changeAdmin(address newAdmin) internal {
        emit AdminChanged(_getAdmin(), newAdmin);
        _setAdmin(newAdmin);
    }

    /**
     * @dev The storage slot of the UpgradeableBeacon contract which defines the implementation for this proxy.
     * This is bytes32(uint256(keccak256('eip1967.proxy.beacon')) - 1)) and is validated in the constructor.
     */
    bytes32 internal constant _BEACON_SLOT = 0xa3f0ad74e5423aebfd80d3ef4346578335a9a72aeaee59ff6cb3582b35133d50;

    /**
     * @dev Emitted when the beacon is upgraded.
     */
    event BeaconUpgraded(address indexed beacon);

    /**
     * @dev Returns the current beacon.
     */
    function _getBeacon() internal view returns (address) {
        return StorageSlot.getAddressSlot(_BEACON_SLOT).value;
    }

    /**
     * @dev Stores a new beacon in the EIP1967 beacon slot.
     */
    function _setBeacon(address newBeacon) private {
        require(Address.isContract(newBeacon), "ERC1967: new beacon is not a contract");
        require(
            Address.isContract(IBeacon(newBeacon).implementation()),
            "ERC1967: beacon implementation is not a contract"
        );
        StorageSlot.getAddressSlot(_BEACON_SLOT).value = newBeacon;
    }

    /**
     * @dev Perform beacon upgrade with additional setup call. Note: This upgrades the address of the beacon, it does
     * not upgrade the implementation contained in the beacon (see {UpgradeableBeacon-_setImplementation} for that).
     *
     * Emits a {BeaconUpgraded} event.
     */
    function _upgradeBeaconToAndCall(
        address newBeacon,
        bytes memory data,
        bool forceCall
    ) internal {
        _setBeacon(newBeacon);
        emit BeaconUpgraded(newBeacon);
        if (data.length > 0 || forceCall) {
            Address.functionDelegateCall(IBeacon(newBeacon).implementation(), data);
        }
    }
}

/**
 * @dev This abstract contract provides a fallback function that delegates all calls to another contract using the EVM
 * instruction `delegatecall`. We refer to the second contract as the _implementation_ behind the proxy, and it has to
 * be specified by overriding the virtual {_implementation} function.
 *
 * Additionally, delegation to the implementation can be triggered manually through the {_fallback} function, or to a
 * different contract through the {_delegate} function.
 *
 * The success and return data of the delegated call will be returned back to the caller of the proxy.
 */
abstract contract Proxy {
    /**
     * @dev Delegates the current call to `implementation`.
     *
     * This function does not return to its internal call site, it will return directly to the external caller.
     */
    function _delegate(address implementation) internal virtual {
        assembly {
            // Copy msg.data. We take full control of memory in this inline assembly
            // block because it will not return to Solidity code. We overwrite the
            // Solidity scratch pad at memory position 0.
            calldatacopy(0, 0, calldatasize())

            // Call the implementation.
            // out and outsize are 0 because we don't know the size yet.
            let result := delegatecall(gas(), implementation, 0, calldatasize(), 0, 0)

            // Copy the returned data.
            returndatacopy(0, 0, returndatasize())

            switch result
            // delegatecall returns 0 on error.
            case 0 {
                revert(0, returndatasize())
            }
            default {
                return(0, returndatasize())
            }
        }
    }

    /**
     * @dev This is a virtual function that should be overridden so it returns the address to which the fallback function
     * and {_fallback} should delegate.
     */
    function _implementation() internal view virtual returns (address);

    /**
     * @dev Delegates the current call to the address returned by `_implementation()`.
     *
     * This function does not return to its internal call site, it will return directly to the external caller.
     */
    function _fallback() internal virtual {
        _beforeFallback();
        _delegate(_implementation());
    }

    /**
     * @dev Fallback function that delegates calls to the address returned by `_implementation()`. Will run if no other
     * function in the contract matches the call data.
     */
    fallback() external payable virtual {
        _fallback();
    }

    /**
     * @dev Fallback function that delegates calls to the address returned by `_implementation()`. Will run if call data
     * is empty.
     */
    receive() external payable virtual {
        _fallback();
    }

    /**
     * @dev Hook that is called before falling back to the implementation. Can happen as part of a manual `_fallback`
     * call, or as part of the Solidity `fallback` or `receive` functions.
     *
     * If overridden should call `super._beforeFallback()`.
     */
    function _beforeFallback() internal virtual {}
}

/**
 * @dev Library for reading and writing primitive types to specific storage slots.
 *
 * Storage slots are often used to avoid storage conflict when dealing with upgradeable contracts.
 * This library helps with reading and writing to such slots without the need for inline assembly.
 *
 * The functions in this library return Slot structs that contain a `value` member that can be used to read or write.
 */
library StorageSlot {
    struct AddressSlot {
        address value;
    }

    struct BooleanSlot {
        bool value;
    }

    struct Bytes32Slot {
        bytes32 value;
    }

    struct Uint256Slot {
        uint256 value;
    }

    /**
     * @dev Returns an `AddressSlot` with member `value` located at `slot`.
     */
    function getAddressSlot(bytes32 slot) internal pure returns (AddressSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `BooleanSlot` with member `value` located at `slot`.
     */
    function getBooleanSlot(bytes32 slot) internal pure returns (BooleanSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Bytes32Slot` with member `value` located at `slot`.
     */
    function getBytes32Slot(bytes32 slot) internal pure returns (Bytes32Slot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Uint256Slot` with member `value` located at `slot`.
     */
    function getUint256Slot(bytes32 slot) internal pure returns (Uint256Slot storage r) {
        assembly {
            r.slot := slot
        }
    }
}
```

## 安全注意事项

此 ERC 依赖于所选存储槽**不**由 Solidity 编译器分配。这确保了实现合约不会意外覆盖代理操作所需的任何信息。因此，选择了具有较高槽号的位置，以避免与编译器分配的槽发生冲突。此外，选择了没有已知原像的位置，以确保具有恶意构建的键的映射的写入不会覆盖它。

意图修改特定于代理的信息的逻辑合约必须有意识地这样做（就像 UUPS 一样），通过写入到特定的存储槽。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权和相关权利。