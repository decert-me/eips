---
eip: 7579
title: 最小模块化智能账户
description: 模块化智能账户接口和行为，以实现账户和模块之间的最小限制的互操作性
author: zeroknots (@zeroknots), Konrad Kopp (@kopy-kat), Taek Lee (@leekt), Fil Makarov (@filmakarov), Elim Poon (@yaonam), Lyu Min (@rockmin216)
discussions-to: https://ethereum-magicians.org/t/erc-7579-minimal-modular-smart-accounts/17336
status: 草案
type: 标准跟踪
category: ERC
created: 2023-12-14
requires: 165, 1271, 2771, 4337
---

## 摘要

本提案概述了模块化智能账户和模块所需的最小接口和行为，以确保跨实现的互操作性。对于账户，标准指定了执行、配置和回退接口，以及遵循 [ERC-165](./eip-165.md) 和 [ERC-1271](./eip-1271.md) 的合规性。对于模块，标准指定了核心接口、模块类型和特定类型接口。

## 动机

合约账户正在获得广泛采用，许多账户是使用模块化架构构建的。这些模块化合约账户（以下简称智能账户）将功能移入外部合约（模块），以提高创新的速度和潜力，确保其未来的适应性，并允许开发者和用户进行定制。然而，目前这些智能账户的构建方式差异很大，造成了模块碎片化和供应商锁定。标准化智能账户对生态系统非常有益的原因包括：

- 模块可以在不同智能账户之间互操作
- 智能账户可以在不同钱包应用和 SDK 之间互操作
- 防止智能账户用户的重大供应商锁定

然而，至关重要的是，这种标准化必须对账户的实现逻辑产生最小影响，以便智能账户供应商能够继续创新，同时也允许一个繁荣的、多账户兼容的模块生态系统。因此，本标准的目标是定义尽可能最小的智能账户和模块接口及行为，同时确保账户和模块之间的互操作性。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按照 RFC 2119 和 RFC 8174 的描述进行解释。

### 定义

- **智能账户** - 具有模块化架构的智能合约账户。
- **模块** - 具有自包含智能账户功能的智能合约。
  - 验证器：在验证阶段用于确定交易是否有效并应在账户上执行的模块。
  - 执行器：可以代表智能账户通过回调执行交易的模块。
  - 回退处理程序：可以扩展智能账户回退功能的模块。
- **入口点** - 根据 [ERC-4337](./eip-4337.md) 规范的受信任单例合约。

### 账户

#### 验证

本标准不规定验证器选择的实现方式。然而，如果智能账户在传递给验证器的数据字段中编码验证器选择机制（例如，在与 ERC-4337 一起使用时的 `userOp.signature`），则智能账户必须在调用验证器之前对受影响的值进行清理。

智能账户的验证函数应返回验证器的返回值。

#### 执行行为

为了遵守本标准，智能账户必须实现以下执行接口：

```solidity
interface IExecution {
    /**
     * @dev Executes a transaction on behalf of the account.
     * @param mode The encoded execution mode of the transaction.
     * @param executionCalldata The encoded execution call data.
     *
     * MUST ensure adequate authorization control: e.g. onlyEntryPointOrSelf if used with ERC-4337
     * If a mode is requested that is not supported by the Account, it MUST revert
     */
    function execute(bytes32 mode, bytes calldata executionCalldata) external;

    /**
     * @dev Executes a transaction on behalf of the account.
     *         This function is intended to be called by Executor Modules
     * @param mode The encoded execution mode of the transaction.
     * @param executionCalldata The encoded execution call data.
     *
     * MUST ensure adequate authorization control: i.e. onlyExecutorModule
     * If a mode is requested that is not supported by the Account, it MUST revert
     */
    function executeFromExecutor(bytes32 mode, bytes calldata executionCalldata)
        external
        returns (bytes[] memory returnData);
}
```

账户还可以根据 ERC-4337 实现以下函数：

```solidity
/**
 * @dev ERC-4337 executeUserOp according to ERC-4337 v0.7
 *         This function is intended to be called by ERC-4337 EntryPoint.sol
 * @param userOp PackedUserOperation struct (see ERC-4337 v0.7+)
 * @param userOpHash The hash of the PackedUserOperation struct
 *
 * MUST ensure adequate authorization control: i.e. onlyEntryPoint
 */
function executeUserOp(PackedUserOperation calldata userOp, bytes32 userOpHash) external;
```

如果账户选择实现 `executeUserOp`，则该方法应确保账户执行 `userOp.calldata`，除了前 4 个最重要的字节，这些字节保留用于 `executeUserOp.selector`，因此 `userOp.callData[4:]` 应表示对账户的有效调用的 calldata。建议账户执行 `delegatecall` 以保留原始的 `msg.sender` 到账户。

示例：
```
(bool success, bytes memory innerCallRet) = address(this).delegatecall(userOp.callData[4:]);
```

执行模式是一个 `bytes32` 值，其结构如下：

- callType (1 字节)：`0x00` 表示单个 `call`，`0x01` 表示批量 `call`，`0xfe` 表示 `staticcall`，`0xff` 表示 `delegatecall`
- execType (1 字节)：`0x00` 表示在失败时回退的执行，`0x01` 表示在失败时不回退但实现某种形式的错误处理的执行
- unused (4 字节)：此范围保留用于未来标准化
- modeSelector (4 字节)：可用于创建进一步执行模式的附加模式选择器
- modePayload (22 字节)：要传递的附加数据

以下是执行模式的可视化表示：

| CallType | ExecType | Unused  | ModeSelector | ModePayload |
| -------- | -------- | ------- | ------------ | ----------- |
| 1 字节   | 1 字节   | 4 字节 | 4 字节      | 22 字节    |

账户不要求实现所有执行模式。账户必须在 `supportsExecutionMode` 中声明支持的模式（见下文），如果请求的模式不被账户支持，则账户必须回退。

账户必须以以下方式编码执行数据：

- 对于单个调用，`target`、`value` 和 `callData` 按此顺序打包（即 Solidity 中的 `abi.encodePacked`）。
- 对于 delegatecalls，`target` 和 `callData` 按此顺序打包（即 Solidity 中的 `abi.encodePacked`）。
- 对于批量调用，`targets`、`values` 和 `callDatas` 被放入一个包含这些字段的 `Execution` 结构数组中（即 `Execution(address target, uint256 value, bytes memory callData)`）。然后，这个数组使用填充进行编码（即 Solidity 中的 `abi.encode`）。

#### 账户配置

为了遵守本标准，智能账户必须实现以下账户配置接口：

```solidity
interface IAccountConfig {
    /**
     * @dev Returns the account id of the smart account
     * @return accountImplementationId the account id of the smart account
     *
     * MUST return a non-empty string
     * The accountId SHOULD be structured like so:
     *        "vendorname.accountname.semver"
     * The id SHOULD be unique across all smart accounts
     */
    function accountId() external view returns (string memory accountImplementationId);

    /**
     * @dev Function to check if the account supports a certain execution mode (see above)
     * @param encodedMode the encoded mode
     *
     * MUST return true if the account supports the mode and false otherwise
     */
    function supportsExecutionMode(bytes32 encodedMode) external view returns (bool);

    /**
     * @dev Function to check if the account supports a certain module typeId
     * @param moduleTypeId the module type ID according to the ERC-7579 spec
     *
     * MUST return true if the account supports the module type and false otherwise
     */
    function supportsModule(uint256 moduleTypeId) external view returns (bool);
}
```

#### 模块配置

为了遵守本标准，智能账户必须实现以下模块配置接口。

在存储已安装模块时，智能账户必须确保有办法区分模块类型。例如，智能账户应该能够实现访问控制，仅允许已安装的执行器，而不允许其他已安装模块调用 `executeFromExecutor` 函数。

```solidity
interface IModuleConfig {
    event ModuleInstalled(uint256 moduleTypeId, address module);
    event ModuleUninstalled(uint256 moduleTypeId, address module);

    /**
     * @dev Installs a Module of a certain type on the smart account
     * @param moduleTypeId the module type ID according to the ERC-7579 spec
     * @param module the module address
     * @param initData arbitrary data that may be required on the module during `onInstall`
     * initialization.
     *
     * MUST implement authorization control
     * MUST call `onInstall` on the module with the `initData` parameter if provided
     * MUST emit ModuleInstalled event
     * MUST revert if the module is already installed or the initialization on the module failed
     */
    function installModule(uint256 moduleTypeId, address module, bytes calldata initData) external;

    /**
     * @dev Uninstalls a Module of a certain type on the smart account
     * @param moduleTypeId the module type ID according the ERC-7579 spec
     * @param module the module address
     * @param deInitData arbitrary data that may be required on the module during `onInstall`
     * initialization.
     *
     * MUST implement authorization control
     * MUST call `onUninstall` on the module with the `deInitData` parameter if provided
     * MUST emit ModuleUninstalled event
     * MUST revert if the module is not installed or the deInitialization on the module failed
     */
    function uninstallModule(uint256 moduleTypeId, address module, bytes calldata deInitData) external;

    /**
     * @dev Returns whether a module is installed on the smart account
     * @param moduleTypeId the module type ID according the ERC-7579 spec
     * @param module the module address
     * @param additionalContext arbitrary data that may be required to determine if the module is installed
     *
     * MUST return true if the module is installed and false otherwise
     */
    function isModuleInstalled(uint256 moduleTypeId, address module, bytes calldata additionalContext) external view returns (bool);
}
```

#### 钩子

钩子是本标准的可选扩展。智能账户可以使用钩子在智能账户执行单个或批量执行之前和/或之后执行自定义逻辑和检查。为了遵守此可选扩展，智能账户：

- 必须在账户执行期间调用一个或多个钩子的 `preCheck` 函数
- 必须在账户执行期间调用一个或多个钩子的 `postCheck` 函数

#### ERC-1271 转发

智能账户必须实现 ERC-1271 接口。`isValidSignature` 函数调用可以转发到验证器。如果实现了 ERC-1271 转发，则必须使用 `isValidSignatureWithSender(address sender, bytes32 hash, bytes signature)` 调用验证器，其中发送者是对智能账户调用的 `msg.sender`。如果智能账户在 `bytes signature` 参数中实现了任何验证器选择编码，则智能账户必须在将其转发给验证器之前对该参数进行清理。
智能账户的 ERC-1271 `isValidSignature` 函数应返回请求转发到的验证器的返回值。

#### 回退

智能账户可以实现一个回退函数，将调用转发到回退处理程序。

如果智能账户安装了回退处理程序，它：

- 必须使用 `call` 或 `staticcall` 来调用回退处理程序
- 必须利用 [ERC-2771](./eip-2771.md) 将原始 `msg.sender` 添加到发送给回退处理程序的 `calldata` 中
- 必须根据 `calldata` 的函数选择器路由到回退处理程序
- 可以实现授权控制，这应通过钩子完成

如果账户通过回退添加功能，这些功能应视为账户原生实现这些功能。
ERC-165 支持（见下文）就是这种方法的一个例子。请注意，仅建议在回退中实现视图函数，以便实现更大的可扩展性。不建议通过回退实现核心账户逻辑。

#### ERC-165

智能账户必须实现 ERC-165。然而，对于每个未实现功能而导致回退的接口函数，智能账户必须返回 `false` 对应的接口 ID。

### 模块

该标准将模块分为以下不同类型，每种类型都有唯一的增量标识符，账户、模块和其他实体必须使用这些标识符来识别模块类型：

- 验证（类型 ID：1）
- 执行（类型 ID：2）
- 回退（类型 ID：3）
- 钩子（类型 ID：4）

注意：单个模块可以属于多种类型。

模块必须实现以下接口：

```solidity
interface IModule {
     /**
     * @dev This function is called by the smart account during installation of the module
     * @param data arbitrary data that may be required on the module during `onInstall` initialization
     *
     * MUST revert on error (e.g. if module is already enabled)
     */
    function onInstall(bytes calldata data) external;

    /**
     * @dev This function is called by the smart account during uninstallation of the module
     * @param data arbitrary data that may be required on the module during `onUninstall` de-initialization
     *
     * MUST revert on error
     */
    function onUninstall(bytes calldata data) external;

    /**
     * @dev Returns boolean value if module is a certain type
     * @param moduleTypeId the module type ID according the ERC-7579 spec
     *
     * MUST return true if the module is of the given type and false otherwise
     */
    function isModuleType(uint256 moduleTypeId) external view returns(bool);
}
```

注意：单个模块如果属于多种类型，可以选择在 `data` 中传递 `moduleTypeId` 到 `onInstall` 和/或 `onUninstall` 方法，以便这些方法能够正确处理各种类型的安装/卸载。  
示例：
```solidity
// Module.sol
function onInstall(bytes calldata data) external {
    // ...
    (uint256 moduleTypeId, bytes memory otherData) = abi.decode(data, (uint256, bytes));
    // ...
}
```

#### 验证器

验证器必须实现 `IModule` 和 `IValidator` 接口，并具有模块类型 ID：`1`。

```solidity
interface IValidator is IModule {
    /**
     * @dev Validates a UserOperation
     * @param userOp the ERC-4337 PackedUserOperation
     * @param userOpHash the hash of the ERC-4337 PackedUserOperation
     *
     * MUST validate that the signature is a valid signature of the userOpHash
     * SHOULD return ERC-4337's SIG_VALIDATION_FAILED (and not revert) on signature mismatch
     */
    function validateUserOp(PackedUserOperation calldata userOp, bytes32 userOpHash) external returns (uint256);

    /**
     * @dev Validates a signature using ERC-1271
     * @param sender the address that sent the ERC-1271 request to the smart account
     * @param hash the hash of the ERC-1271 request
     * @param signature the signature of the ERC-1271 request
     *
     * MUST return the ERC-1271 `MAGIC_VALUE` if the signature is valid
     * MUST NOT modify state
     */
    function isValidSignatureWithSender(address sender, bytes32 hash, bytes calldata signature) external view returns (bytes4);
}
```

#### 执行者

执行者必须实现 `IModule` 接口，并具有模块类型 ID：`2`。

#### 回退处理程序

回退处理程序必须实现 `IModule` 接口，并具有模块类型 ID：`3`。

回退处理程序可以实现授权控制。实现授权控制的回退处理程序，必须不依赖 `msg.sender` 进行授权控制，而必须使用 ERC-2771 的 `_msgSender()`。

#### 钩子

钩子必须实现 `IModule` 和 `IHook` 接口，并具有模块类型 ID：`4`。

```solidity
interface IHook is IModule {
    /**
     * @dev Called by the smart account before execution
     * @param msgSender the address that called the smart account
     * @param value the value that was sent to the smart account
     * @param msgData the data that was sent to the smart account
     *
     * MAY return arbitrary data in the `hookData` return value
     */
    function preCheck(address msgSender, uint256 value, bytes calldata msgData) external returns (bytes memory hookData);

    /**
     * @dev Called by the smart account after execution
     * @param hookData the data that was returned by the `preCheck` function
     *
     * MAY validate the `hookData` to validate transaction context of the `preCheck` function
     */
    function postCheck(bytes calldata hookData) external;
}
```

## 理由

### 最小化方法

智能账户是一个新概念，我们仍在学习构建它们的最佳方法。因此，我们不应对它们的构建方式过于主观。相反，我们应定义最小的接口，以允许智能账户和模块之间的互操作性，以便在不同的账户实现中使用。

我们的方法有两个方面：

1. 从已在生产中使用的现有智能账户和构建它们之间的互操作性层中获取经验
2. 确保接口尽可能最小且开放，以便于替代架构

### 扩展

虽然我们希望保持最小，但我们也希望允许创新和有主见的功能。这些功能中的一些可能也需要标准化（出于与核心接口类似的原因），即使并非所有智能账户都会实现它们。为了确保这一点，我们建议未来的标准化工作作为对该标准的扩展进行。这意味着核心接口不会改变，但可以作为扩展添加新接口。这些应作为单独的 ERC 提出，例如标题为 `[FEATURE] Extension for ERC-7579`。

### 规范

#### 执行模式

账户需要能够以不同的方式执行 `calldata`。我们决定将执行类型编码为单个 `bytes32` 值，而不是为每种执行类型的组合定义单独的函数。这允许更灵活和可扩展的方法，同时使代码更易于编写、阅读、维护和审计。如上所述，执行模式由两个字节组成，编码调用类型和执行类型。调用类型涵盖三种不同的调用方法，即单次、批量和 `delegatecall`（请注意，您可以对多重调用合约进行 `delegatecall` 以批量处理 `delegatecalls`）。执行类型涵盖两种不同类型的执行，即在失败时回退的执行和在失败时不回退但实现某种形式的错误处理的执行。这允许账户将不相关的执行批量在一起，以便如果一个执行失败，其他执行仍然可以执行。这两个字节后面是 4 个未使用的字节，保留用于未来的标准化（如果需要）。接下来是一个 4 字节的项目，这是一个自定义模式选择器，账户可以实现。这允许账户实现不在标准范围内且不需要标准化的自定义执行模式。该项目长度为 4 字节，以确保不同账户供应商之间的碰撞抵抗，具有与 Solidity 函数选择器相同的保证。最后，最后 22 字节保留用于可以传递给账户的自定义数据。这允许账户传递最多 22 字节的任何数据，例如一个 2 字节的标志后跟一个地址，或者指向进一步数据的指针，这些数据打包在执行的 `calldata` 中。例如，这个有效负载可以用于传递一个钩子地址，该地址应在执行之前和/或之后执行。

#### 区分模块类型

不区分模块类型可能在执行授权控制时带来安全问题。例如，如果智能账户将验证器和执行者视为同一类型的模块，则可能允许验证器代表智能账户执行任意交易。

#### 账户 ID

账户配置接口包括一个函数 `accountId`，可用于识别账户。这对于需要确定正在使用的账户类型和版本的前端库特别有用，以便实现正确的账户行为逻辑，这些逻辑并未标准化。替代解决方案包括使用类似 ERC-165 的接口来声明账户的确切差异和支持的功能，或返回账户 ID 的 keccak 哈希。然而，第一个解决方案不如账户 ID 灵活，并且需要就使用的功能集达成一致，而第二个解决方案不如账户 ID 具有可读性。

#### 对 ERC-4337 的依赖

该标准对 ERC-4337 的验证流程有严格的依赖。然而，智能账户构建者可能希望在未来构建不使用 ERC-4337 的模块化账户，例如在 rollup 上的原生账户抽象实现。一旦开始发生这种情况，该标准的建议升级路径是将 ERC-4337 依赖移入扩展（即单独的 ERC），并使智能账户实现它成为可选。如果需要为不同的账户抽象实现标准化验证流程，则这些要求也可以移入单独的扩展。
从一开始没有做到这一点的原因是，目前唯一正在构建的模块化账户是使用 ERC-4337。因此，首先标准化这些账户的接口是有意义的，并在需要时将 ERC-4337 依赖项移入扩展。这是为了最大化关于模块化账户在不同账户抽象实现上构建时的样子学习。

## 向后兼容性

### 已部署的智能账户

已经部署的智能账户很可能能够实现此标准。如果它们作为代理部署，则可以升级到符合此标准的新账户实现。如果它们作为不可升级的合约部署，仍然可能通过添加符合标准的适配器作为后备处理程序来实现合规（如果支持的话）。

## 参考实现

智能账户的完整接口可以在 [`IMSA.sol`](../assets/eip-7579/IMSA.sol) 中找到。

## 安全考虑

需要更多讨论。一些初步考虑：

- 在智能账户上实现 `delegatecall` 执行必须谨慎考虑。请注意，实施 `delegatecall` 的智能账户必须确保目标合约是安全的，否则可能会出现安全漏洞。
- 模块上的 `onInstall` 和 `onUninstall` 函数可能导致意外的回调（例如重入）。账户实现应通过实施适当的保护例程来考虑这一点。此外，模块可能会恶意在 `onUninstall` 上回滚，以阻止账户卸载模块并将其从账户中移除。
- 对于一次只能激活一个模块的模块类型（例如后备处理程序），在新模块上调用 `installModule` 将不会正确卸载先前的模块，除非正确实现。这可能导致意外行为，如果旧模块随后再次添加并留下状态。
- 后备处理程序中的授权控制不足可能导致未经授权的执行。
- 恶意钩子可能在 `preCheck` 或 `postCheck` 上回滚，添加不受信任的钩子可能导致账户的拒绝服务。
- 目前账户配置函数（例如 `installModule`）设计为单一操作。账户可以允许从 `address(this)` 调用这些函数，从而创建批量配置操作的可能性。然而，如果账户对这些函数实施更严格的授权控制，因为它们更敏感，那么这些措施可以通过在对自身的调用中嵌套对配置选项的调用来绕过。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。