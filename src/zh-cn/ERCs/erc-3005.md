---
eip: 3005
title: 批量元交易
author: Matt (@defifuture)
discussions-to: https://ethereum-magicians.org/t/eip-3005-the-economic-viability-of-batched-meta-transactions/4673
status: Stagnant
type: Standards Track
category: ERC
created: 2020-09-25
---

## 简单总结

定义了一个 ERC-20（和其他可替代代币标准）的扩展函数，允许接收和处理一批元交易。

## 摘要

此 EIP 定义了一个名为 `processMetaBatch()` 的新函数，扩展任何可替代代币标准，并允许来自多个发送者的批量元交易在一个链上交易中进行处理。

该函数必须能够接收多个元交易数据并进行处理。这意味着在根据数据进行代币转移之前，必须验证数据和签名。

该函数使发送者能够进行无 gas 交易，同时由于批量处理减少了中继者的 gas 成本。

## 动机

元交易已被证明是一个有用的解决方案，适用于没有以太币但持有 ERC-20 代币并希望转移它们的以太坊账户（无 gas 交易）。

当前的元交易中继实现仅允许一次中继一个元交易。有些还允许来自同一发送者的批量元交易。但没有任何实现提供来自**多个**发送者的批量元交易。

此 EIP 的动机是找到一种方法，允许在**一个链上交易**中中继来自**多个发送者**的批量元交易，同时**减少中继者需要承担的总 gas 成本**。

![](../assets/eip-3005/meta-txs-directly-to-token-smart-contract.png)

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

本文档中的关键字 "MUST (BUT WE KNOW YOU WON'T)"、"SHOULD CONSIDER"、"REALLY SHOULD NOT"、"OUGHT TO"、"WOULD PROBABLY"、"MAY WISH TO"、"COULD"、"POSSIBLE" 和 "MIGHT" 应按 RFC 6919 中的描述进行解释。

### 元交易数据

为了成功验证和转移代币，`processMetaBatch()` 函数必须处理以下关于元交易的数据：

- 发送者地址
- 接收者地址
- 代币数量
- 中继费用
- 一个（元交易）随机数
- 过期日期（这可以是区块号，也可以是区块时间戳）
- 代币地址
- 中继者地址
- 签名

并非所有数据都需要由中继者发送到该函数（请参见函数接口规范）。某些数据可以从其他来源（交易数据和合约状态）推导或提取。

### `processMetaBatch()` 函数输入数据

`processMetaBatch()` 函数必须接收以下数据：

- 发送者地址
- 接收者地址
- 代币数量
- 中继费用
- 过期日期（这可以是区块号，也可以是区块时间戳）
- 签名

以下数据是可选的，因为可以从其他来源提取或推导：

- 一个（元交易）随机数
- 代币地址
- 中继者地址

### 元交易数据哈希

创建元交易数据哈希的伪代码如下：

```
keccak256(address(sender)
	   ++ address(recipient)
	   ++ uint256(amount)
	   ++ uint256(relayerFee)
	   ++ uint256(nonce)
	   ++ uint256(expirationDate)
	   ++ address(tokenContract)
	   ++ address(relayer)
)
```

创建的哈希必须使用发送者的私钥进行签名。

### 验证规则

- 新交易的随机数必须始终比同一发送者到同一代币合约的最后一个成功处理的元交易的随机数大 1。
- 发送到和从 0x0 地址的交易必须被禁止。
- 元交易必须在过期日期之前处理。
- 每个发送者的代币余额必须等于或大于其各自元交易代币数量和中继费用的总和。
- 如果批量中的至少一个元交易不满足上述要求，则该交易不得回滚。相反，失败的元交易必须被跳过或忽略。

### `processMetaBatch()` 函数接口

`processMetaBatch()` 函数必须具有以下接口：

```solidity
function processMetaBatch(address[] memory senders,
                          address[] memory recipients,
                          uint256[] memory amounts,
                          uint256[] memory relayerFees,
                          uint256[] memory blocks,
                          uint8[] memory sigV,
                          bytes32[] memory sigR,
                          bytes32[] memory sigS) public returns (bool);
```

传递的参数概述：

- `senders`: 元交易发送者地址（代币发送者）的数组
- `recipients `: 代币接收者地址的数组
- `amounts`: 从每个发送者到每个接收者发送的代币数量的数组
- `relayerFees`: 发送者支付的中继费用的代币数组。费用接收者是中继者（`msg.address`）
- `blocks`: 表示元交易必须处理的过期日期的区块号数组（或者可以使用时间戳代替区块号）
- `sigV`、`sigR`、`sigS`: 表示元交易签名部分的三个数组

每个数组中的每个条目必须代表一个元交易的数据。数据的顺序非常重要。单个元交易的数据必须在每个数组中具有相同的索引。

### 元交易随机数

代币智能合约必须跟踪每个代币持有者的元交易随机数。

```solidity
mapping (address => uint256) private _metaNonces;
```

`nonceOf()` 函数的接口如下：

```solidity
function nonceOf(address account) public view returns (uint256);
```

### 代币转移

在元交易成功验证后，元交易发送者的元随机数必须增加 1。

然后必须发生两个代币转移：

- 指定的代币数量必须转给接收者。
- 中继费用必须转给中继者（`msg.sender`）。

## 实现

**参考实现**在现有的 ERC-20 代币标准中添加了一些函数：

- `processMetaBatch()`
- `nonceOf()`

您可以在此文件中查看这两个函数的实现：[ERC20MetaBatch.sol](https://github.com/defifuture/erc20-batched-meta-transactions/blob/master/contracts/ERC20MetaBatch.sol)。这是一个扩展的 ERC-20 合约，增加了元交易批量转移的能力。

### `processMetaBatch()`

`processMetaBatch()` 函数负责接收和处理一批更改代币余额的元交易。

```solidity
function processMetaBatch(address[] memory senders,
                          address[] memory recipients,
                          uint256[] memory amounts,
                          uint256[] memory relayerFees,
                          uint256[] memory blocks,
                          uint8[] memory sigV,
                          bytes32[] memory sigR,
                          bytes32[] memory sigS) public returns (bool) {
    
    address sender;
    uint256 newNonce;
    uint256 relayerFeesSum = 0;
    bytes32 msgHash;
    uint256 i;

    // loop through all meta txs
    for (i = 0; i < senders.length; i++) {
        sender = senders[i];
        newNonce = _metaNonces[sender] + 1;

        if(sender == address(0) || recipients[i] == address(0)) {
            continue; // sender or recipient is 0x0 address, skip this meta tx
        }

        // the meta tx should be processed until (including) the specified block number, otherwise it is invalid
        if(block.number > blocks[i]) {
            continue; // if current block number is bigger than the requested number, skip this meta tx
        }

        // check if meta tx sender's balance is big enough
        if(_balances[sender] < (amounts[i] + relayerFees[i])) {
            continue; // if sender's balance is less than the amount and the relayer fee, skip this meta tx
        }

        // check if the signature is valid
        msgHash = keccak256(abi.encode(sender, recipients[i], amounts[i], relayerFees[i], newNonce, blocks[i], address(this), msg.sender));
        if(sender != ecrecover(keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", msgHash)), sigV[i], sigR[i], sigS[i])) {
            continue; // if sig is not valid, skip to the next meta tx
        }

        // set a new nonce for the sender
        _metaNonces[sender] = newNonce;

        // transfer tokens
        _balances[sender] -= (amounts[i] + relayerFees[i]);
        _balances[recipients[i]] += amounts[i];
        relayerFeesSum += relayerFees[i];
    }

	// give the relayer the sum of all relayer fees
    _balances[msg.sender] += relayerFeesSum;

    return true;
}
```

### `nonceOf()`

由于重放保护的需要，随机数是必需的（请参见 *重放攻击* 下的 *安全考虑*）。

```solidity
mapping (address => uint256) private _metaNonces;

// ...

function nonceOf(address account) public view returns (uint256) {
    return _metaNonces[account];
}
```

完整实现的链接（以及 gas 使用结果）在这里：[https://github.com/defifuture/erc20-batched-meta-transactions](https://github.com/defifuture/erc20-batched-meta-transactions)。

> 请注意，这里使用了 OpenZeppelin 的 ERC-20 实现。其他一些实现可能将 `_balances` 映射命名为不同的名称，这将需要在 `processMetaBatch()` 函数中进行小的更改。

## 理由

### 一体化

替代实现（如 GSN）使用多个智能合约来启用元交易，尽管这增加了 gas 使用。此实现（EIP-3005）故意将所有内容保持在一个函数中，从而减少复杂性和 gas 成本。

因此，`processMetaBatch()` 函数的工作是接收一批元交易，验证它们，然后将代币从一个地址转移到另一个地址。

### 函数参数

如您所见，参考实现中的 `processMetaBatch()` 函数接受以下参数：
- 一个 **发送者地址** 的数组（meta tx 发送者，而不是中继者）
- 一个 **接收者地址** 的数组
- 一个 **金额** 的数组
- 一个 **中继费用** 的数组（中继者是 `msg.sender`）
- 一个 **区块编号** 的数组（meta tx 被处理的到期“日期”）
- 三个数组，表示 **签名** 的部分（v, r, s）

**这些数组中的每个项目** 代表 **一个 meta 交易** 的 **数据**。这就是为什么数组中的 **正确顺序** 非常重要。

如果中继者的顺序错误，`processMetaBatch()` 函数会注意到这一点（在验证签名时），因为 meta 交易值的哈希将与签名哈希不匹配。具有无效签名的 meta 交易会被 **跳过**。

### 传递 meta 交易数据到函数的替代方法

参考实现将参数作为数组传递。每个 meta 交易数据类别都有一个单独的数组（那些无法从其他来源推导或提取的）。

另一种方法是将一个 meta 交易的所有数据打包成一个值，然后在智能合约中解包。一个批次的 meta 交易数据将以一个数组的形式发送，但只需要一个数组（打包数据），而不是多个数组。

### 为什么 nonce 不是参考实现中的参数之一？

Meta nonce 用于构造签名哈希（请参见构造 `keccak256` 哈希的 `msgHash` 行 - 你会在那找到一个 nonce）。

由于新的 nonce 必须始终比前一个大 1，因此没有必要将其作为参数数组包含在 `processMetaBatch()` 函数中，因为它的值可以推导出来。

这也有助于避免“堆栈太深”错误。

### EIP-2612 nonce 映射可以重用吗？

EIP-2612（`permit()` 函数）也需要一个 nonce 映射。目前，我还不确定如果一个智能合约同时实现 EIP-3005 和 EIP-2612，这个映射是否应该 **重用**。

乍一看，EIP-2612 的 `nonces` 映射似乎可以重用，但这应该经过深思熟虑（并测试）以考虑可能的安全隐患。

### 代币转移

参考实现中的代币转移可以通过调用 `_transfer()` 函数（OpenZeppelin ERC-20 实现的一部分）来替代，但这会增加 gas 使用量，并且如果某个 meta 交易无效，整个批次也会回滚（当前实现只是跳过它）。

另一个 gas 使用优化是在函数结束时将总中继费用分配给中继者，而不是在 for 循环中的每次代币转移时（从而避免多次 SSTORE 调用，这会消耗 5000 gas）。

## 向后兼容性

批量 meta 交易的代码实现与任何可替代代币标准向后兼容，例如 ERC-20（它仅通过一个函数扩展它）。

## 测试用例

测试链接：[https://github.com/defifuture/erc20-batched-meta-transactions/tree/master/test](https://github.com/defifuture/erc20-batched-meta-transactions/tree/master/test)。

## 安全考虑

以下是潜在安全问题的列表，以及在此实现中如何解决它们。

### 伪造 meta 交易

防止中继者伪造 meta 交易的解决方案是用户使用他们的私钥签署 meta 交易。

`processMetaBatch()` 函数随后使用 `ecrecover()` 验证签名。

### 重放攻击

`processMetaBatch()` 函数对两种类型的重放攻击是安全的：

**在同一代币智能合约中两次使用相同的 meta 交易**

一个 nonce 防止中继者多次发送相同的 meta 交易的重放攻击。

**在不同代币智能合约中两次使用相同的 meta 交易**

代币智能合约地址必须添加到签名哈希中（meta 交易的哈希）。

这个地址不需要作为参数发送到 `processMetaBatch()` 函数。相反，函数在构造哈希时使用 `address(this)` 来验证签名。这样，未针对代币智能合约的 meta 交易将被拒绝（跳过）。

### 签名验证

签署 meta 交易并验证签名对整个方案的正常运作至关重要。

`processMetaBatch()` 函数验证 meta 交易签名，如果它是 **无效的**，则该 meta 交易会被 **跳过**（但整个链上交易不会 **回滚**）。

```solidity
msgHash = keccak256(abi.encode(sender, recipients[i], amounts[i], relayerFees[i], newNonce, blocks[i], address(this), msg.sender));

if(sender != ecrecover(keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", msgHash)), sigV[i], sigR[i], sigS[i])) {
    continue; // if sig is not valid, skip to the next meta tx
}
```

为什么不回滚整个链上交易？因为可能只有一个有问题的 meta 交易，而其他交易不应该因为一个坏苹果而被丢弃。

也就是说，期望中继者在转发之前先验证 meta 交易。这就是为什么无效的 meta 交易中继者没有资格获得中继费用。

### 恶意中继者迫使用户过度支出

恶意中继者可能会延迟发送某个用户的 meta 交易，直到用户决定在链上进行代币交易。

之后，中继者将转发延迟的 meta 交易，这意味着用户将进行两次代币交易（过度支出）。

**解决方案：** 每个 meta 交易应具有“到期日期”。这以区块编号的形式定义，meta 交易必须在此区块编号之前在链上转发。

```solidity
function processMetaBatch(...
                          uint256[] memory blocks,
                          ...) public returns (bool) {
    
    //...

	// loop through all meta txs
    for (i = 0; i < senders.length; i++) {

        // the meta tx should be processed until (including) the specified block number, otherwise it is invalid
        if(block.number > blocks[i]) {
            continue; // if current block number is bigger than the requested number, skip this meta tx
        }

        //...
```

### 前置攻击

恶意中继者可能会侦查以太坊内存池以窃取 meta 交易并抢先于原始中继者。

**解决方案：** `processMetaBatch()` 函数使用的保护措施是要求 meta 交易发送者将中继者的以太坊地址作为哈希中的一个值（然后进行签名）。

当 `processMetaBatch()` 函数生成哈希时，它将 `msg.sender` 地址包含在内：

```solidity
msgHash = keccak256(abi.encode(sender, recipients[i], amounts[i], relayerFees[i], newNonce, blocks[i], address(this), msg.sender));

if(sender != ecrecover(keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", msgHash)), sigV[i], sigR[i], sigS[i])) {
    continue; // if sig is not valid, skip to the next meta tx
}
```

如果 meta 交易被“窃取”，签名检查将失败，因为 `msg.sender` 地址将与预期的中继者地址不相同。

### 恶意（或过于不耐烦）用户通过多个中继者同时发送具有相同 nonce 的 meta 交易

一个恶意或只是急躁的用户可能会向多个中继者提交具有相同 nonce（针对同一代币合约）的 meta 交易。只有一个会获得中继费用（链上的第一个），而其他的将获得无效的 meta 交易。

**解决方案：** 中继者可以 **共享他们的待处理 meta 交易列表**（某种信息内存池）。

中继者不必担心有人会窃取他们各自的待处理交易，因为有前置保护（见上文）。

如果中继者看到来自某个发送者地址的具有相同 nonce 的 meta 交易，并且这些交易应该转发到同一代币智能合约，他们可以决定只有第一个注册的 meta 交易通过，其他的被丢弃（或者在 meta 交易同时注册的情况下，随机选择剩余的 meta 交易）。

至少，中继者需要共享这些 meta 交易数据（以检测 meta 交易冲突）：

- 发送者地址
- 代币地址
- nonce
### 过大的到期区块号

中继者可能会欺骗元交易发送者添加过大的到期区块号——这意味着必须处理元交易的区块。区块号可能远在未来，例如，10 年后。这意味着中继者将有 10 年的时间来提交元交易。

**解决此问题的一种方法**是在智能合约中添加区块号的上限约束。例如，我们可以说指定的到期区块号不得超过当前区块的 100,000 个区块（如果假设区块时间为 15 秒，这大约是未来 17 天）。

```solidity
// the meta tx should be processed until (including) the specified block number, otherwise it is invalid
if(block.number > blocks[i] || blocks[i] > (block.number + 100000)) {
    // If current block number is bigger than the requested due block number, skip this meta tx.
    // Also skip if the due block number is too big (bigger than 100'000 blocks in the future).
    continue;
}
```

这个添加可能会带来新的安全隐患，因此在这个概念验证中被省略。但任何希望实现此功能的人都应该了解这个潜在的约束。

**另一种方法**是保持 `processMetaBatch()` 函数不变，而是在中继者层面检查过大的到期区块号。在这种情况下，用户可以被通知到这个问题，并可以使用另一个具有更低区块参数（以及相同 nonce）的中继者发出新的元交易。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。