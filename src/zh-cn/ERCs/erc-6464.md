---
eip: 6464
title: 多操作员、每个代币的 ERC-721 授权
description: 扩展 ERC-721 以允许代币拥有者在每个代币基础上批准多个操作员控制其资产。
author: Cristian Espinoza (@crisgarner), Simon Fremaux (@dievardump), David Huber (@cxkoda), 和 Arran Schlosberg (@aschlosberg)
discussions-to: https://ethereum-magicians.org/t/fine-grained-erc721-approval-for-multiple-operators/12796
status: Stagnant
type: Standards Track
category: ERC
created: 2023-02-02
requires: 165, 721
---

## 摘要

[ERC-721](./eip-721.md) 并未预见到批准多个操作员代表其拥有者管理特定代币的情况。这导致 `setApprovalForAll()` 成为授权操作员的主要方式，这使得被批准的地址可以控制所有资产，并造成不必要的广泛安全风险，已经在多次网络钓鱼攻击中被利用。所提出的 EIP 通过引入一种细粒度的链上批准机制扩展了 ERC-721，允许拥有者在每个代币基础上授权多个特定操作员；这消除了不必要的访问权限，并将攻击面缩小到最小。提供的参考实现进一步支持在每个拥有者或每个代币基础上廉价撤销所有批准。

## 动机

在 ERC-721 中定义的 NFT 标准允许代币拥有者“批准”任意地址控制其代币——被批准的地址称为“操作员”。定义了两种类型的批准：

1. `approve(address,uint256)` 提供了仅批准单个操作员管理给定 `tokenId` 的机制；以及
2. `setApprovalForAll(address,bool)` 切换操作员是否被批准管理 `msg.sender` 拥有的*每个*代币。

随着多个 NFT 市场的出现，如果卖家希望允许每个市场在销售时转移代币，则需要批准多个操作员管理特定代币。然而，没有机制可以在不使用 `setApprovalForAll()` 的情况下实现这一点。这与最小权限原则相悖，并创建了一个攻击向量，网络钓鱼者利用这一点获取恶意（即零成本）卖方签名，这些签名由合法市场合约执行。

因此，该 EIP 定义了一种细粒度的方法来批准多个操作员，但仅限于特定代币。

### 目标

1. 便于市场的采用；对现有工作流程的更改最小。
2. 便于链下批准索引服务的采用。
3. 简单撤销批准；即不需要每个授权都撤销。

### 非目标

1. 除通过限制操作员批准的范围外，保护 NFT 的安全措施。
2. 与 [ERC-1155](./eip-1155.md) 半可替代代币的兼容性。然而，我们注意到此处描述的机制也适用于 ERC-1155 代币*类型*，而无需对所有其他类型进行批准。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“NOT RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

为了遵守此 EIP，合约必须实现 `IERC6464`（在此定义）以及 `ERC165` 和 `ERC721` 接口；请参见 [ERC-165](./eip-165.md) 和 ERC-721。

```solidity
/**
 * @notice Extends ERC-721 to include per-token approval for multiple operators.
 * @dev Off-chain indexers of approvals SHOULD assume that an operator is approved if either of `ERC721.Approval(…)` or
 * `ERC721.ApprovalForAll(…, true)` events are witnessed without the corresponding revocation(s), even if an
 * `ExplicitApprovalFor(…, false)` is emitted.
 * @dev TODO: the ERC-165 identifier for this interface is TBD.
 */
interface IERC6464 is ERC721 {
    /**
     * @notice Emitted when approval is explicitly granted or revoked for a token.
     */
    event ExplicitApprovalFor(
        address indexed operator,
        uint256 indexed tokenId,
        bool approved
    );

    /**
     * @notice Emitted when all explicit approvals, as granted by either `setExplicitApprovalFor()` function, are
     * revoked for all tokens.
     * @dev MUST be emitted upon calls to `revokeAllExplicitApprovals()`.
     */
    event AllExplicitApprovalsRevoked(address indexed owner);

    /**
     * @notice Emitted when all explicit approvals, as granted by either `setExplicitApprovalFor()` function, are
     * revoked for the specific token.
     * @param owner MUST be `ownerOf(tokenId)` as per ERC721; in the case of revocation due to transfer, this MUST be
     * the `from` address expected to be emitted in the respective `ERC721.Transfer()` event.
     */
    event AllExplicitApprovalsRevoked(
        address indexed owner,
        uint256 indexed tokenId
    );

    /**
     * @notice Approves the operator to manage the asset on behalf of its owner.
     * @dev Throws if `msg.sender` is not the current NFT owner, or an authorised operator of the current owner.
     * @dev Approvals set via this method MUST be revoked upon transfer of the token to a new owner; equivalent to
     * calling `revokeAllExplicitApprovals(tokenId)`, including associated events.
     * @dev MUST emit `ApprovalFor(operator, tokenId, approved)`.
     * @dev MUST NOT have an effect on any standard ERC721 approval setters / getters.
     */
    function setExplicitApproval(
        address operator,
        uint256 tokenId,
        bool approved
    ) external;

    /**
     * @notice Approves the operator to manage the token(s) on behalf of their owner.
     * @dev MUST be equivalent to calling `setExplicitApprovalFor(operator, tokenId, approved)` for each `tokenId` in
     * the array.
     */
    function setExplicitApproval(
        address operator,
        uint256[] memory tokenIds,
        bool approved
    ) external;

    /**
     * @notice Revokes all explicit approvals granted by `msg.sender`.
     * @dev MUST emit `AllExplicitApprovalsRevoked(msg.sender)`.
     */
    function revokeAllExplicitApprovals() external;

    /**
     * @notice Revokes all excplicit approvals granted for the specified token.
     * @dev Throws if `msg.sender` is not the current NFT owner, or an authorised operator of the current owner.
     * @dev MUST emit `AllExplicitApprovalsRevoked(msg.sender, tokenId)`.
     */
    function revokeAllExplicitApprovals(uint256 tokenId) external;

    /**
     * @notice Query whether an address is an approved operator for a token.
     */
    function isExplicitlyApprovedFor(address operator, uint256 tokenId)
        external
        view
        returns (bool);
}

interface IERC6464AnyApproval is ERC721 {
    /**
     * @notice Returns true if any of the following criteria are met:
     * 1. `isExplicitlyApprovedFor(operator, tokenId) == true`; OR
     * 2. `isApprovedForAll(ownerOf(tokenId), operator) == true`; OR
     * 3. `getApproved(tokenId) == operator`.
     * @dev The criteria MUST be extended if other mechanism(s) for approving operators are introduced. The criteria
     * MUST include all approval approaches.
     */
    function isApprovedFor(address operator, uint256 tokenId)
        external
        view
        returns (bool);
}
```

## 理由

### 草案说明待扩展

1. 通过新引入的方法授予的批准称为*显式*，以便于与通过标准 `ERC721.approve()` 和 `ERC721.setApprovalForAll()` 函数授予的批准区分。然而，它们遵循相同的意图：授权操作员代表拥有者行事。
2. 将 `isApprovedFor()` 抽象为 `IERC6464AnyApproval` 接口，而不是将其保留在 `IERC6464` 中，可以实现 `IERC6464` 实现的模块化，同时标准化与特定实现和任何未来批准 EIP 交互时检查批准的接口。
3. 在 `AllExplicitApprovalsRevoked(address,uint256)` 中包含一个索引的拥有者地址，有助于链下索引现有批准。
4. 关于 `IERC6464AnyApproval`：随着批准机制数量的增加，市场整合它们变得繁琐，因为它们必须查询多个接口以检查是否被批准管理代币。这提供了一个简化的接口，旨在简化数据摄取。

<!--
  理由通过描述设计动机和特定设计决策的原因来充实规范。它应描述考虑过的替代设计和相关工作，例如该功能在其他语言中的支持。

  当前的占位符对于草案是可以接受的。

  TODO: 提交前删除此评论
-->

## 向后兼容性

此扩展旨在对原始 ERC-721 规范进行最小更改，同时仍提供在每个代币基础上授予、撤销和跟踪多个操作员批准的机制。

扩展合约与所有现有平台完全兼容。

**注意** `Security Considerations` 子部分中的 `Other risks` 关于批准类型的相互作用。

## 参考实现

TODO: 在实现到位时添加指向资产目录的内部链接。

包括通过递增随机数进行广泛撤销批准的有效机制。

## 安全考虑

### 威胁模型

### 缓解措施

### 其他风险

TODO: 与 `setApprovalForAll()` 的相互作用。

<!--
  所有 EIP 必须包含一个讨论与提议变更相关的安全影响/考虑的部分。包括可能对安全讨论重要的信息，表面风险，并可在提案的整个生命周期中使用。例如，包括与安全相关的设计决策、关注点、重要讨论、特定实现的指导和陷阱、威胁和风险的概述以及如何应对这些风险。缺少“安全考虑”部分的 EIP 将被拒绝。EIP 在没有审阅者认为足够的安全考虑讨论的情况下，无法进入“最终”状态。

  当前的占位符对于草案是可以接受的。

  TODO: 提交前删除此评论
-->

需要讨论。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。