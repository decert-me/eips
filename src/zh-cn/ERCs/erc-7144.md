---
eip: 7144
title: 带有交易验证步骤的 ERC-20
description: 一种新的验证步骤用于转账和批准调用，以防止钱包被盗时的安全措施。
author: Eduard López i Fina (@eduardfina)
discussions-to: https://ethereum-magicians.org/t/erc721-with-a-validation-step/14071
status: Review
type: Standards Track
category: ERC
created: 2023-05-07
requires: 20
---

## 摘要

该标准是 [ERC-20](./eip-20.md) 的扩展。它定义了新的验证功能以避免钱包被盗：每个 `transfer` 或 `approve` 将被锁定，等待验证。

## 动机

区块链的力量同时也是其弱点：赋予用户对其数据的完全责任。

目前存在许多代币被盗的案例，而当前的代币反盗窃方案，如将代币转移到冷钱包，使得代币使用不便。

在每个 `transfer` 和 `approve` 之前进行验证步骤将为智能合约开发者提供创建安全代币反盗窃方案的机会。

一个实现示例是一个系统，其中一个验证者地址负责验证所有智能合约交易。

该地址将连接到一个 dApp，用户可以在其中查看其代币的验证请求并接受正确的请求。

仅赋予该地址验证交易的权力将使系统更加安全，窃贼必须同时拥有用户地址和验证者地址才能窃取代币。

## 规范

本文档中的关键词 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

符合 [ERC-20](./eip-20.md) 的合约 MAY 实现此 EIP。

所有更改代币所有权的操作，如 `transfer`/`transferFrom`，SHALL 创建一个待验证的 `TransferValidation` 并发出 `ValidateTransfer`，并且 SHALL NOT 转移代币。

所有启用管理代币的批准操作，如 `approve`，SHALL 创建一个待验证的 `ApprovalValidation` 并发出 `ValidateApproval`，并且 SHALL NOT 启用批准。

当转账由已批准的账户而非所有者调用时，必须直接执行，而无需验证。这是为了适应所有当前需要批准以直接移动代币的项目。

在验证 `TransferValidation` 或 `ApprovalValidation` 时，valid 字段必须设置为 true，并且必须不再验证。

验证 `TransferValidation` 的操作 SHALL 更改代币的所有权。

验证 `ApprovalValidation` 的操作 SHALL 启用批准。

### 合约接口

```solidity
interface IERC7144 {

    struct TransferValidation {
        // The address of the owner.
        address from;
        // The address of the receiver.
        address to;
        // The token amount.
        uint256 amount;
        // Whether is a valid transfer.
        bool valid;
    }

    struct ApprovalValidation {
        // The address of the owner.
        address owner;
        // The spender address.
        address spender;
        // The token amount approved.
        uint256 amount;
        // Whether is a valid approval.
        bool valid;
    }

    /**
     * @dev Emitted when a new transfer validation has been requested.
     */
    event ValidateTransfer(address indexed from, address indexed to, uint256 amount, uint256 indexed transferValidationId);

    /**
    * @dev Emitted when a new approval validation has been requested.
    */
    event ValidateApproval(address indexed owner, address indexed spender, uint256 amount, uint256 indexed approvalValidationId);

    /**
     * @dev Returns true if this contract is a validator ERC20.
     */
    function isValidatorContract() external view returns (bool);

    /**
     * @dev Returns the transfer validation struct using the transfer ID.
     *
     */
    function transferValidation(uint256 transferId) external view returns (TransferValidation memory);

    /**
    * @dev Returns the approval validation struct using the approval ID.
    *
    */
    function approvalValidation(uint256 approvalId) external view returns (ApprovalValidation memory);

    /**
     * @dev Return the total amount of transfer validations created.
     *
     */
    function totalTransferValidations() external view returns (uint256);

    /**
     * @dev Return the total amount of transfer validations created.
     *
     */
    function totalApprovalValidations() external view returns (uint256);
}
  ```

`isValidatorContract()` 函数必须实现为 `public`。

`transferValidation(uint256 transferId)` 函数可以实现为 `public` 或 `external`。

`approvalValidation(uint256 approveId)` 函数可以实现为 `public` 或 `external`。

`totalTransferValidations()` 函数可以实现为 `pure` 或 `view`。

`totalApprovalValidations()` 函数可以实现为 `pure` 或 `view`。

## 理由

### 普遍性

该标准仅定义验证函数，而不定义它们的使用方式。它将验证定义为内部，并让用户决定如何管理它们。

一个示例可以是将地址验证者连接到 dApp，以便用户可以管理他们的验证。

该验证者可以用于所有代币或仅用于某些用户。

它也可以作为现有 ERC-20 的包装智能合约使用，允许与现有代币进行 1/1 转换。

### 可扩展性

该标准仅定义验证函数，但不定义必须验证的系统。第三方协议可以根据自己的意愿定义如何调用这些函数。

## 向后兼容性

该标准是 [ERC-20](./eip-20.md) 的扩展，兼容所有操作，除了 `transfer`/`transferFrom`/`approve`。

这些操作将被重写，以创建验证请求，而不是转移代币或启用批准。

## 参考实现

```solidity
// SPDX-License-Identifier: CC0-1.0

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "./IERC7144.sol";

/**
 * @dev Implementation of ERC7144
 */
contract ERC7144 is IERC7144, ERC20 {

    // Mapping from transfer ID to transfer validation
    mapping(uint256 => TransferValidation) private _transferValidations;

    // Mapping from approval ID to approval validation
    mapping(uint256 => ApprovalValidation) private _approvalValidations;

    // Total number of transfer validations
    uint256 private _totalTransferValidations;

    // Total number of approval validations
    uint256 private _totalApprovalValidations;

    /**
     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.
     */
    constructor(string memory name_, string memory symbol_) ERC20(name_, symbol_){
    }

    /**
    * @dev Returns true if this contract is a validator ERC721.
    */
    function isValidatorContract() public pure returns (bool) {
        return true;
    }

    /**
     * @dev Returns the transfer validation struct using the transfer ID.
     *
     */
    function transferValidation(uint256 transferId) public view override returns (TransferValidation memory) {
        require(transferId < _totalTransferValidations, "ERC7144: invalid transfer ID");
        TransferValidation memory v = _transferValidation(transferId);

        return v;
    }

    /**
     * @dev Returns the approval validation struct using the approval ID.
     *
     */
    function approvalValidation(uint256 approvalId) public view override returns (ApprovalValidation memory) {
        require(approvalId < _totalApprovalValidations, "ERC7144: invalid approval ID");
        ApprovalValidation memory v = _approvalValidation(approvalId);

        return v;
    }

    /**
     * @dev Return the total amount of transfer validations created.
     *
     */
    function totalTransferValidations() public view override returns (uint256) {
        return _totalTransferValidations;
    }

    /**
     * @dev Return the total amount of approval validations created.
     *
     */
    function totalApprovalValidations() public view override returns (uint256) {
        return _totalApprovalValidations;
    }

    /**
     * @dev Returns the transfer validation of the `transferId`. Does NOT revert if transfer doesn't exist
     */
    function _transferValidation(uint256 transferId) internal view virtual returns (TransferValidation memory) {
        return _transferValidations[transferId];
    }

    /**
     * @dev Returns the approval validation of the `approvalId`. Does NOT revert if transfer doesn't exist
     */
    function _approvalValidation(uint256 approvalId) internal view virtual returns (ApprovalValidation memory) {
        return _approvalValidations[approvalId];
    }

    /**
     * @dev Validate the transfer using the transfer ID.
     *
     */
    function _validateTransfer(uint256 transferId) internal virtual {
        TransferValidation memory v = transferValidation(transferId);
        require(!v.valid, "ERC721V: the transfer is already validated");

        super._transfer(v.from, v.to, v.amount);

        _transferValidations[transferId].valid = true;
    }

    /**
     * @dev Validate the approval using the approval ID.
     *
     */
    function _validateApproval(uint256 approvalId) internal virtual {
        ApprovalValidation memory v = approvalValidation(approvalId);
        require(!v.valid, "ERC7144: the approval is already validated");

        super._approve(v.owner, v.spender, v.amount);

        _approvalValidations[approvalId].valid = true;
    }

    /**
     * @dev Create a transfer petition of `tokenId` from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     *
     * Emits a {ValidateTransfer} event.
     */
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual override {
        require(from != address(0), "ERC7144: transfer from the zero address");
        require(to != address(0), "ERC7144: transfer to the zero address");

        if(_msgSender() == from) {
            TransferValidation memory v;

            v.from = from;
            v.to = to;
            v.amount = amount;

            _transferValidations[_totalTransferValidations] = v;

            emit ValidateTransfer(from, to, amount, _totalTransferValidations);

            _totalTransferValidations++;
        } else {
            super._transfer(from, to, amount);
        }
    }

    /**
     * @dev Create an approval petition from `owner` to operate the `amount`
     *
     * Emits an {ValidateApproval} event.
     */
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual override {
        require(owner != address(0), "ERC7144: approve from the zero address");
        require(spender != address(0), "ERC7144: approve to the zero address");

        ApprovalValidation memory v;

        v.owner = owner;
        v.spender = spender;
        v.amount = amount;

        _approvalValidations[_totalApprovalValidations] = v;

        emit ValidateApproval(v.owner, spender, amount, _totalApprovalValidations);

        _totalApprovalValidations++;
    }
}
```

## 安全考虑

如规范中所定义，更改代币所有权或启用管理代币的批准的操作 SHALL 创建一个待验证的 `TransferValidation` 或 `ApprovalValidation`，并且 SHALL NOT 转移代币或启用批准。

在此前提下，负责验证 `TransferValidation` 或 `ApprovalValidation` 的操作必须受到所应用系统所需的最大安全保护。

例如，一个有效的系统将是一个由验证者地址负责验证交易的系统。

再举一个例子，允许每个用户选择其验证者地址的系统也是正确的。

无论如何，安全性的重要性在于没有地址可以在未获得所选系统的许可的情况下验证 `TransferValidation` 或 `ApprovalValidation`。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。