---
eip: 7634
title: 限制转移次数的 NFT
description: 一种 ERC-721 扩展，基于 NFT 的数量限制可转移性
author: 秦王 (@qinwang-git), Saber Yu (@OniReimu), Shiping Chen <shiping.chen@data61.csiro.au>
discussions-to: https://ethereum-magicians.org/t/erc-7634-limited-transferable-nft/18861
status: 草案
type: 标准跟踪
category: ERC
created: 2024-02-22
requires: 165, 721
---

## 摘要

该标准扩展了 [ERC-721](./eip-721.md)，引入了一种机制，允许铸造者通过一个名为 `TransferCount` 的参数自定义 NFT 的可转移性。`TransferCount` 设置了 NFT 可以被转移的次数限制。该标准指定了一个接口，包括设置和检索转移限制、跟踪转移次数以及定义转移前后状态的函数。该标准使 NFT 的所有权和转移权利能够更精细地控制，确保 NFT 可以被编程为具有特定的、可执行的转移限制。

## 动机

一旦 NFT 被出售，它们就与铸造者（创作者）脱离关系，并可以在此后被永久转移。然而，许多情况下需要对 NFT 的发行进行精确控制。我们从三个维度概述了它们的优势。

首先，通过对 NFT 销售或交易频率施加限制，可以保护稀有 NFT 的价值。例如，在拍卖中，限制对某个热门物品的竞标轮次可以维持其高价（尤其是在荷兰拍卖中）。同样，在知识产权领域，专利可以在成为自由可用（进入 CC0）之前，限制转移的次数。在游戏领域，武器、服装和车辆等物品可能具有有限的生命周期，每次使用或交换都会导致磨损，达到预定阈值后自动退役（销毁）。

其次，强制限制交易频率可以通过减轻与恶意 NFT 套利相关的风险（包括高频交易 HFT）来增强网络安全性和稳定性。虽然这是一种常见的脆弱性，但缺乏易于部署和有效的方法来解决这一问题，使得我们的方法特别有价值。

此外，限制转移轮次可以减轻与（重新）质押 NFT 相关的经济风险，从而抑制潜在的泡沫。随着重新质押机制的快速发展，用户可能很快会参与多轮 NFT 质押（例如，NFT → stNFT → st^2NFT），类似于在 EigenLayer（以太坊）、Babylon（比特币）和 Picasso（Solana）等第三方平台上质押流动性代币。值得注意的是，EigenLayer 的当前设置使用 NFT 作为参与者的重新质押位置（一种证明质押的方式）。如果这个 NFT 被反复质押到市场上，可能会放大杠杆并加剧泡沫动态。通过对质押迭代次数施加限制，我们可以主动防止质押生态系统中出现类似庞氏的动态。

### 关键要点

该标准提供了几个优势：

*控制价值保护*：通过允许铸造者为 NFT 设置自定义转移限制，该标准促进了数字资产的价值保护。正如实体收藏品因稀缺性而获得或维持价值，限制 NFT 的转移次数可以帮助确保其长期价值。

*确保预期使用*：设置转移限制可以确保 NFT 以符合其预期用途的方式使用。例如，如果一个 NFT 代表限量版数字艺术作品，限制转移可以防止其被过度交易并可能贬值。

*扩展使用案例*：这些增强功能通过为创作者和拥有者提供更多控制和灵活性，拓宽了 NFT 的潜在应用。例如，NFT 可以用于代表具有有限可转移性的会员资格或许可证，为数字所有权模型开辟新的可能性。

*易于集成*：为了确保广泛采用和易于集成，该标准扩展了现有的 [ERC-721](./eip-721.md) 接口。通过定义一个单独的接口 (`IERC7634`)，包括新函数，该标准允许现有的 [ERC-721](./eip-721.md) 合约以最小的更改采用新功能。这种方法促进了向后兼容性，并鼓励将转移限制无缝整合到当前的 NFT 项目中。

## 规范

本文档中的关键字“必须”、“不得”、“要求”、“应”、“不应”、“应该”、“不应该”、“推荐”、“可以”和“可选”应按 RFC 2119 中的描述进行解释。

- `setTransferLimit`：一个函数，用于为 tokenId 建立转移限制。
- `transferLimitOf`：一个函数，用于检索 tokenId 的转移限制。
- `transferCountOf`：一个函数，返回 tokenId 的当前转移次数。

该标准的实现者 **必须** 具备以下所有函数：

```solidity

pragma solidity ^0.8.4;

/// @title IERC7634 Interface for Limited Transferable NFT
/// @dev Interface for ERC7634 Limited Transferable NFT extension for ERC721
/// @author Saber Yu

interface IERC7634 {

    /**
     * @dev Emitted when transfer count is set or updated
     */
    event TransferCount(uint256 indexed tokenId, address owner, uint256 counts);

    /**
     * @dev Returns the current transfer count for a tokenId
     */
    function transferCountOf(uint256 tokenId) external view returns (uint256);

    /**
     * @dev Sets the transfer limit for a tokenId. Can only be called by the token owner or an approved address.
     * @param tokenId The ID of the token for which to set the limit
     * @param limit The maximum number of transfers allowed for the token
     */
    function setTransferLimit(uint256 tokenId, uint256 limit) external;

    /**
     * @dev Returns the transfer limit for a tokenId
     */
    function transferLimitOf(uint256 tokenId) external view returns (uint256);
}
    
```

## 理由

### 跟踪内部转移次数是否重要？

是的，也不是。这是可选的，具体取决于实际需求。下面给出的参考实现是推荐的，如果您选择跟踪。`_incrementTransferCount` 函数和相关的检索函数（`transferLimitOf` 和 `transferCountOf`）旨在跟踪 NFT 经历的转移次数。这个内部跟踪机制对于执行铸造者的转移限制至关重要，确保一旦达到限制，就无法进行进一步的转移。

### 如果选择跟踪，那我们是否只需要跟踪这些？

建议还要跟踪转移前后的状态。可选的 `_beforeTokenTransfer` 和 `_afterTokenTransfer` 函数被重写，以定义 NFT 在转移前后的状态。这些函数确保在转移过程中根据转移限制和次数执行任何必要的检查或更新。通过将这些检查集成到转移过程中，该标准确保转移限制得到一致执行。

## 向后兼容性

该标准可以通过添加扩展函数集完全兼容 [ERC-721](./eip-721.md)。

### 扩展

该标准可以与现有 NFT 协议一起增强，增加额外的高级功能。例如：

- 引入销毁功能（例如，[ERC-5679](./eip-5679.md)）将使 NFT 在达到转移限制后自动过期，类似于 Snapchat 消息在多次查看后消失的短暂特性。

- 引入非转移功能，如 SBT 标准中定义的，将使 NFT 在经过预定数量的交易后与单一所有者结算和绑定。这种功能类似于竞标者在参与多轮竞标后最终获得国库的情景。

## 参考实现

推荐的实现如下所示：

- `_incrementTransferCount`：一个内部函数，用于增加转移次数。
- `_beforeTokenTransfer`：一个重写的函数，定义转移前的状态。
- `_afterTokenTransfer`：一个重写的函数，概述转移后的状态。
```solidity

pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IERC7634.sol";

/// @title Limited Transferable NFT Extension for ERC721
/// @dev Implementation of the Limited Transferable NFT extension for ERC721
/// @author Saber Yu

contract ERC7634 is ERC721, IERC7634 {

    // Mapping from tokenId to the transfer count
    mapping(uint256 => uint256) private _transferCounts;

    // Mapping from tokenId to its maximum transfer limit
    mapping(uint256 => uint256) private _transferLimits;

    /**
     * @dev See {IERC7634-transferCountOf}.
     */
    function transferCountOf(uint256 tokenId) public view override returns (uint256) {
        require(_exists(tokenId), "ERC7634: Nonexistent token");
        return _transferCounts[tokenId];
    }

    /**
     * @dev See {IERC7634-setTransferLimit}.
     */
    function setTransferLimit(uint256 tokenId, uint256 limit) public override {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC7634: caller is not owner nor approved");
        _transferLimits[tokenId] = limit;
    }

    /**
     * @dev See {IERC7634-transferLimitOf}.
     */
    function transferLimitOf(uint256 tokenId) public view override returns (uint256) {
        require(_exists(tokenId), "ERC7634: Nonexistent token");
        return _transferLimits[tokenId];
    }

    /**
     * @dev Internal function to increment transfer count.
     */
    function _incrementTransferCount(uint256 tokenId) internal {
        _transferCounts[tokenId] += 1;
        emit TransferCount(tokenId, ownerOf(tokenId), _transferCounts[tokenId]);
    }

    /**
     * @dev Override {_beforeTokenTransfer} to enforce transfer limit.
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal override {
        require(_transferCounts[tokenId] < _transferLimits[tokenId], "ERC7634: Transfer limit reached");
        super._beforeTokenTransfer(from, to, tokenId);
    }

    /**
     * @dev Override {_afterTokenTransfer} to handle post-transfer logic.
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 tokenId,
        uint256 quantity
    ) internal virtual override {
        _incrementTransferCount(tokenId);

        if (_transferCounts[tokenId] == _transferLimits[tokenId]) {
            // Optional post-transfer operations once the limit is reached
            // Uncomment the following based on the desired behavior such as the `burn` opearation
            // ---------------------------------------
            // _burn(tokenId); // Burn the token
            // ---------------------------------------
        }

        super._afterTokenTransfer(from, to, tokenId, quantity);
    }


    /**
     * @dev Override {supportsInterface} to declare support for IERC7634.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(IERC165, ERC721) returns (bool) {
        return interfaceId == type(IERC7634).interfaceId || super.supportsInterface(interfaceId);
    }
}

```

## 安全考虑

- 确保每个 NFT 铸造者可以调用此函数以设置转移限制。
- 考虑在设置后使转移限制不可变，以防止篡改或未经授权的修改。
- 避免在与可能在执行期间超过 gas 限制的高级功能集成时执行资源密集型操作。


## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。