---
eip: 5496
title: 多权限管理 NFT 扩展
description: 为 EIP-721 创建可共享的多权限 NFT
author: Jeremy Z (@wnft)
discussions-to: https://ethereum-magicians.org/t/eip-5496-multi-privilege-management-extension-for-erc-721/10427
status: 最后呼叫
last-call-deadline: 2022-11-29
type: 标准跟踪
category: ERC
created: 2022-07-30
requires: 721
---


## 摘要

本 EIP 定义了一个扩展 [EIP-721](./eip-721.md) 的接口，以提供可共享的多权限 NFT。权限可以是链上（投票权、领取空投的权限）或链下（在线商店的优惠券、当地餐厅的折扣、机场的 VIP 休息室访问权限）。每个 NFT 可以包含多个权限，权限的持有者可以可验证地将该权限转移给他人。权限可以是不可共享的或可共享的。可共享的权限可以被克隆，提供者可以根据传播路径调整细节。每个权限也可以设置到期时间。

## 动机

该标准旨在实时高效地管理附加到 NFT 的权限。许多 NFT 除了作为头像或艺术收藏品外，还有其他功能，它们在不同场景中可能具有实际效用。例如，时尚商店可能会为其 NFT 持有者提供折扣；DAO 成员 NFT 持有者可以对如何使用其国库的提案进行投票；dApp 可能会创建一个空投活动，以吸引某些群体的人，例如一些蓝筹 NFT 持有者进行领取；杂货店可以在链上发行其会员卡（作为 NFT），并在会员在杂货店购物时给予某些权限等。有些情况下，拥有 NFT 的人不一定想使用他们的权限。通过提供额外的数据记录 NFT 集合所拥有的不同权限和管理它们的接口，用户可以在不失去 NFT 所有权的情况下转移或出售权限。

[EIP-721](./eip-721.md) 仅记录所有权及其转移，NFT 的权限未在链上记录。此扩展将允许商家/项目向特定群体提供某种权限，权限的所有者可以独立管理每个权限。这为 NFT 的实际用途提供了极大的可能性。

例如，一家航空公司向 Crypto Punk 持有者发行一系列 [EIP-721](./eip-721.md)/[EIP-1155](./eip-1155.md) 代币，以给予他们特权，以吸引他们加入其俱乐部。然而，由于这些代币并未绑定到原始 NFT，如果原始 NFT 被转移，这些权限仍然掌握在原始持有者手中，新持有者无法自动享受这些权限。因此，我们提出了一组接口，可以将权限绑定到基础 NFT，同时允许用户独立管理这些权限。

## 规范

本文档中的关键字 “MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL” 应按 RFC 2119 中的描述进行解释。

每个符合该标准的合约 MUST 实现 `IERC5496` 接口。**可共享的多权限扩展** 对 EIP-721 合约是 OPTIONAL 的。

```solidity
/// @title multi-privilege extension for EIP-721
///  Note: the EIP-165 identifier for this interface is 0x076e1bbb
interface IERC5496{
    /// @notice Emitted when `owner` changes the `privilege holder` of a NFT.
    event PrivilegeAssigned(uint256 tokenId, uint256 privilegeId, address user, uint256 expires);
    /// @notice Emitted when `contract owner` changes the `total privilege` of the collection
    event PrivilegeTotalChanged(uint256 newTotal, uint256 oldTotal);

    /// @notice set the privilege holder of a NFT.
    /// @dev expires should be less than 30 days
    /// Throws if `msg.sender` is not approved or owner of the tokenId.
    /// @param tokenId The NFT to set privilege for
    /// @param privilegeId The privilege to set
    /// @param user The privilege holder to set
    /// @param expires For how long the privilege holder can have
    function setPrivilege(uint256 tokenId, uint256 privilegeId, address user, uint256 expires) external;

    /// @notice Return the expiry timestamp of a privilege
    /// @param tokenId The identifier of the queried NFT
    /// @param privilegeId The identifier of the queried privilege
    /// @return Whether a user has a certain privilege
    function privilegeExpires(uint256 tokenId, uint256 privilegeId) external view returns(uint256);

    /// @notice Check if a user has a certain privilege
    /// @param tokenId The identifier of the queried NFT
    /// @param privilegeId The identifier of the queried privilege
    /// @param user The address of the queried user
    /// @return Whether a user has a certain privilege
    function hasPrivilege(uint256 tokenId, uint256 privilegeId, address user) external view returns(bool);
}
```

每个实现该标准的合约 SHOULD 在设置任何权限之前设置最大权限数量，`privilegeId` MUST NOT 大于最大权限数量。

当调用 `setPrivilege` 时，必须发出 `PrivilegeAssigned` 事件。

当集合的 `total privilege` 发生变化时，必须发出 `PrivilegeTotalChanged` 事件。

当使用 `0x076e1bbb` 调用 `supportsInterface` 方法时，必须返回 `true`。

```solidity
/// @title Cloneable extension - Optional for EIP-721
interface IERC721Cloneable {
    /// @notice Emitted when set the `privilege ` of a NFT cloneable.
    event PrivilegeCloned(uint tokenId, uint privId, address from, address to);

    /// @notice set a certain privilege cloneable
    /// @param tokenId The identifier of the queried NFT
    /// @param privilegeId The identifier of the queried privilege
    /// @param referrer The address of the referrer
    /// @return Whether the operation is successful or not
    function clonePrivilege(uint tokenId, uint privId, address referrer) external returns (bool);
}
```

当调用 `clonePrivilege` 时，必须发出 `PrivilegeCloned` 事件。

对于合规合约，建议使用 [EIP-1271](./eip-1271.md) 来验证签名。

## 理由

### 可共享权限

如果权限不可共享，则权限持有者的数量受 NFT 数量的限制。可共享权限意味着原始权限持有者可以复制该权限并将其给予他人，而不是将自己的权限转移给他们。这种机制极大地增强了权限的传播以及 NFT 的采用。

### 到期日期类型

权限的到期时间戳是一个时间戳，存储在 `uint256` 类型的变量中。

### 推荐人的受益人

例如，一家当地的比萨店提供 30% 的折扣券，店主鼓励消费者与朋友分享该优惠券，那么朋友们也可以获得该优惠券。假设汤姆从商店获得了 30% 的折扣券，并且他将该优惠券分享给了爱丽丝。爱丽丝也获得了该优惠券，爱丽丝的推荐人是汤姆。在某些情况下，汤姆可能会从商店获得更多奖励。这将帮助商家在消费者中传播促销活动。

### 提案：NFT 转移

如果 NFT 的所有者将所有权转移给另一个用户，则对“权限”没有影响。但是，如果所有者尝试通过 `unwrap()` 从包装的 NFT 中提取原始 [EIP-721](./eip-721.md) 代币，而任何可用的权限仍在进行中，则可能会发生错误。我们保护权限持有者的权利，以检查权限的最后到期日期。

```solidity
function unwrap(uint256 tokenId, address to) external {
    require(getBlockTimestamp() >= privilegeBook[tokenId].lastExpiresAt, "privilege not yet expired");

    require(ownerOf(tokenId) == msg.sender, "not owner");

    _burn(tokenId);

    IERC721(nft).transferFrom(address(this), to, tokenId);

    emit Unwrap(nft, tokenId, msg.sender, to);
}
```

## 向后兼容性

该 EIP 与任何遵循 EIP-721 标准的 NFT 兼容。它仅添加更多功能和数据结构，而不干扰原始 [EIP-721](./eip-721.md) 标准。

## 测试用例

测试用例是通过参考实现来实现的。

### 测试代码

[test.js](../assets/eip-5496/test/test.js)

在终端中运行：

```shell
truffle test ./test/test.js
```

[testCloneable.js](../assets/eip-5496/test/testCloneable.js)

在终端中运行：

```shell
truffle test ./test/testCloneable.js
```

## 参考实现

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0; 

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import "./IERC5496.sol";

contract ERC5496 is ERC721, IERC5496 {
    struct PrivilegeRecord {
        address user;
        uint256 expiresAt;
    }
    struct PrivilegeStorage {
        uint lastExpiresAt;
        // privId => PrivilegeRecord
        mapping(uint => PrivilegeRecord) privilegeEntry;
    }

    uint public privilegeTotal;
    // tokenId => PrivilegeStorage
    mapping(uint => PrivilegeStorage) public privilegeBook;
    mapping(address => mapping(address => bool)) private privilegeDelegator;

    constructor(string memory name_, string memory symbol_)
    ERC721(name_,symbol_)
    {
    
    }

    function setPrivilege(
        uint tokenId,
        uint privId,
        address user,
        uint64 expires
    ) external virtual {
        require((hasPrivilege(tokenId, privId, ownerOf(tokenId)) && _isApprovedOrOwner(msg.sender, tokenId)) || _isDelegatorOrHolder(msg.sender, tokenId, privId), "ERC721: transfer caller is not owner nor approved");
        require(expires < block.timestamp + 30 days, "expire time invalid");
        require(privId < privilegeTotal, "invalid privilege id");
        privilegeBook[tokenId].privilegeEntry[privId].user = user;
        if (_isApprovedOrOwner(msg.sender, tokenId)) {
            privilegeBook[tokenId].privilegeEntry[privId].expiresAt = expires;
            if (privilegeBook[tokenId].lastExpiresAt < expires) {
                privilegeBook[tokenId].lastExpiresAt = expires;
            }
        }
        emit PrivilegeAssigned(tokenId, privId, user, uint64(privilegeBook[tokenId].privilegeEntry[privId].expiresAt));
    }

    function hasPrivilege(
        uint256 tokenId,
        uint256 privId,
        address user
    ) public virtual view returns(bool) {
        if (privilegeBook[tokenId].privilegeEntry[privId].expiresAt >= block.timestamp){
            return privilegeBook[tokenId].privilegeEntry[privId].user == user;
        }
        return ownerOf(tokenId) == user;
    }

    function privilegeExpires(
        uint256 tokenId,
        uint256 privId
    ) public virtual view returns(uint256){
        return privilegeBook[tokenId].privilegeEntry[privId].expiresAt;
    }

    function _setPrivilegeTotal(
        uint total
    ) internal {
        emit PrivilegeTotalChanged(total, privilegeTotal);
        privilegeTotal = total;
    }

    function getPrivilegeInfo(uint tokenId, uint privId) external view returns(address user, uint256 expiresAt) {
        return (privilegeBook[tokenId].privilegeEntry[privId].user, privilegeBook[tokenId].privilegeEntry[privId].expiresAt);
    }

    function setDelegator(address delegator, bool enabled) external {
        privilegeDelegator[msg.sender][delegator] = enabled;
    }

    function _isDelegatorOrHolder(address delegator, uint256 tokenId, uint privId) internal virtual view returns (bool) {
        address holder = privilegeBook[tokenId].privilegeEntry[privId].user;
         return (delegator == holder || isApprovedForAll(holder, delegator) || privilegeDelegator[holder][delegator]);
    }

    function supportsInterface(bytes4 interfaceId) public override virtual view returns (bool) {
        return interfaceId == type(IERC5496).interfaceId || super.supportsInterface(interfaceId);
    }
}
```

## 安全考虑

实现必须充分考虑谁有权限设置或克隆权限。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。