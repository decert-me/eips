---
eip: 6981
title: 保留所有权账户
description: 一个用于生成由外部服务用户拥有的未来部署智能合约账户的注册表
author: Paul Sullivan (@sullivph) <paul.sullivan@manifold.xyz>, Wilkins Chung (@wwchung) <wilkins@manifold.xyz>, Kartik Patel (@Slokh) <kartik@manifold.xyz>
discussions-to: https://ethereum-magicians.org/t/erc-6981-reserved-ownership-accounts/14118
status: 草案
type: 标准跟踪
category: ERC
created: 2023-04-25
requires: 1167, 1271, 6492
---

## 摘要

以下内容指定了一个系统，供服务将其用户链接到可索取的以太坊地址。服务可以向其用户提供签名消息和唯一盐值，这些可以通过注册合约使用 `create2` 操作码部署到确定性地址的智能合约钱包。

## 动机

网络服务通常允许其用户通过托管钱包持有链上资产。这些钱包通常是外部拥有账户（EOA）、部署的智能合约钱包或综合合约，私钥或资产所有权信息存储在传统数据库中。该提案概述了一种解决方案，避免了与历史方法相关的安全问题，并消除了服务控制用户资产的需求和影响。

选择利用以下规范的外部服务用户可以获得一个以太坊地址，以接收资产，而无需进行任何链上交易。这些用户可以选择在未来某个时间点获得对该地址的控制权。因此，链上资产可以在此之前发送给并由用户拥有，从而在不需要用户与底层区块链交互的情况下，形成链上身份。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 概述

创建保留所有权账户的系统包括：

1. 一个账户注册表，根据服务用户的识别盐值提供确定性地址，并实现一个签名验证函数，使服务的最终用户能够索取账户实例。
2. 通过账户注册表由最终用户创建的账户实例，允许在账户实例部署之前访问在确定性地址接收到的资产。

希望为其用户提供保留所有权账户的外部服务必须维护用户识别凭证与盐值之间的关系。外部服务应引用账户注册表实例以检索给定盐值的确定性账户地址。给定服务的用户必须能够通过外部服务验证其识别凭证来创建账户实例，外部服务应为用户的盐值提供签名消息。签名应由外部服务使用已知于账户注册表实例的签名地址生成。用户应将此消息和签名传递给服务的账户注册表实例，以调用 `claimAccount` 在确定性地址上部署和索取账户实例。

### 账户注册表

账户注册表必须实现以下接口：

```solidity
interface IAccountRegistry {
    /**
     * @dev Registry instances emit the AccountCreated event upon successful account creation
     */
    event AccountCreated(address account, address accountImplementation, uint256 salt);

    /**
     * @dev Registry instances emit the AccountClaimed event upon successful claim of account by owner
     */
    event AccountClaimed(address account, address owner);

    /**
     * @dev Creates a smart contract account.
     *
     * If account has already been created, returns the account address without calling create2.
     *
     * @param salt       - The identifying salt for which the user wishes to deploy an Account Instance
     *
     * Emits AccountCreated event
     * @return the address for which the Account Instance was created
     */
    function createAccount(uint256 salt) external returns (address);

    /**
     * @dev Allows an owner to claim a smart contract account created by this registry.
     *
     * If the account has not already been created, the account will be created first using `createAccount`
     *
     * @param owner      - The initial owner of the new Account Instance
     * @param salt       - The identifying salt for which the user wishes to deploy an Account Instance
     * @param expiration - If expiration > 0, represents expiration time for the signature.  Otherwise
     *                     signature does not expire.
     * @param message    - The keccak256 message which validates the owner, salt, expiration
     * @param signature  - The signature which validates the owner, salt, expiration
     *
     * Emits AccountClaimed event
     * @return the address of the claimed Account Instance
     */
    function claimAccount(
        address owner,
        uint256 salt,
        uint256 expiration,
        bytes32 message,
        bytes calldata signature
    ) external returns (address);

    /**
     * @dev Returns the computed address of a smart contract account for a given identifying salt
     *
     * @return the computed address of the account
     */
    function account(uint256 salt) external view returns (address);

    /**
     * @dev Fallback signature verification for unclaimed accounts
     */
    function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4);
}
```

#### createAccount

`createAccount` 用于为给定盐值部署账户实例。

- 此函数必须部署一个新的账户实例作为 [ERC-1167](./eip-1167.md) 代理，指向账户实现。
- 此函数应将账户实例的初始所有者设置为账户注册表实例。
- 账户实现地址必须是不可变的，因为它用于计算账户实例的确定性地址。
- 在成功部署账户实例后，注册表应发出 `AccountCreated` 事件。

#### claimAccount

`claimAccount` 用于索取给定盐值的账户实例的所有权。

- 如果给定盐值尚不存在账户实例，则此函数必须创建一个新的账户实例。
- 此函数应验证 msg.sender 是否有权索取给定盐值和初始所有者的账户实例的所有权。验证应通过使用 ECDSA 对 EOA 签名者或 [ERC-1271](./eip-1271.md) 对智能合约签名者验证消息和签名与所有者、盐值和过期时间进行验证。
- 此函数应验证 block.timestamp < expiration 或 expiration == 0。
- 在成功验证签名后，注册表必须完全放弃对账户实例的控制，并通过调用 `setOwner` 将所有权分配给初始所有者。
- 在成功索取账户实例后，注册表应发出 `AccountClaimed` 事件。

#### isValidSignature

`isValidSignature` 是未索取账户使用的回退签名验证函数。有效签名应由注册表签名者通过对原始消息哈希和账户实例地址的复合哈希进行签名生成（例如 `bytes32 compositeHash = keccak256(abi.encodePacked(originalHash, accountAddress))`）。该函数必须重建复合哈希，其中 `originalHash` 是传递给函数的哈希，`accountAddress` 是 `msg.sender`（未索取的账户实例）。该函数必须验证签名是否与复合哈希和注册表签名者匹配。

### 账户实例

账户实例必须实现以下接口：

```solidity
interface IAccount is IERC1271 {
    /**
     * @dev Sets the owner of the Account Instance.
     *
     * Only callable by the current owner of the instance, or by the registry if the Account
     * Instance has not yet been claimed.
     *
     * @param owner      - The new owner of the Account Instance
     */
    function setOwner(address owner) external;
}
```

- 所有账户实例必须使用账户注册表实例创建。
- 账户实例应提供对先前发送到部署账户实例的地址的资产的访问。
- `setOwner` 应更新所有者，并应由账户实例的当前所有者调用。
- 如果账户实例已部署但未索取，则账户实例的所有者必须初始化为账户注册表实例。
- 账户实例应通过检查所有者是否为账户注册表实例来确定其是否已被索取。

#### 账户实例签名

账户实例必须通过实现 `isValidSignature` 函数来支持 [ERC-1271](./eip-1271.md)。当账户实例的所有者想要签署消息（例如，登录到 dApp）时，签名必须根据账户实例的状态以以下方式之一生成：

1. 如果账户实例已部署并已索取，则所有者应生成签名，`isValidSignature` 应验证消息哈希和签名是否对账户实例的当前所有者有效。
2. 如果账户实例已部署但未索取，则注册表签名者应使用原始消息和上述描述的账户实例地址的复合哈希生成签名，`isValidSignature` 应将消息哈希和签名转发到账户注册表实例的 `isValidSignature` 函数。
3. 如果账户实例未部署，则注册表签名者应在复合哈希上生成签名，如情况 2 中所做，并根据 [ERC-6492](./eip-6492.md#signer-side) 包装签名（例如 `concat(abi.encode((registryAddress, createAccountCalldata, compositeHashSignature), (address, bytes, bytes)), magicBytes)`）。
签名验证对于账户实例应根据 [ERC-6492](./eip-6492.md#verifier-side) 进行。

## 理由

### 服务拥有的注册实例

虽然实现和部署一个通用的保留所有权账户注册表似乎更符合用户友好性，但我们认为外部服务提供商拥有拥有和控制自己账户注册表的选项是重要的。这提供了实施他们自己的权限控制和账户部署授权框架的灵活性。

我们提供了一个参考注册工厂，可以为外部服务部署账户注册表，包含：

- 不可变的账户实例实现
- 通过 ECDSA 对 EOA 签名者的 `claimAccount` 方法进行验证，或对智能合约签名者进行 [ERC-1271](./eip-1271.md) 验证
- 账户注册表部署者可以更改用于 `claimAccount` 验证的签名地址的能力

### 账户注册表与账户实现的耦合

由于账户实例作为 [ERC-1167](./eip-1167.md) 代理部署，账户实现地址会影响从给定账户注册表部署的账户地址。要求注册实例链接到单一的、不可变的账户实现确保了用户的盐值与给定账户注册表实例上的链接地址之间的一致性。

这还允许服务通过以可信账户实现地址的引用部署他们的注册表来获得用户的信任。

此外，账户实现可以设计为可升级的，因此用户不一定受限于用于创建其账户的账户注册表实例所指定的实现。

### 分开的 `createAccount` 和 `claimAccount` 操作

创建和认领账户实例的操作故意分开。这允许服务在账户实例部署之前向用户提供有效的 [ERC-6492](./eip-6492.md) 签名。

## 参考实现

以下是一个账户注册表工厂的示例，外部服务提供商可以使用它来部署自己的账户注册表实例。

### 账户注册表工厂

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

/// @author: manifold.xyz

import {Create2} from "openzeppelin/utils/Create2.sol";

import {Address} from "../../lib/Address.sol";
import {ERC1167ProxyBytecode} from "../../lib/ERC1167ProxyBytecode.sol";
import {IAccountRegistryFactory} from "./IAccountRegistryFactory.sol";

contract AccountRegistryFactory is IAccountRegistryFactory {
    using Address for address;

    error InitializationFailed();

    address private immutable registryImplementation = 0x076B08EDE2B28fab0c1886F029cD6d02C8fF0E94;

    function createRegistry(
        uint96 index,
        address accountImplementation,
        bytes calldata accountInitData
    ) external returns (address) {
        bytes32 salt = _getSalt(msg.sender, index);
        bytes memory code = ERC1167ProxyBytecode.createCode(registryImplementation);
        address _registry = Create2.computeAddress(salt, keccak256(code));

        if (_registry.isDeployed()) return _registry;

        _registry = Create2.deploy(0, salt, code);

        (bool success, ) = _registry.call(
            abi.encodeWithSignature(
                "initialize(address,address,bytes)",
                msg.sender,
                accountImplementation,
                accountInitData
            )
        );
        if (!success) revert InitializationFailed();

        emit AccountRegistryCreated(_registry, accountImplementation, index);

        return _registry;
    }

    function registry(address deployer, uint96 index) external view override returns (address) {
        bytes32 salt = _getSalt(deployer, index);
        bytes memory code = ERC1167ProxyBytecode.createCode(registryImplementation);
        return Create2.computeAddress(salt, keccak256(code));
    }

    function _getSalt(address deployer, uint96 index) private pure returns (bytes32) {
        return bytes32(abi.encodePacked(deployer, index));
    }
}
```

### 账户注册表

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

/// @author: manifold.xyz

import {Create2} from "openzeppelin/utils/Create2.sol";
import {ECDSA} from "openzeppelin/utils/cryptography/ECDSA.sol";
import {Ownable} from "openzeppelin/access/Ownable.sol";
import {Initializable} from "openzeppelin/proxy/utils/Initializable.sol";
import {IERC1271} from "openzeppelin/interfaces/IERC1271.sol";
import {SignatureChecker} from "openzeppelin/utils/cryptography/SignatureChecker.sol";

import {Address} from "../../lib/Address.sol";
import {IAccountRegistry} from "../../interfaces/IAccountRegistry.sol";
import {ERC1167ProxyBytecode} from "../../lib/ERC1167ProxyBytecode.sol";

contract AccountRegistryImplementation is Ownable, Initializable, IAccountRegistry {
    using Address for address;
    using ECDSA for bytes32;

    struct Signer {
        address account;
        bool isContract;
    }

    error InitializationFailed();
    error ClaimFailed();
    error Unauthorized();

    address public accountImplementation;
    bytes public accountInitData;
    Signer public signer;

    constructor() {
        _disableInitializers();
    }

    function initialize(
        address owner,
        address accountImplementation_,
        bytes calldata accountInitData_
    ) external initializer {
        _transferOwnership(owner);
        accountImplementation = accountImplementation_;
        accountInitData = accountInitData_;
    }

    /**
     * @dev See {IAccountRegistry-createAccount}
     */
    function createAccount(uint256 salt) external override returns (address) {
        bytes memory code = ERC1167ProxyBytecode.createCode(accountImplementation);
        address _account = Create2.computeAddress(bytes32(salt), keccak256(code));

        if (_account.isDeployed()) return _account;

        _account = Create2.deploy(0, bytes32(salt), code);

        (bool success, ) = _account.call(accountInitData);
        if (!success) revert InitializationFailed();

        emit AccountCreated(_account, accountImplementation, salt);

        return _account;
    }

    /**
     * @dev See {IAccountRegistry-claimAccount}
     */
    function claimAccount(
        address owner,
        uint256 salt,
        uint256 expiration,
        bytes32 message,
        bytes calldata signature
    ) external override returns (address) {
        _verify(owner, salt, expiration, message, signature);
        address _account = this.createAccount(salt);

        (bool success, ) = _account.call(
            abi.encodeWithSignature("transferOwnership(address)", owner)
        );
        if (!success) revert ClaimFailed();

        emit AccountClaimed(_account, owner);
        return _account;
    }

    /**
     * @dev See {IAccountRegistry-account}
     */
    function account(uint256 salt) external view override returns (address) {
        bytes memory code = ERC1167ProxyBytecode.createCode(accountImplementation);
        return Create2.computeAddress(bytes32(salt), keccak256(code));
    }

    /**
     * @dev See {IAccountRegistry-isValidSignature}
     */
    function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4) {
        bytes32 expectedHash = keccak256(abi.encodePacked(hash, msg.sender));
        bool isValid = SignatureChecker.isValidSignatureNow(
            signer.account,
            expectedHash,
            signature
        );
        if (isValid) {
            return IERC1271.isValidSignature.selector;
        }

        return "";
    }

    function updateSigner(address newSigner) external onlyOwner {
        uint32 signerSize;
        assembly {
            signerSize := extcodesize(newSigner)
        }
        signer.account = newSigner;
        signer.isContract = signerSize > 0;
    }

    function _verify(
        address owner,
        uint256 salt,
        uint256 expiration,
        bytes32 message,
        bytes calldata signature
    ) internal view {
        address signatureAccount;

        if (signer.isContract) {
            if (!SignatureChecker.isValidSignatureNow(signer.account, message, signature))
                revert Unauthorized();
        } else {
            signatureAccount = message.recover(signature);
        }

        bytes32 expectedMessage = keccak256(
            abi.encodePacked("\x19Ethereum Signed Message:\n84", owner, salt, expiration)
        );

        if (
            message != expectedMessage ||
            (!signer.isContract && signatureAccount != signer.account) ||
            (expiration != 0 && expiration < block.timestamp)
        ) revert Unauthorized();
    }
}
```

### 示例账户实现

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

/// @author: manifold.xyz

import {IERC1271} from "openzeppelin/interfaces/IERC1271.sol";
import {SignatureChecker} from "openzeppelin/utils/cryptography/SignatureChecker.sol";
import {IERC165} from "openzeppelin/utils/introspection/IERC165.sol";
import {ERC165Checker} from "openzeppelin/utils/introspection/ERC165Checker.sol";
import {IERC721} from "openzeppelin/token/ERC721/IERC721.sol";
import {IERC721Receiver} from "openzeppelin/token/ERC721/IERC721Receiver.sol";
import {IERC1155Receiver} from "openzeppelin/token/ERC1155/IERC1155Receiver.sol";
import {Initializable} from "openzeppelin/proxy/utils/Initializable.sol";
import {Ownable} from "openzeppelin/access/Ownable.sol";
import {IERC1967Account} from "./IERC1967Account.sol";

import {IAccount} from "../../interfaces/IAccount.sol";

/**
 * @title ERC1967AccountImplementation
 * @notice A lightweight, upgradeable smart contract wallet implementation
 */
contract ERC1967AccountImplementation is
    IAccount,
    IERC165,
    IERC721Receiver,
    IERC1155Receiver,
    IERC1967Account,
    Initializable,
    Ownable
{
    address public registry;

    constructor() {
        _disableInitializers();
    }

    function initialize() external initializer {
        registry = msg.sender;
        _transferOwnership(registry);
    }

    function supportsInterface(bytes4 interfaceId) external pure returns (bool) {
        return (interfaceId == type(IAccount).interfaceId ||
            interfaceId == type(IERC1967Account).interfaceId ||
            interfaceId == type(IERC1155Receiver).interfaceId ||
            interfaceId == type(IERC721Receiver).interfaceId ||
            interfaceId == type(IERC165).interfaceId);
    }

    function onERC721Received(
        address,
        address,
        uint256,
        bytes memory
    ) public pure returns (bytes4) {
        return this.onERC721Received.selector;
    }

    function onERC1155Received(
        address,
        address,
        uint256,
        uint256,
        bytes memory
    ) public pure returns (bytes4) {
        return this.onERC1155Received.selector;
    }

    function onERC1155BatchReceived(
        address,
        address,
        uint256[] memory,
        uint256[] memory,
        bytes memory
    ) public pure returns (bytes4) {
        return this.onERC1155BatchReceived.selector;
    }

    /**
     * @dev {See IERC1967Account-executeCall}
     */
    function executeCall(
        address _target,
        uint256 _value,
        bytes calldata _data
    ) external payable override onlyOwner returns (bytes memory _result) {
        bool success;
        // solhint-disable-next-line avoid-low-level-calls
        (success, _result) = _target.call{value: _value}(_data);
        require(success, string(_result));
        emit TransactionExecuted(_target, _value, _data);
        return _result;
    }

    /**
     * @dev {See IAccount-setOwner}
     */
    function setOwner(address _owner) external override onlyOwner {
        _transferOwnership(_owner);
    }

    receive() external payable {}

    function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4) {
        if (owner() == registry) {
            return IERC1271(registry).isValidSignature(hash, signature);
        }

        bool isValid = SignatureChecker.isValidSignatureNow(owner(), hash, signature);
        if (isValid) {
            return IERC1271.isValidSignature.selector;
        }

        return "";
    }
}
```

## 安全考虑

### 前置运行

通过调用 `createAccount` 通过账户注册表实例部署保留所有权账户可能会被恶意行为者前置运行。然而，如果恶意行为者试图更改 calldata 中的 `owner` 参数，账户注册表实例会发现签名无效，并回滚交易。因此，任何成功的前置运行交易将部署与原始交易相同的账户实例，原始所有者仍将控制该地址。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。