---
eip: 6358
title: 跨链代币状态同步
description: 一种在多个现有公链上同步代币状态的范式
author: Shawn Zheng (@xiyu1984), Jason Cheng <chengjingxx@gmail.com>, George Huang (@virgil2019), Kay Lin (@kay404)
discussions-to: https://ethereum-magicians.org/t/add-eip-6358-omniverse-distributed-ledger-technology/12625
status: 审核中
type: 标准跟踪
category: ERC
created: 2023-01-17
---

## 摘要

本 ERC 标准化了一个合约层共识无关的可验证跨链桥接接口，通过该接口，我们可以在多条链上定义一个新的全球代币，继承自 [ERC-20](./erc-20.md)/[ERC-721](./eip-721.md)。

### 图 1 架构

![img](../assets/eip-6358/img/o-dlt.png)    

通过这个 ERC，我们可以创建一个全球代币协议，利用现有区块链上的智能合约或类似机制同步记录代币状态。同步可以通过无信任的链下同步器进行。

## 动机

- 当前的代币桥接范式使资产碎片化。
- 如果通过当前的代币桥将 ETH 转移到另一条链上，如果链崩溃，用户的 ETH 将会丢失。

本 ERC 的核心是同步而不是转移，即使所有其他链崩溃，只要以太坊仍在运行，用户的资产将不会丢失。

- 碎片化问题将得到解决。
- 用户的多链资产安全性可以大大增强。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### Omniverse 账户

本 ERC 应有一个全球用户标识符，建议在本文中称为 Omniverse 账户（简称 `o-account`）。  
建议将 `o-account` 表示为由椭圆曲线 `secp256k1` 创建的公钥。建议为不同环境提供一个 [映射机制](#mapping-mechanism-for-different-environments)。

### 数据结构

Omniverse 交易（简称 `o-transaction`）必须用以下数据结构描述：

```solidity
/**
 * @notice Omniverse transaction data structure
 * @member nonce: The number of the o-transactions. If the current nonce of an omniverse account is `k`, the valid nonce of this o-account in the next o-transaction is `k+1`. 
 * @member chainId: The chain where the o-transaction is initiated
 * @member initiateSC: The contract address from which the o-transaction is first initiated
 * @member from: The Omniverse account which signs the o-transaction
 * @member payload: The encoded bussiness logic data, which is maintained by the developer
 * @member signature: The signature of the above informations. 
 */
struct ERC6358TransactionData {
    uint128 nonce;
    uint32 chainId;
    bytes initiateSC;
    bytes from;
    bytes payload;
    bytes signature;
}
```

- 数据结构 `ERC6358TransactionData` 必须如上所定义。
- 成员 `nonce` 必须定义为 `uint128`，以便更好地兼容更多区块链技术栈。
- 成员 `chainId` 必须定义为 `uint32`。
- 成员 `initiateSC` 必须定义为 `bytes`。
- 成员 `from` 必须定义为 `bytes`。
- 成员 `payload` 必须定义为 `bytes`。它是从与 o-transaction 相关的用户定义数据编码而来的。例如：  
    - 对于可替代代币，建议如下：

        ```solidity
        /**
        * @notice Fungible token data structure, from which the field `payload` in `ERC6358TransactionData` will be encoded
        *
        * @member op: The operation type
        * NOTE op: 0-31 are reserved values, 32-255 are custom values
        *           op: 0 - omniverse account `from` transfers `amount` tokens to omniverse account `exData`, `from` have at least `amount` tokens
        *           op: 1 - omniverse account `from` mints `amount` tokens to omniverse account `exData`
        *           op: 2 - omniverse account `from` burns `amount` tokens from his own, `from` have at least `amount` tokens
        * @member exData: The operation data. This sector could be empty and is determined by `op`. For example: 
                    when `op` is 0 and 1, `exData` stores the omniverse account that receives.
                    when `op` is 2, `exData` is empty.
        * @member amount: The amount of tokens being operated
        */
        struct Fungible {
            uint8 op;
            bytes exData;
            uint256 amount;
        }
        ```

        - o-transaction 中 `signature` 的相关原始数据建议为 `op`、`exData` 和 `amount` 的原始字节的连接。

    - 对于不可替代代币，建议如下：

        ```solidity
        /**
        * @notice Non-Fungible token data structure, from which the field `payload` in `ERC6358TransactionData` will be encoded
        *
        * @member op: The operation type
        * NOTE op: 0-31 are reserved values, 32-255 are custom values
        *           op: 0 omniverse account `from` transfers token `tokenId` to omniverse account `exData`, `from` have the token with `tokenId`
        *           op: 1 omniverse account `from` mints token `tokenId` to omniverse account `exData`
        *           op: 2 omniverse account `from` burns token `tokenId`, `from` have the token with `tokenId`
        * @member exData: The operation data. This sector could be empty and is determined by `op`
        *           when `op` is 0 and 1, `exData` stores the omniverse account that receives.
                    when `op` is 2, `exData` is empty.
        * @member tokenId: The tokenId of the non-fungible token being operated
        */
        struct NonFungible {
            uint8 op;
            bytes exData;
            uint256 tokenId;
        }
        ```

        - o-transaction 中 `signature` 的相关原始数据建议为 `op`、`exData` 和 `tokenId` 的原始字节的连接。

- 成员 `signature` 必须定义为 `bytes`。建议如下创建。  
    - 可选地将 `ERC6358TransactionData` 中的各个部分连接如下（以可替代代币为例），并使用 `keccak256` 计算哈希：

        ```solidity
        /**
        * @notice Decode `_data` from bytes to Fungible
        * @return A `Fungible` instance
        */
        function decodeData(bytes memory _data) internal pure returns (Fungible memory) {
            (uint8 op, bytes memory exData, uint256 amount) = abi.decode(_data, (uint8, bytes, uint256));
            return Fungible(op, exData, amount);
        }
        
        /**
        * @notice Get the hash of a transaction
        * @return Hash value of the raw data of an `ERC6358TransactionData` instance
        */
        function getTransactionHash(ERC6358TransactionData memory _data) public pure returns (bytes32) {
            Fungible memory fungible = decodeData(_data.payload);
            bytes memory payload = abi.encodePacked(fungible.op, fungible.exData, fungible.amount);
            bytes memory rawData = abi.encodePacked(_data.nonce, _data.chainId, _data.initiateSC, _data.from, payload);
            return keccak256(rawData);
        }
        ```

    - 可选地根据 `EIP-712` 封装 `ERC6358TransactionData` 中的各个部分。
    - 签署哈希值。

### 智能合约接口

- 每个 [ERC-6358](./eip-6358.md) 合规合约必须实现 `IERC6358`  

    ```solidity
    /**
    * @notice Interface of the ERC-6358
    */
    interface IERC6358 {
        /**
        * @notice Emitted when a o-transaction which has nonce `nonce` and was signed by user `pk` is sent by calling {sendOmniverseTransaction}
        */
        event TransactionSent(bytes pk, uint256 nonce);

        /**
        * @notice Sends an `o-transaction` 
        * @dev 
        * Note: MUST implement the validation of the `_data.signature`
        * Note: A map maintaining the  `o-account` and the related transaction nonce is RECOMMENDED  
        * Note: MUST implement the validation of the `_data.nonce` according to the current account nonce
        * Note: MUST implement the validation of the `_data. payload`
        * Note: This interface is just for sending an `o-transaction`, and the execution MUST NOT be within this interface 
        * Note: The actual execution of an `o-transaction` is RECOMMENDED to be in another function and MAY be delayed for a time
        * @param _data: the `o-transaction` data with type {ERC6358TransactionData}
        * See more information in the defination of {ERC6358TransactionData}
        *
        * Emit a {TransactionSent} event
        */
        function sendOmniverseTransaction(ERC6358TransactionData calldata _data) external;

        /**
        * @notice Get the number of omniverse transactions sent by user `_pk`, 
        * which is also the valid `nonce` of a new omniverse transactions of user `_pk` 
        * @param _pk: Omniverse account to be queried
        * @return The number of omniverse transactions sent by user `_pk`
        */
        function getTransactionCount(bytes memory _pk) external view returns (uint256);

        /**
        * @notice Get the transaction data `txData` and timestamp `timestamp` of the user `_use` at a specified nonce `_nonce`
        * @param _user Omniverse account to be queried
        * @param _nonce The nonce to be queried
        * @return Returns the transaction data `txData` and timestamp `timestamp` of the user `_use` at a specified nonce `_nonce`
        */
        function getTransactionData(bytes calldata _user, uint256 _nonce) external view returns (ERC6358TransactionData memory, uint256);

        /**
        * @notice Get the chain ID
        * @return Returns the chain ID
        */
        function getChainId() external view returns (uint32);
    }
    ```

    - `sendOmniverseTransaction` 函数可以实现为 `public` 或 `external`
    - `getTransactionCount` 函数可以实现为 `public` 或 `external`
    - `getTransactionData` 函数可以实现为 `public` 或 `external`
    - `getChainId` 函数可以实现为 `pure` 或 `view`
    - 当调用 `sendOmniverseTransaction` 函数时，必须触发 `TransactionSent` 事件
- 可选扩展：可替代代币  

    ```solidity
    // import "{IERC6358.sol}";

    /**
    * @notice Interface of the ERC-6358 fungible token, which inherits {IERC6358}
    */
    interface IERC6358Fungible is IERC6358 {
        /**
        * @notice Get the omniverse balance of a user `_pk`
        * @param _pk `o-account` to be queried
        * @return Returns the omniverse balance of a user `_pk`
        */
        function omniverseBalanceOf(bytes calldata _pk) external view returns (uint256);
    }
    ```

    - `omniverseBalanceOf` 函数可以实现为 `public` 或 `external`

- 可选扩展：不可替代代币  

    ```solidity
    import "{IERC6358.sol}";

    /**
    * @notice Interface of the ERC-6358 non fungible token, which inherits {IERC6358}
    */
    interface IERC6358NonFungible is IERC6358 {
        /**
        * @notice Get the number of omniverse NFTs in account `_pk`
        * @param _pk `o-account` to be queried
        * @return Returns the number of omniverse NFTs in account `_pk`
        */
        function omniverseBalanceOf(bytes calldata _pk) external view returns (uint256);

        /**
        * @notice Get the owner of an omniverse NFT with `tokenId`
        * @param _tokenId Omniverse NFT id to be queried
        * @return Returns the owner of an omniverse NFT with `tokenId`
        */
        function omniverseOwnerOf(uint256 _tokenId) external view returns (bytes memory);
    }
    ```

    - `omniverseBalanceOf` 函数可以实现为 `public` 或 `external`
    - `omniverseOwnerOf` 函数可以实现为 `public` 或 `external`

## 理由

### 架构

如 [图 1](#figure1-architecture) 所示，部署在多条链上的智能合约通过无信任的链下同步器同步执行 ERC-6358 代币的 `o-transactions`。

- ERC-6358 智能合约被称为 **抽象节点**。在不同区块链上部署的抽象节点记录的状态可以视为全球状态的副本，并且它们是最终一致的。  
- **同步器** 是一个链下执行程序，负责将已发布的 `o-transactions` 从一条区块链上的 ERC-6358 智能合约传递到其他链。同步器以无信任的方式工作，因为它们只传递带有他人签名的 `o-transactions`，详细信息可以在 [工作流程](#workflow) 中找到。

### 原则

- `o-account` 已在 [上文](#omniverse-account) 中提到。
- `o-transactions` 的同步保证了所有链上代币状态的最终一致性。相关数据结构见 [此处](#data-structure)。

    - 引入了 `nonce` 机制以使状态在全球范围内一致。
    - `nonce` 出现在两个地方，一个是 `o-transaction` 数据结构中的 `nonce`，另一个是由链上 ERC-6358 智能合约维护的 `account nonce`。 
    - 在同步时，将通过将 `o-transaction` 中的 `nonce` 与 `account nonce` 进行比较来检查。

#### 工作流程

- 假设一个普通用户 `A` 及其相关操作 `account nonce` 为 $k$。
- `A` 通过调用 `IERC6358::sendOmniverseTransaction` 在以太坊上发起一个 `o-transaction`。此时，`A` 在部署在以太坊上的 ERC-6358 智能合约中的当前 `account nonce` 为 $k$，因此 `o-transaction` 中 `nonce` 的有效值需要为 $k+1$。  
- ERC-6358 智能合约在以太坊上验证 `o-transaction` 数据的签名。如果验证成功，`o-transaction` 数据将由以太坊侧的智能合约发布。验证包括：
    - 余额（FT）或所有权（NFT）是否有效
    - 以及 `o-transaction` 中的 `nonce` 是否为 $k+1$
- `o-transaction` 不应立即在以太坊上执行，而是等待一段时间。  
- 此时，`A` 在以太坊上提交的最新 `o-transaction` 中的 `nonce` 为 $k+1$，但在其他链上仍为 $k$。
- 链下同步器将发现以太坊上有一个新发布的 `o-transaction`，但在其他链上没有。  
- 接下来，同步器将迅速传递此消息，因为有奖励机制。（奖励策略可以由 ERC-6358 代币的部署者决定。例如，奖励可以来自服务费或挖矿机制。） 
- 最后，部署在其他链上的 ERC-6358 智能合约将接收到所有 `o-transaction` 数据，验证签名并在 **等待时间结束** 时执行。 
- 执行后，所有链上的 `account nonce` 将加 1。现在账户 `A` 的所有 `account nonce` 都将为 $k+1$，相关账户的余额状态也将相同。  
## 参考实现

### Omniverse 账户

- 一个 Omniverse 账户示例： `3092860212ceb90a13e4a288e444b685ae86c63232bcb50a064cb3d25aa2c88a24cd710ea2d553a20b4f2f18d2706b8cc5a9d4ae4a50d475980c2ba83414a796`
    - Omniverse 账户是椭圆曲线 `secp256k1` 的公钥
    - 示例的相关私钥是： `cdfa0e50d672eb73bc5de00cc0799c70f15c5be6b6fca4a1c82c35c7471125b6`

#### 不同环境的映射机制

在最简单的实现中，我们可以构建两个映射来实现。一个是 `基于 sece256k1 的 pk => 特殊环境中的账户地址`，另一个是反向映射。

`Flow` 上的 `账户系统` 是一个典型的例子。

- `Flow` 有一个内置机制用于 `账户地址 => pk`。公钥可以绑定到一个账户（一个特殊的内置数据结构），并且可以直接从 `账户地址` 获取公钥。
- 可以通过创建一个映射 `{String: Address}` 来构建 `pk` 到 `账户地址` 的映射，其中 `String` 表示用于表示公钥的数据类型，`Address` 是 Flow 上的 `账户地址` 的数据类型。

### ERC-6358 代币

ERC-6358 代币可以使用上述提到的 [接口](#smart-contract-interface) 实现。它也可以与 [ERC-20](./erc-20.md)/[ERC-721](./eip-721.md) 结合使用。

- 接口的实现示例可以在以下位置找到：

    - [接口 `IERC6358`](../assets/eip-6358/src/contracts/interfaces/IERC6358.sol)，上述提到的基本 ERC-6358 接口
    - [接口 `IERC6358Fungible`](../assets/eip-6358/src/contracts/interfaces/IERC6358Fungible.sol)，ERC-6358 可替代代币的接口
    - [接口 `IERC6358NonFungible`](../assets/eip-6358/src/contracts/interfaces/IERC6358NonFungible.sol)，ERC-6358 非可替代代币的接口

- 一些操作 ERC-6358 的常用工具的实现示例可以在以下位置找到：

    - [常用工具](../assets/eip-6358/src/contracts/libraries/OmniverseProtocolHelper.sol)。

- ERC-6358 可替代代币和 ERC-6358 非可替代代币的实现示例可以在以下位置找到：

    - [ERC-6358 可替代代币示例](../assets/eip-6358/src/contracts/ERC6358FungibleExample.sol)
    - [ERC-6358 非可替代代币示例](../assets/eip-6358/src/contracts/ERC6358NonFungibleExample.sol)

## 安全考虑

### 攻击向量分析

根据上述内容，有两个角色：

- **普通用户** 是发起 `o-transaction` 的人
- **同步者** 是在不同链之间发现差异时仅传递 `o-transaction` 数据的人。

这两个角色可能是攻击发生的地方：

#### **同步者会作弊吗？**

- 简单来说，这与 **同步者** 无关，因为 **他们无法创建其他用户的签名**，除非某些 **普通用户** 告诉他们，但在这一点上，我们认为这是 **普通用户** 的问题。
- **同步者** 没有意图，也无法作恶，因为他们传递的 `o-transaction` 数据是经过其他 **普通用户** 的相关 **签名** 验证的。
- 只要 **同步者** 提交有效的 `o-transaction` 数据，他们就会获得奖励，而 *有效* 仅意味着签名和金额都是有效的。这将在稍后分析 **普通用户** 的角色时详细解释。
- 一旦 **同步者** 发现不同链之间的差异，他们将进行交付：
    - 如果一个链上的当前 `账户 nonce` 小于另一个链上发布的 `o-transaction` 中的 `nonce`
    - 如果一个链上与特定 `o-transaction` 中的 `nonce` 相关的交易数据与另一个链上具有相同 `nonce` 的已发布 `o-transaction` 数据不同

- **结论：*同步者* 不会作弊，因为他们没有利益，也没有办法这样做。**

#### **普通用户会作弊吗？**

- 简单来说，**他们可能会**，但幸运的是，**他们无法成功**。
- 假设 **普通用户** `A` 在所有链上的当前 `账户 nonce` 是 $k$。`A` 拥有 100 个代币 `X`，这是 ERC-6358 代币的一个实例。
- 普通用户 `A` 首先在 Polkadot 的一个平行链上发起 `o-transaction`，其中 `A` 将 `10` 个 `X` 转移到 **普通用户** `B` 的 `o-account`。`o-transaction` 中的 `nonce` 需要是 $k+1$。在签名和数据验证后，`o-transaction` 数据（简称 `ot-P-ab`）将在 Polkadot 上发布。
- 与此同时，`A` 在以太坊上发起一个具有 **相同 nonce** $k+1$ 但 **不同数据**（例如将 `10` 个 `X` 转移到另一个 `o-account` `C`）的 `o-transaction`。这个 `o-transaction`（简称 `ot-E-ac`）将在以太坊上首先通过验证并被发布。
- 此时，似乎 `A` 完成了一次 ***双重支付攻击***，而 Polkadot 和以太坊上的状态不同。
- **应对策略**：
    - 正如我们上面提到的，同步者将把 `ot-P-ab` 传递到以太坊，并将 `ot-E-ac` 传递到 Polkadot，因为它们不同，尽管具有相同的 nonce。首先提交 `o-transaction` 的同步者将获得奖励，因为签名是有效的。
    - 一旦 ERC-6358 智能合约或 Polkadot 和以太坊上的类似机制接收到 `ot-E-ac` 和 `ot-P-ab`，将会发现 `A` 进行了作弊，因为 `A` 的签名是不可否认的。
    - 我们提到过，`o-transaction` 的执行不会立即完成，而是需要固定的等待时间。因此，`A` 造成的 `双重支付攻击` 不会成功。
    - 将会有许多同步者等待传递 o-transaction 以获取奖励。因此，尽管 **普通用户** 几乎不可能向两个链提交两个 `o-transaction`，但如果没有同步者成功传递 `o-transaction`，因为网络问题或其他原因，我们仍然提供解决方案：
        - 同步者将连接到每个公链的多个本地节点，以避免恶意本地节点。
        - 如果确实发生所有同步者的网络中断，`o-transaction` 将在网络恢复时进行同步。如果等待时间到期且作弊的 `o-transaction` 已被执行，我们仍然能够根据 `o-transaction` 中的 `nonce` 和 `账户 nonce` 从作弊发生的地方撤回它。
- 最终，`A` 无法逃避惩罚（例如，锁定他的账户或其他措施，这取决于开发者根据自身情况确定的特定代币经济学）。

- **结论：*普通用户* 可能会作弊，但不会成功。**

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。