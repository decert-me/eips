---
eip: 5791
title: 实体支持代币
description: 将 ERC-721 NFT 的所有权与物理芯片链接的最小接口
author: 2pmflow (@2pmflow), locationtba (@locationtba), Cameron Robertson (@ccamrobertson), cygaar (@cygaar), Brian Weick (@bweick), vectorized (@vectorized), djdabs (@djdabs)
discussions-to: https://ethereum-magicians.org/t/physical-backed-tokens/11350
status: 草案
type: 标准跟踪
category: ERC
created: 2022-10-17
requires: 191, 721
---

## 摘要

该标准是 [ERC-721](./eip-721.md) 的扩展。它提出了一个最小接口，使 [ERC-721](./eip-721.md) NFT 可以“实体支持”，并由拥有 NFT 实体对应物的人拥有。

## 动机

NFT 收藏者喜欢收集数字资产并与他人在线分享。然而，目前没有这样的标准来展示具有验证真实性和所有权的实体资产作为 NFT。现有解决方案是分散的，并且往往容易受到以下至少一种情况的影响：

- 实体物品的所有权与 NFT 的所有权脱钩。

- 验证实体物品的真实性需要可信的第三方采取行动（例如 StockX）。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

### 要求

该方法要求实体物品必须附有一个芯片，该芯片应安全并能发出真实性信号：

- 芯片可以安全地生成和存储一对非对称密钥；
- 芯片可以使用先前生成的非对称密钥对消息进行签名；
- 芯片公开公钥；并且
- 私钥不能被提取或复制，设计上如此

该方法还要求合约使用 [ERC-721](./eip-721.md) 的账户绑定实现（所有转移的 [ERC-721](./eip-721.md) 函数必须抛出，例如在 [ERC-721](./eip-721.md) 中提到的“只读 NFT 注册表”实现）。这确保了必须拥有实体物品才能启动转移并管理 NFT 的所有权，通过下面描述的在此接口中引入的新函数。

### 方法

每个 NFT 在概念上与一个物理芯片相链接。

当 chipId 与 tokenId 配对时，将发出一个事件。这让下游索引器知道哪些芯片地址映射到 NFT 集合中的哪些代币。没有将其 token id 链接到特定芯片，NFT 不能被铸造。

该接口包括一个名为 `transferToken` 的函数，如果传入有效的由芯片签名的签名，则将 NFT 转移给函数调用者。有效签名必须遵循 [ERC-191](./eip-191.md) 和 [EIP-2](./eip-2.md) 中规定的方案（s 值限制），其中要签名的数据包括目标接收者地址（函数调用者）、芯片地址、区块时间戳以及用于实现中额外自定义逻辑的任何额外参数。

该接口还包括其他函数，允许任何人验证物理物品中的芯片是否支持集合中的现有 NFT。

### 接口

```solidity

interface IERC5791 {
    /// @dev Returns the ERC-721 `tokenId` for a given chip address.
    ///      Reverts if `chipId` has not been paired to a `tokenId`.
    ///      For minimalism, this will NOT revert if the `tokenId` does not exist.
    ///      If there is a need to check for token existence, external contracts can
    ///      call `ERC721.ownerOf(uint256 tokenId)` and check if it passes or reverts.
    /// @param chipId The address for the chip embedded in the physical item
    ///               (computed from the chip's public key).
    function tokenIdFor(address chipId) external view returns (uint256 tokenId);

    /// @dev Returns true if `signature` is signed by the chip assigned to `tokenId`, else false.
    ///      Reverts if `tokenId` has not been paired to a chip.
    ///      For minimalism, this will NOT revert if the `tokenId` does not exist.
    ///      If there is a need to check for token existence, external contracts can
    ///      call `ERC721.ownerOf(uint256 tokenId)` and check if it passes or reverts.
    /// @param tokenId ERC-721 `tokenId`.
    /// @param data      Arbitrary bytes string that is signed by the chip to produce `signature`.
    /// @param signature EIP-191 signature by the chip to check.
    function isChipSignatureForToken(uint256 tokenId, bytes calldata data, bytes calldata signature)
        external
        view
        returns (bool);

    /// @dev Transfers the token into the address.
    ///      Returns the `tokenId` transferred.
    /// @param to                  The recipient. Dynamic to allow easier transfers to vaults.
    /// @param chipId              Chip ID (address) of chip being transferred.
    /// @param chipSignature       EIP-191 signature by the chip to authorize the transfer.
    /// @param signatureTimestamp  Timestamp used in `chipSignature`.
    /// @param useSafeTransferFrom Whether ERC-721's `safeTransferFrom` should be used,
    ///                            instead of `transferFrom`.
    /// @param extras              Additional data that can be used for additional logic/context
    ///                            when the PBT is transferred.
    function transferToken(
        address to,
        address chipId,
        bytes calldata chipSignature,
        uint256 signatureTimestamp,
        bool useSafeTransferFrom,
        bytes calldata extras
    ) external returns (uint256 tokenId);

    /// @dev Emitted when `chipId` is paired to `tokenId`.
    /// `tokenId` may not necessarily exist during assignment.
    /// Indexers can combine this event with the {ERC721.Transfer} event to
    /// infer which tokens exists and are paired with a chip ID.
    event ChipSet(uint256 indexed tokenId, address indexed chipId);
}

```

为了帮助识别 [ERC-721](./eip-721.md) 代币通过此 EIP 实现物理绑定：在调用 [ERC-165](./eip-165.md) 的 `function supportsInterface(bytes4 interfaceID) external view returns (bool)` 时，使用 `interfaceID=0x4901df9f`，实现此 EIP 的合约必须返回 true。

铸造接口由实现决定。铸造的 NFT 的所有者应为物理芯片的所有者（此身份验证可以使用为 `transferToken` 定义的签名方案实现）。

## 理由

该解决方案的意图是成为将实体物品与数字 NFT 连接的最简单路径，而无需集中权威。

该接口包括一个 `transferToken` 函数，该函数对签名方案持有明确意见，以便支持未来任何实现此 EIP 的 NFT 转移的下游聚合产品。

芯片地址包含在 `transferToken` 中，以允许智能合约进行签名验证。这确保了物理支持代币中的芯片不严格依赖于实现 secp256k1 签名，而是可以使用多种签名方案，例如 P256 或 BabyJubJub。

### 超出范围

以下是一些故意不在此 EIP 范围内的外围问题：

- 信任特定 NFT 集合的芯片地址实际上映射到嵌入在物品中的物理芯片，而不是声称是芯片的任意 EOA
- 确保芯片不会恶化或损坏
- 确保芯片保持附着在物理物品上
- 等等

这些挑战的工作正在并行进行。

将代币 id 映射到芯片地址也超出范围。这可以通过多种方式完成，例如让合约所有者在铸造前预设此映射，或通过将 `(tokenId, chipId)` 元组传递到预先由合约信任的地址签名的铸造函数中，或通过在可信注册表中查找，或通过在铸造时按先到先得的方式分配代币 id 等等。

此外，物理物品的所有者可以将 NFT 转移到其他人拥有的钱包（通过将芯片签名发送给该其他人使用）。我们仍然认为 NFT 是实体支持的，因为所有权管理与物理物品相关联。这可以解释为物品的所有者暂时将物品借给其他人，因为（1）物品的所有者必须作为使用芯片签名的参与者，且（2）物品的所有者可以随时收回 NFT 的所有权。

## 向后兼容性

该提案在 API 级别上与 [ERC-721](./eip-721.md) 向后兼容。如上所述，为了使代币具有实体支持，合约必须使用 [ERC-721](./eip-721.md) 的账户绑定实现（所有转移的 [ERC-721](./eip-721.md) 函数必须抛出），以便转移通过此处引入的新函数进行，该函数需要芯片签名。

## 参考实现

以下是如何在转移事件中验证芯片签名的代码片段。

```solidity
import '@openzeppelin/contracts/utils/cryptography/ECDSA.sol';

/// @dev Transfers the `tokenId` assigned to `chipId` to `to`.
function transferToken(
    address to,
    address chipId,
    bytes memory chipSignature,
    uint256 signatureTimestamp,
    bool useSafeTransfer,
    bytes memory extras
) public virtual returns (uint256 tokenId) {
    tokenId = tokenIdFor(chipId);
    _validateSigAndUpdateNonce(to, chipId, chipSignature, signatureTimestamp, extras);
    if (useSafeTransfer) {
        _safeTransfer(ownerOf(tokenId), to, tokenId, "");
    } else {
        _transfer(ownerOf(tokenId), to, tokenId);
    }
}

/// @dev Validates the `chipSignature` and update the nonce for the future signature of `chipId`.
function _validateSigAndUpdateNonce(
    address to,
    address chipId,
    bytes memory chipSignature,
    uint256 signatureTimestamp,
    bytes memory extras
) internal virtual {
    bytes32 hash = _getSignatureHash(signatureTimestamp, chipId, to, extras);
    if (!SignatureCheckerLib.isValidSignatureNow(chipId, hash, chipSignature)) {
        revert InvalidSignature();
    }
    chipNonce[chipId] = bytes32(uint256(hash) ^ uint256(blockhash(block.number - 1)));
}

/// @dev Returns the digest to be signed by the `chipId`.
function _getSignatureHash(uint256 signatureTimestamp, address chipId, address to, bytes memory extras)
    internal
    virtual
    returns (bytes32)
{
    if (signatureTimestamp > block.timestamp) revert SignatureTimestampInFuture();
    if (signatureTimestamp + maxDurationWindow < block.timestamp) revert SignatureTimestampTooOld();
    bytes32 hash = keccak256(
        abi.encode(address(this), block.chainid, chipNonce[chipId], to, signatureTimestamp, keccak256(extras))
    );
    return ECDSA.toEthSignedMessageHash(hash);
}

```

## 安全考虑

传递给 `transferToken` 的 [ERC-191](./eip-191.md) 签名要求函数调用者的地址在其签名数据中，以便签名不能用于重放攻击。它还要求最近的区块时间戳，以便恶意芯片所有者不能预先生成签名以在短时间窗口后使用（例如，在物理物品的所有者更改后）。建议在生成签名时使用非确定性的 `chipNonce`。

此外，一个人对代币是否实体支持的信任程度取决于物理芯片的安全性，这在上述 EIP 中已提到超出范围。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。