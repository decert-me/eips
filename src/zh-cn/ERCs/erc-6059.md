---
eip: 6059
title: 父级管理的可嵌套非同质化代币
description: 一个可嵌套非同质化代币的接口，强调父级代币对关系的控制。
author: Bruno Škvorc (@Swader), Cicada (@CicadaNCR), Steven Pineda (@steven2308), Stevan Bogosavljevic (@stevyhacker), Jan Turk (@ThunderDeliverer)
discussions-to: https://ethereum-magicians.org/t/eip-6059-parent-governed-nestable-non-fungible-tokens/11914
status: Final
type: Standards Track
category: ERC
created: 2022-11-15
requires: 165, 721
---

## 摘要

父级管理的可嵌套 NFT 标准扩展了 [ERC-721](./eip-721.md)，允许新的 NFT 之间的关系和交互。

该提案的核心思想很简单：NFT 的拥有者不必是外部拥有账户（EOA）或智能合约，它也可以是一个 NFT。

将一个 NFT 嵌套到另一个 NFT 的过程在功能上与将其发送给另一个用户相同。将代币从另一个代币中发送的过程涉及从拥有父级代币的账户发出交易。

一个 NFT 可以被单个其他 NFT 拥有，但它又可以拥有多个 NFT。该提案建立了 NFT 的父子关系框架。父级代币是拥有另一个代币的代币。子代币是被另一个代币拥有的代币。一个代币可以同时是父级和子级。给定代币的子代币可以由父级代币的拥有者完全管理，但可以由任何人提议。

![可嵌套代币](../assets/eip-6059/img/eip-6059-nestable-tokens.png)

该图说明了子代币如何也可以是父代币，但两者仍由根父代币的拥有者管理。

## 动机

随着 NFT 在以太坊生态系统中成为广泛使用的代币形式，并用于各种用例，现在是时候为它们标准化额外的实用性了。代币能够拥有其他代币的能力允许更大的实用性、可用性和向前兼容性。

自 [ERC-721](./eip-721.md) 发布以来的四年中，额外功能的需求导致了无数扩展。该 ERC 在以下方面改进了 ERC-721：

- [捆绑](#bundling)
- [收集](#collecting)
- [会员](#membership)
- [委托](#delegation)

### 捆绑

[ERC-721](./eip-721.md) 最常见的用途之一是传播与代币相关的多媒体内容。如果有人想提供来自不同集合的 NFT 捆绑包，目前没有简单的方法将所有这些捆绑在一起并作为单个交易处理其销售。该提案引入了一种标准化的方法来实现这一点。将所有代币嵌套到一个简单的捆绑包中并出售该捆绑包将以单个交易将所有代币的控制权转移给买方。

### 收集

许多 NFT 消费者根据无数标准进行收集。有些人追求代币的实用性，有些人追求独特性，有些人追求视觉吸引力等。没有标准化的方法来对与特定账户相关的 NFT 进行分组。通过根据所有者的偏好嵌套 NFT，该提案引入了这一能力。根父代币可以代表某一组代币，所有嵌套在其中的子代币都属于它。

不可转让的灵魂绑定代币的兴起引入了该提案的另一个需求。拥有多个灵魂绑定特征（子代币）的代币允许多种用例。一个具体的例子可以从供应链用例中得出。一个由 NFT 表示的运输集装箱，具有其自身的特征，可以有多个子代币表示其旅程的每一段。

### 会员

与 NFT 相关的一个常见实用性是对去中心化自治组织（DAO）或其他某些闭门访问组的会员资格。这些组织和团体有时会向当前持有会员 NFT 的人铸造 NFT。通过将代币嵌套铸造到代币中，这种铸造可以简化，只需将奖励 NFT 直接铸造到会员 NFT 中即可。

### 委托

DAO 的核心功能之一是投票，并且有多种方法来实现。一个这样的机制是使用可替代的投票代币，成员可以通过将这些代币发送给其他成员来委托他们的投票。使用该提案，委托投票可以通过将您的投票 NFT 嵌套到您委托投票的代币中来处理，并在成员不再希望委托他们的投票时转移它。

## 规范

本文档中的关键字“必须”、“不得”、“要求”、“应”、“不应”、“推荐”、“可能”和“可选”应按 RFC 2119 中的描述进行解释。

```solidity
/// @title EIP-6059 Parent-Governed Nestable Non-Fungible Tokens
/// @dev See https://eips.ethereum.org/EIPS/eip-6059
/// @dev Note: the ERC-165 identifier for this interface is 0x42b0e56f.

pragma solidity ^0.8.16;

interface IERC6059 /* is ERC165 */ {
    /**
     * @notice The core struct of ownership.
     * @dev The `DirectOwner` struct is used to store information of the next immediate owner, be it the parent token,
     * an `ERC721Receiver` contract or an externally owned account.
     * @dev If the token is not owned by an NFT, the `tokenId` MUST equal `0`.
     * @param tokenId ID of the parent token
     * @param ownerAddress Address of the owner of the token. If the owner is another token, then the address MUST be
     *  the one of the parent token's collection smart contract. If the owner is externally owned account, the address
     *  MUST be the address of this account
     */
    struct DirectOwner {
        uint256 tokenId;
        address ownerAddress;
    }

    /**
     * @notice Used to notify listeners that the token is being transferred.
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     * @param from Address of the previous immediate owner, which is a smart contract if the token was nested.
     * @param to Address of the new immediate owner, which is a smart contract if the token is being nested.
     * @param fromTokenId ID of the previous parent token. If the token was not nested before, the value MUST be `0`
     * @param toTokenId ID of the new parent token. If the token is not being nested, the value MUST be `0`
     * @param tokenId ID of the token being transferred
     */
    event NestTransfer(
        address indexed from,
        address indexed to,
        uint256 fromTokenId,
        uint256 toTokenId,
        uint256 indexed tokenId
    );

    /**
     * @notice Used to notify listeners that a new token has been added to a given token's pending children array.
     * @dev Emitted when a child NFT is added to a token's pending array.
     * @param tokenId ID of the token that received a new pending child token
     * @param childIndex Index of the proposed child token in the parent token's pending children array
     * @param childAddress Address of the proposed child token's collection smart contract
     * @param childId ID of the child token in the child token's collection smart contract
     */
    event ChildProposed(
        uint256 indexed tokenId,
        uint256 childIndex,
        address indexed childAddress,
        uint256 indexed childId
    );

    /**
     * @notice Used to notify listeners that a new child token was accepted by the parent token.
     * @dev Emitted when a parent token accepts a token from its pending array, migrating it to the active array.
     * @param tokenId ID of the token that accepted a new child token
     * @param childIndex Index of the newly accepted child token in the parent token's active children array
     * @param childAddress Address of the child token's collection smart contract
     * @param childId ID of the child token in the child token's collection smart contract
     */
    event ChildAccepted(
        uint256 indexed tokenId,
        uint256 childIndex,
        address indexed childAddress,
        uint256 indexed childId
    );

    /**
     * @notice Used to notify listeners that all pending child tokens of a given token have been rejected.
     * @dev Emitted when a token removes all a child tokens from its pending array.
     * @param tokenId ID of the token that rejected all of the pending children
     */
    event AllChildrenRejected(uint256 indexed tokenId);

    /**
     * @notice Used to notify listeners a child token has been transferred from parent token.
     * @dev Emitted when a token transfers a child from itself, transferring ownership.
     * @param tokenId ID of the token that transferred a child token
     * @param childIndex Index of a child in the array from which it is being transferred
     * @param childAddress Address of the child token's collection smart contract
     * @param childId ID of the child token in the child token's collection smart contract
     * @param fromPending A boolean value signifying whether the token was in the pending child tokens array (`true`) or
     *  in the active child tokens array (`false`)
     */
    event ChildTransferred(
        uint256 indexed tokenId,
        uint256 childIndex,
        address indexed childAddress,
        uint256 indexed childId,
        bool fromPending
    );

    /**
     * @notice The core child token struct, holding the information about the child tokens.
     * @return tokenId ID of the child token in the child token's collection smart contract
     * @return contractAddress Address of the child token's smart contract
     */
    struct Child {
        uint256 tokenId;
        address contractAddress;
    }

    /**
     * @notice Used to retrieve the *root* owner of a given token.
     * @dev The *root* owner of the token is the top-level owner in the hierarchy which is not an NFT.
     * @dev If the token is owned by another NFT, it MUST recursively look up the parent's root owner.
     * @param tokenId ID of the token for which the *root* owner has been retrieved
     * @return owner The *root* owner of the token
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @notice Used to retrieve the immediate owner of the given token.
     * @dev If the immediate owner is another token, the address returned, MUST be the one of the parent token's
     *  collection smart contract.
     * @param tokenId ID of the token for which the direct owner is being retrieved
     * @return address Address of the given token's owner
     * @return uint256 The ID of the parent token. MUST be `0` if the owner is not an NFT
     * @return bool The boolean value signifying whether the owner is an NFT or not
     */
    function directOwnerOf(uint256 tokenId)
        external
        view
        returns (
            address,
            uint256,
            bool
        );

    /**
     * @notice Used to burn a given token.
     * @dev When a token is burned, all of its child tokens are recursively burned as well.
     * @dev When specifying the maximum recursive burns, the execution MUST be reverted if there are more children to be
     *  burned.
     * @dev Setting the `maxRecursiveBurn` value to 0 SHOULD only attempt to burn the specified token and MUST revert if
     *  there are any child tokens present.
     * @param tokenId ID of the token to burn
     * @param maxRecursiveBurns Maximum number of tokens to recursively burn
     * @return uint256 Number of recursively burned children
     */
    function burn(uint256 tokenId, uint256 maxRecursiveBurns)
        external
        returns (uint256);

    /**
     * @notice Used to add a child token to a given parent token.
     * @dev This adds the child token into the given parent token's pending child tokens array.
     * @dev The destination token MUST NOT be a child token of the token being transferred or one of its downstream
     *  child tokens.
     * @dev This method MUST NOT be called directly. It MUST only be called from an instance of `IERC6059` as part of a 
        `nestTransfer` or `transferChild` to an NFT.
     * @dev Requirements:
     *
     *  - `directOwnerOf` on the child contract MUST resolve to the called contract.
     *  - the pending array of the parent contract MUST not be full.
     * @param parentId ID of the parent token to receive the new child token
     * @param childId ID of the new proposed child token
     */
    function addChild(uint256 parentId, uint256 childId) external;

    /**
     * @notice Used to accept a pending child token for a given parent token.
     * @dev This moves the child token from parent token's pending child tokens array into the active child tokens
     *  array.
     * @param parentId ID of the parent token for which the child token is being accepted
     * @param childIndex Index of the child token to accept in the pending children array of a given token
     * @param childAddress Address of the collection smart contract of the child token expected to be at the specified
     *  index
     * @param childId ID of the child token expected to be located at the specified index
     */
    function acceptChild(
        uint256 parentId,
        uint256 childIndex,
        address childAddress,
        uint256 childId
    ) external;

    /**
     * @notice Used to reject all pending children of a given parent token.
     * @dev Removes the children from the pending array mapping.
     * @dev The children's ownership structures are not updated.
     * @dev Requirements:
     *
     * - `parentId` MUST exist
     * @param parentId ID of the parent token for which to reject all of the pending tokens
     * @param maxRejections Maximum number of expected children to reject, used to prevent from
     *  rejecting children which arrive just before this operation.
     */
    function rejectAllChildren(uint256 parentId, uint256 maxRejections) external;

    /**
     * @notice Used to transfer a child token from a given parent token.
     * @dev MUST remove the child from the parent's active or pending children.
     * @dev When transferring a child token, the owner of the token MUST be set to `to`, or not updated in the event of `to`
     *  being the `0x0` address.
     * @param tokenId ID of the parent token from which the child token is being transferred
     * @param to Address to which to transfer the token to
     * @param destinationId ID of the token to receive this child token (MUST be 0 if the destination is not a token)
     * @param childIndex Index of a token we are transferring, in the array it belongs to (can be either active array or
     *  pending array)
     * @param childAddress Address of the child token's collection smart contract
     * @param childId ID of the child token in its own collection smart contract
     * @param isPending A boolean value indicating whether the child token being transferred is in the pending array of the
     *  parent token (`true`) or in the active array (`false`)
     * @param data Additional data with no specified format, sent in call to `to`
     */
    function transferChild(
        uint256 tokenId,
        address to,
        uint256 destinationId,
        uint256 childIndex,
        address childAddress,
        uint256 childId,
        bool isPending,
        bytes data
    ) external;

    /**
     * @notice Used to retrieve the active child tokens of a given parent token.
     * @dev Returns array of Child structs existing for parent token.
     * @dev The Child struct consists of the following values:
     *  [
     *      tokenId,
     *      contractAddress
     *  ]
     * @param parentId ID of the parent token for which to retrieve the active child tokens
     * @return struct[] An array of Child structs containing the parent token's active child tokens
     */
    function childrenOf(uint256 parentId)
        external
        view
        returns (Child[] memory);

    /**
     * @notice Used to retrieve the pending child tokens of a given parent token.
     * @dev Returns array of pending Child structs existing for given parent.
     * @dev The Child struct consists of the following values:
     *  [
     *      tokenId,
     *      contractAddress
     *  ]
     * @param parentId ID of the parent token for which to retrieve the pending child tokens
     * @return struct[] An array of Child structs containing the parent token's pending child tokens
     */
    function pendingChildrenOf(uint256 parentId)
        external
        view
        returns (Child[] memory);

    /**
     * @notice Used to retrieve a specific active child token for a given parent token.
     * @dev Returns a single Child struct locating at `index` of parent token's active child tokens array.
     * @dev The Child struct consists of the following values:
     *  [
     *      tokenId,
     *      contractAddress
     *  ]
     * @param parentId ID of the parent token for which the child is being retrieved
     * @param index Index of the child token in the parent token's active child tokens array
     * @return struct A Child struct containing data about the specified child
     */
    function childOf(uint256 parentId, uint256 index)
        external
        view
        returns (Child memory);

    /**
     * @notice Used to retrieve a specific pending child token from a given parent token.
     * @dev Returns a single Child struct locating at `index` of parent token's active child tokens array.
     * @dev The Child struct consists of the following values:
     *  [
     *      tokenId,
     *      contractAddress
     *  ]
     * @param parentId ID of the parent token for which the pending child token is being retrieved
     * @param index Index of the child token in the parent token's pending child tokens array
     * @return struct A Child struct containing data about the specified child
     */
    function pendingChildOf(uint256 parentId, uint256 index)
        external
        view
        returns (Child memory);

    /**
     * @notice Used to transfer the token into another token.
     * @dev The destination token MUST NOT be a child token of the token being transferred or one of its downstream
     *  child tokens.
     * @param from Address of the direct owner of the token to be transferred
     * @param to Address of the receiving token's collection smart contract
     * @param tokenId ID of the token being transferred
     * @param destinationId ID of the token to receive the token being transferred
     */
    function nestTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        uint256 destinationId
    ) external;
}
```

ID 绝不能是 `0` 值，因为该提案使用 `0` 值来表示代币/目标不是 NFT。

## 理由

在设计该提案时，我们考虑了以下问题：

1. **如何命名该提案？**\
为了提供尽可能多的信息，我们确定了该提案最重要的方面；父级中心的嵌套控制。子代币的角色仅仅是能够被“可嵌套”，并支持一个代币拥有它。这就是我们最终选择“父级中心”部分作为标题的原因。
2. **为什么自动接受子代币使用 [EIP-712](./eip-712.md) 许可样式签名不属于该提案？**\
为了保持一致性。该提案扩展了 ERC-721，后者已经使用 1 次交易来批准代币的操作。将此与支持资产操作的消息签名结合在一起是不一致的。
3. **为什么使用索引？**\
为了减少 gas 消耗。如果使用代币 ID 来查找要接受或拒绝的代币，则需要对数组进行迭代，操作的成本将取决于活动或待处理子代币数组的大小。使用索引，成本是固定的。每个代币需要维护活动和待处理子代币的列表，因为获取它们的方法是提议接口的一部分。\
为了避免索引变化导致的竞争条件，预期的代币 ID 以及预期代币的集合智能合约包含在需要代币索引的操作中，以验证使用索引访问的代币是否是预期的。\
尝试实现内部使用映射跟踪索引的实现。接受子代币的最低成本增加了超过 20%，铸造的成本增加了超过 15%。我们得出结论认为这对该提案不是必要的，可以作为愿意接受增加交易成本的用例的扩展来实现。在提供的示例实现中，有几个钩子使这成为可能。
4. **为什么待处理子代币数组是有限的，而不是支持分页？**\
待处理子代币数组并不是为了收集父代币的根拥有者想要保留但不够资格提升为活动子代币的代币，而是为了提供一个易于遍历的子代币候选列表，并应定期维护；通过接受或拒绝提议的子代币。待处理子代币数组不需要无限制，因为活动子代币数组是无限制的。\
有限的子代币数组的另一个好处是防止垃圾邮件和恶意行为。由于铸造恶意或垃圾代币可能相对简单且成本低，有限的待处理数组确保其中的所有代币都易于识别，并且在发生垃圾邮件代币洪水时，合法代币不会丢失。\
与此问题相关的一个考虑是如何确保在清除待处理子代币数组时不会意外拒绝合法代币。我们在清除待处理子代币数组调用中添加了最大待处理子代币拒绝参数。这确保只有预期数量的待处理子代币被拒绝，如果在准备此类调用并执行时向待处理子代币数组添加了新代币，则清除该数组应导致交易回滚。
5. **我们是否应该允许代币嵌套到其子代币中？**\
该提案强制要求父代币不能嵌套到其子代币或下游子代币中。父代币及其子代币均由父代币的根拥有者管理。这意味着如果一个代币嵌套到其子代币中，这将创建所有权循环，循环内的代币将无法再被管理。
6. **为什么没有“安全”的嵌套转移方法？**\
`nestTransfer` 始终是“安全的”，因为它必须检查目标的 `IERC6059` 兼容性。
7. **该提案与其他试图解决类似问题的提案有何不同？**\
该接口允许代币既可以发送也可以接收其他代币。提议-接受和父级管理模式允许更安全的使用。向后兼容性仅针对 ERC-721 添加，允许更简单的接口。该提案还允许不同集合之间的互操作，这意味着嵌套并不锁定在单个智能合约中，而可以在完全独立的 NFT 集合之间执行。
### 子代币管理的提议-提交模式

将子代币添加到父代币必须以提议-提交模式进行，以允许第三方有限的可变性。当将子代币添加到父代币时，首先将其放置在*"待处理"*数组中，并且必须由父代币的根所有者迁移到*"活动"*数组中。*"待处理"*子代币数组应限制为 128 个槽，以防止垃圾邮件和恶意行为。

只有根所有者可以接受子代币的限制也引入了提议固有的信任。这确保了代币的根所有者对代币拥有完全的控制权。没有人可以强迫用户接受他们不想要的子代币。

### 父级治理模式

嵌套代币的父 NFT 和父级的根所有者在所有方面都是其真正的所有者。一旦您将代币发送给另一个代币，您就放弃了所有权。

我们继续使用 ERC-721 的`ownerOf`功能，该功能现在将递归查找父级，直到找到一个不是 NFT 的地址，这被称为*根所有者*。此外，我们提供`directOwnerOf`，它使用 3 个值返回代币的最直接所有者：所有者地址，如果直接所有者不是 NFT，则 tokenId 必须为 0，以及一个指示父级是否为 NFT 的标志。

根所有者或被批准的方必须能够对子代币执行以下操作：`acceptChild`、`rejectAllChildren`和`transferChild`。

根所有者或被批准的方还必须仅在代币不被 NFT 拥有时被允许执行以下操作：`transferFrom`、`safeTransferFrom`、`nestTransferFrom`、`burn`。

如果代币由 NFT 拥有，则仅允许父 NFT 本身执行上述操作。转移必须从父代币进行，使用`transferChild`，该方法应调用子代币智能合约中的`nestTransferFrom`或`safeTransferFrom`，具体取决于目标是否为 NFT。对于销毁，代币必须首先转移到 EOA，然后再销毁。

我们添加此限制以防止父合约上的不一致性，因为只有`transferChild`方法在代币被转移出时负责从父代币中移除子代币。

### 子代币管理

该提议引入了一些子代币管理功能。除了从*"待处理"*到*"活动"*子代币数组的权限迁移外，本提议的主要代币管理功能是`transferChild`函数。通过它，可以实现以下子代币的状态转换：

1. 拒绝子代币
2. 放弃子代币
3. 解除嵌套子代币
4. 将子代币转移到 EOA 或`ERC721Receiver`
5. 将子代币转移到新的父代币中

为了更好地理解如何实现这些状态转换，我们必须查看传递给`transferChild`的可用参数：

```solidity
    function transferChild(
        uint256 tokenId,
        address to,
        uint256 destinationId,
        uint256 childIndex,
        address childAddress,
        uint256 childId,
        bool isPending,
        bytes data
    ) external;
```

根据所需的状态转换，这些参数的值必须相应设置（以下示例中未设置的任何参数取决于正在管理的子代币）：

1. **拒绝子代币**\
![拒绝子代币](../assets/eip-6059/img/eip-6059-reject-child.png)
2. **放弃子代币**\
![放弃子代币](../assets/eip-6059/img/eip-6059-abandon-child.png)
3. **解除嵌套子代币**\
![解除嵌套子代币](../assets/eip-6059/img/eip-6059-unnest-child.png)
4. **将子代币转移到 EOA 或`ERC721Receiver`**\
![将子代币转移到 EOA](../assets/eip-6059/img/eip-6059-transfer-child-to-eoa.png)
5. **将子代币转移到新的父代币**\
! [将子代币转移到父代币](../assets/eip-6059/img/eip-6059-transfer-child-to-token.png) \
此状态变化将代币放置在新父代币的待处理数组中。子代币仍需由新父代币的根所有者接受，以便放入该代币的活动数组中。

## 向后兼容性

可嵌套代币标准已与 [ERC-721](./eip-721.md) 兼容，以利用可用于 ERC-721 实现的强大工具，并确保与现有 ERC-721 基础设施的兼容性。

## 测试用例

测试包含在 [`nestable.ts`](../assets/eip-6059/test/nestable.ts) 中。

要在终端中运行它们，您可以使用以下命令：

```
cd ../assets/eip-6059
npm install
npx hardhat test
```

## 参考实现

请参见 [`NestableToken.sol`](../assets/eip-6059/contracts/NestableToken.sol)。

## 安全考虑

与 [ERC-721](./eip-721.md) 相同的安全考虑适用：任何函数中可能存在隐藏逻辑，包括销毁、添加子代币、接受子代币等。

由于当前代币的所有者被允许管理代币，因此在父代币上市出售后，卖方可能会在销售前删除子代币，从而导致买方无法收到预期的子代币。这是该标准设计固有的风险。市场应考虑这一点，并提供一种方法来验证在父代币出售时预期的子代币是否存在，或以其他方式防止这种恶意行为。

处理未审计的合约时请谨慎。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。