---
eip: 6909
title: 最小多代币接口
description: 一种通过其 ID 在单个合约中管理多个代币的最小规范。
author: JT Riley (@jtriley-eth), Dillon (@d1ll0n), Sara (@snreynolds), Vectorized (@Vectorized), Neodaoist (@neodaoist)
discussions-to: https://ethereum-magicians.org/t/eip-6909-multi-token-standard/13891
status: 审核中
type: 标准跟踪
category: ERC
created: 2023-04-19
requires: 165
---

## 摘要

以下内容指定了一种多代币合约，作为 [ERC-1155](./eip-1155.md) 多代币标准的简化替代方案。与 ERC-1155 相比，接口中移除了回调和批处理，权限系统采用混合的操作员-批准方案，以实现细粒度和可扩展的权限。从功能上讲，接口已减少到管理同一合约下多个代币所需的最低限度。

## 动机

ERC-1155 标准包含不必要的功能，例如要求接收账户具有代码以实现返回特定值的回调和规范中的批量调用。此外，单一操作员权限方案在合约中的每个代币 ID 上授予无限制的授权。向后兼容性仅在必要时故意移除。为了最小化所需的外部接口，规范中故意省略了批量调用、增加和减少授权方法以及其他用户体验改进等附加功能。

根据 ERC-1155，合约账户的每次转移和批量转移都需要回调。这在接收账户是合约账户时，可能需要不必要的外部调用。虽然在某些情况下这种行为可能是可取的，但没有选项可以选择退出这种行为，正如 [ERC-721](./eip-721.md) 具有 `transferFrom` 和 `safeTransferFrom` 的情况一样。除了代币合约本身的运行时性能外，这还会影响接收合约账户的运行时性能和代码大小，要求多个回调函数和返回值以接收代币。

批量转移虽然有用，但在本标准中被排除，以允许在不同实现上进行有见地的批量转移操作。例如，不同的 ABI 编码可能在不同环境中提供不同的好处，例如针对具有 calldata 存储承诺的 rollup 的 calldata 大小优化或在 gas 费用昂贵的环境中的运行时性能。

混合的授权-操作员权限方案使代币批准的控制既细粒度又可扩展。授权使外部账户能够代表用户按其 ID 转移单个代币 ID 的代币，而操作员则被授予用户所有代币 ID 的完全转移权限。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 定义

- infinite: uint256 的最大值（`2 ** 256 - 1`）。
- caller: 当前上下文的调用者（`msg.sender`）。
- spender: 代表另一个账户转移代币的账户。
- operator: 对另一个账户的所有代币 ID 拥有无限转移权限的账户。
- mint: 创建一定数量的代币。这可以在铸造方法中发生，或作为从零地址的转移。
- burn: 移除一定数量的代币。这可以在销毁方法中发生，或作为转移到零地址。

### 方法

#### `balanceOf`

一个 `owner` 拥有的代币 `id` 的总 `amount`。

```yaml
- name: balanceOf
  type: function
  stateMutability: view

  inputs:
    - name: owner
      type: address
    - name: id
      type: uint256

  outputs:
    - name: amount
      type: uint256
```

#### `allowance`

一个 spender 被允许代表一个 owner 转移的代币 ID 的总 `amount`。

```yaml
- name: allowance
  type: function
  stateMutability: view

  inputs:
    - name: owner
      type: address
    - name: spender
      type: address
    - name: id
      type: uint256

  outputs:
    - name: amount
      type: uint256
```

#### `isOperator`

如果 `spender` 被批准为 `owner` 的操作员，则返回 `true`。

```yaml
- name: isOperator
  type: function
  stateMutability: view

  inputs:
    - name: owner
      type: address
    - name: spender
      type: address

  outputs:
    - name: status
      type: bool
```

#### `transfer`

将一个代币 `id` 的 `amount` 从调用者转移到 `receiver`。

当调用者的代币 `id` 余额不足时，必须回滚。

必须记录 `Transfer` 事件。

必须返回 True。

```yaml
- name: transfer
  type: function
  stateMutability: nonpayable

  inputs:
    - name: receiver
      type: address
    - name: id
      type: uint256
    - name: amount
      type: uint256

  outputs:
    - name: success
      type: bool
```

#### `transferFrom`

将一个代币 `id` 的 `amount` 从 `sender` 转移到 `receiver`，由调用者执行。

当调用者既不是 `sender` 也不是 `sender` 的操作员，并且调用者对 `sender` 的代币 `id` 的授权不足时，必须回滚。

当 `sender` 的代币 ID 余额不足时，必须回滚。

必须记录 `Transfer` 事件。

如果调用者不是 `sender` 的操作员，并且调用者的 `allowance` 不是无限的，则必须减少调用者的 `allowance`，与 `sender` 的余额减少的 `amount` 相同。

如果 `allowance` 是无限的，则不应减少调用者对 `sender` 的代币 `id` 的 `allowance`。

如果调用者是操作员或 `sender`，则不应减少调用者对 `sender` 的代币 `id` 的 `allowance`。

必须返回 True。

```yaml
- name: transferFrom
  type: function
  stateMutability: nonpayable

  inputs:
    - name: sender
      type: address
    - name: receiver
      type: address
    - name: id
      type: uint256
    - name: amount
      type: uint256

  outputs:
    - name: success
      type: bool
```

#### `approve`

批准一个 `spender` 被允许代表调用者转移的代币 `id` 的 `amount`。

必须将调用者的代币 `id` 的 `spender` 的 `allowance` 设置为 `amount`。

必须记录 `Approval` 事件。

必须返回 True。

```yaml
- name: approve
  type: function
  stateMutability: nonpayable

  inputs:
    - name: spender
      type: address
    - name: id
      type: uint256
    - name: amount
      type: uint256

  outputs:
    - name: success
      type: bool
```

#### `setOperator`

授予或撤销 `spender` 对任何代币 `id` 的无限转移权限，代表调用者。

必须将操作员状态设置为 `approved` 值。

必须记录 `OperatorSet` 事件。

必须返回 True。

```yaml
- name: setOperator
  type: function
  stateMutability: nonpayable

  inputs:
    - name: spender
      type: address
    - name: approved
      type: bool

  outputs:
    - name: success
      type: bool
```

### 事件

#### `Transfer`

`caller` 发起将一个代币 `id` 的 `amount` 从 `sender` 转移到 `receiver`。

当一个代币 `id` 的 `amount` 从一个账户转移到另一个账户时，必须记录。

当一个代币 `id` 的 `amount` 被铸造时，必须记录 `sender` 地址为零地址。

当一个代币 `id` 的 `amount` 被销毁时，必须记录 `receiver` 地址为零地址。

```yaml
- name: Transfer
  type: event

  inputs:
    - name: caller
      indexed: false
      type: address
    - name: sender
      indexed: true
      type: address
    - name: receiver
      indexed: true
      type: address
    - name: id
      indexed: true
      type: uint256
    - name: amount
      indexed: false
      type: uint256
```

#### `OperatorSet`

`owner` 已将 `approved` 状态设置为 `spender`。

当操作员状态被设置时，必须记录。

当操作员状态被设置为与当前调用之前的状态相同时，可以记录。

```yaml
- name: OperatorSet
  type: event

  inputs:
    - name: owner
      indexed: true
      type: address
    - name: spender
      indexed: true
      type: address
    - name: approved
      indexed: false
      type: bool
```

#### `Approval`

`owner` 已批准 `spender` 代表 `owner` 转移一个代币 `id` 的 `amount`。

当 `allowance` 被 `owner` 设置时，必须记录。

```yaml
- name: Approval
  type: event

  inputs:
    - name: owner
      indexed: true
      type: address
    - name: spender
      indexed: true
      type: address
    - name: id
      indexed: true
      type: uint256
    - name: amount
      indexed: false
      type: uint256
```

### 接口 ID

接口 ID 为 `0x0f632fb3`。

### 元数据扩展

#### 方法

##### name

合约的 `name`。

```yaml
- name: name
  type: function
  stateMutability: view

  inputs:
    - name: id
      type: uint256

  outputs:
    - name: name
      type: string
```

##### symbol

合约的股票 `symbol`。

```yaml
- name: symbol
  type: function
  stateMutability: view

  inputs:
    - name: id
      type: uint256

  outputs:
    - name: symbol
      type: string
```

##### decimals

代币 `id` 的小数位数。

```yaml
- name: decimals
  type: function
  stateMutability: view

  inputs:
    - name: id
      type: uint256

  outputs:
  - name: amount
    type: uint8
```

### 内容 URI 扩展

#### 方法

##### contractURI

代币 `id` 的 `URI`。

```yaml
- name: contractURI
  type: function
  stateMutability: view

  inputs: []

  outputs:
    - name: uri
      type: string
```

##### tokenURI

代币 `id` 的 `URI`。

如果代币 `id` 不存在，可能会回滚。

必须用客户端返回的 URI 字符串替换 `{id}` 的出现。

```yaml
- name: tokenURI
  type: function
  stateMutability: view

  inputs:
    - name: id
      type: uint256

  outputs:
    - name: uri
      type: string
```

#### 元数据结构

##### 合约 URI

JSON Schema:

```json
{
  "title": "Contract Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "The name of the contract."
    },
    "description": {
      "type": "string",
      "description": "The description of the contract."
    },
    "image_url": {
      "type": "string",
      "format": "uri",
      "description": "The URL of the image representing the contract."
    },
    "banner_image_url": {
      "type": "string",
      "format": "uri",
      "description": "The URL of the banner image of the contract."
    },
    "external_link": {
      "type": "string",
      "format": "uri",
      "description": "The external link of the contract."
    },
    "editors": {
      "type": "array",
      "items": {
        "type": "string",
        "description": "An Ethereum address representing an authorized editor of the contract."
      },
      "description": "An array of Ethereum addresses representing editors (authorized editors) of the contract."
    },
    "animation_url": {
      "type": "string",
      "description": "An animation URL for the contract."
    }
  },
  "required": ["name"]
}
```
JSON 示例（最小）：

```json
{
  "name": "Example Contract Name",
}
```

##### 代币 URI

必须用客户端替换返回的 URI 字符串中的 `{id}` 出现的地方。

JSON Schema：

```json
{
  "title": "Asset Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "Identifies the token"
    },
    "description": {
      "type": "string",
      "description": "Describes the token"
    },
    "image": {
      "type": "string",
      "description": "A URI pointing to an image resource."
    },
    "animation_url": {
      "type": "string",
      "description": "An animation URL for the token."
    }
  },
  "required": ["name", "description", "image"]
}
```

JSON 示例（最小）：

```json
{
  "name": "Example Token Name",
  "description": "Example Token Description",
  "image": "exampleurl/{id}"
}
```

### 代币供应扩展

#### 方法

##### totalSupply

代币 `id` 的 `totalSupply`。

```yaml
- name: totalSupply
  type: function
  stateMutability: view

  inputs:
    - name: id
      type: uint256

  outputs:
    - name: supply
      type: uint256
```

## 理由

### 细粒度授权

虽然 ERC-1155 标准中的“操作员模型”允许一个账户将另一个账户设置为操作员，赋予其代表所有者转移任何数量的任何代币 id 的完全权限，但这并不总是所需的权限方案。来自 [ERC-20](./eip-20.md) 的“授权模型”允许一个账户设置另一个账户可以代表所有者支出的代币的明确数量。该标准要求两者都必须实现，唯一的修改是“授权模型”，其中代币 id 也必须被指定。这允许一个账户对特定代币 id 授予特定授权、对特定代币 id 授予无限授权，或对所有代币 id 授予无限授权。

### 移除批处理

虽然批处理操作是有用的，但其位置不应在标准本身，而应根据具体情况而定。这允许在 calldata 布局方面进行不同的权衡，这在特定应用程序（如将 calldata 提交到全局存储的 roll-up）中可能特别有用。

### 移除必需的回调

要求回调不必要地给没有特定回调用例或更喜欢定制回调机制的实现者增加了负担。减少此类要求可以节省合约大小、提高 gas 效率和降低复杂性。

### 移除“安全”命名

`safeTransfer` 和 `safeTransferFrom` 的命名约定具有误导性，特别是在 ERC-1155 和 ERC-721 标准的上下文中，因为它们要求对接收账户进行外部调用，并将执行流程传递给任意合约，前提是接收合约返回特定值。移除强制回调和从所有方法名称中移除“安全”一词的组合默认提高了控制流的安全性。

## 向后兼容性

这与 ERC-1155 不向后兼容，因为某些方法被移除。然而，可以为 ERC-20、ERC-721 和 ERC-1155 标准实现包装器。

## 参考实现

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity 0.8.19;

/// @title ERC6909 Multi-Token Reference Implementation
/// @author jtriley.eth
contract ERC6909 {
    /// @dev Thrown when owner balance for id is insufficient.
    /// @param owner The address of the owner.
    /// @param id The id of the token.
    error InsufficientBalance(address owner, uint256 id);

    /// @dev Thrown when spender allowance for id is insufficient.
    /// @param spender The address of the spender.
    /// @param id The id of the token.
    error InsufficientPermission(address spender, uint256 id);

    /// @notice The event emitted when a transfer occurs.
    /// @param sender The address of the sender.
    /// @param receiver The address of the receiver.
    /// @param id The id of the token.
    /// @param amount The amount of the token.
    event Transfer(address caller, address indexed sender, address indexed receiver, uint256 indexed id, uint256 amount);

    /// @notice The event emitted when an operator is set.
    /// @param owner The address of the owner.
    /// @param spender The address of the spender.
    /// @param approved The approval status.
    event OperatorSet(address indexed owner, address indexed spender, bool approved);

    /// @notice The event emitted when an approval occurs.
    /// @param owner The address of the owner.
    /// @param spender The address of the spender.
    /// @param id The id of the token.
    /// @param amount The amount of the token.
    event Approval(address indexed owner, address indexed spender, uint256 indexed id, uint256 amount);

    /// @notice Owner balance of an id.
    mapping(address owner => mapping(uint256 id => uint256 amount)) public balanceOf;

    /// @notice Spender allowance of an id.
    mapping(address owner => mapping(address spender => mapping(uint256 id => uint256 amount))) public allowance;

    /// @notice Checks if a spender is approved by an owner as an operator.
    mapping(address owner => mapping(address spender => bool)) public isOperator;

    /// @notice Transfers an amount of an id from the caller to a receiver.
    /// @param receiver The address of the receiver.
    /// @param id The id of the token.
    /// @param amount The amount of the token.
    function transfer(address receiver, uint256 id, uint256 amount) public returns (bool) {
        if (balanceOf[msg.sender][id] < amount) revert InsufficientBalance(msg.sender, id);
        balanceOf[msg.sender][id] -= amount;
        balanceOf[receiver][id] += amount;
        emit Transfer(msg.sender, msg.sender, receiver, id, amount);
        return true;
    }

    /// @notice Transfers an amount of an id from a sender to a receiver.
    /// @param sender The address of the sender.
    /// @param receiver The address of the receiver.
    /// @param id The id of the token.
    /// @param amount The amount of the token.
    function transferFrom(address sender, address receiver, uint256 id, uint256 amount) public returns (bool) {
        if (sender != msg.sender && !isOperator[sender][msg.sender]) {
            uint256 senderAllowance = allowance[sender][msg.sender][id];
            if (senderAllowance < amount) revert InsufficientPermission(msg.sender, id);
            if (senderAllowance != type(uint256).max) {
                allowance[sender][msg.sender][id] = senderAllowance - amount;
            }
        }
        if (balanceOf[sender][id] < amount) revert InsufficientBalance(sender, id);
        balanceOf[sender][id] -= amount;
        balanceOf[receiver][id] += amount;
        emit Transfer(msg.sender, sender, receiver, id, amount);
        return true;
    }

    /// @notice Approves an amount of an id to a spender.
    /// @param spender The address of the spender.
    /// @param id The id of the token.
    /// @param amount The amount of the token.
    function approve(address spender, uint256 id, uint256 amount) public returns (bool) {
        allowance[msg.sender][spender][id] = amount;
        emit Approval(msg.sender, spender, id, amount);
        return true;
    }


    /// @notice Sets or removes a spender as an operator for the caller.
    /// @param spender The address of the spender.
    /// @param approved The approval status.
    function setOperator(address spender, bool approved) public returns (bool) {
        isOperator[msg.sender][spender] = approved;
        emit OperatorSet(msg.sender, spender, approved);
        return true;
    }

    /// @notice Checks if a contract implements an interface.
    /// @param interfaceId The interface identifier, as specified in ERC-165.
    /// @return supported True if the contract implements `interfaceId`.
    function supportsInterface(bytes4 interfaceId) public pure returns (bool supported) {
        return interfaceId == 0x0f632fb3 || interfaceId == 0x01ffc9a7;
    }

    function _mint(address receiver, uint256 id, uint256 amount) internal {
      // WARNING: important safety checks should precede calls to this method.
      balanceOf[receiver][id] += amount;
      emit Transfer(msg.sender, address(0), receiver, id, amount);
    }

    function _burn(address sender, uint256 id, uint256 amount) internal {
      // WARNING: important safety checks should precede calls to this method.
      balanceOf[sender][id] -= amount;
      emit Transfer(msg.sender, sender, address(0), id, amount);
    }
}
```

## 安全考虑

### 授权和操作员

该规范包括两种代币转移权限系统，“授权”和“操作员”模型。关于委托转移权限，有两个安全考虑。

第一个考虑与所有委托权限模型一致。任何拥有授权的账户可以在任何时间出于任何原因转移全部授权，直到授权被撤销。任何拥有操作员权限的账户可以代表所有者转移任何代币 id 的任何数量，直到操作员权限被撤销。

第二个考虑是独特于同时具有委托权限模型的系统。如果一个账户同时拥有操作员权限和不足的授权进行特定转移，则在操作员检查之前执行授权检查会导致回退，而在授权检查之前执行操作员检查则不会。该规范故意将此保持不受约束，以便实现者可以跟踪授权，尽管操作员状态。尽管如此，这是一个显著的考虑。

```solidity
contract ERC6909OperatorPrecedence {
  // -- snip --

  function transferFrom(address sender, address receiver, uint256 id, uint256 amount) public {
    // check if `isOperator` first
    if (msg.sender != sender && !isOperator[sender][msg.sender]) {
      require(allowance[sender][msg.sender][id] >= amount, "insufficient allowance");
      allowance[sender][msg.sender][id] -= amount;
    }
  
    // -- snip --
  }
}

contract ERC6909AllowancePrecedence {
  // -- snip --

  function transferFrom(address sender, address receiver, uint256 id, uint256 amount) public {
    // check if allowance is sufficient first
    if (msg.sender != sender && allowance[sender][msg.sender][id] < amount) {
      require(isOperator[sender][msg.sender], "insufficient allowance");
    }

    // ERROR: when allowance is insufficient, this panics due to arithmetic underflow, regardless of
    // whether the caller has operator permissions.
    allowance[sender][msg.sender][id] -= amount;

    // -- snip
  }
}
```

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。