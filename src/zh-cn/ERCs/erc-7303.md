---
eip: 7303
title: 代币控制的代币流通
description: 基于代币持有的访问控制方案。
author: Ko Fujimura (@kofujimura)
discussions-to: https://ethereum-magicians.org/t/erc-7303-token-controlled-token-circulation/15020
status: Draft
type: Standards Track
category: ERC
created: 2023-07-09
requires: 721, 1155, 5679
---
## 摘要

本 ERC 引入了一种称为代币控制的代币流通（TCTC）的访问控制方案。通过将与角色相关的特权表示为 [ERC-721](./eip-721.md) 或 [ERC-1155](./eip-1155.md) 代币（称为 `control token`），可以通过铸造或销毁相应的 `control token` 来简化授予或撤销角色的过程。

## 动机

实现特权操作的访问控制有多种方法。常用的模式是 [ERC-5982](./eip-5982.md) 中规定的“基于角色”的访问控制。然而，该方法需要使用离线管理工具通过其界面授予或撤销所需的角色。此外，由于许多钱包缺乏显示角色授予特权的用户界面，用户通常无法通过钱包理解其特权状态。

### 用例

本 ERC 适用于许多使用 [ERC-5982](./eip-5982.md) 描述的基于角色的访问控制的场景。具体用例包括：

**铸造/销毁权限：**
在流通票据、优惠券、会员卡和网站访问权限等代币的应用中，需要为系统管理员提供铸造或销毁这些代币的权限。这些权限可以在此方案中实现为 `control tokens`。

**转移权限：**
在这些应用中的某些情况下，可能希望将代币的转移能力限制为特定机构。在这些情况下，作为 `control token` 发放机构证书。拥有该 `control token` 则提供了调节代币转移的手段。

**地址验证：**
许多应用需要地址验证，以防在铸造或转移目标代币时出现接收者地址错误。作为地址验证的证明，向用户发放 `control token`，在执行铸造或转移交易时，接收者需要提供该证明，从而防止误投。在某些情况下，该地址验证的 `control token` 可能由政府机构或特定公司在身份验证过程后发放。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

1. 实现 [ERC-7303](./eip-7303.md) 标准的智能合约 MUST 将角色所需的特权表示为 ERC-721 代币或 ERC-1155 代币。表示特权的代币在本 ERC 中称为 `control tokens`。`control token` 可以是任何类型的代币，其交易可能会被另一个 `control token` 递归控制。
2. 为了将所需的 `control token` 与角色关联，必须使用先前部署的 `control token` 合约的地址。
3. 为了确定一个账户是否拥有必要的角色，应该确认 `control token` 的余额是否超过 0，利用 ERC-721 或 ERC-1155 中定义的 `balanceOf` 方法。注意，如果使用 ERC-1155 代币进行 `balanceOf` 方法，则必须指定 `typeId`。
4. 要将角色授予账户，应该使用 [ERC-5679](./eip-5679.md) 中定义的 `safeMint` 方法向该账户铸造表示特权的 `control token`。
5. 要从账户撤销角色，应该使用 ERC-5679 中定义的 `burn` 方法销毁表示特权的 `control token`。
6. 合规智能合约中的角色以 `bytes32` 格式表示。建议将此角色的值计算为角色名称字符串的 `keccak256` 哈希，格式为：`bytes32 role = keccak256("<role_name>")`，例如 `bytes32 role = keccak256("MINTER")`。

## 理由

选择使用 ERC-721 或 ERC-1155 代币作为特权的控制代币增强了这些特权在钱包中的可见性，从而简化了用户的特权管理。

通常，在将特权实现为代币时，会使用像灵魂绑定代币（例如 [ERC-5192](./eip-5192.md)）这样的规范。鉴于 ERC-5192 继承自 ERC-721，本 ERC 选择 ERC-721 作为控制代币的要求。

使用可转让的控制代币可以满足需要角色委托的场景。例如，当组织内的某个权威被替换或休假时，将其特权转移给其他成员的能力变得可能。是否将控制代币指定为可转让将取决于应用的具体需求。

## 向后兼容性

本 ERC 旨在与 [ERC-721](./eip-721)、[ERC-1155](./eip-1155) 和 [ERC-5679](./eip-5679) 兼容。

## 参考实现

ERC-7303 提供了一个修饰符，以便在应用中实现 TCTC 访问控制。该修饰符检查账户是否拥有必要的角色。ERC-7303 还包括一个函数，用于将特定角色授予指定账户。

```solidity
// SPDX-License-Identifier: Apache-2.0

pragma solidity ^0.8.9;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";

abstract contract ERC7303 {
    struct ERC721Token {
        address contractId;
    }

    struct ERC1155Token {
        address contractId;
        uint256 typeId;
    }

    mapping (bytes32 => ERC721Token[]) private _ERC721_Contracts;
    mapping (bytes32 => ERC1155Token[]) private _ERC1155_Contracts;

    modifier onlyHasToken(bytes32 role, address account) {
        require(_checkHasToken(role, account), "ERC7303: not has a required token");
        _;
    }

    /**
     * @notice Grant a role to user who owns a control token specified by the ERC-721 contractId. 
     * Multiple calls are allowed, in this case the user must own at least one of the specified token.
     * @param role byte32 The role which you want to grant.
     * @param contractId address The address of contractId of which token the user required to own.
     */
    function _grantRoleByERC721(bytes32 role, address contractId) internal {
        require(
            IERC165(contractId).supportsInterface(type(IERC721).interfaceId),
            "ERC7303: provided contract does not support ERC721 interface"
        );
        _ERC721_Contracts[role].push(ERC721Token(contractId));
    }

    /**
     * @notice Grant a role to user who owns a control token specified by the ERC-1155 contractId. 
     * Multiple calls are allowed, in this case the user must own at least one of the specified token.
     * @param role byte32 The role which you want to grant.
     * @param contractId address The address of contractId of which token the user required to own.
     * @param typeId uint256 The token type id that the user required to own.
     */
    function _grantRoleByERC1155(bytes32 role, address contractId, uint256 typeId) internal {
        require(
            IERC165(contractId).supportsInterface(type(IERC1155).interfaceId),
            "ERC7303: provided contract does not support ERC1155 interface"
        );
        _ERC1155_Contracts[role].push(ERC1155Token(contractId, typeId));
    }

    function _checkHasToken(bytes32 role, address account) internal view returns (bool) {
        ERC721Token[] memory ERC721Tokens = _ERC721_Contracts[role];
        for (uint i = 0; i < ERC721Tokens.length; i++) {
            if (IERC721(ERC721Tokens[i].contractId).balanceOf(account) > 0) return true;
        }

        ERC1155Token[] memory ERC1155Tokens = _ERC1155_Contracts[role];
        for (uint i = 0; i < ERC1155Tokens.length; i++) {
            if (IERC1155(ERC1155Tokens[i].contractId).balanceOf(account, ERC1155Tokens[i].typeId) > 0) return true;
        }

        return false;
    }
}
```

以下是一个在 ERC-721 代币中利用 `ERC7303` 定义“铸造者”和“销毁者”角色的简单示例。拥有这些角色的账户被允许创建新代币和销毁现有代币，通过指定 ERC-721 或 ERC-1155 控制代币来实现：

```solidity
// SPDX-License-Identifier: Apache-2.0

pragma solidity ^0.8.9;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "./ERC7303.sol";

contract MyToken is ERC721, ERC7303 {
    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant BURNER_ROLE = keccak256("BURNER_ROLE");

    constructor() ERC721("MyToken", "MTK") {
        // Specifies the deployed contractId of ERC721 control token.
        _grantRoleByERC721(MINTER_ROLE, 0x...);
        _grantRoleByERC721(BURNER_ROLE, 0x...);

        // Specifies the deployed contractId and typeId of ERC1155 control token.
        _grantRoleByERC1155(MINTER_ROLE, 0x..., ...);
        _grantRoleByERC1155(BURNER_ROLE, 0x..., ...);
    }

    function safeMint(address to, uint256 tokenId)
        public onlyHasToken(MINTER_ROLE, msg.sender)
    {
        _safeMint(to, tokenId);
    }

    function burn(uint256 tokenId) 
        public onlyHasToken(BURNER_ROLE, msg.sender) 
    {
        _burn(tokenId);
    }
}
```

## 安全考虑

受流通代币的安全性在很大程度上依赖于控制代币的安全性。必须仔细考虑与管理权限、铸造/转移/销毁权限以及控制代币的合约更新可能性相关的设置。

特别是，使控制代币可转让允许灵活操作，例如临时委托管理权。然而，这也提高了流通代币的权利可能落入不当第三方手中的可能性。因此，控制代币通常应设为不可转让。如果控制代币要设为可转让，至少应由可信的管理员保留销毁这些代币的权利。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。