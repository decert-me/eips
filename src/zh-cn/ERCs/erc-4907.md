---
eip: 4907
title: 租赁 NFT，EIP-721 的扩展
description: 为 EIP-721 代币添加具有限制权限的时间限制角色。
author: Anders (@0xanders), Lance (@LanceSnow), Shrug <shrug@emojidao.org>
discussions-to: https://ethereum-magicians.org/t/idea-erc-721-user-and-expires-extension/8572
status: Final
type: Standards Track
category: ERC
created: 2022-03-11
requires: 165, 721
---

## 摘要

该标准是 [EIP-721](./eip-721.md) 的扩展。它提出了一个额外的角色（`user`），可以授予地址，以及一个角色自动撤销的时间（`expires`）。`user` 角色表示“使用” NFT 的权限，但不具备转让或设置用户的能力。

## 动机

一些 NFT 具有特定的实用性。例如，虚拟土地可以“使用”来构建场景，代表游戏资产的 NFT 可以在游戏中“使用”。在某些情况下，所有者和用户可能并不总是相同。可能存在一个 NFT 的所有者将其租给“用户”。“用户”可以对 NFT 采取的行动与“所有者”不同（例如，“用户”通常不应能够出售 NFT 的所有权）。在这些情况下，拥有识别地址是“所有者”还是“用户”的单独角色并相应管理执行操作的权限是有意义的。

一些项目已经在不同名称下使用这种设计方案，例如“操作员”或“控制器”，但随着其越来越普遍，我们需要一个统一的标准来促进所有应用程序之间的协作。

此外，这种模型的应用（例如租赁）通常要求用户地址仅具有临时使用 NFT 的权限。通常，这意味着所有者需要提交两个链上交易，一个是在租赁开始时将新地址列为新用户角色，另一个是在结束时收回用户角色。这在劳动和 gas 上都是低效的，因此引入了一个“expires”功能，可以在不需要第二个交易的情况下自动结束使用期限。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

### 合约接口
带有 NatSpec 和 OpenZeppelin v4 接口的 Solidity 接口（也可在 [`IERC4907.sol`](../assets/eip-4907/contracts/IERC4907.sol) 中找到）：

```solidity
interface IERC4907 {

    // Logged when the user of an NFT is changed or expires is changed
    /// @notice Emitted when the `user` of an NFT or the `expires` of the `user` is changed
    /// The zero address for user indicates that there is no user address
    event UpdateUser(uint256 indexed tokenId, address indexed user, uint64 expires);

    /// @notice set the user and expires of an NFT
    /// @dev The zero address indicates there is no user
    /// Throws if `tokenId` is not valid NFT
    /// @param user  The new user of the NFT
    /// @param expires  UNIX timestamp, The new user could use the NFT before expires
    function setUser(uint256 tokenId, address user, uint64 expires) external;

    /// @notice Get the user address of an NFT
    /// @dev The zero address indicates that there is no user or the user is expired
    /// @param tokenId The NFT to get the user address for
    /// @return The user address for this NFT
    function userOf(uint256 tokenId) external view returns(address);

    /// @notice Get the user expires of an NFT
    /// @dev The zero value indicates that there is no user
    /// @param tokenId The NFT to get the user expires for
    /// @return The user expires for this NFT
    function userExpires(uint256 tokenId) external view returns(uint256);
}
```

`userOf(uint256 tokenId)` 函数可以实现为 `pure` 或 `view`。

`userExpires(uint256 tokenId)` 函数可以实现为 `pure` 或 `view`。

`setUser(uint256 tokenId, address user, uint64 expires)` 函数可以实现为 `public` 或 `external`。

当用户地址更改或用户过期更改时，必须触发 `UpdateUser` 事件。

当调用 `supportsInterface` 方法时，必须返回 `true`，并且参数为 `0xad092b5c`。

## 理由

该模型旨在促进简单实现。以下是该标准解决的一些问题：

### 清晰的权利分配

通过双重“所有者”和“用户”角色，管理借贷双方可以做什么和不能做什么（换句话说，他们的权利）变得显著容易。此外，所有者可以控制用户是谁，其他项目也可以轻松地将自己的权利分配给所有者或用户。

### 简单的链上时间管理

一旦租赁期结束，用户角色需要重置，“用户”必须失去使用 NFT 的权利。通常通过第二个链上交易来实现，但这在 gas 上效率低下，并且可能导致不精确的复杂情况。通过 `expires` 功能，不再需要另一个交易，因为“用户”在租赁期结束后会自动失效。

### 便于第三方集成

本着无权限互操作性的精神，该标准使第三方协议更容易管理 NFT 使用权，而无需 NFT 发行者或 NFT 应用的许可。一旦项目采用了额外的 `user` 角色和 `expires`，任何其他项目都可以直接与这些功能交互并实现自己的交易类型。例如，使用该标准的 PFP NFT 可以同时集成到一个租赁平台，用户可以租用 NFT 30 天，同时也可以集成到一个抵押贷款平台，用户可以在最终购买 NFT 所有权的同时使用 NFT。这一切都无需原始 PFP 项目的许可。

## 向后兼容性

如规范部分所述，该标准可以通过添加扩展功能集完全兼容 EIP-721。

此外，本标准中引入的新功能与 EIP-721 中的现有功能有许多相似之处。这使得开发人员能够快速轻松地采用该标准。

## 测试用例

### 测试合约
`ERC4907Demo` 实现：[`ERC4907Demo.sol`](../assets/eip-4907/contracts/ERC4907Demo.sol)

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

import "./ERC4907.sol";

contract ERC4907Demo is ERC4907 {

    constructor(string memory name, string memory symbol)
     ERC4907(name,symbol)
     {         
     }

    function mint(uint256 tokenId, address to) public {
        _mint(to, tokenId);
    }

}
```

### 测试代码
[test.js](../assets/eip-4907/test/test.js)

```JavaScript
const { assert } = require("chai");

const ERC4907Demo = artifacts.require("ERC4907Demo");

contract("test", async accounts => {

    it("should set user to Bob", async () => {
        // Get initial balances of first and second account.
        const Alice = accounts[0];
        const Bob = accounts[1];

        const instance = await ERC4907Demo.deployed("T", "T");
        const demo = instance;

        await demo.mint(1, Alice);
        let expires = Math.floor(new Date().getTime()/1000) + 1000;
        await demo.setUser(1, Bob, BigInt(expires));

        let user_1 = await demo.userOf(1);

        assert.equal(
            user_1,
            Bob,
            "User of NFT 1 should be Bob"
        );

        let owner_1 = await demo.ownerOf(1);
        assert.equal(
            owner_1,
            Alice ,
            "Owner of NFT 1 should be Alice"
        );
    });
});


```

在终端中运行：
```
truffle test ./test/test.js
```

## 参考实现
实现：[`ERC4907.sol`](../assets/eip-4907/contracts/ERC4907.sol)
```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IERC4907.sol";

contract ERC4907 is ERC721, IERC4907 {
    struct UserInfo 
    {
        address user;   // address of user role
        uint64 expires; // unix timestamp, user expires
    }

    mapping (uint256  => UserInfo) internal _users;

    constructor(string memory name_, string memory symbol_)
     ERC721(name_, symbol_)
     {
     }
    
    /// @notice set the user and expires of an NFT
    /// @dev The zero address indicates there is no user
    /// Throws if `tokenId` is not valid NFT
    /// @param user  The new user of the NFT
    /// @param expires  UNIX timestamp, The new user could use the NFT before expires
    function setUser(uint256 tokenId, address user, uint64 expires) public virtual{
        require(_isApprovedOrOwner(msg.sender, tokenId), "ERC4907: transfer caller is not owner nor approved");
        UserInfo storage info =  _users[tokenId];
        info.user = user;
        info.expires = expires;
        emit UpdateUser(tokenId, user, expires);
    }

    /// @notice Get the user address of an NFT
    /// @dev The zero address indicates that there is no user or the user is expired
    /// @param tokenId The NFT to get the user address for
    /// @return The user address for this NFT
    function userOf(uint256 tokenId) public view virtual returns(address){
        if( uint256(_users[tokenId].expires) >=  block.timestamp){
            return  _users[tokenId].user;
        }
        else{
            return address(0);
        }
    }

    /// @notice Get the user expires of an NFT
    /// @dev The zero value indicates that there is no user
    /// @param tokenId The NFT to get the user expires for
    /// @return The user expires for this NFT
    function userExpires(uint256 tokenId) public view virtual returns(uint256){
        return _users[tokenId].expires;
    }

    /// @dev See {IERC165-supportsInterface}.
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC4907).interfaceId || super.supportsInterface(interfaceId);
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override{
        super._beforeTokenTransfer(from, to, tokenId);

        if (from != to && _users[tokenId].user != address(0)) {
            delete _users[tokenId];
            emit UpdateUser(tokenId, address(0), 0);
        }
    }
} 
```

## 安全考虑

该 EIP 标准可以完全保护所有者的权利，所有者可以随时更改 NFT 用户和过期时间。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。