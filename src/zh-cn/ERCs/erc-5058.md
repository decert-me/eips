---
eip: 5058
title: 可锁定非同质化代币
description: 可锁定的 EIP-721 代币
author: Tyler (@radiocaca), Alex (@gojazdev), John (@sfumato00)
discussions-to: https://ethereum-magicians.org/t/eip-5058-erc-721-lockable-standard/9201
status: Stagnant
type: Standards Track
category: ERC
created: 2022-04-30
requires: 20, 165, 721
---

## 摘要

我们提议通过安全的锁定机制扩展 [EIP-721](./eip-721.md) 标准。NFT 所有者通过 `setLockApprovalForAll()` 或 `lockApprove()` 授权操作员锁定 NFT。被授权的操作员通过 `lock()` 锁定 NFT。锁定的 NFT 在锁定期结束之前无法转移。一个直接的用例是允许 NFT 参与智能合约，而不离开其所有者的钱包。

## 动机

由 [EIP-721](./eip-721.md) 启用的 NFT 需求激增。市场总值和生态系统持续增长，越来越多的蓝筹 NFT，约等于传统意义上流行的知识产权。尽管取得了巨大的成功，但仍有一些不足之处。流动性一直是 NFT 面临的最大挑战之一。为了解决流动性问题，已经进行了一些尝试：NFTFi 和 BendDAO 等。利用当前流行的 EIP-721 标准，这些项目要求参与的 NFT 转移到项目的合约中，这给所有者带来了不便和风险：

1. 智能合约风险：由于合约中的漏洞或缺陷，NFT 可能会丢失或被盗。
2. 功能丧失：NFT 具有实用价值，例如头像和炫耀权，当 NFT 不再在所有者的保管下时，这些价值会丧失。
3. 错过空投：所有者无法直接接收与 NFT 相关的空投。考虑到某些空投的价值和价格波动，错过或未能及时获得空投可能会对所有者造成经济影响。

以上所有问题都是糟糕的用户体验，我们相信通过采用原生锁定机制可以改进 EIP-721 标准：

1. NFT 不会转移到智能合约，而是保持在自我保管中但被锁定。
2. 当 NFT 被锁定时，其转移被禁止。其他属性不受影响。
3. 所有者可以自行接收或索取空投。

NFT 的价值可以体现在两个方面：收藏价值和实用价值。收藏价值需要确保持有者的钱包永远保留 NFT 的所有权。实用价值要求确保持有者能够在其他项目中验证其 NFT 所有权。这两个方面都要求 NFT 保持在其所有者的钱包中。

所提议的标准通过扩展 EIP-721 标准，原生支持常见的 NFTFi 用例，包括锁定、质押、借贷和众筹，从而安全便捷地管理底层 NFT 资产。我们相信，所提议的标准将鼓励 NFT 所有者更积极地参与 NFTFi 项目，从而改善整个 NFT 生态系统的生计。

## 规范

本文档中的关键字 “MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL” 应按 RFC 2119 中的描述进行解释。

可锁定的 EIP-721 **MUST** 实现 `IERC5058` 接口：

```solidity
// SPDX-License-Identifier: CC0-1.0

pragma solidity ^0.8.8;

/**
 * @dev EIP-721 Non-Fungible Token Standard, optional lockable extension
 * ERC721 Token that can be locked for a certain period and cannot be transferred.
 * This is designed for a non-escrow staking contract that comes later to lock a user's NFT
 * while still letting them keep it in their wallet.
 * This extension can ensure the security of user tokens during the staking period.
 * If the nft lending protocol is compatible with this extension, the trouble caused by the NFT
 * airdrop can be avoided, because the airdrop is still in the user's wallet
 */
interface IERC5058 {
    /**
     * @dev Emitted when `tokenId` token is locked by `operator` from `from`.
     */
    event Locked(address indexed operator, address indexed from, uint256 indexed tokenId, uint256 expired);

    /**
     * @dev Emitted when `tokenId` token is unlocked by `operator` from `from`.
     */
    event Unlocked(address indexed operator, address indexed from, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to lock the `tokenId` token.
     */
    event LockApproval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to lock all of its tokens.
     */
    event LockApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the locker who is locking the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function lockerOf(uint256 tokenId) external view returns (address locker);

    /**
     * @dev Lock `tokenId` token until the block number is greater than `expired` to be unlocked.
     *
     * Requirements:
     *
     * - `tokenId` token must be owned by `owner`.
     * - `expired` must be greater than block.number
     * - If the caller is not `owner`, it must be approved to lock this token
     * by either {lockApprove} or {setLockApprovalForAll}.
     *
     * Emits a {Locked} event.
     */
    function lock(uint256 tokenId, uint256 expired) external;

    /**
     * @dev Unlock `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` token must be owned by `owner`.
     * - the caller must be the operator who locks the token by {lock}
     *
     * Emits a {Unlocked} event.
     */
    function unlock(uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to lock `tokenId` token.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved lock operator.
     * - `tokenId` must exist.
     *
     * Emits an {LockApproval} event.
     */
    function lockApprove(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an lock operator for the caller.
     * Operators can call {lock} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {LockApprovalForAll} event.
     */
    function setLockApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns the account lock approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getLockApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to lock all of the assets of `owner`.
     *
     * See {setLockApprovalForAll}
     */
    function isLockApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Returns if the `tokenId` token is locked.
     */
    function isLocked(uint256 tokenId) external view returns (bool);

    /**
     * @dev Returns the `tokenId` token lock expired time.
     */
    function lockExpiredTime(uint256 tokenId) external view returns (uint256);
}
```

## 理由

### NFT 锁定批准

NFT 所有者可以通过批准函数授予另一个受信任的操作员锁定其 NFT 的权利。`lockApprove()` 函数仅批准指定的 NFT，而 `setLockApprovalForAll()` 则批准钱包下所有 NFT。当用户参与 NFTFi 项目时，项目合约调用 `lock()` 来锁定用户的 NFT。锁定的 NFT 不能转移，但 NFTFi 项目合约可以使用解锁函数 `unlock()` 来解锁 NFT。

### NFT 锁定/解锁

授权的项目合约有权使用 `lock` 方法锁定 NFT。锁定的 NFT 在锁定时间到期之前无法转移。项目合约也有权通过 `unlock` 函数提前解锁 NFT。请注意，只有被锁定 NFT 的地址有权解锁该 NFT。

### NFT 锁定期

锁定 NFT 时，必须指定锁定到期的区块号，该区块号必须大于当前区块号。当当前区块号超过到期区块号时，NFT 会自动释放并可以转移。

### 绑定 NFT

绑定 NFT 是此 EIP 的扩展，实现在 NFT 锁定期间铸造绑定 NFT 的能力。绑定 NFT 与锁定 NFT 的元数据相同，可以转移。然而，绑定 NFT 仅在 NFT 锁定期间存在，并将在 NFT 解锁后被销毁。绑定 NFT 可用于借贷，作为合约的质押凭证。凭证可以锁定在合约中，也可以锁定给用户。在 NFT 租赁中，绑定 NFT 可以租给用户，因为绑定 NFT 本质上等同于 NFT。如果所有项目都接受这一共识，绑定 NFT 将为 NFT 带来更多创造力。

### 绑定 NFT 工厂

绑定 NFT 工厂是一个通用的绑定 NFT 工厂，类似于 Uniswap 的 [EIP-20](./erc-20.md) 对应工厂。它使用 create2 方法为任何 NFT 创建一个确定性的绑定 NFT 合约地址。已创建的绑定 NFT 合约只能由原始 NFT 合约控制。

## 向后兼容性

该标准与 EIP-721 兼容。

## 测试用例

使用 hardhat 编写的测试用例可以在 [这里](../assets/eip-5058/test/test.ts) 找到。

## 参考实现

您可以在 [assets](../assets/eip-5058/ERC5058.sol) 文件夹中找到该标准的实现。

## 安全考虑

锁定后，NFT 不能转移，因此在授权其他项目合约的锁定权之前，必须确认该项目合约可以解锁 NFT。否则，存在 NFT 被永久锁定的风险。建议在项目中给予合理的锁定期。NFT 可以自动解锁，这在一定程度上可以降低风险。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。