---
eip: 7651
title: 分数表示的非同质化代币
description: 分数表示的非同质化代币的规范。
author: Acme (@0xacme), Calder (@caldereth)
discussions-to: https://ethereum-magicians.org/t/erc-7651-fractionally-represented-non-fungible-token/19176
status: 草案
type: 标准跟踪
category: ERC
created: 2024-03-05
requires: 20, 165, 721
---

## 摘要

本提案引入了一种分数表示的非同质化代币标准，允许在单个合约中以分数方式管理和拥有 NFT。这种方法使 NFT 能够与基础的同质化表示无缝共存，增强流动性和可访问性，而无需分割 NFT 本身或要求明确的转换步骤。该标准包括分数和整体代币转移、批准和事件发射的机制。该规范借鉴了 [ERC-721](./eip-721.md) 和 [ERC-20](./eip-20.md) 的设计，但与这两种标准并不完全兼容。

## 动机

NFT 的分数所有权历史上依赖于外部协议来管理将单个 NFT 划分和重组为分数表示的过程。划分特定 NFT 的方法导致总代币供应的流动性碎片化，因为两个 NFT 的分数表示并不等同，因此必须单独交易。此外，这种方法要求锁定分数化的 NFT，防止在重组之前进行自由转移。

该标准提供了一个统一的分数所有权解决方案，旨在提高 NFT 的流动性和可访问性，而不影响可转让性或灵活性。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“NOT RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 分数表示的非同质化代币接口

所有符合 [ERC-7651](./eip-7651.md) 的合约必须实现 ERC-7651 和 [ERC-165](./eip-165.md) 接口。

合规合约必须在分数表示的代币批准或转移时发出分数批准或转移事件。

合规合约还必须在所有批准、批准和非同质化表示的转移时发出非同质化的 ApprovalForAll、Approval 或 Transfer 事件。

请注意，该接口借鉴了 [ERC-721](./eip-721.md) 和 [ERC-20](./eip-20.md) 标准中类似定义的函数，但与这两种标准并不完全向后兼容。

```solidity
interface IERC7651 is IERC165 {
  /// @dev This emits when fractional representation approval for a given spender
  ///      is changed or reaffirmed.
  event FractionalApproval(address indexed owner, address indexed spender, uint256 value);

  /// @dev This emits when ownership of fractionally represented tokens changes
  ///      by any mechanism. This event emits when tokens are both created and destroyed,
  ///      ie. when from and to are assigned to the zero address respectively.
  event FractionalTransfer(address indexed from, address indexed to, uint256 amount);

  /// @dev This emits when an operator is enabled or disabled for an owner.
  ///      The operator can manage all NFTs of the owner.
  event ApprovalForAll(
    address indexed owner,
    address indexed operator,
    bool approved
  );

  /// @dev This emits when the approved spender is changed or reaffirmed for a given NFT.
  ///      A zero address emitted as spender implies that no addresses are approved for
  ///      this token.
  event NonFungibleApproval(
    address indexed owner,
    address indexed spender,
    uint256 indexed id
  );

  /// @dev This emits when ownership of any NFT changes by any mechanism.
  ///      This event emits when NFTs are both created and destroyed, ie. when
  ///      from and to are assigned to the zero address respectively.
  event NonFungibleTransfer(address indexed from, address indexed to, uint256 indexed id);

  /// @notice Decimal places in fractional representation
  /// @dev Decimals are used as a means of determining when balances or amounts
  ///      contain whole or purely fractional components
  /// @return Number of decimal places used in fractional representation
  function decimals() external view returns (uint8 decimals);

  /// @notice The total supply of a token in fractional representation
  /// @dev The total supply of NFTs may be recovered by computing
  ///      `totalSupply() / 10 ** decimals()`
  /// @return Total supply of the token in fractional representation
  function totalSupply() external view returns (uint256 totalSupply);

  /// @notice Balance of a given address in fractional representation
  /// @dev The total supply of NFTs may be recovered by computing
  ///      `totalSupply() / 10 ** decimals()`
  /// @param owner_ The address that owns the tokens
  /// @return Balance of a given address in fractional representation
  function balanceOf(address owner_) external view returns (uint256 balance);

  /// @notice Query if an address is an authorized operator for another address
  /// @param owner_ The address that owns the NFTs
  /// @param operator_ The address being checked for approval to act on behalf of the owner
  /// @return True if `operator_` is an approved operator for `owner_`, false otherwise
  function isApprovedForAll(
    address owner_,
    address operator_
  ) external view returns (bool isApproved);

  /// @notice Query the allowed amount an address can spend for another address
  /// @param owner_ The address that owns tokens in fractional representation
  /// @param spender_ The address being checked for allowance to spend on behalf of the owner
  /// @return The amount of tokens `spender_` is approved to spend on behalf of `owner_`
  function allowance(
    address owner_,
    address spender_
  ) external view returns (uint256 allowance);

  /// @notice Query the owner of a specific NFT.
  /// @dev Tokens owned by the zero address are considered invalid and should revert on
  ///      ownership query.
  /// @param id_ The unique identifier for an NFT.
  /// @return The address of the token's owner.
  function ownerOf(uint256 id_) external view returns (address owner);

  /// @notice Set approval for an address to spend a fractional amount,
  ///         or to spend a specific NFT.
  /// @dev There must be no overlap between valid ids and fractional values.
  /// @dev Throws unless `msg.sender` is the current NFT owner, or an authorized
  ///      operator of the current owner if an id is provided.
  /// @dev Throws if the id is not a valid NFT
  /// @param spender_ The spender of a given token or value.
  /// @param amountOrId_ A fractional value or id to approve.
  /// @return Whether the approval operation was successful or not.
  function approve(
    address spender_,
    uint256 amountOrId_
  ) external returns (bool success);

  /// @notice Set approval for a third party to manage all of the callers
  ///         non-fungible assets
  /// @param operator_ Address to add to the callers authorized operator set
  /// @param approved_ True if the operator is approved, false if not approved
  function setApprovalForAll(address operator_, bool approved_) external;

  /// @notice Transfer fractional tokens or an NFT from one address to another
  /// @dev There must be no overlap between valid ids and fractional values
  /// @dev The operation should revert if the caller is not `from_` or is not approved
  ///      to spent the tokens or NFT owned by `from_`
  /// @dev The operation should revert if value is less than the balance of `from_` or
  ///      if the NFT is not owned by `from_`
  /// @dev Throws if the id is not a valid NFT
  /// @param from_ The address to transfer fractional tokens or an NFT from
  /// @param to_ The address to transfer fractional tokens or an NFT to
  /// @param amountOrId_ The fractional value or a distinct NFT id to transfer
  /// @return True if the operation was successful
  function transferFrom(
    address from_,
    address to_,
    uint256 amountOrId_
  ) external returns (bool success);

  /// @notice Transfer fractional tokens from one address to another
  /// @dev The operation should revert if amount is less than the balance of `from_`
  /// @param to_ The address to transfer fractional tokens to
  /// @param amount_ The fractional value to transfer
  /// @return True if the operation was successful
  function transfer(address to_, uint256 amount_) external returns (bool success);

  /// @notice Transfers the ownership of an NFT from one address to another address
  /// @dev Throws unless `msg.sender` is the current owner, an authorized
  ///      operator, or the approved address for this NFT
  /// @dev Throws if `from_` is not the current owner
  /// @dev Throws if `to_` is the zero address
  /// @dev Throws if `tokenId_` is not a valid NFT
  /// @dev When transfer is complete, this function checks if `to_` is a
  ///      smart contract (code size > 0). If so, it calls `onERC721Received`
  ///      on `to_` and throws if the return value is not
  ///      `bytes4(keccak256("onERC721Received(address,uint256,bytes)"))`.
  /// @param from_ The address to transfer the NFT from
  /// @param to_ The address to transfer the NFT to
  /// @param tokenId_ The NFT to transfer
  /// @param data_ Additional data with no specified format, sent in call to `to_`
  function safeTransferFrom(
    address from_,
    address to_,
    uint256 id_,
    bytes calldata data_
  ) external;

  /// @notice Transfers the ownership of an NFT from one address to another address
  /// @dev This is identical to the above function safeTransferFrom interface
  ///      though must pass empty bytes as data to `to_`
  /// @param from_ The address to transfer the NFT from
  /// @param to_ The address to transfer the NFT to
  /// @param tokenId_ The NFT to transfer
  function safeTransferFrom(address from_, address to_, uint256 id_) external;
}

interface IERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID_ The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///      uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///         `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID_) external view returns (bool);
}
```

### 分数表示的非同质化代币元数据接口

这是一个推荐的接口，其定义与 [ERC-721](./eip-721.md) 元数据接口相同。这里应使用一个不同的元数据接口，而不是直接使用此接口，以避免与 ERC-721 继承相关的混淆。鉴于此处的函数定义相同，重要的是要注意该规范的元数据接口与 ERC-721 的 ERC-165 `interfaceId`将是相同的。

```solidity
/// @title ERC-7651 Fractional Non-Fungible Token Standard, optional metadata extension
interface IERC7651Metadata {
  /// @notice A descriptive, long-form name for a given token collection
  function name() external view returns (string memory name);

  /// @notice An abbreviated, short-form name for a given token collection
  function symbol() external view returns (string memory symbol);

  /// @notice A distinct Uniform Resource Identifier (URI) for a given asset.
  /// @dev Throws if `tokenId_` is not a valid NFT. URIs are defined in RFC
  ///      3986. The URI may point to a JSON file that conforms to the "ERC721
  ///      Metadata JSON Schema".
  /// @param id_ The NFT to fetch a token URI for
  /// @return The token's URI as a string
  function tokenURI(uint256 id_) external view returns (string memory uri);
}
```

### 分数表示的非同质化代币银行接口

这是一个推荐的接口，旨在供实现 [ERC-7651](./eip-7651.md) 的合约使用，该合约实现 NFT ID 重用。

```solidity
interface IERC7651NFTBanking {
  /// @notice Get the number of NFTs that have been minted but are not currently owned.
  /// @dev This should be the number of unowned NFTs, limited by the total
  ///      fractional supply.
  /// @return The number of NFTs not currently owned.
  function getBankedNFTsLength() external view returns (uint256 bankedNFTsLength);

  /// @notice Get a paginated list of NFTs that have been minted but are not currently owned.
  /// @param start_ Start index in bank.
  /// @param count_ Number of tokens to return from start index, inclusive.
  /// @return An array of banked NFTs from `start_`, of maximum length `count_`.
  function getBankedNFTs(
    uint256 start_,
    uint256 count_
  ) external view returns (uint256[] memory bankedNFTs);

  /// @notice Query the current supply of NFTs in circulation.
  /// @dev Given supply may remain banked or unminted, this function should always be
  ///      inclusively upper-bounded by `totalSupply() / 10 ** decimals()`.
  /// @return The current supply of minted NFTs
  function totalNonFungibleSupply() external view returns (unit256);
}
```

### 分数表示的非同质化代币转移豁免接口

这是一个推荐的接口，旨在供希望允许用户选择不进行 NFT 转移的 [ERC-7651](./eip-7651.md) 实现使用。

```solidity
interface IERC7651NFTTransferExemptable {
  /// @notice Returns whether an address is NFT transfer exempt.
  /// @param account_ The address to check.
  /// @return Whether the address is NFT transfer exempt.
  isNFTTransferExempt(address account_) external view returns (bool);

  /// @notice Allows an address to set themselves as NFT transfer exempt.
  /// @param isExempt_ The flag, true being exempt and false being non-exempt.
  setSelfNFTTransferExempt(bool isExempt_) external;
}
```

## 理由

该标准将分数所有权的表示与非同质化代币模型统一，紧密对齐 [ERC-721](./eip-721.md) 原则，同时启用 [ERC-20](./eip-20.md) 转移的功能。这种双重兼容性旨在减轻现有协议的集成复杂性。我们的目标是隐式支持尽可能高的与 ERC-20 和 ERC-721 标准的向后兼容性，以减少或消除现有协议的集成负担。该分数 NFT 标准的核心理由集中在两个主要策略上：首先，设计与 ERC-721 或 ERC-20 标准明确对齐的接口，以避免歧义；其次，详细说明实现方法，以明确区分重叠功能的逻辑。

### ID 与金额隔离

确保代币 ID 和分数金额之间的清晰区分是该设计的核心。该非重叠设计原则意味着没有输入应被模糊地解释为 ID 和金额。我们不会深入实施指南，但实现可以通过多种方式实现，例如验证 ID 输入的所有权或为代币 ID 保留特定范围。

这种方法确保“重叠”接口中的逻辑同样被隔离，从而最大限度地减少意外结果的可能性。

### 事件

[ERC-20](./eip-20.md)和 [ERC-721](./eip-721.md) 标准之间事件签名的重叠为我们分数 NFT 标准的向后兼容性带来了挑战。我们探索了多种方法，包括与单一标准的事件对齐或引入具有不同参数索引的独特事件以解决冲突。

我们认为，在迈向标准化的过程中，确保事件描述清晰且隔离是理想的解决方案，尽管这会给索引软件带来复杂性。因此，我们遵循传统的转移和批准事件定义，但通过`Fractional`或`NonFungible`前缀来区分这些事件。

### 转移

在标准的 [ERC-7651](./eip-7651.md) 转移中，可以通过指定分数金额或特定 NFT ID 来转移价值。

NFT ID 转移：通过 NFT ID 转移是直接的。指定的 NFT 及其所有相关的分数值（等于 10 \*\* decimals()）将从发送者转移到接收者。

分数金额转移：转移分数金额在管理 NFT 分配时引入了复杂性。主要有三种情况：

1. 整体代币余额没有变化：如果转移不改变任一方的整体余额，则 NFT 分配保持不变。
2. 发送者的整体代币余额减少：如果发送者的整体余额减少到最近的整数以下，则必须从其持有中移除相应数量的 NFT。
3. 接收者的整体代币余额增加：相反，如果接收者的整体余额增加到最近的整数以上，则必须相应增加其 NFT 持有量。

虽然 [ERC-7651](./eip-7651.md) 提供了分数 NFT 的广泛框架，但并未规定处理这些情况的具体方法。常见做法包括单调铸造或销毁代币以反映变化，或在转移分数金额时使用堆栈或队列跟踪 NFT 所有权。

### NFT 转移豁免

转移分数金额意味着可以在单个交易中移动大量 NFT，这可能在 gas 使用上成本高昂。我们建议采用可选的选择机制，以豁免 NFT 转移，EOA 和合约均可使用，以减少在不需要 NFT 表示时转移大量代币时的 gas 负担。
在执行函数调用以选择加入或退出 NFT 转移时，地址持有的 NFT 将进行方向性再平衡，以确保它们与新的豁免状态保持同步。换句话说，当选择退出 NFT 转移时，地址的 NFT 将被存入银行，其 NFT 余额设置为 0。当选择加入 NFT 转移时，将从银行提取足够的 NFT 并转移到地址，以匹配其分数代币余额。

### NFT 银行

正如在转移部分讨论的，当一个地址在分数上新获得一个完整的代币时，他们因此应获得一个 NFT。同样，当一个地址在分数上低于一个完整的代币时，必须从其余额中移除一个 NFT，以保持与其分数余额的同步。

NFT 银行机制提供了一个空间，用于跟踪相对于供应的未拥有但可用的 NFT。我们在这里对实现保持中立，但希望提供一些符合规范的示例。

一种调和银行的方法是通过单调地燃烧和铸造 NFT ID，当它们从流通中提取和重新添加时，分别进行燃烧和铸造。这一策略的铸造部分可能会产生显著的 gas 成本，而这些成本通常无法通过删除燃烧代币 ID 的存储空间所获得的微小 gas 退款来弥补。这种方法还为希望拥有持久、有限 ID 空间的集合引入了不灵活性。

[ERC-7651](./eip-7651.md) 的另一种实现包括一种机制，用于存储和重用 ID，而不是反复燃烧和铸造它们。这节省了显著的 gas 成本，并且具有提供可预测和可外部读取的代币 ID 流的附加好处，这些 ID 可以在队列、栈或其他数据结构中保持以供后续重用。用于此银行机制的具体数据结构并不重要，留给任何遵循该标准的实现自行决定。

### ERC-165 接口

我们在规范中包含 [ERC-165](./eip-165.md) 接口，既是为了遵循 [ERC-721](./eip-721.md) 的设计理念，也是作为在合约层面暴露接口的一种手段。我们认为这是一个有价值的、被接受的标准，以便集成应用能够识别基础规范。

请注意，[ERC-7651](./eip-7651.md) 合约不应通过 `supportsInterface` 声称支持 [ERC-721](./eip-721.md) 或 [ERC-20](./eip-20.md) 标准，因为尽管进行了强大的向后兼容性努力，这些合约无法完全遵循现有规范。

### 元数据

与 [ERC-721](./eip-721.md) 一致，我们决定通过一个单独的接口来隔离复制的元数据功能。该接口包括传统的命名和代币 URI 逻辑，但也引入了围绕代币银行可见性的模式，如上文在 NFT 银行和转移逻辑部分所述。

## 向后兼容性

分数非同质代币标准旨在与现有的 [ERC-721](./eip-721.md) 和 [ERC-20](./eip-20.md) 标准几乎向后兼容，尽管并不声称完全遵循这两者，因此通过一个独特的接口提出。

### 事件

[ERC-721](./eip-721.md) 和 [ERC-20](./eip-20.md) 规范中的事件在批准和转移上共享冲突的签名，这意味着无法实现两者的混合。

这是为数不多的几个故意打破向后兼容性的领域之一，导致一系列新的事件，前缀为 `Fractional` 或 `NonFungible`。我们认为在这里果断转向一个不冲突的、描述性的解决方案是理想的，尽管这需要外部支持以便于索引软件。

### balanceOf

`balanceOf` 函数在 [ERC-20](./eip-20.md) 和 [ERC-721](./eip-721.md) 标准中定义的实际表现各不相同，分别表示分数或完整代币的所有权。鉴于分数非同质代币应遵循基础的分数表示，因此该函数应返回该表示中的余额。然而，这确实意味着分数 NFT 合约无法完全遵循 ERC-721 提供的 `balanceOf` 规范。

### 成功返回值

`transfer` 和 `approve` 函数都返回一个布尔值，指示成功或失败。这对于 [ERC-721](./eip-721.md) 规范来说并不标准，但对于 [ERC-20](./eip-20.md) 来说是标准的。分数非同质代币遵循返回布尔值以满足 ERC-20 标准的最低期望，承认这偏离了理想的向后兼容状态。

## 安全考虑

### 接口误解

本节是关于将 [ERC-7651](./eip-7651.md) 错误识别为 ERC-20 或 ERC-721 的进一步讨论的占位符。即，关于接口误解的潜在安全影响的讨论需要彻底考虑。

需要讨论。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。