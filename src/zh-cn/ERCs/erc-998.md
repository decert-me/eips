---
eip: 998
title: 可组合非同质化代币
description: 扩展 ERC-721 以拥有其他 ERC-721 和 ERC-20 代币。
author: Matt Lockyer <mattdlockyer@gmail.com>, Nick Mudge <nick@perfectabstractions.com>, Jordan Schalm <jordan.schalm@gmail.com>, sebastian echeverry <sebastian.echeverry@robotouniverse.com>, Zainan Victor Zhou (@xinbenlv)
discussions-to: https://ethereum-magicians.org/t/erc-998-composable-non-fungible-tokens-cnfts/387
status: 草案
type: 标准跟踪
category: ERC
created: 2018-07-07
requires: 20, 165, 721
---

## 摘要

对 [ERC-721 标准](./eip-721.md) 的扩展，以使 ERC-721 代币能够拥有其他 ERC-721 代币和 [ERC-20](./eip-20.md) 代币。

对 [ERC-20](./eip-20.md) 和 `ERC-223 https://github.com/ethereum/EIPs/issues/223` 标准的扩展，以使 ERC-20 和 `ERC-223` 代币能够被 ERC-721 代币拥有。

本规范涵盖四种不同类型的可组合代币：

1. [`ERC998ERC721` 自上而下的可组合代币，接收、持有和转移 ERC-721 代币](#erc-721-top-down-composable)
2. [`ERC998ERC20` 自上而下的可组合代币，接收、持有和转移 ERC-20 代币](#erc-20-top-down-composable)
3. [`ERC998ERC721` 自下而上的可组合代币，附加到其他 ERC-721 代币。](#erc-721-bottom-up-composable)
4. [`ERC998ERC20` 自下而上的可组合代币，附加到 ERC-721 代币。](#erc-20-bottom-up-composable)

它们对应于

1. `ERC998ERC721` 自上而下的可组合代币是具有额外功能的 ERC-721 代币，用于拥有其他 ERC-721 代币。
2. `ERC998ERC20` 自上而下的可组合代币是具有额外功能的 ERC-721 代币，用于拥有 ERC-20 代币。
3. `ERC998ERC721` 自下而上的可组合代币是具有额外功能的 ERC-721 代币，用于被 ERC-721 代币拥有。
4. `ERC998ERC20` 自下而上的可组合代币是具有额外功能的 ERC-20 代币，用于被 ERC-721 代币拥有。

自上而下的可组合合约存储并跟踪其每个代币的子代币。

自下而上的可组合合约存储并跟踪其每个代币的父代币。

通过可组合代币，可以构建由所有权连接的 ERC-721 和 ERC-20 代币的列表或树。任何这样的结构在结构的根部将有一个单一的所有者地址，该地址是整个组合的所有者。整个组合可以通过更改根所有者一次性转移。

不同的可组合代币，自上而下和自下而上，各有其优缺点，这在 [理由部分](#rationale) 中进行了说明。一个代币可以是一个或多种类型的可组合代币。

如果非同质化代币实现了以下一个或多个接口，则符合并可组合此 EIP：

* `ERC998ERC721TopDown`
* `ERC998ERC20TopDown`
* `ERC998ERC721BottomUp`
* `ERC998ERC20BottomUp`

## 规范

### ERC-721

`ERC998ERC721` 自上而下、`ERC998ERC20` 自上而下和 `ERC998ERC721` 自下而上的可组合合约必须实现 [ERC-721 接口](./eip-721.md)。

### ERC-20

`ERC998ERC20` 自下而上的可组合合约必须实现 [ERC-20 接口](./eip-20.md)。

### [ERC-165](./eip-165.md)

[ERC-165 标准](./eip-165.md) 必须应用于每个使用的 [ERC-998](./eip-998.md) 接口。

### 认证

验证用户或合约是否可以执行某些操作的方式对于 `ERC998ERC721` 自上而下和 `ERC998ERC721` 自下而上的可组合代币是相同的。

`rootOwner` 指的是可组合代币和 ERC-721 代币树顶端的所有者地址。

在任何可组合代币内的认证是通过找到 rootOwner 并将其与 `msg.sender`、`getApproved(tokenId)` 的返回结果以及 `isApprovedForAll(rootOwner, msg.sender)` 的返回结果进行比较来完成的。如果找到匹配，则认证通过，否则认证失败，合约抛出异常。

以下是认证代码的示例：

```solidity
address rootOwner = address(rootOwnerOf(_tokenId));
require(rootOwner == msg.sender || 
  isApprovedForAll(rootOwner,msg.sender) ||
  getApproved(tokenId) == msg.sender;
```

`approve(address _approved, uint256 _tokenId)` 和 `getApproved(uint256 _tokenId)` ERC-721 函数专门为 rootOwner 实现。这使得可组合代币的树可以转移到新的 rootOwner，而无需担心哪些地址在子可组合代币中已被批准，因为任何先前的批准只能由先前的 rootOwner 使用。

以下是示例实现：

```solidity
function approve(address _approved, uint256 _tokenId) external {
  address rootOwner = address(rootOwnerOf(_tokenId));	
  require(rootOwner == msg.sender || isApprovedForAll(rootOwner,msg.sender));

  rootOwnerAndTokenIdToApprovedAddress[rootOwner][_tokenId] = _approved;
  emit Approval(rootOwner, _approved, _tokenId);
}

function getApproved(uint256 _tokenId) public view returns (address)  {
  address rootOwner = address(rootOwnerOf(_tokenId));
  return rootOwnerAndTokenIdToApprovedAddress[rootOwner][_tokenId];
}
```

### 遍历

可组合代币的 rootOwner 通过调用 `rootOwnerOf(uint256 _tokenId)` 或 `rootOwnerOfChild(address _childContract, uint256 _childTokenId)` 来获取。这些函数由自上而下和自下而上的可组合代币使用，以遍历可组合代币和 ERC-721 代币的树，找到 rootOwner。

`ERC998ERC721` 自上而下和自下而上的可组合代币是相互兼容的。自上而下的可组合代币可以拥有自下而上的可组合代币，或者自上而下的可组合代币可以拥有一个拥有自下而上代币的 ERC-721 代币。在任何配置中，对可组合代币调用 `rootOwnerOf(uint256 _tokenID)` 将返回所有权树顶端的根所有者地址。

正确获取 `rootOwnerOf` 的遍历逻辑非常重要。`rootOwnerOf` 的逻辑无论可组合代币是自下而上、自上而下还是两者都是相同的。
以下是逻辑：

```
Logic for rootOwnerOf(uint256 _tokenId)

If the token is a bottom-up composable and has a parent token then call rootOwnerOf for the parent token.
    If the call was successful then the returned address is the rootOwner.
    Otherwise call rootOwnerOfChild for the parent token.
        If the call was successful then the returned address is the rootOwner.
        Otherwise get the owner address of the token and that is the rootOwner.
Otherwise call rootOwnerOfChild for the token
    If the call was successful then the returned address is the rootOwner.
    Otherwise get the owner address of the token and that is the rootOwner.
```

对一个代币调用 `rootOwnerOfChild` 意味着以下逻辑：

```solidity
// Logic for calling rootOwnerOfChild for a tokenId
address tokenOwner = ownerOf(tokenId);
address childContract = address(this);
bytes32 rootOwner = ERC998ERC721(tokenOwner).rootOwnerOfChild(childContract, tokenId);
```

但要理解，实际调用 `rootOwnerOfChild` 应该使用汇编，以便代码可以检查调用是否失败，并且使用 `staticcall` 操作码以确保不修改任何状态。

实现上述认证和遍历功能的代币/合约被称为“可组合感知”。

### 可组合转移函数参数格式

进行转移的可组合函数遵循相同的参数格式：**from:to:what**。

例如，`getChild(address _from, uint256 _tokenId, address _childContract, uint256 _childTokenId)` 可组合函数将 ERC-721 代币从一个地址转移到一个自上而下的可组合代币。`_from` 参数是 **from**，`_tokenId` 参数是 **to**，而 `address _childContract, uint256 _childTokenId` 参数是 **what**。

另一个示例是 `safeTransferChild(uint256 _fromTokenId, address _to, address _childContract, uint256 _childTokenId)` 函数。`_fromTokenId` 是 **from**，`_to` 是 **to**，而 `address _childContract, address _childTokenId` 参数是 **what**。

### transferFrom/safeTransferFrom 函数不转移由代币拥有的代币

在自下而上和自上而下的可组合合约中，如果直接调用 `transferFrom` 和 `safeTransferFrom` 函数以转移由另一个代币拥有的代币，则必须抛出异常。

原因在于这些函数没有明确指定哪个代币拥有要转移的代币。[有关此更多信息，请参见理由部分。](#explicit-transfer-parameters)

`transferFrom/safeTransferFrom` 函数必须用于转移由地址拥有的代币。

### ERC-721 自上而下可组合

ERC-721 自上而下的可组合代币充当 ERC-721 代币的容器。

ERC-721 自上而下的可组合代币是可以接收、持有和转移 ERC-721 代币的 ERC-721 代币。
有两种方法可以将 ERC-721 代币转移到自上而下的可组合合约：

1. 使用 `function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data)` 函数。`_to` 参数是自上而下的可组合合约地址。`bytes data` 参数包含要转移的 ERC-721 代币的自上而下可组合 tokenId 的整数值。
2. 在 ERC-721 代币合约中调用 `approve` 以授权自上而下的可组合合约。然后在可组合合约中调用 `getChild`。

第一种方法适用于具有 `safeTransferFrom` 函数的 ERC-721 合约。第二种方法适用于没有此函数的合约，例如 CryptoKitties。

以下是将 ERC-721 代币 3 从一个地址转移到自上而下的可组合代币 6 的示例：

```solidity
uint256 tokenId = 6;
bytes memory tokenIdBytes = new bytes(32);
assembly { mstore(add(tokenIdBytes, 32), tokenId) }
ERC721(contractAddress).safeTransferFrom(userAddress, composableAddress, 3, tokenIdBytes);
```

每个符合 ERC-721 自上而下可组合标准的合约必须实现 `ERC998ERC721TopDown` 接口。

`ERC998ERC721TopDownEnumerable` 和 `ERC998ERC20TopDownEnumerable` 接口是可选的。

```solidity
pragma solidity ^0.4.24;

/// @title `ERC998ERC721` Top-Down Composable Non-Fungible Token
/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-998.md
///  Note: the ERC-165 identifier for this interface is 0xcde244d9
interface ERC998ERC721TopDown {

  /// @dev This emits when a token receives a child token.
  /// @param _from The prior owner of the token.
  /// @param _toTokenId The token that receives the child token.
  event ReceivedChild(
    address indexed _from, 
    uint256 indexed _toTokenId, 
    address indexed _childContract, 
    uint256 _childTokenId
  );
  
  /// @dev This emits when a child token is transferred from a token to an address.
  /// @param _fromTokenId The parent token that the child token is being transferred from.
  /// @param _to The new owner address of the child token.
  event TransferChild(
    uint256 indexed _fromTokenId, 
    address indexed _to, 
    address indexed _childContract, 
    uint256 _childTokenId
  );

  /// @notice Get the root owner of tokenId.
  /// @param _tokenId The token to query for a root owner address
  /// @return rootOwner The root owner at the top of tree of tokens and ERC-998 magic value.
  function rootOwnerOf(uint256 _tokenId) public view returns (bytes32 rootOwner);
  
  /// @notice Get the root owner of a child token.
  /// @param _childContract The contract address of the child token.
  /// @param _childTokenId The tokenId of the child.
  /// @return rootOwner The root owner at the top of tree of tokens and ERC-998 magic value.
  function rootOwnerOfChild(
    address _childContract, 
    uint256 _childTokenId
  ) 
    public 
    view
    returns (bytes32 rootOwner);
  
  /// @notice Get the parent tokenId of a child token.
  /// @param _childContract The contract address of the child token.
  /// @param _childTokenId The tokenId of the child.
  /// @return parentTokenOwner The parent address of the parent token and ERC-998 magic value
  /// @return parentTokenId The parent tokenId of _tokenId
  function ownerOfChild(
    address _childContract, 
    uint256 _childTokenId
  ) 
    external 
    view 
    returns (
      bytes32 parentTokenOwner, 
      uint256 parentTokenId
    );
  
  /// @notice A token receives a child token
  /// @param _operator The address that caused the transfer.
  /// @param _from The owner of the child token.
  /// @param _childTokenId The token that is being transferred to the parent.
  /// @param _data Up to the first 32 bytes contains an integer which is the receiving parent tokenId.  
  function onERC721Received(
    address _operator, 
    address _from, 
    uint256 _childTokenId, 
    bytes _data
  ) 
    external 
    returns(bytes4);
    
  /// @notice Transfer child token from top-down composable to address.
  /// @param _fromTokenId The owning token to transfer from.
  /// @param _to The address that receives the child token
  /// @param _childContract The ERC-721 contract of the child token.
  /// @param _childTokenId The tokenId of the token that is being transferred.
  function transferChild(
    uint256 _fromTokenId,
    address _to, 
    address _childContract, 
    uint256 _childTokenId
  ) 
    external;
  
  /// @notice Transfer child token from top-down composable to address.
  /// @param _fromTokenId The owning token to transfer from.
  /// @param _to The address that receives the child token
  /// @param _childContract The ERC-721 contract of the child token.
  /// @param _childTokenId The tokenId of the token that is being transferred.
  function safeTransferChild(
    uint256 _fromTokenId,
    address _to, 
    address _childContract, 
    uint256 _childTokenId
  ) 
    external;
  
  /// @notice Transfer child token from top-down composable to address.
  /// @param _fromTokenId The owning token to transfer from.
  /// @param _to The address that receives the child token
  /// @param _childContract The ERC-721 contract of the child token.
  /// @param _childTokenId The tokenId of the token that is being transferred.
  /// @param _data Additional data with no specified format
  function safeTransferChild(
    uint256 _fromTokenId,
    address _to, 
    address _childContract, 
    uint256 _childTokenId, 
    bytes _data
  ) 
    external;
  
  /// @notice Transfer bottom-up composable child token from top-down composable to other ERC-721 token.
  /// @param _fromTokenId The owning token to transfer from.
  /// @param _toContract The ERC-721 contract of the receiving token
  /// @param _toTokenId The receiving token  
  /// @param _childContract The bottom-up composable contract of the child token.
  /// @param _childTokenId The token that is being transferred.
  /// @param _data Additional data with no specified format
  function transferChildToParent(
    uint256 _fromTokenId, 
    address _toContract, 
    uint256 _toTokenId, 
    address _childContract, 
    uint256 _childTokenId, 
    bytes _data
  ) 
    external;
  
  /// @notice Get a child token from an ERC-721 contract.
  /// @param _from The address that owns the child token.
  /// @param _tokenId The token that becomes the parent owner
  /// @param _childContract The ERC-721 contract of the child token
  /// @param _childTokenId The tokenId of the child token
  function getChild(
    address _from, 
    uint256 _tokenId, 
    address _childContract, 
    uint256 _childTokenId
  ) 
    external;
}
```

#### `rootOwnerOf` 1

```solidity
/// @notice Get the root owner of tokenId.
/// @param _tokenId The token to query for a root owner address
/// @return rootOwner The root owner at the top of tree of tokens and ERC-998 magic value.
function rootOwnerOf(uint256 _tokenId) public view returns (bytes32 rootOwner);
```

此函数遍历代币所有者，直到找到 `_tokenId` 的根所有者地址。

根所有者的前 4 个字节包含 ERC-998 魔法值 `0xcd740db5`。最后 20 个字节包含根所有者地址。

返回魔法值是因为此函数可能在不确定合约是否具有 `rootOwnerOf` 函数时被调用。魔法值用于此类调用，以确保接收到有效的返回值。

如果不确定合约是否具有 `rootOwnerOf` 函数，则必须将 `rootOwner` 返回值的前四个字节与 `0xcd740db5` 进行比较。

`0xcd740db5` 等于：

```solidity
this.rootOwnerOf.selector ^ this.rootOwnerOfChild.selector ^ 
this.tokenOwnerOf.selector ^ this.ownerOfChild.selector;
```

以下是 `rootOwnerOf` 返回的值的示例。
`0xcd740db50000000000000000e5240103e1ff986a2c8ae6b6728ffe0d9a395c59`

#### rootOwnerOfChild

```solidity
/// @notice Get the root owner of a child token.
/// @param _childContract The contract address of the child token.
/// @param _childTokenId The tokenId of the child.
/// @return rootOwner The root owner at the top of tree of tokens and ERC-998 magic value.
function rootOwnerOfChild(
  address _childContract, 
  uint256 _childTokenId
) 
  public 
  view 
  returns (bytes32 rootOwner);
```

此函数遍历代币所有者，直到找到提供的子代币的根所有者地址。

根所有者的前 4 个字节包含 ERC-998 魔法值 `0xcd740db5`。最后 20 个字节包含根所有者地址。

返回魔法值是因为此函数可能在不确定合约是否具有 `rootOwnerOf` 函数时被调用。魔法值用于此类调用，以确保接收到有效的返回值。

如果不确定合约是否具有 `rootOwnerOfChild` 函数，则必须将 `rootOwner` 返回值的前四个字节与 `0xcd740db5` 进行比较。

#### ownerOfChild

```solidity
/// @notice Get the parent tokenId of a child token.
/// @param _childContract The contract address of the child token.
/// @param _childTokenId The tokenId of the child.
/// @return parentTokenOwner The parent address of the parent token and ERC-998 magic value
/// @return parentTokenId The parent tokenId of _tokenId
function ownerOfChild(
  address _childContract, 
  uint256 _childTokenId
) 
  external 
  view 
  returns (
    address parentTokenOwner, 
    uint256 parentTokenId
  );
```

此函数用于获取子代币的父代币 ID 并获取父代币的所有者地址。

父代币所有者的前 4 个字节包含 ERC-998 魔法值 `0xcd740db5`。最后 20 个字节包含父代币所有者地址。

返回魔法值是因为此函数可能在不确定合约是否具有 `ownerOfChild` 函数时被调用。魔法值用于此类调用，以确保接收到有效的返回值。

如果不确定合约是否具有 `ownerOfChild` 函数，则必须将 `parentTokenOwner` 返回值的前四个字节与 `0xcd740db5` 进行比较。

#### `onERC721Received`

```solidity
/// @notice A token receives a child token
/// @param _operator The address that caused the transfer.
/// @param _from The prior owner of the child token.
/// @param _childTokenId The token that is being transferred to the parent.
/// @param _data Up to the first 32 bytes contains an integer which is the receiving parent tokenId.  
function onERC721Received(
  address _operator, 
  address _from, 
  uint256 _childTokenId, 
  bytes _data
) 
  external 
  returns(bytes4);
```

这是 ERC-721 标准中定义的一个函数。当调用 `safeTransferFrom` 时，此函数在 ERC-721 合约中被调用。`bytes _data` 参数包含一个从 1 到 32 字节长的整数值，即 ERC-721 代币转移到的父代币 ID。

`onERC721Received` 函数是自上而下的可组合合约被通知 ERC-721 代币已转移到它以及在自上而下的可组合合约中哪个 tokenId 是父代币 ID 的方式。

`onERC721Received` 的返回值是魔法值 `0x150b7a02`，其等于 `bytes4(keccak256(abi.encodePacked("onERC721Received(address,address,uint256,bytes)")))`。

#### transferChild

```solidity
/// @notice Transfer child token from top-down composable to address.
/// @param _fromTokenId The owning token to transfer from.
/// @param _to The address that receives the child token
/// @param _childContract The ERC-721 contract of the child token.
/// @param _childTokenId The tokenId of the token that is being transferred.
function transferChild(
  uint256 _fromTokenId,
  address _to, 
  address _childContract, 
  uint256 _childTokenId
) 
  external;
```

此函数验证 `msg.sender` 并将子代币从自上而下的可组合合约转移到另一个地址。

此函数在内部调用：

```solidity
ERC721(_childContract).transferFrom(this, _to, _childTokenId);
```

#### safeTransferChild 1

```solidity
/// @notice Transfer child token from top-down composable to address.
/// @param _fromTokenId The owning token to transfer from.
/// @param _to The address that receives the child token
/// @param _childContract The ERC-721 contract of the child token.
/// @param _childTokenId The tokenId of the token that is being transferred.
function safeTransferChild(
  uint256 _fromTokenId,
  address _to, 
  address _childContract, 
  uint256 _childTokenId
) 
  external;
```

此函数验证 `msg.sender` 并将子代币从自上而下的可组合合约转移到另一个地址。

此函数在内部调用：

```solidity
ERC721(_childContract).safeTransferFrom(this, _to, _childTokenId);
```

#### safeTransferChild 2

```solidity
/// @notice Transfer child token from top-down composable to address or other top-down composable.
/// @param _fromTokenId The owning token to transfer from.
/// @param _to The address that receives the child token
/// @param _childContract The ERC721 contract of the child token.
/// @param _childTokenId The tokenId of the token that is being transferred.
/// @param _data Additional data with no specified format, can be used to specify tokenId to transfer to
function safeTransferChild(
  uint256 _fromTokenId,
  address _to, 
  address _childContract, 
  uint256 _childTokenId, 
  bytes _data
) 
  external;
```

此函数验证 `msg.sender` 并将子代币从自上而下的可组合合约转移到另一个地址或另一个自上而下的可组合合约。

如果 `_to` 地址是自上而下的可组合合约，并且 `bytes _data` 提供了一个表示父代币 ID 的整数，则子代币将被转移到另一个自上而下的可组合合约。

此函数在内部调用：

```solidity
ERC721(_childContract).safeTransferFrom(this, _to, _childTokenId, _data);
```

#### transferChildToParent

```solidity
/// @notice Transfer bottom-up composable child token from top-down composable to other ERC-721 token.
/// @param _fromTokenId The owning token to transfer from.
/// @param _toContract The ERC-721 contract of the receiving token
/// @param _toToken The receiving token  
/// @param _childContract The bottom-up composable contract of the child token.
/// @param _childTokenId The token that is being transferred.
/// @param _data Additional data with no specified format
function transferChildToParent(
  uint256 _fromTokenId, 
  address _toContract, 
  uint256 _toTokenId, 
  address _childContract, 
  uint256 _childTokenId, 
  bytes _data
) 
  external
```

此函数验证 `msg.sender` 并将子代币从自上而下的可组合合约转移到另一个 ERC-721 代币。此函数只能在子代币是自下而上的可组合代币时使用。它旨在在一个交易中将自下而上的可组合代币从自上而下的可组合合约转移到 ERC-721 代币（自下而上样式）。

此函数在内部调用：

```solidity
ERC998ERC721BottomUp(_childContract).transferToParent(
  address(this), 
  _toContract, 
  _toTokenId, 
  _childTokenId, 
  _data
);
```

#### getChild 

```solidity
/// @notice Get a child token from an ERC-721 contract.
/// @param _from The address that owns the child token.
/// @param _tokenId The token that becomes the parent owner
/// @param _childContract The ERC-721 contract of the child token
/// @param _childTokenId The tokenId of the child token
function getChild(
  address _from, 
  uint256 _tokenId, 
  address _childContract, 
  uint256 _childTokenId
) 
  external;
```

此函数用于在其合约没有 `safeTransferChild(uint256 _fromTokenId, address _to, address _childContract, uint256 _childTokenId, bytes _data)` 函数时转移 ERC-721 代币。

使用此函数的转移分为两个步骤：

1. ERC-721 代币的所有者在 ERC-721 合约中调用 `approve` 或 `setApprovalForAll` 以授权自上而下的可组合合约。
2. ERC-721 代币的所有者在自上而下的可组合合约中调用 `getChild` 以获取 ERC-721 代币。

`getChild` 函数必须验证 `msg.sender` 是 ERC-721 合约中 ERC-721 代币的所有者，或者是 ERC-721 合约中 ERC-721 代币的批准者或操作员。

#### ERC-721 自上而下可组合枚举

自上而下可组合枚举的可选接口：

```solidity
///  @dev The ERC-165 identifier for this interface is 0xa344afe4
interface ERC998ERC721TopDownEnumerable {

  /// @notice Get the total number of child contracts with tokens that are owned by tokenId.
  /// @param _tokenId The parent token of child tokens in child contracts
  /// @return uint256 The total number of child contracts with tokens owned by tokenId.
  function totalChildContracts(uint256 _tokenId) external view returns(uint256);
  
  /// @notice Get child contract by tokenId and index
  /// @param _tokenId The parent token of child tokens in child contract
  /// @param _index The index position of the child contract
  /// @return childContract The contract found at the tokenId and index.
  function childContractByIndex(
    uint256 _tokenId, 
    uint256 _index
  ) 
    external 
    view 
    returns (address childContract);
  
  /// @notice Get the total number of child tokens owned by tokenId that exist in a child contract.
  /// @param _tokenId The parent token of child tokens
  /// @param _childContract The child contract containing the child tokens
  /// @return uint256 The total number of child tokens found in child contract that are owned by tokenId.
  function totalChildTokens(
    uint256 _tokenId, 
    address _childContract
  ) 
    external 
    view 
    returns(uint256);
  
  /// @notice Get child token owned by tokenId, in child contract, at index position
  /// @param _tokenId The parent token of the child token
  /// @param _childContract The child contract of the child token
  /// @param _index The index position of the child token.
  /// @return childTokenId The child tokenId for the parent token, child token and index
  function childTokenByIndex(
    uint256 _tokenId, 
    address _childContract, 
    uint256 _index
  )
    external 
    view 
    returns (uint256 childTokenId);
}
```

### ERC-20 自上而下可组合

ERC-20 自上而下可组合充当 ERC-20 代币的容器。

ERC-20 自上而下可组合是可以接收、持有和转移 ERC-20 代币的 ERC-721 代币。

有两种方法可以将 ERC-20 代币转移到 ERC-20 自上而下可组合：

1. 使用 `transfer(address _to, uint256 _value, bytes _data);` 函数从 `ERC-223` 合约中。`_to` 参数是 ERC-20 自上而下可组合合约地址。`_value` 参数是要转移的 ERC-20 代币数量。`bytes` 参数包含接收 ERC-20 代币的自上而下可组合 tokenId 的整数值。
2. 在 ERC-20 合约中调用 `approve` 以授权 ERC-20 自上而下可组合合约。然后从 ERC-20 自上而下可组合合约调用 `getERC20(address _from, uint256 _tokenId, address _erc20Contract, uint256 _value)`。

第一种方法适用于支持 `ERC-223` 标准的 ERC-20 合约。第二种方法适用于不支持的合约。
ERC-20 自上而下的可组合接口实现如下：

```solidity
/// @title `ERC998ERC20` Top-Down Composable Non-Fungible Token
/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-998.md
///  Note: the ERC-165 identifier for this interface is 0x7294ffed
interface ERC998ERC20TopDown {

  /// @dev This emits when a token receives ERC-20 tokens.
  /// @param _from The prior owner of the token.
  /// @param _toTokenId The token that receives the ERC-20 tokens.
  /// @param _erc20Contract The ERC-20 contract.
  /// @param _value The number of ERC-20 tokens received.
  event ReceivedERC20(
    address indexed _from, 
    uint256 indexed _toTokenId, 
    address indexed _erc20Contract, 
    uint256 _value
  );
  
  /// @dev This emits when a token transfers ERC-20 tokens.
  /// @param _tokenId The token that owned the ERC-20 tokens.
  /// @param _to The address that receives the ERC-20 tokens.
  /// @param _erc20Contract The ERC-20 contract.
  /// @param _value The number of ERC-20 tokens transferred.
  event TransferERC20(
    uint256 indexed _fromTokenId, 
    address indexed _to, 
    address indexed _erc20Contract, 
    uint256 _value
  );

  /// @notice A token receives ERC-20 tokens
  /// @param _from The prior owner of the ERC-20 tokens
  /// @param _value The number of ERC-20 tokens received
  /// @param _data Up to the first 32 bytes contains an integer which is the receiving tokenId.  
  function tokenFallback(address _from, uint256 _value, bytes _data) external;
  
  /// @notice Look up the balance of ERC-20 tokens for a specific token and ERC-20 contract
  /// @param _tokenId The token that owns the ERC-20 tokens
  /// @param _erc20Contract The ERC-20 contract
  /// @return The number of ERC-20 tokens owned by a token from an ERC-20 contract
  function balanceOfERC20(
    uint256 _tokenId, 
    address _erc20Contract
  ) 
    external 
    view 
    returns(uint256);
  
  /// @notice Transfer ERC-20 tokens to address
  /// @param _tokenId The token to transfer from
  /// @param _value The address to send the ERC-20 tokens to
  /// @param _erc20Contract The ERC-20 contract
  /// @param _value The number of ERC-20 tokens to transfer
  function transferERC20(
    uint256 _tokenId, 
    address _to, 
    address _erc20Contract, 
    uint256 _value
  ) 
    external;
  
  /// @notice Transfer ERC-20 tokens to address or ERC-20 top-down composable
  /// @param _tokenId The token to transfer from
  /// @param _value The address to send the ERC-20 tokens to
  /// @param _erc223Contract The `ERC-223` token contract
  /// @param _value The number of ERC-20 tokens to transfer
  /// @param _data Additional data with no specified format, can be used to specify tokenId to transfer to
  function transferERC223(
    uint256 _tokenId, 
    address _to, 
    address _erc223Contract, 
    uint256 _value, 
    bytes _data
  )
    external;
  
  /// @notice Get ERC-20 tokens from ERC-20 contract.
  /// @param _from The current owner address of the ERC-20 tokens that are being transferred.
  /// @param _tokenId The token to transfer the ERC-20 tokens to.
  /// @param _erc20Contract The ERC-20 token contract
  /// @param _value The number of ERC-20 tokens to transfer  
  function getERC20(
    address _from, 
    uint256 _tokenId, 
    address _erc20Contract, 
    uint256 _value
  ) 
    external;
}
```

#### tokenFallback

```solidity
/// @notice A token receives ERC-20 tokens
/// @param _from The prior owner of the ERC-20 tokens
/// @param _value The number of ERC-20 tokens received
/// @param _data Up to the first 32 bytes contains an integer which is the receiving tokenId.  
function tokenFallback(address _from, uint256 _value, bytes _data) external;  
```

此函数来自 `ERC-223`，它是 ERC-20 标准的扩展。当 ERC-20 代币被转移时，此函数会从发送合约被调用到接收合约。此函数是 ERC-20 自上而下可组合合约被通知其代币接收到 ERC-20 代币的方式。接收到 ERC-20 代币的代币在 `_data` 参数中指定。

#### `balanceOfERC20`

```solidity
/// @notice Look up the balance of ERC-20 tokens for a specific token and ERC-20 contract
/// @param _tokenId The token that owns the ERC-20 tokens
/// @param _erc20Contract The ERC-20 contract
/// @return The number of ERC-20 tokens owned by a token from an ERC-20 contract
function balanceOfERC20(
  uint256 _tokenId, 
  address _erc20Contract
) 
  external 
  view 
  returns(uint256);
```

获取特定 ERC-20 合约中某个代币拥有的 ERC-20 代币余额。

#### `transferERC20`

```solidity
/// @notice Transfer ERC-20 tokens to address
/// @param _tokenId The token to transfer from
/// @param _value The address to send the ERC-20 tokens to
/// @param _erc20Contract The ERC-20 contract
/// @param _value The number of ERC-20 tokens to transfer
function transferERC20(
  uint256 _tokenId, 
  address _to, 
  address _erc20Contract, 
  uint256 _value
)
  external;
```

此函数用于将 ERC-20 代币从一个代币转移到一个地址。此函数调用 `ERC20(_erc20Contract).transfer(_to, _value)`。

此函数必须验证 `msg.sender`。

#### `transferERC223`

```solidity
  /// @notice Transfer ERC-20 tokens to address or ERC-20 top-down composable
  /// @param _tokenId The token to transfer from
  /// @param _value The address to send the ERC-20 tokens to
  /// @param _erc223Contract The `ERC-223` token contract
  /// @param _value The number of ERC-20 tokens to transfer
  /// @param _data Additional data with no specified format, can be used to specify tokenId to transfer to
  function transferERC223(
    uint256 _tokenId, 
    address _to, 
    address _erc223Contract, 
    uint256 _value, 
    bytes _data
  )
    external;
```

此函数来自 `ERC-223`。它用于将 ERC-20 代币从一个代币转移到一个地址或另一个代币，通过在 `_data` 参数中放入一个整数代币值。

此函数必须验证 `msg.sender`。

#### `getERC20`

```solidity
/// @notice Get ERC-20 tokens from ERC-20 contract.
/// @param _from The current owner address of the ERC-20 tokens that are being transferred.
/// @param _tokenId The token to transfer the ERC-20 tokens to.
/// @param _erc20Contract The ERC-20 token contract
/// @param _value The number of ERC-20 tokens to transfer  
function getERC20(
  address _from, 
  uint256 _tokenId, 
  address _erc20Contract, 
  uint256 _value
) 
  external;
```

此函数用于将 ERC-20 代币转移到 ERC-20 自上而下可组合合约，当 ERC-20 合约没有 `transferERC223(uint256 _tokenId, address _to, address _erc223Contract, uint256 _value, bytes _data)` 函数时。

在使用此函数之前，必须在 ERC-20 合约中批准 ERC-20 自上而下可组合合约地址以转移 ERC-20 代币。

此函数必须验证 `msg.sender` 等于 `_from` 或已在 ERC-20 合约中获得批准。

#### ERC-20 自上而下可组合枚举

自上而下可组合枚举的可选接口：

```solidity
/// @dev The ERC-165 identifier for this interface is 0xc5fd96cd
interface ERC998ERC20TopDownEnumerable {
  
  /// @notice Get the number of ERC-20 contracts that token owns ERC-20 tokens from
  /// @param _tokenId The token that owns ERC-20 tokens.
  /// @return uint256 The number of ERC-20 contracts
  function totalERC20Contracts(uint256 _tokenId) external view returns(uint256);
  
  /// @notice Get an ERC-20 contract that token owns ERC-20 tokens from by index
  /// @param _tokenId The token that owns ERC-20 tokens.
  /// @param _index The index position of the ERC-20 contract.
  /// @return address The ERC-20 contract
  function erc20ContractByIndex(
    uint256 _tokenId, 
    uint256 _index
  ) 
    external 
    view 
    returns(address);
}
```

### ERC-721 自下而上可组合

ERC-721 自下而上可组合是将自己附加到其他 ERC-721 代币的 ERC-721 代币。

ERC-721 自下而上可组合合约存储代币的拥有地址和父代 tokenId（如果有）。

```solidity
/// @title `ERC998ERC721` Bottom-Up Composable Non-Fungible Token
/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-998.md
///  Note: the ERC-165 identifier for this interface is 0xa1b23002
interface ERC998ERC721BottomUp {

  /// @dev This emits when a token is transferred to an ERC-721 token
  /// @param _toContract The contract the token is transferred to
  /// @param _toTokenId The token the token is transferred to
  /// @param _tokenId The token that is transferred  
  event TransferToParent(
    address indexed _toContract, 
    uint256 indexed _toTokenId, 
    uint256 _tokenId
  );
  
  /// @dev This emits when a token is transferred from an ERC-721 token
  /// @param _fromContract The contract the token is transferred from
  /// @param _fromTokenId The token the token is transferred from
  /// @param _tokenId The token that is transferred  
  event TransferFromParent(
    address indexed _fromContract, 
    uint256 indexed _fromTokenId, 
    uint256 _tokenId
  );
  
  /// @notice Get the root owner of tokenId.
  /// @param _tokenId The token to query for a root owner address
  /// @return rootOwner The root owner at the top of tree of tokens and ERC-998 magic value.
  function rootOwnerOf(uint256 _tokenId) external view returns (bytes32 rootOwner);

  /// @notice Get the owner address and parent token (if there is one) of a token
  /// @param _tokenId The tokenId to query.
  /// @return tokenOwner The owner address of the token
  /// @return parentTokenId The parent owner of the token and ERC-998 magic value
  /// @return isParent True if parentTokenId is a valid parent tokenId and false if there is no parent tokenId
  function tokenOwnerOf(
    uint256 _tokenId
  )
    external 
    view
    returns (
      bytes32 tokenOwner, 
      uint256 parentTokenId, 
      bool isParent
    );
 
  /// @notice Transfer token from owner address to a token
  /// @param _from The owner address
  /// @param _toContract The ERC-721 contract of the receiving token
  /// @param _toToken The receiving token
  /// @param _data Additional data with no specified format
  function transferToParent(
    address _from, 
    address _toContract, 
    uint256 _toTokenId, 
    uint256 _tokenId, 
    bytes _data
  )
    external;
   
  /// @notice Transfer token from a token to an address
  /// @param _fromContract The address of the owning contract
  /// @param _fromTokenId The owning token
  /// @param _to The address the token is transferred to.
  /// @param _tokenId The token that is transferred
  /// @param _data Additional data with no specified format
  function transferFromParent(
    address _fromContract, 
    uint256 _fromTokenId, 
    address _to, 
    uint256 _tokenId, 
    bytes _data
  )
    external;
  
  /// @notice Transfer a token from a token to another token
  /// @param _fromContract The address of the owning contract
  /// @param _fromTokenId The owning token
  /// @param _toContract The ERC-721 contract of the receiving token
  /// @param _toToken The receiving token
  /// @param _tokenId The token that is transferred
  /// @param _data Additional data with no specified format
  function transferAsChild(
    address _fromContract, 
    uint256 _fromTokenId, 
    address _toContract, 
    uint256 _toTokenId, 
    uint256 _tokenId, 
    bytes _data
  )
    external;
}
```

#### `rootOwnerOf`

```solidity
/// @notice Get the root owner of tokenId.
/// @param _tokenId The token to query for a root owner address
/// @return rootOwner The root owner at the top of tree of tokens and ERC-998 magic value.
function rootOwnerOf(uint256 _tokenId) public view returns (bytes32 rootOwner);
```

此函数遍历代币拥有者，直到找到 `_tokenId` 的根拥有者地址。

根拥有者的前 4 个字节包含 ERC-998 魔法值 `0xcd740db5`。最后 20 个字节包含根拥有者地址。

返回魔法值是因为此函数可能在未知合约上被调用，而这些合约是否具有 `rootOwnerOf` 函数尚不清楚。魔法值在此类调用中用于确保接收到有效的返回值。

如果不确定合约是否具有 `rootOwnerOf` 函数，则必须将 `rootOwner` 返回值的前四个字节与 `0xcd740db5` 进行比较。

`0xcd740db5` 等于：

```solidity
this.rootOwnerOf.selector ^ this.rootOwnerOfChild.selector ^ 
this.tokenOwnerOf.selector ^ this.ownerOfChild.selector;
```

以下是 `rootOwnerOf` 返回的值的示例。
`0xcd740db50000000000000000e5240103e1ff986a2c8ae6b6728ffe0d9a395c59`

#### tokenOwnerOf

```solidity
/// @notice Get the owner address and parent token (if there is one) of a token
/// @param _tokenId The tokenId to query.
/// @return tokenOwner The owner address of the token and ERC-998 magic value.
/// @return parentTokenId The parent owner of the token
/// @return isParent True if parentTokenId is a valid parent tokenId and false if there is no parent tokenId
function tokenOwnerOf(
  uint256 _tokenId
)
  external 
  view 
  returns (
    bytes32 tokenOwner, 
    uint256 parentTokenId, 
    bool isParent
  );
```

此函数用于获取代币的拥有地址和父代 tokenId（如果在合约中存储）。

如果 `isParent` 为真，则 `tokenOwner` 是拥有的 ERC-721 合约地址，`parentTokenId` 是有效的父代 tokenId。如果 `isParent` 为假，则 `tokenOwner` 是用户地址，`parentTokenId` 不包含有效的父代 tokenId，必须被忽略。

`tokenOwner` 的前 4 个字节包含 ERC-998 魔法值 `0xcd740db5`。最后 20 个字节包含代币拥有者地址。

返回魔法值是因为此函数可能在未知合约上被调用，而这些合约是否具有 `tokenOwnerOf` 函数尚不清楚。魔法值在此类调用中用于确保接收到有效的返回值。

如果不确定合约是否具有 `rootOwnerOf` 函数，则必须将 `tokenOwner` 返回值的前四个字节与 `0xcd740db5` 进行比较。

#### transferToParent

```solidity
/// @notice Transfer token from owner address to a token
/// @param _from The owner address
/// @param _toContract The ERC-721 contract of the receiving token
/// @param _toToken The receiving token
/// @param _data Additional data with no specified format
function transferToParent(
  address _from, 
  address _toContract, 
  uint256 _toTokenId, 
  uint256 _tokenId, 
  bytes _data
)
  external;
```

此函数用于将代币从一个地址转移到一个代币。必须验证 `msg.sender`。

此函数必须检查 `_toToken` 是否存在于 `_toContract` 中，如果不存在则抛出异常。

#### transferFromParent

```solidity
/// @notice Transfer token from a token to an address
/// @param _fromContract The address of the owning contract
/// @param _fromTokenId The owning token
/// @param _to The address the token is transferred to.
/// @param _tokenId The token that is transferred
/// @param _data Additional data with no specified format
function transferFromParent(
  address _fromContract, 
  uint256 _fromTokenId, 
  address _to, 
  uint256 _tokenId, 
  bytes _data
)
  external;
```

此函数用于将代币从一个代币转移到一个地址。必须验证 `msg.sender`。

此函数必须检查 `_fromContract` 和 `_fromTokenId` 是否拥有 `_tokenId`，如果没有则抛出异常。

#### transferAsChild

```solidity
/// @notice Transfer a token from a token to another token
/// @param _fromContract The address of the owning contract
/// @param _fromTokenId The owning token
/// @param _toContract The ERC-721 contract of the receiving token
/// @param _toToken The receiving token
/// @param _tokenId The token that is transferred
/// @param _data Additional data with no specified format
function transferAsChild(
  address _fromContract, 
  uint256 _fromTokenId, 
  address _toContract, 
  uint256 _toTokenId, 
  uint256 _tokenId, 
  bytes _data
)
  external;
```

此函数用于将代币从一个代币转移到另一个代币。必须验证 `msg.sender`。

此函数必须检查 `_toToken` 是否存在于 `_toContract` 中，如果不存在则抛出异常。

此函数必须检查 `_fromContract` 和 `_fromTokenId` 是否拥有 `_tokenId`，如果没有则抛出异常。

#### ERC-721 自下而上可组合枚举

自下而上可组合枚举的可选接口：

```solidity
/// @dev The ERC-165 identifier for this interface is 0x8318b539
interface ERC998ERC721BottomUpEnumerable {
  
  /// @notice Get the number of ERC-721 tokens owned by parent token.
  /// @param _parentContract The contract the parent ERC-721 token is from.
  /// @param _parentTokenId The parent tokenId that owns tokens
  //  @return uint256 The number of ERC-721 tokens owned by parent token.
  function totalChildTokens(
    address _parentContract, 
    uint256 _parentTokenId
  ) 
    external 
    view 
    returns (uint256);
  
  /// @notice Get a child token by index
  /// @param _parentContract The contract the parent ERC-721 token is from.
  /// @param _parentTokenId The parent tokenId that owns the token
  /// @param _index The index position of the child token
  /// @return uint256 The child tokenId owned by the parent token
  function childTokenByIndex(
    address _parentContract, 
    uint256 _parentTokenId, 
    uint256 _index
  ) 
    external 
    view 
    returns (uint256);
}
```

### ERC-20 自下而上可组合

ERC-20 自下而上可组合是将自己附加到 ERC-721 代币的 ERC-20 代币，或像标准 ERC-20 代币一样由用户地址拥有。

当由 ERC-721 代币拥有时，ERC-20 自下而上可组合合约存储代币的拥有地址和父代 tokenId。ERC-20 自下而上可组合为 ERC-20 和 `ERC-223` 接口添加了几个方法，允许查询父代代币的余额，并在父代代币之间转移代币。

此功能可以通过添加一个额外的映射来实现，以跟踪代币的余额，除了用于跟踪用户地址余额的标准映射。

```solidity
/// @dev This mapping tracks standard ERC20/`ERC-223` ownership, where an address owns
///  a particular amount of tokens.
mapping(address => uint) userBalances;

/// @dev This additional mapping tracks ERC-998 ownership, where an ERC-721 token owns
///  a particular amount of tokens. This tracks contractAddres => tokenId => balance
mapping(address => mapping(uint => uint)) nftBalances;
```

完整接口如下。

```solidity
/// @title `ERC998ERC20` Bottom-Up Composable Fungible Token
/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-998.md
/// Note: The ERC-165 identifier for this interface is 0xffafa991
interface ERC998ERC20BottomUp {
  
  /// @dev This emits when a token is transferred to an ERC-721 token
  /// @param _toContract The contract the token is transferred to
  /// @param _toTokenId The token the token is transferred to
  /// @param _amount The amount of tokens transferred
  event TransferToParent(
    address indexed _toContract, 
    uint256 indexed _toTokenId, 
    uint256 _amount
  );

  /// @dev This emits when a token is transferred from an ERC-721 token
  /// @param _fromContract The contract the token is transferred from
  /// @param _fromTokenId The token the token is transferred from
  /// @param _amount The amount of tokens transferred
  event TransferFromParent(
    address indexed _fromContract, 
    uint256 indexed _fromTokenId, 
    uint256 _amount
  );

  /// @notice Get the balance of a non-fungible parent token
  /// @param _tokenContract The contract tracking the parent token
  /// @param _tokenId The ID of the parent token
  /// @return amount The balance of the token
  function balanceOfToken(
    address _tokenContract, 
    uint256 _tokenId
  )
    external
    view
    returns (uint256 amount);

  /// @notice Transfer tokens from owner address to a token
  /// @param _from The owner address
  /// @param _toContract The ERC-721 contract of the receiving token
  /// @param _toToken The receiving token
  /// @param _amount The amount of tokens to transfer
  function transferToParent(
    address _from, 
    address _toContract, 
    uint256 _toTokenId, 
    uint256 _amount
  )
    external;

  /// @notice Transfer token from a token to an address
  /// @param _fromContract The address of the owning contract
  /// @param _fromTokenId The owning token
  /// @param _to The address the token is transferred to
  /// @param _amount The amount of tokens to transfer
  function transferFromParent(
    address _fromContract, 
    uint256 _fromTokenId, 
    address _to, 
    uint256 _amount
  )
    external;

  /// @notice Transfer token from a token to an address, using `ERC-223` semantics
  /// @param _fromContract The address of the owning contract
  /// @param _fromTokenId The owning token
  /// @param _to The address the token is transferred to
  /// @param _amount The amount of tokens to transfer
  /// @param _data Additional data with no specified format, can be used to specify the sender tokenId
  function transferFromParentERC223(
    address _fromContract, 
    uint256 _fromTokenId, 
    address _to, 
    uint256 _amount, 
    bytes _data
  )
    external;

  /// @notice Transfer a token from a token to another token
  /// @param _fromContract The address of the owning contract
  /// @param _fromTokenId The owning token
  /// @param _toContract The ERC-721 contract of the receiving token
  /// @param _toToken The receiving token
  /// @param _amount The amount tokens to transfer
  function transferAsChild(
    address _fromContract, 
    uint256 _fromTokenId, 
    address _toContract, 
    uint256 _toTokenId, 
    uint256 _amount
   )
    external;
}
```

#### balanceOfToken

```solidity
/// @notice Get the balance of a non-fungible parent token
/// @param _tokenContract The contract tracking the parent token
/// @param _tokenId The ID of the parent token
/// @return amount The balance of the token
function balanceOfToken(
  address _tokenContract, 
  uint256 _tokenId
)
  external
  view
  returns (uint256 amount);
```

此函数返回非同质代币的余额。它镜像标准 ERC-20 方法 `balanceOf`，但接受父代代币合约的地址和父代代币的 ID。此方法的行为与 `balanceOf` 相同，但检查的是 ERC-721 代币的拥有权，而不是用户地址。

#### `transferToParent`

```solidity
/// @notice Transfer tokens from owner address to a token
/// @param _from The owner address
/// @param _toContract The ERC-721 contract of the receiving token
/// @param _toToken The receiving token
/// @param _amount The amount of tokens to transfer
function transferToParent(
  address _from, 
  address _toContract, 
  uint256 _toTokenId, 
  uint256 _amount
)
  external;
```

此函数将一定数量的代币从用户地址转移到 ERC-721 代币。此函数必须确保接收合约使用 ERC-165 `supportsInterface` 函数实现 ERC-721。此函数应确保接收代币确实存在，通过在接收代币的合约上调用 `ownerOf`，并确保它既不抛出异常也不返回零地址。此函数必须在成功转移时发出 `TransferToParent` 事件（除了标准 ERC-20 `Transfer` 事件！）。如果 `_from` 账户余额不足以支出，则此函数必须抛出异常。

#### `transferFromParent`

```solidity
/// @notice Transfer token from a token to an address
/// @param _fromContract The address of the owning contract
/// @param _fromTokenId The owning token
/// @param _to The address the token is transferred to
/// @param _amount The amount of tokens to transfer
function transferFromParent(
  address _fromContract, 
  uint256 _fromTokenId, 
  address _to, 
  uint256 _amount
)
  external;
```

此函数将一定数量的代币从 ERC-721 代币转移到一个地址。此函数必须在成功转移时发出 `TransferFromParent` 事件（除了标准 ERC-20 `Transfer` 事件！）。如果发送者的 ERC-721 代币余额少于指定的 `_amount`，则此函数必须抛出异常。此函数必须验证 `msg.sender` 拥有发送者的 ERC-721 代币，否则必须抛出异常。

#### `transferFromParentERC223`

```solidity
/// @notice Transfer token from a token to an address, using `ERC-223` semantics
/// @param _fromContract The address of the owning contract
/// @param _fromTokenId The owning token
/// @param _to The address the token is transferred to
/// @param _amount The amount of tokens to transfer
/// @param _data Additional data with no specified format, can be used to specify the sender tokenId
function transferFromParentERC223(
  address _fromContract, 
  uint256 _fromTokenId, 
  address _to, 
  uint256 _amount, 
  bytes _data
)
  external;
```
此函数将一定数量的代币从 ERC-721 代币转移到一个地址。此函数与 `transferFromParent` 的要求相同，除了它还必须在接收地址是合约的情况下调用 `tokenFallback`，如 `ERC-223` 所规定。

#### transferAsChild 1

```solidity
/// @notice Transfer a token from a token to another token
/// @param _fromContract The address of the owning contract
/// @param _fromTokenId The owning token
/// @param _toContract The ERC-721 contract of the receiving token
/// @param _toToken The receiving token
/// @param _amount The amount tokens to transfer
function transferAsChild(
  address _fromContract, 
  uint256 _fromTokenId, 
  address _toContract, 
  uint256 _toTokenId, 
  uint256 _amount
)
  external;
```

此函数将一定数量的代币从一个 ERC-721 代币转移到另一个 ERC-721 代币。此函数必须同时发出 `TransferFromParent` 和 `TransferToParent` 事件（除了标准的 ERC-20 `Transfer` 事件！）。如果发送者的 ERC-721 代币余额少于指定的 `_amount`，此函数必须抛出异常。此函数必须验证 `msg.sender` 拥有发送者的 ERC-721 代币，否则必须抛出异常。此函数必须确保接收合约通过 ERC-165 `supportsInterface` 函数实现了 ERC-721。此函数应确保接收代币确实存在，通过在接收代币的合约上调用 `ownerOf`，并确保它既不抛出异常也不返回零地址。

### 注意事项

为了向后兼容，实现必须在发生转移时发出标准的 ERC-20 `Transfer` 事件，无论发送者和接收者是地址还是 ERC-721 代币。如果发送者或接收者是代币，则 `Transfer` 事件中的相应参数应为代币的合约地址。

实现必须实现所有 ERC-20 和 `ERC-223` 函数，以及此接口中指定的函数。

## 理由

存在两种不同类型的可组合（自上而下和自下而上）以处理不同的用例。常规的 ERC-721 代币不能拥有自上而下的可组合，但可以拥有自下而上的可组合。自下而上的可组合不能拥有常规的 ERC-721，但自上而下的可组合可以拥有常规的 ERC-721 代币。拥有多种类型的可组合使不同的代币所有权成为可能。

### 使用哪种类型的可组合？

如果您想将常规的 ERC-721 代币转移到非同质代币，则使用自上而下的可组合。

如果您想将非同质代币转移到常规的 ERC-721 代币，则使用自下而上的可组合。

### 显式转移参数

每个 ERC-998 转移函数都包含显式参数以指定代币的先前所有者和新所有者。显式提供 **from** 和 **to** 是为了避免代币以意外的方式转移的情况。

以下是如果在转移函数中未显式提供 **from** 可能发生的情况示例：
> 一个交易合约是用户 A、用户 B 和用户 C 的特定可组合合约中的批准操作员。
>
> 用户 A 将代币 1 转移给用户 B。与此同时，交易合约将代币 1 转移给用户 C（隐含意图是从用户 A 转移）。用户 B 在一分钟内获得代币 1，然后它被错误地转移给用户 C。第二次转移应该失败，但由于未提供显式的 **from**，以确保代币 1 来自用户 A，因此没有失败。

## 向后兼容性

可组合设计为与 ERC-721、`ERC-223` 和 ERC-20 代币一起工作。

一些较旧的 ERC-721 合约没有 `safeTransferFrom` 函数。`getChild` 函数仍然可以用于将代币转移到 ERC-721 自上而下的可组合中。

如果 ERC-20 合约没有 `ERC-223` 函数 `transfer(address _to, uint _value, bytes _data)`，则仍然可以使用 `getERC20` 函数将 ERC-20 代币转移到 ERC-20 自上而下的可组合中。

## 参考实现

可以在此处找到实现：`https://github.com/mattlockyer/composables-998`

## 安全考虑

需要讨论。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。