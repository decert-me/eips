---
eip: 7484
title: ERC-7579 的注册扩展
description: 允许模块化智能账户使用模块注册表验证模块安全性的适配器
author: Konrad Kopp (@kopy-kat), zeroknots (@zeroknots)
discussions-to: https://ethereum-magicians.org/t/erc-7484-registry-adapters-for-smart-accounts/15434
status: 草案
type: 标准跟踪
category: ERC
created: 2023-08-14
requires: 7579
---

## 摘要

本提案标准化了模块注册表的接口和功能，允许模块化智能账户使用注册适配器验证模块的安全性。它还提供了一个单例模块注册表的参考实现。

## 动机

[ERC-4337](./eip-4337.md) 标准化了合约账户的执行流程，而 [ERC-7579](./eip-7579.md) 标准化了这些账户的模块化实现，允许任何开发者为这些模块化账户（以下简称智能账户）构建模块。然而，未经检查地将第三方模块添加到智能账户中会打开广泛的攻击向量。

解决此安全问题的一个方案是创建一个模块注册表，该注册表存储关于模块的安全证明，并允许智能账户在使用模块之前查询这些证明。该标准旨在实现两个目标：

1. 标准化模块注册表的接口和所需功能。
2. 标准化允许智能账户查询模块注册表的适配器的功能。

这确保了智能账户可以安全地查询模块注册表，并正确处理注册表行为，无论其架构、执行流程和安全假设如何。该标准还提供了一个无所有者的单例模块注册表的参考实现，可以被任何智能账户使用。虽然我们看到整个生态系统使用这个单一模块注册表的许多好处（见 `Rationale`），但我们承认使用单例存在权衡，因此该标准不要求智能账户使用参考实现。因此，该标准确保智能账户可以查询任何实现所需接口和功能的模块注册表，从而减少集成开销并确保智能账户的互操作性。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 定义

- **智能账户** - 一个 ERC-7579 模块化智能账户。
- **模块** - 自包含的智能账户功能。
- **证明** - 关于模块安全性的链上声明。
- **证明者** - 对模块做出证明的实体。
- **（模块）注册表** - 存储关于模块的链上证明列表的合约。
- **适配器** - 处理从注册表获取和验证证明的智能账户功能。

### 所需的注册表功能

注册表的核心接口如下：

```solidity
interface IERC7484Registry {
    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*               Check with internal attester(s)              */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/
    function check(address module) external view;

    function checkForAccount(address smartAccount, address module) external view;

    function check(address module, uint256 moduleType) external view;

    function checkForAccount(
        address smartAccount,
        address module,
        uint256 moduleType
    )
        external
        view;

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*                   Set internal attester(s)                 */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function trustAttesters(uint8 threshold, address[] calldata attesters) external;


    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*              Check with external attester(s)               */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function check(
        address module,
        address[] calldata attesters,
        uint256 threshold
    )
        external
        view;

    function check(
        address module,
        uint256 moduleType,
        address[] calldata attesters,
        uint256 threshold
    )
        external
        view;
}
```

注册表还必须实现以下功能：

- 验证证明者是证明的创建者，例如通过检查 `msg.sender` 或使用签名，然后再存储它。
- 允许证明者撤销他们所做的证明。
- 存储证明数据或对证明数据的引用。

注册表还应该实现以下附加功能：

- 允许证明者为其证明指定到期日期，并在检查时如果证明已过期则回退。
- 实现一个视图函数，允许适配器或链下客户端读取特定证明的数据。

#### `check` 函数

- 如果对 `module` 做出证明的 `attesters` 数量小于 `threshold`，注册表必须回退。
- 如果任何 `attester` 撤销了他们对 `module` 的证明，注册表必须回退。
- 提供的 `attesters` 必须是唯一且已排序的，如果不是，注册表必须回退。

#### 带有 moduleType 的 `check` 函数

- 如果存储的 `module` 的模块类型与提供的 `moduleType` 不符，注册表必须回退。

#### 带有内部证明者的函数

- 注册表必须使用存储的 `smartAccount` 或 `msg.sender`（如果前者不是参数）的证明者。
- 如果 `smartAccount` 或 `msg.sender`（如果前者不是参数）没有存储的证明者，注册表必须回退。

#### `trustAttesters`

- 注册表必须存储 `msg.sender` 的 `threshold` 和 `attesters`。
- 提供的 `attesters` 必须是唯一且已排序的，如果不是，注册表必须回退。

### 适配器行为

智能账户必须实现以下适配器功能，或者在账户中本地实现，或者作为模块实现。该适配器功能必须确保：

- 在调用模块 `A` 的第一次交易之前或期间，至少查询一次注册表关于模块 `A` 的信息。
- 注册表回退被视为安全风险。

此外，适配器应该实现以下功能：

- 当注册表回退时，回退交易流程。
- 在安装模块 `A` 时查询注册表关于模块 `A` 的信息。
- 在执行模块 `A` 时查询注册表关于模块 `A` 的信息。

示例：使用 `check` 的适配器流程
![使用 check() 的适配器流程](../assets/eip-7484/check-sequence.jpg)

## 理由

### 证明

证明是关于模块的链上声明。这些声明可能涉及模块的安全性（类似于常规智能合约审计）、模块是否遵循某个标准或关于这些模块的任何其他类型的声明。虽然其中一些声明可以在链上可行地验证，但大多数无法做到。

一个例子是确定特定模块可以写入哪些存储槽，如果智能账户使用 DELEGATECALL 调用模块，这可能会很有用。这个声明在链上几乎无法验证，但可以轻松地在链下验证。因此，证明者可以在链下执行此检查，并在链上发布证明，证明给定模块只能写入其指定的存储槽。

虽然证明始终是关于模块的某种声明，但本提案故意允许证明数据为任何类型的数据或指向数据的指针。这确保了任何类型的数据都可以用作声明，从简单的布尔标志（指定模块是安全的）到复杂的运行时模块行为证明。

### 单例注册表

为了使证明能够在链上查询，它们需要以某种形式存储在智能合约中的列表中。本提案包括一个无所有者的单例注册表的参考实现，该注册表作为证明的真实来源。

提出单例注册表的原因如下：

**安全性**：单例注册表通过将账户集成集中到一个真实来源，创建了更大的安全性，在这里最大数量的安全实体进行证明。这有许多好处：a) 它增加了每个模块的最大潜在数量和类型的证明，b) 消除了账户验证不同注册表的真实性和安全性的需要，将信任委托集中到进行证明的链上实体。结果是，账户能够以更低的 gas 开销查询多个证明者，以增加安全保证，并且账户无需额外工作来验证不同注册表的安全性。
**互操作性**：单例注册表不仅创造了更高水平的“证明流动性”，而且还增加了模块流动性，并确保更高水平的模块互操作性。开发者只需将其模块部署到一个地方即可接收证明，并最大化模块在所有集成账户中的分发。证明者还可以通过链接证明并从这些依赖链中获得持续的安全性，从而受益于之前的审计工作。这允许诸如遍历证明历史或开发者的版本控制等好处。

然而，使用单例显然存在权衡。单例注册表创建了一个单点故障，如果被利用，可能会对智能账户造成严重后果。这些攻击向量中最严重的将是攻击者能够代表受信任的证明者对恶意模块进行证明。在这里的一个权衡是，使用多个注册表时，安全证明的变化（例如发现漏洞并撤销证明）在生态系统中的传播速度较慢，给攻击者提供了更长的时间来利用漏洞，甚至在看到特定注册表中指出的问题但在其他注册表中没有时找到并利用它们。

由于是单例，注册表需要非常灵活，因此与狭窄的优化注册表相比，可能计算效率较低。这意味着查询单例注册表可能比查询更狭窄的注册表更具计算（以及由此产生的 gas）密集性。这里的权衡是，单例使得同时查询多个方的证明变得更便宜。因此，根据注册表架构，存在一个查询证明的数量（N），在此之后使用灵活的单例实际上在计算上比查询 N 个狭窄注册表更便宜。然而，参考实现也考虑了 gas 使用，专门的注册表不太可能显著降低 gas 超过参考实现的基准。

### 模块类型

模块可以有不同类型，账户确保模块是某种类型可能很重要。例如，如果一个账户想要安装一个处理账户验证逻辑的模块，那么它可能希望确保证明者已确认该模块确实能够执行此验证逻辑。否则，账户可能面临安装一个无法执行验证逻辑的模块的风险，这可能导致账户无法使用。

尽管如此，注册表本身不需要关心具体的模块类型意味着什么。相反，证明者可以提供这些类型，注册表可以存储它们。

### 相关工作

注册表的参考实现受到以太坊证明服务的强烈启发。然而，该提案的具体用例需要对 EAS 进行一些自定义修改和添加，这意味着使用现有的 EAS 合约作为模块注册表并不理想。然而，经过一些修改，使用 EAS 作为模块注册表是可能的。

## 向后兼容性

未发现向后兼容性问题。

## 参考实现

### Adapter.sol

```solidity
contract Adapter {
    IRegistry registry;

    function checkModule(address module) internal {
        // Check module attestation on Registry
        registry.check(module);
    }

    function checkModuleWithModuleTypeAndAttesters(address module, address[] memory attesters, uint256 threshold,  uint16 moduleType) internal {
        // Check list of module attestations on Registry
        registry.check(module, attesters, threshold, moduleType);
    }

}
```

### Account.sol

**注意**：这是一个符合上述 `Specification` 的具体示例，但该实现并不具有约束力。

```solidity
contract Account is Adapter {
    ...

    // installs a module
    function installModule(
        uint256 moduleTypeId,
        address module,
        bytes calldata initData
    )
        external
        payable
    {
        checkModule(module);
        ...
    }

    // executes a module
    function executeFromExecutor(
        ModeCode mode,
        bytes calldata executionCalldata
    )
        external
        payable
        returns (bytes[] memory returnData)
    {
        checkModule(module);
        ...
    }

    ...
}
```

### Registry

```solidity
/**
* @dev this implementation is unoptimized in order to make the reference implementation shorter to read
* @dev some function implementations are missing for brevity
*/
contract Registry is IERC7484Registry {
    ...

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*               Check with internal attester(s)              */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/
    function check(address module) external view {
        (address[] calldata attesters, uint256 threshold) = _getAttesters(msg.sender);

        uint256 validCount = 0;
        for (uint256 i = 0; i < attesters.length; i++) {
            bool isValid = _check(module, attesters[i]);
            if (isValid) validCount++;
        }
        if (validCount < threshold) revert AttestationThresholdNotMet();
    }

    function checkForAccount(address smartAccount, address module) external view {
        (address[] calldata attesters, uint256 threshold) = _getAttesters(smartAccount);

        ...
    }

    function check(address module, uint256 moduleType) external view {
        (address[] calldata attesters, uint256 threshold) = _getAttesters(msg.sender);

        uint256 validCount = 0;
        for (uint256 i = 0; i < attesters.length; i++) {
            bool isValid = _check(module, attesters[i]);
            if (isValid) validCount++;

            AttestationRecord storage attestation = _getAttestation(module, attester);
            if (attestation.moduleType != moduleType) revert ModuleTypeMismatch();
        }
        if (validCount < threshold) revert AttestationThresholdNotMet();
    }

    function checkForAccount(
        address smartAccount,
        address module,
        uint256 moduleType
    )
        external
        view {
        (address[] calldata attesters, uint256 threshold) = _getAttesters(smartAccount);

        ...
    }

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*                   Set internal attester(s)                 */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function trustAttesters(uint8 threshold, address[] calldata attesters) external {
        ...
    }

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*              Check with external attester(s)               */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function check(
        address module,
        address[] calldata attesters,
        uint256 threshold
    )
        external
        view
    {
        uint256 validCount = 0;
        for (uint256 i = 0; i < attesters.length; i++) {
            bool isValid = _check(module, attesters[i]);
            if (isValid) validCount++;
        }
        if (validCount < threshold) revert AttestationThresholdNotMet();
    }

    function check(
        address module,
        uint256 moduleType,
        address[] calldata attesters,
        uint256 threshold
    )
        external
        view
    {
        uint256 validCount = 0;
        for (uint256 i = 0; i < attesters.length; i++) {
            bool isValid = _check(module, attesters[i]);
            if (isValid) validCount++;

            AttestationRecord storage attestation = _getAttestation(module, attester);
            if (attestation.moduleType != moduleType) revert ModuleTypeMismatch();
        }
        if (validCount < threshold) revert AttestationThresholdNotMet();
    }

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*                         Internal                           */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function _check(address module, address attester) external view returns (bool isValid){
        AttestationRecord storage attestation = _getAttestation(module, attester);

        uint48 expirationTime = attestation.expirationTime;
        uint48 attestedAt =
            expirationTime != 0 && expirationTime < block.timestamp ? 0 : attestation.time;
        if (attestedAt == 0) return;

        uint48 revokedAt = attestation.revocationTime;
        if (revokedAt != 0) return;

        isValid = true;
    }

    function _getAttestation(
        address module,
        address attester
    )
        internal
        view
        virtual
        returns (AttestationRecord storage)
    {
        return _moduleToAttesterToAttestations[module][attester];
    }

    function _getAttesters(
        address account
    )
        internal
        view
        virtual
        returns (address[] calldata attesters, uint256 threshold)
    {
        ...
    }

    ...
}
```

## 安全考虑

需要讨论。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。