---
eip: 7548
title: 基于 NFT 的开放知识产权协议
description: 一种协议，允许用户重混 NFT 并生成新的 NFT 衍生作品，同时其关系可以在链上追踪。
author: Combo <combo@1combo.io>, Saitama (@saitama2009), CT29 <CT29@1combo.io>, Luigi <luigi@1combo.io>
discussions-to: https://ethereum-magicians.org/t/draft-open-ip-protocol/16373
status: Draft
type: Standards Track
category: ERC
created: 2023-10-31
requires: 165, 721
---

## 摘要

本提案旨在建立一种标准化的方法，通过去中心化的方式重混多个现有知识产权（IP），以创建新的知识产权。

该协议建立在 NFT（非同质化代币）的基础上。在该协议中，每个知识产权都表示为一个 NFT。它扩展了 [ERC-721](./eip-721.md) 标准，使用户能够通过重混多个现有 NFT 生成新的 NFT。为了确保创建过程的透明性和可追溯性，新 NFT 与原始 NFT 之间的关系被记录在区块链上并公开可访问。

此外，为了增强知识产权的流动性，用户不仅可以重混他们拥有的 NFT，还可以授权他人使用他们的 NFT 参与新 NFT 的创建。

## 动机

互联网每天都有大量新内容涌现，但在传统的知识产权基础设施下，知识产权注册和许可对数字创作者来说是一项麻烦事。内容的快速创作已经超越了知识产权注册的缓慢进程，导致许多内容未受到保护。这意味着数字创作者无法公平地从其作品的传播中获利。

||传统知识产权基础设施|开放知识产权基础设施|
|-|-|-|
|知识产权注册|漫长的等待、繁琐的文书工作和乏味的反复沟通。|NFT 代表知识产权；NFT 的拥有者拥有该知识产权的权利。|
|知识产权许可|冗长的讨论、法律术语和逐案协议。|一个一站式全球知识产权许可市场，支持各种许可协议。|

在这种背景下，我们热衷于为当今的数字创作者构建一个开放知识产权生态系统。在这里，创作者只需几次点击即可在全球范围内注册、许可和变现他们的内容，而不受地理或语言障碍的限制。

## 规范

本文档中的关键词“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

**接口**

该协议标准化了如何重混多个现有 NFT 并创建新的 NFT 衍生作品（称为组合），同时其关系可以在区块链上追踪。它包含三个核心模块：重混模块、网络模块和许可模块。

### 重混模块

该模块扩展了 ERC-721 标准，使用户能够通过重混多个现有 NFT（无论是 ERC-721 还是 [ERC-1155](./eip-1155.md)）来创建新的 NFT。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.10;

interface IERC721X {
    // Events

    /// @dev Emits when a combo is minted.
    /// @param owner The owner address of the newly minted combo
    /// @param comboId The newly minted combo identifier
    event ComboMinted(address indexed owner, uint256 indexed comboId);

    // Structs

    /// @param tokenAddress The NFT's collection address
    /// @param tokenId The NFT identifier
    struct Token {
        address tokenAddress;
        uint256 tokenId;
    }

    /// @param amount The number of NFTs used
    /// @param licenseId Which license to be used to verify this component
    struct Component {
        Token token;
        uint256 amount;
        uint256 licenseId;
    }

    // Functions

    /// @dev Mints a NFT by remixing multiple existing NFTs.
    /// @param components The NFTs remixed to mint a combo
    /// @param hash The hash representing the algorithm about how to generate the combo's metadata when remixing multiple existing NFTs.
    function mint(
        Component[] calldata components,
        string calldata hash
    ) external;

    /// @dev Retrieve a combo's components.
    function getComponents(
        uint256 comboId
    ) external view returns (Component[] memory);
}
```

### 许可模块

默认情况下，用户只能重混他们拥有的多个 NFT 以创建新的 NFT 衍生作品。该模块使 NFT 持有者能够授权他人使用他们的 NFT 进行重混。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.10;

import "./IERC721X.sol";

interface ILicense {
    /// @dev Verify the permission when minting a combo
    /// @param user The minter
    /// @param combo The new NFT to be minted by remixing multiple existing NFTs
    /// @return components The multiple existing NFTs used to mint the new combo
    function verify(
        address user,
        IERC721X.Token calldata combo,
        IERC721X.Component[] calldata components
    ) external returns (bool);
}
```

### 网络模块

该模块遵循单例模式，用于跟踪原始 NFT 与其 NFT 衍生作品之间的所有关系。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.10;

import "./IERC721X.sol";

interface INFTNetIndexer {
    /// @dev Verify if the `child` was created by remixing the `parent` with other NFTs.
    /// @param parent Any NFT
    /// @param child Any NFT
    function isParent(
        IERC721X.Token calldata parent,
        IERC721X.Token calldata child
    ) external view returns (bool);

    /// @dev Verify if `a` and `b` have common `parent`s
    /// @param a Any NFT
    /// @param b Any NFT
    function isSibling(
        IERC721X.Token calldata a,
        IERC721X.Token calldata b
    ) external view returns (bool, IERC721X.Token[] memory commonParents);

    /// @dev Return all parents of a `token`
    /// @param token Any NFT
    /// @return parents All NFTs used to mint the `token`
    function getParents(
        IERC721X.Token calldata token
    ) external view returns (IERC721X.Token[] memory parents);
}
```

## 理由

开放知识产权协议建立在“1 个前提，2 个扩展，1 个常量”的原则上。

“1 个前提”意味着在开放知识产权生态系统中，任何知识产权都由一个 NFT 代表。因此，如果你拥有该 NFT，你就拥有该知识产权。这就是为什么开放知识产权协议被设计为与 ERC-721 兼容的扩展协议。

“2 个扩展”指的是知识产权许可和重混的多样化。

- 知识产权许可方法多种多样。例如，将 NFT 委托给其他人是一种许可类型，为使用权设定价格是另一种许可类型，甚至基于拍卖、AMM 或其他定价机制定价也可以发展出不同的许可方法。因此，许可模块被设计为允许各种自定义许可方法。

- 知识产权重混规则也多种多样。在重混多个现有 NFT 时，是否支持 ERC-1155，是否限制 NFT 选择范围，以及重混后 NFT 是否被消耗，都没有标准。因此，重混模块被设计为支持自定义重混规则。

“1 个常量”指的是知识产权许可的可追溯性信息始终是公开且不可更改的。无论用户如何许可或重混知识产权，原始知识产权与新知识产权之间的关系始终保持一致。此外，如果所有知识产权关系都记录在同一个数据库中，将会创建一个庞大的知识产权网络。如果其他社交或游戏 dApp 利用这个网络，可能会带来全新的用户体验。因此，该协议的网络模块被设计为单例。

## 向后兼容性

本提案与现有的 ERC-721 标准完全向后兼容，扩展了不影响核心功能的新功能。

<!-- TODO: add reference implementation -->

## 安全考虑

该标准强调了几个需要关注的安全问题：

* **所有权和权限**：只有 NFT 的拥有者或被其授权的人应被允许将 NFT 重混为 NFT 衍生作品。必须有严格的访问控制，以防止未经授权的创作。

* **重入风险**：创建衍生作品可能需要与多个外部合约交互，如重混、许可和网络模块。这可能会打开重入攻击的门，因此需要采取保护措施。

* **Gas 使用**：重混 NFT 可能计算密集且涉及许多合约交互，这可能导致高 Gas 费用。优化这些过程以降低成本并保持用户友好性非常重要。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。