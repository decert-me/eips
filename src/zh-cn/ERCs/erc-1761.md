---
eip: 1761
title: 范围批准接口
author: Witek Radomski <witek@enjin.io>, Andrew Cooke <ac0dem0nk3y@gmail.com>, James Therien <james@enjin.io>, Eric Binet <eric@enjin.io>
type: 标准跟踪
category: ERC
status: 停滞
created: 2019-02-18
discussions-to: https://github.com/ethereum/EIPs/issues/1761
requires: 165
---

## 简单总结

一个标准接口，用于通过定义一个或多个代币 ID 的“范围”来允许代币合约中的限制批准。

## 摘要

该接口旨在与具有“ID”域的代币合约一起使用，例如 ERC-1155 或 ERC-721。这使得对特定“范围”内的一个或多个代币 ID 进行限制批准成为可能。当考虑管理来自多个不同域的代币的智能合约时，限制批准到这些域是有意义的。范围批准是这一思想的概括。实现者可以根据需要定义范围。

范围的示例用例：

* 一家公司可能会在区块链上表示其车辆队伍，并为每个区域办公室创建一个范围。
* 游戏开发者可以共享一个 [ERC-1155](./eip-1155.md) 合约，其中每个开发者在指定范围内管理代币。
* 不同价值的代币可以分成单独的范围。高价值代币可以保存在较小的单独范围内，而低价值代币可能保存在共享范围内。用户可以将整个低价值代币范围批准给第三方智能合约、交易所或其他应用，而不必担心在出现问题时丢失高价值代币。

## 动机

在某些应用中，可能希望限制批准。限制批准可以防止用户在未审核其批准的合约时造成损失。没有提供管理范围的标准 API，因为这取决于具体实现。一些实现可能选择提供固定数量的范围，或将特定范围分配给某些类型。其他实现可能会向其用户开放范围配置，并提供创建范围和将 ID 分配给它们的方法。

# 规范

```solidity
pragma solidity ^0.5.2;

/**
    Note: The ERC-165 identifier for this interface is 0x30168307.
*/
interface ScopedApproval {
    /**
        @dev MUST emit when approval changes for scope.
    */
    event ApprovalForScope(address indexed _owner, address indexed _operator, bytes32 indexed _scope, bool _approved);

    /**
        @dev MUST emit when the token IDs are added to the scope.
        By default, IDs are in no scope.
        The range is inclusive: _idStart, _idEnd, and all IDs in between have been added to the scope.
        _idStart must be lower than or equal to _idEnd.
    */
    event IdsAddedToScope(uint256 indexed _idStart, uint256 indexed _idEnd, bytes32 indexed _scope);

    /**
        @dev MUST emit when the token IDs are removed from the scope.
        The range is inclusive: _idStart, _idEnd, and all IDs in between have been removed from the scope.
        _idStart must be lower than or equal to _idEnd.
    */
    event IdsRemovedFromScope(uint256 indexed _idStart, uint256 indexed _idEnd, bytes32 indexed _scope);

    /** @dev MUST emit when a scope URI is set or changes.
        URIs are defined in RFC 3986.
        The URI MUST point a JSON file that conforms to the "Scope Metadata JSON Schema".
    */
    event ScopeURI(string _value, bytes32 indexed _scope);

    /**
        @notice     Returns the number of scopes that contain _id.
        @param _id  The token ID
        @return     The number of scopes containing the ID
    */
    function scopeCountForId(uint256 _id) public view returns (uint32);

    /**
        @notice             Returns a scope that contains _id.
        @param _id          The token ID
        @param _scopeIndex  The scope index to  query (valid values are 0 to scopeCountForId(_id)-1)
        @return             The Nth scope containing the ID
    */
    function scopeForId(uint256 _id, uint32 _scopeIndex) public view returns (bytes32);

    /**
        @notice Returns a URI that can be queried to get scope metadata. This URI should return a JSON document containing, at least the scope name and description. Although supplying a URI for every scope is recommended, returning an empty string "" is accepted for scopes without a URI.
        @param  _scope  The queried scope
        @return         The URI describing this scope.
    */
    function scopeUri(bytes32 _scope) public view returns (string memory);

    /**
        @notice Enable or disable approval for a third party ("operator") to manage the caller's tokens in the specified scope.
        @dev MUST emit the ApprovalForScope event on success.
        @param _operator    Address to add to the set of authorized operators
        @param _scope       Approval scope (can be identified by calling scopeForId)
        @param _approved    True if the operator is approved, false to revoke approval
    */
    function setApprovalForScope(address _operator, bytes32 _scope, bool _approved) external;

    /**
        @notice Queries the approval status of an operator for a given owner, within the specified scope.
        @param _owner       The owner of the Tokens
        @param _operator    Address of authorized operator
        @param _scope       Scope to test for approval (can be identified by calling scopeForId)
        @return             True if the operator is approved, false otherwise
    */
    function isApprovedForScope(address _owner, address _operator, bytes32 _scope) public view returns (bool);
}
```

## 范围元数据 JSON 架构

该架构允许本地化。`{id}` 和 `{locale}` 应由客户端替换为适当的值。

```json
{
    "title": "Scope Metadata",
    "type": "object",
    "required": ["name"],
    "properties": {
        "name": {
            "type": "string",
            "description": "Identifies the scope in a human-readable way.",
        },
        "description": {
            "type": "string",
            "description": "Describes the scope to allow users to make informed approval decisions.",
        },
        "localization": {
            "type": "object",
            "required": ["uri", "default", "locales"],
            "properties": {
                "uri": {
                    "type": "string",
                    "description": "The URI pattern to fetch localized data from. This URI should contain the substring `{locale}` which will be replaced with the appropriate locale value before sending the request."
                },
                "default": {
                    "type": "string",
                    "description": "The locale of the default data within the base JSON"
                },
                "locales": {
                    "type": "array",
                    "description": "The list of locales for which data is available. These locales should conform to those defined in the Unicode Common Locale Data Repository (http://cldr.unicode.org/)."
                }
            }
        }
    }
}
```

### 本地化

元数据本地化应标准化，以提高所有语言的展示统一性。因此，提出了一种简单的覆盖方法来启用本地化。如果元数据 JSON 文件包含 `localization` 属性，则其内容可用于提供需要本地化的字段的本地化值。`localization` 属性应为一个子对象，包含三个属性：`uri`、`default` 和 `locales`。如果字符串 `{locale}` 存在于任何 URI 中，则所有客户端软件必须用所选的语言环境替换它。

## 理由

最初的设计被提议作为 ERC-1155 的扩展： [讨论线程 - 评论 1](https://github.com/ethereum/EIPs/issues/1155#issuecomment-459505728)。经过一些讨论：[评论 2](https://github.com/ethereum/EIPs/issues/1155#issuecomment-460603439) 和社区的建议，将此批准机制实现为外部合约 [评论 3](https://github.com/ethereum/EIPs/issues/1155#issuecomment-461758755)，决定作为接口标准，这一设计将允许许多不同的代币标准，如 ERC-721 和 ERC-1155，实现范围批准，而不强迫系统进入所有代币的实现。

### 元数据 JSON

添加范围元数据 JSON 架构是为了支持多种语言的人类可读范围名称和描述。

## 参考

**标准**
- [ERC-1155 多代币标准](./eip-1155.md)
- [ERC-165 标准接口检测](./eip-165.md)
- [JSON 架构](https://json-schema.org/)

**实现**
- [Enjin Coin](https://enjincoin.io) ([github](https://github.com/enjin))

**文章与讨论**
- [GitHub - 原始讨论线程](https://github.com/ethereum/EIPs/issues/1761)
- [GitHub - ERC-1155 讨论线程](https://github.com/ethereum/EIPs/issues/1155)

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。