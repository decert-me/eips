---
eip: 918
title: 可挖矿代币标准
author: Jay Logelin <jlogelin@alumni.harvard.edu>, Infernal_toast <admin@0xbitcoin.org>, Michael Seiler <mgs33@cornell.edu>, Brandon Grill <bg2655@columbia.edu>
type: 标准跟踪
category: ERC
status: 停滞
created: 2018-03-07
---
 
### 简单总结

一个使用工作量证明算法进行分发的标准化可挖矿代币的规范。

### 摘要

该规范描述了一种在代币合约中最初锁定代币并通过 mint() 函数缓慢分发它们的方法，该函数类似于水龙头。此 mint() 函数使用工作量证明算法以最小化 gas 费用并控制分发速率。此外，可挖矿代币的标准化将促进标准化的 CPU 和 GPU 代币挖矿软件、代币挖矿池以及代币挖矿生态系统中的其他外部工具的出现。

### 动机

通过 ICO 模型及其衍生品进行的代币分发容易受到人类行为的非法影响。此外，新代币项目是集中化的，因为单个实体必须处理和控制所有初始代币和所有筹集的 ICO 资金。通过通过“初始挖矿发行”（或 IMO）分发代币，代币合约的所有权不再属于部署者，部署者只是“另一个用户”。因此，利用挖矿代币分发模型的投资者风险暴露显著降低。该标准旨在独立存在，允许与 ERC20、ERC721 等的最大互操作性。

### 规范

#### 接口
一般行为规范包括一个定义代币铸造操作的主要函数，一个可选的合并铸造操作用于发行多个代币，挑战编号、挖矿难度、挖矿目标和当前奖励的获取器，最后是一个 Mint 事件，在成功解决验证和代币发行时发出。合约至少必须遵循此接口（不包括可选的合并操作）。建议合约与下面描述的更具行为定义的抽象合约接口，以利用更明确的构造，从而通过重写阶段函数实现更简单的外部实现。（见下文“抽象合约”）

``` solidity
interface ERC918  {
   
   function mint(uint256 nonce) public returns (bool success);

   function getAdjustmentInterval() public view returns (uint);

   function getChallengeNumber() public view returns (bytes32);

   function getMiningDifficulty() public view returns (uint);

   function getMiningTarget() public view returns (uint);

   function getMiningReward() public view returns (uint);
   
   function decimals() public view returns (uint8);

   event Mint(address indexed from, uint rewardAmount, uint epochCount, bytes32 newChallengeNumber);
}
```

#### 抽象合约（可选）

抽象合约遵循 EIP918 接口，并通过引入 4 个内部阶段的代币挖矿和铸造：哈希、奖励、纪元和调整难度，在 mint() 操作期间调用。该构造在使用时提供了足够的灵活性，同时为多种挖矿实现类型提供了充足的空间。

### 字段

#### adjustmentInterval
难度调整之间的时间间隔（以秒为单位）。

``` solidity
bytes32 public adjustmentInterval;
```

#### challengeNumber
当前挑战编号。预计在铸造新奖励后会生成新的挑战编号。

``` solidity
bytes32 public challengeNumber;
```

#### difficulty
当前挖矿难度，应通过 _adjustDifficulty 铸造阶段进行调整。

``` solidity
uint public difficulty;
```

#### tokensMinted
已铸造代币的累计计数，通常在 _reward 阶段进行修改。

``` solidity
uint public tokensMinted;
```

#### epochCount
已挖矿的“区块”数量。

``` solidity
uint public epochCount;
```

### 挖矿操作

#### mint

返回一个标志，指示哈希摘要验证成功，并将奖励分配给 msg.sender。为了防止 MiTM 攻击，建议摘要包含最近的以太坊区块哈希和 msg.sender 的地址。一旦验证，mint 函数计算并将挖矿奖励发送给发送者，并在合约的供应上执行内部会计操作。

铸造操作作为一个公共函数存在，调用 4 个独立的阶段，分别表示为函数 hash、_reward、_newEpoch 和 _adjustDifficulty。为了创建最灵活的实现，同时遵循必要的合约协议，建议代币实现者重写内部方法，允许基础合约通过 mint 处理它们的执行。

此外部函数由矿工调用以验证挑战摘要、计算奖励、填充统计数据、变更纪元变量并根据需要调整解决方案难度。一旦完成，在返回布尔成功标志之前发出 Mint 事件。

``` solidity
contract AbstractERC918 is EIP918Interface {

    // the amount of time between difficulty adjustments
    uint public adjustmentInterval;
     
    // generate a new challenge number after a new reward is minted
    bytes32 public challengeNumber;
    
    // the current mining target
    uint public miningTarget;

    // cumulative counter of the total minted tokens
    uint public tokensMinted;

    // number of blocks per difficulty readjustment
    uint public blocksPerReadjustment;

    //number of 'blocks' mined
    uint public epochCount;
   
    /*
     * Externally facing mint function that is called by miners to validate challenge digests, calculate reward,
     * populate statistics, mutate epoch variables and adjust the solution difficulty as required. Once complete,
     * a Mint event is emitted before returning a success indicator.
     **/
    function mint(uint256 nonce) public returns (bool success) {
        require(msg.sender != address(0));

        // perform the hash function validation
        hash(nonce);
        
        // calculate the current reward
        uint rewardAmount = _reward();
        
        // increment the minted tokens amount
        tokensMinted += rewardAmount;
        
        epochCount = _epoch();

        //every so often, readjust difficulty. Don't readjust when deploying
        if(epochCount % blocksPerReadjustment == 0){
            _adjustDifficulty();
        }
       
        // send Mint event indicating a successful implementation
        emit Mint(msg.sender, rewardAmount, epochCount, challengeNumber);
        
        return true;
    }
}
```

##### *Mint 事件*

在成功验证和奖励后，铸造方法调度一个 Mint 事件，指示奖励地址、奖励金额、纪元计数和最新挑战编号。

``` solidity
event Mint(address indexed from, uint reward_amount, uint epochCount, bytes32 newChallengeNumber);
```

#### hash

公共接口函数 hash，旨在在实现中重写以定义哈希算法和验证。返回验证的摘要。

``` solidity
function hash(uint256 nonce) public returns (bytes32 digest);
```

#### _reward

内部接口函数 _reward，旨在在实现中重写以计算和分配奖励金额。该方法必须返回奖励金额。

``` solidity
function _reward() internal returns (uint);
```

#### _newEpoch

内部接口函数 _newEpoch，旨在在实现中重写以定义变更挖矿变量的切点，为下一个挖矿阶段做准备。

``` solidity
function _newEpoch(uint256 nonce) internal returns (uint);
```
 
#### _adjustDifficulty
 
内部接口函数 _adjustDifficulty，旨在在实现中重写以根据需要调整挖矿的难度（通过字段 difficulty）。

``` solidity
function _adjustDifficulty() internal returns (uint);
```

#### getAdjustmentInterval

难度调整操作之间的时间（以秒为单位）。

``` solidity
function getAdjustmentInterval() public view returns (uint);
```

#### getChallengeNumber

最近的以太坊区块哈希，用于防止预挖未来区块。

``` solidity
function getChallengeNumber() public view returns (bytes32);
```

#### getMiningDifficulty

PoW 解决方案的摘要所需的数字数量，通常在奖励生成期间自动调整。

``` solidity
function getMiningDifficulty() public view returns (uint)
```

#### getMiningReward

返回当前奖励金额。根据算法，通常在每个奖励时代分配奖励，以提供稀缺性。

``` solidity
function getMiningReward() public view returns (uint)
```

### 示例挖矿函数
一个用 Python 编写的通用挖矿函数，用于查找 keccak256 可挖矿代币的有效 nonce，如下所示：
``` python
def generate_nonce():
  myhex =  b'%064x' % getrandbits(32*8)
  return codecs.decode(myhex, 'hex_codec')
  
def mine(challenge, public_address, difficulty):
  while True:
    nonce = generate_nonce()
    hash1 = int(sha3.keccak_256(challenge+public_address+nonce).hexdigest(), 16)
    if hash1 < difficulty:
      return nonce, hash1
```

一旦找到 nonce 和 hash1，这些将用于调用智能合约的 mint() 函数以接收代币奖励。

### 合并挖矿扩展（可选）
为了支持合并挖矿多个代币，可以将可选的合并挖矿扩展作为 ERC918 标准的一部分实现。重要的是要注意，以下函数仅在基础合约使用 tx.origin 而不是 msg.sender 应用奖励时才能正常工作。如果不是，奖励的代币将发送到调用合约，而不是最终用户。

``` solidity
/**
 * @title ERC-918 Mineable Token Standard, optional merged mining functionality
 * @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-918.md
 * 
 */
contract ERC918Merged is AbstractERC918 {
    /*
     * @notice Externally facing merge function that is called by miners to validate challenge digests, calculate reward,
     * populate statistics, mutate state variables and adjust the solution difficulty as required. Additionally, the
     * merge function takes an array of target token addresses to be used in merged rewards. Once complete,
     * a Mint event is emitted before returning a success indicator.
     *
     * @param _nonce the solution nonce
     **/
    function merge(uint256 _nonce, address[] _mineTokens) public returns (bool) {
      for (uint i = 0; i < _mineTokens.length; i++) {
        address tokenAddress = _mineTokens[i];
        ERC918Interface(tokenAddress).mint(_nonce);
      }
    }

    /*
     * @notice Externally facing merge function kept for backwards compatibility with previous definition
     *
     * @param _nonce the solution nonce
     * @param _challenge_digest the keccak256 encoded challenge number + message sender + solution nonce
     **/
     function merge(uint256 _nonce, bytes32 _challenge_digest, address[] _mineTokens) public returns (bool) {
       //the challenge digest must match the expected
       bytes32 digest = keccak256( abi.encodePacked(challengeNumber, msg.sender, _nonce) );
       require(digest == _challenge_digest, "Challenge digest does not match expected digest on token contract [ ERC918Merged.mint() ]");
       return merge(_nonce, _mineTokens);
     }
}
```

### 委托铸造扩展（可选）
为了促进第三方铸造提交范式，例如矿工向池操作员和/或系统提交解决方案的情况，可以使用委托铸造扩展，允许池账户代表用户提交解决方案，以便矿工可以避免直接支付以太坊交易费用。这是通过一个离线挖矿账户打包并签署一个标准化的铸造解决方案包并将其发送到池或第三方进行提交来完成的。
ERC918 可挖矿铸造包元数据应使用以下模式准备：
``` solidity
{
    "title": "Mineable Mint Packet Metadata",
    "type": "object",
    "properties": {
        "nonce": {
            "type": "string",
            "description": "Identifies the target solution nonce",
        },
        "origin": {
            "type": "string",
            "description": "Identifies the original user that mined the solution nonce",
        },
        "signature": {
            "type": "string",
            "description": "The signed hash of tightly packed variables sha3('delegatedMintHashing(uint256,address)')+nonce+origin_account",
        }
    }
}
```
在 JavaScript 客户端上准备可挖矿铸造包的过程如下所示：

``` solidity
function prepareDelegatedMintTxn(nonce, account) {
  var functionSig = web3.utils.sha3("delegatedMintHashing(uint256,address)").substring(0,10)
  var data = web3.utils.soliditySha3( functionSig, nonce, account.address )
  var sig = web3.eth.accounts.sign(web3.utils.toHex(data), account.privateKey )
  // prepare the mint packet
  var packet = {}
  packet.nonce = nonce
  packet.origin = account.address
  packet.signature = sig.signature
  // deliver resulting JSON packet to pool or third party
  var mineableMintPacket = JSON.stringify(packet, null, 4)
  /* todo: send mineableMintPacket to submitter */
  ...
}
```
一旦包准备好并格式化，它可以被路由到一个第三方，该第三方将提交交易到合约的 delegatedMint() 函数，从而支付交易 gas 并接收结果代币。池/第三方必须手动将铸造的代币（扣除费用后）偿还给原始铸造者。

以下代码示例展示了第三方包中继：
``` solidity
//received by minter
var mineableMintPacket = ...
var packet = JSON.parse(mineableMintPacket)
erc918MineableToken.delegatedMint(packet.nonce, packet.origin, packet.signature)
```
委托铸造扩展在 ERC918 的基础上实现为一个子合约：
``` js
import 'openzeppelin-solidity/contracts/contracts/cryptography/ECDSA.sol';

contract ERC918DelegatedMint is AbstractERC918, ECDSA {
   /**
     * @notice Hash (keccak256) of the payload used by delegatedMint
     * @param _nonce the golden nonce
     * @param _origin the original minter
     * @param _signature the original minter's elliptical curve signature
     */
    function delegatedMint(uint256 _nonce, address _origin, bytes _signature) public returns (bool success) {
        bytes32 hashedTx = delegatedMintHashing(_nonce, _origin);
        address minter = recover(hashedTx, _signature);
        require(minter == _origin, "Origin minter address does not match recovered signature address [ AbstractERC918.delegatedMint() ]");
        require(minter != address(0), "Invalid minter address recovered from signature [ ERC918DelegatedMint.delegatedMint() ]");
        success = mintInternal(_nonce, minter);
    }

    /**
     * @notice Hash (keccak256) of the payload used by delegatedMint
     * @param _nonce the golden nonce
     * @param _origin the original minter
     */
    function delegatedMintHashing(uint256 _nonce, address _origin) public pure returns (bytes32) {
        /* "0x7b36737a": delegatedMintHashing(uint256,address) */
        return toEthSignedMessageHash(keccak256(abi.encodePacked( bytes4(0x7b36737a), _nonce, _origin)));
    }
}
```

### 可挖矿代币元数据（可选）
为了提供更丰富且可能可变的特定可挖矿代币的元数据，提供对该数据的链外引用更为可行。这需要实现一个单一接口方法 'metadataURI()'，该方法返回一个 JSON 字符串，编码了字符串字段 symbol、name、description、website、image 和 type。

可挖矿代币元数据的 Solidity 接口：
``` solidity
/**
 * @title ERC-918 Mineable Token Standard, optional metadata extension
 * @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-918.md
 * 
 */
interface ERC918Metadata is AbstractERC918 {
    /**
     * @notice A distinct Uniform Resource Identifier (URI) for a mineable asset.
     */
    function metadataURI() external view returns (string);
}
```

可挖矿代币元数据 JSON 模式定义：
``` solidity
{
    "title": "Mineable Token Metadata",
    "type": "object",
    "properties": {
        "symbol": {
            "type": "string",
            "description": "Identifies the Mineable Token's symbol",
        },
        "name": {
            "type": "string",
            "description": "Identifies the Mineable Token's name",
        },
        "description": {
            "type": "string",
            "description": "Identifies the Mineable Token's long description",
        },
        "website": {
            "type": "string",
            "description": "Identifies the Mineable Token's homepage URI",
        },
        "image": {
            "type": "string",
            "description": "Identifies the Mineable Token's image URI",
        },
        "type": {
            "type": "string",
            "description": "Identifies the Mineable Token's hash algorithm ( ie.keccak256 ) used to encode the solution",
        }
    }
}
```

### 理由

Solidity keccak256 算法并不一定要使用，但推荐使用，因为它是一种在 EVM 中执行的成本效益高的单向算法，并且在 Solidity 中简单易行。nonce 是矿工尝试找到的解决方案，因此它是哈希算法的一部分。challengeNumber 也是哈希的一部分，以便未来的区块无法被挖掘，因为它充当了一段随机数据，直到挖矿轮次开始才会被揭示。msg.sender 地址是哈希的一部分，以便 nonce 解决方案仅对特定以太坊账户有效，因此该解决方案不易受到中间人攻击。这也允许矿池在不易被矿工欺骗的情况下运作，因为矿池可以强制矿工在哈希算法中使用矿池的地址进行挖矿。

将电力和硬件转化为挖矿代币资产的经济学为去中心化矿工社区提供了直接参与以太坊代币经济的选项。通过以哈希算力投票，矿工被激励参与早期代币交易以回收初始成本，为矿工和早期投资者之间提供了一种启动刺激机制。

对于挖矿代币，社区关注的一个问题是能源使用而没有保护网络的功能。尽管代币挖矿并不保护网络，但它为保护社区免受腐败提供了功能，因为它提供了一个去中心化 ICO 的替代方案。此外，初始挖矿发行可能持续的时间短至一周、一天或一小时，此时所有代币将被铸造。

### 向后兼容性
该标准的早期版本在 mint() 函数中包含了一个冗余的 'challenge_digest' 参数，该参数对打包变量 challengeNumber、msg.sender 和 nonce 进行了哈希编码。决定将其从标准中移除，以帮助减少处理并降低铸造操作期间的 gas 使用。然而，为了与现有挖矿程序和矿池软件的互操作性，可以将以下合约添加到继承树中：

``` solidity
/**
 * @title ERC-918 Mineable Token Standard, optional backwards compatibility function
 * @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-918.md
 * 
 */
contract ERC918BackwardsCompatible is AbstractERC918 {

    /*
     * @notice Externally facing mint function kept for backwards compatibility with previous mint() definition
     * @param _nonce the solution nonce
     * @param _challenge_digest the keccak256 encoded challenge number + message sender + solution nonce
     **/
    function mint(uint256 _nonce, bytes32 _challenge_digest) public returns (bool success) {
        //the challenge digest must match the expected
        bytes32 digest = keccak256( abi.encodePacked(challengeNumber, msg.sender, _nonce) );
        require(digest == _challenge_digest, "Challenge digest does not match expected digest on token contract [ AbstractERC918.mint() ]");
        success = mint(_nonce);
    }
}
```

### 测试用例
（影响共识变化的 EIP 的实现必须包含测试用例。其他 EIP 可以选择在适用时包含测试用例的链接。）

### 实现

简单示例：
https://github.com/0xbitcoin/EIP918-Mineable-Token/blob/master/contracts/SimpleERC918.sol

复杂示例：

https://github.com/0xbitcoin/EIP918-Mineable-Token/blob/master/contracts/0xdogeExample.sol
https://github.com/0xbitcoin/EIP918-Mineable-Token/blob/master/contracts/0xdogeExample2.sol
https://github.com/0xbitcoin/EIP918-Mineable-Token/blob/master/contracts/0xBitcoinBase.sol

0xBitcoin 代币合约：
https://etherscan.io/address/0xb6ed7644c69416d67b522e20bc294a9a9b405b31

MVI OpenCL 代币矿工 
https://github.com/mining-visualizer/MVis-tokenminer/releases

PoWAdv 代币合约：
https://etherscan.io/address/0x1a136ae98b49b92841562b6574d1f3f5b0044e4c

### 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。