---
eip: 6864
title: 可升级的可替代代币
description: 可升级的可替代代币，ERC-20 的简单扩展
author: Jeff Huang (@jeffishjeff)
discussions-to: https://ethereum-magicians.org/t/eip-6864-upgradable-fungible-token-a-simple-extension-to-erc-20/13781
status: 草案
type: 标准跟踪
category: ERC
created: 2023-04-05
requires: 20
---

## 摘要

本提案概述了一个智能合约接口，用于在保持用户余额的情况下升级/降级现有的 [ERC-20](./erc-20.md) 智能合约。该接口本身是对 ERC-20 标准的扩展，以便其他智能合约可以继续与升级后的智能合约进行交互，而无需更改任何内容，除了地址。

## 动机

从设计上讲，智能合约是不可变的，像 ERC-20 这样的代币标准是极简的。虽然这些设计原则在去中心化应用中是基本的，但在某些合理和实际的情况下，升级 ERC-20 代币的能力是可取的，例如：

- 解决错误并消除限制
- 采用新功能和标准
- 遵守变化的法规

使用 `delegatecall` 操作码的代理模式提供了一种合理的、通用的解决方案，以调和不可变性和可升级性特性，但也有其自身的缺点：

- 智能合约必须从一开始就支持代理模式，即不能在未使用代理部署的合约上使用
- 升级是静默且不可逆的，即用户没有选择退出的选项

相比之下，通过将范围缩小到特定的 ERC-20 代币，本提案标准化了一个与任何现有或未来 ERC-20 智能合约兼容的 ERC-20 扩展，实施和维护起来更简单，可以被撤回或嵌套，并为所有用户提供了明确选择升级的双重确认机会。

[ERC-4931](./eip-4931.md) 试图通过引入第三个“桥接”合约来解决同样的问题，以帮助促进升级/降级操作。虽然这种设计将升级/降级逻辑与代币逻辑解耦，但 ERC-4931 将要求代币在目标智能合约中预铸造并由桥接合约拥有，而不是在调用升级时即时铸造。它也无法支持如下面所述的升级-转移和透视功能。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“NOT RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

```solidity
pragma solidity ^0.8.0;

/**
    @title Upgradable Fungible Token
    @dev See https://eips.ethereum.org/EIPS/eip-6864
 */
interface IERC6864 is IERC20 {
    /**
      @dev MUST be emitted when tokens are upgraded
      @param from Previous owner of base ERC-20 tokens
      @param to New owner of ERC-6864 tokens
      @param amount The amount that is upgraded
    */
    event Upgrade(address indexed from, address indexed to, uint256 amount);

    /**
      @dev MUST be emitted when tokens are downgraded
      @param from Previous owner of ERC-6864 tokens
      @param to New owner of base ERC-20 tokens
      @param amount The amount that is downgraded
    */
    event Downgrade(address indexed from, address indexed to, uint256 amount);

    /**
      @notice Upgrade `amount` of base ERC-20 tokens owned by `msg.sender` to ERC-6864 tokens under `to`
      @dev `msg.sender` must directly own sufficient base ERC-20 tokens
      MUST revert if `to` is the zero address
      MUST revert if `msg.sender` does not directly own `amount` or more of base ERC-20 tokens
      @param to The address to receive ERC-6864 tokens
      @param amount The amount of base ERC-20 tokens to upgrade
    */
    function upgrade(address to, uint256 amount) external;

    /**
      @notice Downgrade `amount` of ERC-6864 tokens owned by `from` to base ERC-20 tokens under `to`
      @dev `msg.sender` must either directly own or be approved to spend sufficient ERC-6864 tokens for `from`
      MUST revert if `to` is the zero address
      MUST revert if `from` does not directly own `amount` or more of ERC-6864 tokens
      MUST revret if `msg.sender` is not `from` and is not approved to spend `amount` or more of ERC-6864 tokens for `from`
      @param from The address to release ERC-6864 tokens
      @param to The address to receive base ERC-20 tokens
      @param amount The amount of ERC-6864 tokens to downgrade
    */
    function downgrade(address from, address to, uint256 amount) external;

    /**
      @notice Get the base ERC-20 smart contract address
      @return The address of the base ERC-20 smart contract
    */
    function baseToken() external view returns (address);
}
```

### 透视扩展

**透视扩展**是可选的。它允许轻松查看此 [ERC-6864](./eip-6864.md) 与基础 ERC-20 智能合约之间的组合状态。

```solidity
pragma solidity ^0.8.0;

interface IERC6864SeeThrough is IERC6864 {
  /**
    @notice Get the combined total token supply between this ERC-6864 and base ERC-20 smart contracts
    @return The combined total token supply
  */
  function combinedTotalSupply() external view returns (uint256);

  /**
    @notice Get the combined token balance of `account` between this ERC-6864 and base ERC-20 smart contracts
    @param account The address that owns the tokens
    @return The combined token balance
  */
  function combinedBalanceOf(address account) external view returns (uint256);

  /**
    @notice Get the combined allowance that `spender` is allowed to spend for `owner` between this ERC-6864 and base ERC-20 smart contracts
    @param owner The address that owns the tokens
    @param spender The address that is approve to spend the tokens
    @return The combined spending allowance
  */
  function combinedAllowance(address owner, address spender) external view returns (uint256);
}

```

## 理由

### 扩展 ERC-20 标准

本提案的目标是在不影响用户余额的情况下进行升级，因此利用现有的数据结构和方法是最少工程努力的路径，也是最具互操作性的。

### 支持降级

降级的能力使得在同一基础 ERC-20 智能合约上在多个 IERC-6864 实现之间移动成为可能。它还提供了一种解决方案，以防在 ERC-6864 智能合约上发现错误或限制，或者用户简单地改变主意。

### 可选的透视扩展

虽然这些功能在许多情况下是有用的，但它们的实现是微不足道的，结果可以通过其他公共函数计算，因此决定将它们包含在可选扩展中，而不是核心接口。

## 向后兼容性

ERC-6864 与 ERC-20 标准通常兼容。唯一的警告是某些智能合约可能选择实现 `transfer` 以处理整个组合余额（这减少了用户摩擦，参见参考实现），而不是标准的 `balanceOf` 数量。在这种情况下，建议该合约实现 `totalSupply` 和 `balanceOf` 以返回此 ERC-6864 与基础 ERC-20 智能合约之间的组合金额。

## 参考实现

```solidity
import {IERC20, ERC20} from "@openzeppelin-contracts/token/ERC20/ERC20.sol";

contract ERC6864 is IERC6864, ERC20 {
  IERC20 private immutable s_baseToken;

    constructor(string memory name, string memory symbol, address baseToken_) ERC20(name, symbol) {
        s_baseToken = IERC20(baseToken_);
    }

    function baseToken() public view virtual override returns (address) {
        return address(s_baseToken);
    }

    function upgrade(address to, uint256 amount) public virtual override {
        address from = _msgSender();

        s_baseToken.transferFrom(from, address(this), amount);
        _mint(to, amount);

        emit Upgrade(from, to, amount);
    }

    function downgrade(address from, address to, uint256 amount) public virtual override {
        address spender = _msgSender();

        if (from != spender) {
            _spendAllowance(from, spender, amount);
        }
        _burn(from, amount);
        s_baseToken.transfer(to, amount);

        emit Downgrade(from, to, amount);
    }

    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address from = _msgSender();
        uint256 balance = balanceOf(from);

        if (balance < amount) {
            upgrade(from, amount - balance);
        }

        _transfer(from, to, amount);
        return true;
    }

    function totalSupply() public view virtual override returns (uint256) {
        return return super.totalSupply() + s_baseToken.totalSupply() - s_baseToken.balanceOf(address(this));
    }

    function balanceOf(address account) public view virtual override returns (uint256) {
        return super.balanceOf(account) + s_baseToken.balanceOf(account);
    }
}
```

## 安全考虑

- 选择升级基础 ERC-20 代币的用户必须首先 `approve` ERC-6864 智能合约以支出它们。因此，用户有责任验证 ERC-6864 智能合约是否安全可靠，以及他或她批准的金额是否合适。这与任何 `approve` 操作的安全考虑相同。
- ERC-6864 智能合约可以根据需要实现任何转换函数进行升级/降级：1 对 1、线性、非线性。在非线性转换函数的情况下，`upgrade` 和 `downgrade` 可能会受到前置运行或夹击攻击的影响（无论是否对攻击者有利）。这与使用类似非线性曲线进行转换的任何自动化做市商（AMM）的安全考虑相同。
- ERC-6864 智能合约可能会要求用户批准无限制的授权和/或尝试在 `transfer` 期间自动升级（参见参考实现）。这消除了用户三重确认其升级意图的机会（`approve` 是双重确认）。
- 多个 IERC-6864 实现可以应用于同一基础 ERC-20 代币，并且 ERC-6864 智能合约可以嵌套。这将增加代币的复杂性，并可能导致现有仪表板报告不正确或不一致的结果。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。