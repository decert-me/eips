---
eip: 6956
title: 资产绑定非同质化代币
description: 资产绑定 NFT 将代币 1:1 锚定到资产上，操作通过对资产控制的预言机证明进行授权
author: Thomas Bergmueller (@tbergmueller), Lukas Meyer (@ibex-technology)
discussions-to: https://ethereum-magicians.org/t/erc-6956-asset-bound-non-fungible-tokens/14056
status: Review
type: Standards Track
category: ERC
created: 2023-04-29
requires: 165, 721
---

## 摘要

该标准允许通过扩展 [ERC-721](eip-721.md) 将物理和数字资产集成到 dApps/web3 中，而无需签名能力。

资产，例如物理对象，使用唯一可识别的锚进行标记。锚以安全且不可分割的方式 1:1 绑定到链上的 NFT 上——在资产的整个生命周期内。

通过证明，预言机证明在定义某些操作（铸造、转移、销毁、批准等）的`to`地址时，特定的与锚相关的资产已被控制。预言机在链下签署证明。通过在链上验证证明是否已由受信任的预言机签署来授权操作。请注意，授权仅通过证明提供，换句话说，通过对资产的控制证明。资产的控制者保证是资产绑定 NFT 的控制者。

提议的通过证明授权的操作，如`transferAnchor(attestation)`是无权限的，这意味着当前所有者（`from`地址）和接收者（`to`地址）都不需要签名。

图 1 显示了资产绑定 NFT 转移的数据流。简化的系统使用智能手机作为用户设备与物理资产进行交互并指定`to`地址。

![图 1：示例系统](../assets/eip-6956/img/erc6956_concept.svg)

## 动机

众所周知的 [ERC-721](eip-721.md) 确立了 NFT 可以代表“对物理财产的所有权[...]以及数字收藏品，甚至更抽象的事物，如责任”——从更广泛的意义上讲，我们将所有这些事物称为资产，这些资产通常对人们有价值。

### 问题

ERC-721 概述了“NFT 可以代表对数字或物理资产的所有权”。当用于代表对数字链上资产的所有权时，ERC-721 在这方面表现出色，即当资产“持有特定合约的代币”或资产是 NFT 的元数据时。如今，人们通常将 NFT 的元数据（图像、特征等）视为资产类别，其稀缺性通常直接定义了单个 NFT 的价值。

然而，我们看到 ERC-721 无法解决的完整性问题，主要是在 NFT 用于代表链外资产（“对物理产品的所有权”、“数字收藏品”、“游戏内资产”、“责任”等）时。在资产的生命周期中，资产的所有权和占有状态会多次变化，有时甚至数千次。每一次状态变化可能会导致相关方的义务和特权的转移。因此，*没有*强制性地将资产相关的义务和属性锚定到代币上的资产的代币化是不完整的。如今，链外资产通常通过将资产标识符添加到 NFT 的元数据中“锚定”。

**NFT-资产完整性：** 与 NFT 投资者的普遍看法相反，元数据是数据，往往是可变的且在链外。因此，通过存储在可变元数据中的资产标识符与 NFT 之间的链接，充其量可以被视为脆弱。

确保元数据（=对资产的引用）与代币之间完整性的方法存在。这通常通过在链上存储元数据哈希来实现。通过哈希会出现额外问题；对于许多应用，元数据（除了资产标识符）应该是可更新的。因此，通过存储哈希使元数据不可变是有问题的。此外，通过 tokenURI 指定的链外元数据资源必须永远可用，这在历史上曾遭遇失败（IPFS 桶消失，中心化 tokenURI 提供者出现停机等）。

**链外-链上完整性：** 有一些方法可以通过拥有链上表示来强制或条件链外资产的所有权。常见的方法是燃烧代币以获得（物理）资产，因为完整性无法维持。然而，目前没有已知的方法可以通过拥有链外资产的所有权来强制链上所有权。特别是当当前 NFT 的所有者不合作或无能为力时，完整性通常由于缺乏当前 NFT 所有者的签名权而失败。

元数据在链外。大多数实现完全忽视元数据是可变的。更严肃的实现努力通过例如对元数据进行哈希并将哈希映射到链上的 tokenId 来保持完整性。然而，这种方法不允许使用案例，其中元数据除了资产标识符外，例如特征、“游戏时长”等，应该是可变或可演变的。

### 资产绑定非同质化代币

在本标准中，我们提议

1. 通过将资产不可分割地锚定到 NFT 上，提升代表物理或数字链外资产的概念。
1. 对资产的链外控制必须意味着对锚定 NFT 的链上控制。
1. （相关）链外资产的所有权变化必然应通过锚定 NFT 的链上所有权变化来反映，即使当前所有者不合作或无能为力。

正如 2 和 3 所示，资产的控制/所有权/占有应是唯一的真实来源，而*不是*NFT 的占有。因此，我们提议一种资产绑定 NFT，其中对资产的链外控制强制对锚定 NFT 的链上控制。
此外，提议的资产绑定 NFT 还允许将数字元数据不可分割地锚定到资产上。当资产是物理资产时，这允许以最纯粹的形式设计“物理数字资产”，即创建一个具有不可分割的物理和数字组件的“物理数字”资产。请注意，元数据本身仍然可以更改，例如对于“可演变 NFT”。

我们提议通过另一种机制补充现有的根据 [ERC-721](eip-721.md) 的代币转移控制机制；证明。证明由预言机在链下签署，只有在预言机验证指定`to`地址或受益人地址的人同时控制资产时，才可以发出。证明的`to`地址可用于转移以及批准和其他授权。

通过证明授权的交易不需要来自`from`（捐赠者、所有者、发送者）或`to`（受益人、接收者）账户的签名或批准，即使转移是无权限的。理想情况下，交易也应独立于预言机进行签名，从而允许在 gas 费用方面的不同场景。

最后，我们想提到使用提议标准的两个主要附带好处，这大大降低了 Web2 用户的入门障碍并提高了他们的安全性；

- 新用户，例如`0xaa...aa`（图 1），可以使用无 gas 钱包，因此可以参与 Web3/dApps/DeFi，并在从未拥有加密货币的情况下铸造和转移代币。Gas 费用可以通过第三方账户`0x..gasPayer`（图 1）支付。Gas 通常由资产发行者承担，他们签署`transferAnchor()`交易。
- 用户无法被欺骗。常见攻击（例如钱包排水骗局）不再可能或容易恢复，因为只能盗取锚定的 NFT，而不是资产本身。此外，诸如将 NFT 转移到错误账户、失去对账户的访问等意外情况，可以通过基于对资产的控制证明执行另一个`transferAnchor()`交易来减轻，即物理对象。
### 相关工作

我们的主要目标是将物理或数字资产接入 dApps，这些资产本身没有签名能力（与其他依赖于加密芯片的提案相反）。请注意，我们并不认为有任何限制阻止将此类解决方案与该标准结合使用，因为加密芯片的地址符合锚点的定义。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按照 RFC 2119 和 RFC 8174 中的描述进行解释。

### 定义（按字母顺序）

- **锚点** 唯一标识离线资产，无论是物理的还是数字的。
- **锚点技术** 必须确保
  - 锚点与资产（物理或其他）不可分割
  - 一个预言机可以在合理怀疑之外建立对资产的控制证明
  - 对于物理资产，必须考虑额外的 [物理资产的安全考虑](#security-considerations-for-physical-assets)

- **资产** 指通过提议的标准表示的“事物”，无论是物理的还是数字的。通常，资产没有签名能力。

- **证明** 是在指定 `to`（接收者，受益人）地址时确认控制证明已建立。

- **控制证明** 意味着拥有或以其他方式控制资产。控制证明的建立方式取决于资产，并可能通过技术、法律或其他手段实现。对于物理资产，控制通常通过证明物理资产与用于指定 `to` 地址的输入设备（例如智能手机）之间的物理接近性来验证。

- 一个 **预言机** 具有签名能力。必须能够以一种可以在链上验证签名的方式在链外签署证明。

### 基础接口

每个符合该标准的合约必须实现 [提议的标准接口](../assets/eip-6956/contracts/IERC6956.sol)、[ERC-721](eip-721.md) 和 [ERC-165](eip-165.md) 接口，并遵循以下 [注意事项](#caveats-for-base-interface)：

```solidity
// SPDX-License-Identifier: MIT OR CC0-1.0
pragma solidity ^0.8.18;

/**
 * @title IERC6956 Asset-Bound Non-Fungible Tokens 
 * @notice Asset-bound Non-Fungible Tokens anchor a token 1:1 to a (physical or digital) asset and token transfers are authorized through attestation of control over the asset
 * @dev See https://eips.ethereum.org/EIPS/eip-6956
 *      Note: The ERC-165 identifier for this interface is 0xa9cf7635
 */
interface IERC6956 {
   
    /** @dev Authorization, typically mapped to authorizationMaps, where each bit indicates whether a particular ERC6956Role is authorized 
     *      Typically used in constructor (hardcoded or params) to set burnAuthorization and approveAuthorization
     *      Also used in optional updateBurnAuthorization, updateApproveAuthorization, I
     */ 
    enum Authorization {
        NONE,               // = 0,      // None of the above
        OWNER,              // = (1<<OWNER), // The owner of the token, i.e. the digital representation
        ISSUER,             // = (1<<ISSUER), // The issuer of the tokens, i.e. this smart contract
        ASSET,              // = (1<<ASSET), // The asset, i.e. via attestation
        OWNER_AND_ISSUER,   // = (1<<OWNER) | (1<<ISSUER),
        OWNER_AND_ASSET,    // = (1<<OWNER) | (1<<ASSET),
        ASSET_AND_ISSUER,   // = (1<<ASSET) | (1<<ISSUER),
        ALL                 // = (1<<OWNER) | (1<<ISSUER) | (1<<ASSET) // Owner + Issuer + Asset
    }
    
    /**
     * @notice This emits when approved address for an anchored tokenId is changed or reaffirmed via attestation
     * @dev This emits when approveAnchor() is called and corresponds to ERC-721 behavior
     * @param owner The owner of the anchored tokenId
     * @param approved The approved address, address(0) indicates there is no approved address
     * @param anchor The anchor, for which approval has been changed
     * @param tokenId ID (>0) of the anchored token
     */
    event AnchorApproval(address indexed owner, address approved, bytes32 indexed anchor, uint256 tokenId);

    /**
     * @notice This emits when the ownership of any anchored NFT changes by any mechanism
     * @dev This emits together with tokenId-based ERC-721.Transfer and provides an anchor-perspective on transfers
     * @param from The previous owner, address(0) indicate there was none.
     * @param to The new owner, address(0) indicates the token is burned
     * @param anchor The anchor which is bound to tokenId
     * @param tokenId ID (>0) of the anchored token
     */
    event AnchorTransfer(address indexed from, address indexed to, bytes32 indexed anchor, uint256 tokenId);
    /**
     * @notice This emits when an attestation has been used indicating no second attestation with the same attestationHash will be accepted
     * @param to The to address specified in the attestation
     * @param anchor The anchor specified in the attestation
     * @param attestationHash The hash of the attestation, see ERC-6956 for details
     * @param totalUsedAttestationsForAnchor The total number of attestations already used for the particular anchor
     */
    event AttestationUse(address indexed to, bytes32 indexed anchor, bytes32 indexed attestationHash, uint256 totalUsedAttestationsForAnchor);

    /**
     * @notice This emits when the trust-status of an oracle changes. 
     * @dev Trusted oracles must explicitly be specified. 
     *      If the last event for a particular oracle-address indicates it's trusted, attestations from this oracle are valid.
     * @param oracle Address of the oracle signing attestations
     * @param trusted indicating whether this address is trusted (true). Use (false) to no longer trust from an oracle.
     */
    event OracleUpdate(address indexed oracle, bool indexed trusted);

    /**
     * @notice Returns the 1:1 mapped anchor for a tokenId
     * @param tokenId ID (>0) of the anchored token
     * @return anchor The anchor bound to tokenId, 0x0 if tokenId does not represent an anchor
     */
    function anchorByToken(uint256 tokenId) external view returns (bytes32 anchor);
    /**
     * @notice Returns the ID of the 1:1 mapped token of an anchor.
     * @param anchor The anchor (>0x0)
     * @return tokenId ID of the anchored token, 0 if no anchored token exists
     */
    function tokenByAnchor(bytes32 anchor) external view returns (uint256 tokenId);

    /**
     * @notice The number of attestations already used to modify the state of an anchor or its bound tokens
     * @param anchor The anchor(>0)
     * @return attestationUses The number of attestation uses for a particular anchor, 0 if anchor is invalid.
     */
    function attestationsUsedByAnchor(bytes32 anchor) view external returns (uint256 attestationUses);
    /**
     * @notice Decodes and returns to-address, anchor and the attestation hash, if the attestation is valid
     * @dev MUST throw when
     *  - Attestation has already been used (an AttestationUse-Event with matching attestationHash was emitted)
     *  - Attestation is not signed by trusted oracle (the last OracleUpdate-Event for the signer-address does not indicate trust)
     *  - Attestation is not valid yet or expired
     *  - [if IERC6956AttestationLimited is implemented] attestationUsagesLeft(attestation.anchor) <= 0
     *  - [if IERC6956ValidAnchors is implemented] validAnchors(data) does not return true. 
     * @param attestation The attestation subject to the format specified in ERC-6956
     * @param data Optional additional data, may contain proof as the first abi-encoded argument when IERC6956ValidAnchors is implemented
     * @return to Address where the ownership of an anchored token or approval shall be changed to
     * @return anchor The anchor (>0)
     * @return attestationHash The attestation hash computed on-chain as `keccak256(attestation)`
     */
    function decodeAttestationIfValid(bytes memory attestation, bytes memory data) external view returns (address to, bytes32 anchor, bytes32 attestationHash);

    /**
     * @notice Indicates whether any of ASSET, OWNER, ISSUER is authorized to burn
     */
    function burnAuthorization() external view returns(Authorization burnAuth);

    /**
     * @notice Indicates whether any of ASSET, OWNER, ISSUER is authorized to approve
     */
    function approveAuthorization() external view returns(Authorization approveAuth);

    /**
     * @notice Corresponds to transferAnchor(bytes,bytes) without additional data
     * @param attestation Attestation, refer ERC-6956 for details
     */
    function transferAnchor(bytes memory attestation) external;

    /**
     * @notice Changes the ownership of an NFT mapped to attestation.anchor to attestation.to address.
     * @dev Permissionless, i.e. anybody invoke and sign a transaction. The transfer is authorized through the oracle-signed attestation.
     *  - Uses decodeAttestationIfValid()
     *  - When using a centralized "gas-payer" recommended to implement IERC6956AttestationLimited.
     *  - Matches the behavior of ERC-721.safeTransferFrom(ownerOf[tokenByAnchor(attestation.anchor)], attestation.to, tokenByAnchor(attestation.anchor), ..) and mint an NFT if `tokenByAnchor(anchor)==0`.
     *  - Throws when attestation.to == ownerOf(tokenByAnchor(attestation.anchor))
     *  - Emits AnchorTransfer  
     *  
     * @param attestation Attestation, refer ERC-6956 for details
     * @param data Additional data, may be used for additional transfer-conditions, may be sent partly or in full in a call to safeTransferFrom
     * 
     */
    function transferAnchor(bytes memory attestation, bytes memory data) external;

     /**
     * @notice Corresponds to approveAnchor(bytes,bytes) without additional data
     * @param attestation Attestation, refer ERC-6956 for details
     */
    function approveAnchor(bytes memory attestation) external;

     /**
     * @notice Approves attestation.to the token bound to attestation.anchor. .
     * @dev Permissionless, i.e. anybody invoke and sign a transaction. The transfer is authorized through the oracle-signed attestation.
     *  - Uses decodeAttestationIfValid()
     *  - When using a centralized "gas-payer" recommended to implement IERC6956AttestationLimited.
     *  - Matches the behavior of ERC-721.approve(attestation.to, tokenByAnchor(attestation.anchor)).
     *  - Throws when ASSET is not authorized to approve.
     * 
     * @param attestation Attestation, refer ERC-6956 for details 
     */
    function approveAnchor(bytes memory attestation, bytes memory data) external;

    /**
     * @notice Corresponds to burnAnchor(bytes,bytes) without additional data
     * @param attestation Attestation, refer ERC-6956 for details
     */
    function burnAnchor(bytes memory attestation) external;
   
    /**
     * @notice Burns the token mapped to attestation.anchor. Uses ERC-721._burn.
     * @dev Permissionless, i.e. anybody invoke and sign a transaction. The transfer is authorized through the oracle-signed attestation.
     *  - Uses decodeAttestationIfValid()
     *  - When using a centralized "gas-payer" recommended to implement IERC6956AttestationLimited.
     *  - Throws when ASSET is not authorized to burn
     * 
     * @param attestation Attestation, refer ERC-6956 for details
     */
    function burnAnchor(bytes memory attestation, bytes memory data) external;
}
```

#### 基础接口的注意事项

- 必须实现 ERC-721 和 ERC-165
- 必须具有双向映射 `tokenByAnchor(anchor)` 和 `anchorByToken(tokenId)`。这意味着每个锚点最多存在一个代币。
- 必须有机制来确定锚点是否对合约有效。建议实现提议的 [有效锚点接口](#validanchors-interface)
- 必须实现 `decodeAttestationIfValid(attestation, data)` 以验证和解码证明，如 [预言机部分](#oracle) 中所述
  - 必须返回 `attestation.to`、`attestation.anchor`、`attestation.attestationHash`。
  - 必须不修改状态，因为此函数可用于检查证明的有效性而不进行赎回。
  - 必须在以下情况下抛出异常
    - 证明未由受信任的预言机签名。
    - 证明已过期或尚未生效
    - 证明尚未被赎回。“赎回”被定义为至少一个状态变更操作已通过特定证明获得授权。
    - 如果实现了 [证明限制接口](#attestationlimited-interface)：当 `attestationUsagesLeft(attestation.to) <= 0`
    - 如果实现了 [有效锚点接口](#validanchors-interface)：当 `validAnchor() != true`。
  - 如果实现了 [有效锚点接口](#validanchors-interface)：必须调用 `validAnchor(attestation.to, abi.decode('bytes32[]',data))`，这意味着 `data` 参数中的第一个 abi 编码值对应于 `proof`。
- 必须有一个锚点释放机制，指示锚定的 NFT 是否已释放/可转让。
  - 任何锚点默认不得释放。
- 必须扩展任何 ERC-721 代币转移机制：
  - 必须在锚点未释放时抛出异常。
  - 必须在批量大小 > 1 时抛出异常，即此合约不支持批量转移。
  - 必须发出 `AnchorTransfer(from, to, anchorByToken[tokenId], tokenId)`

- 必须实现 `attestationsUsedByAnchor(anchor)`，返回特定锚点已使用的证明数量。

- 必须实现状态变更的 `transferAnchor(..)`、`burnAnchor(..)`、`approveAnchor(..)`，并可选择性地实现额外的状态变更操作：
  - 必须使用 `decodeAttestationIfValid()` 来确定 `to`、`anchor` 和 `attestationHash`
  - 必须在与任何授权的状态变更操作相同的交易中赎回每个证明。建议存储每个已使用的 `attestationHash`
  - 必须增加 `attestationsUsedByAnchor[anchor]`
  - 必须发出 `AttestationUsed`
  - `transferAnchor(attestation)` 必须像 `ERC-721.safeTransferFrom(ownerOf[tokenByAnchor(attestation.anchor)], attestation.to, tokenByAnchor(attestation.anchor), ..)` 一样行为并发出事件，并在 `tokenByAnchor(anchor)==0` 时铸造 NFT。

- 建议实现 `tokenURI(tokenId)` 以返回基于锚点的 URI，即 `baseURI/anchor`。将此锚定元数据与资产关联。在锚点首次使用之前，锚点与 tokenId 的映射是未知的。因此，优先使用锚点而不是 tokenId。

### 预言机

- 必须提供证明。下面我们定义预言机如何证明转移的 `to` 地址是在与特定锚点相关的控制证明的前提下指定的。
- 证明必须 abi 编码以下内容：
  - `to`，必须是地址，指定受益人，例如目标地址、批准的账户等。
  - 锚点，即资产标识符，必须与资产具有 1:1 的关系
  - `attestationTime`，UTC 秒，证明由预言机签署的时间，
  - `validStartTime` UTC 秒，证明有效期的开始时间
  - `validEndTime`，UTC 秒，证明有效期的结束时间
  - `signature`，ETH 签名（65 字节）。预言机签署 `attestationHash = keccak256([to, anchor, attestationTime, validStartTime, validEndTime])` 的输出。
- 如何通过锚点技术详细建立控制证明不在本标准的范围内。某些预言机要求和使用物理资产时的锚点技术要求在 [物理资产的安全考虑](#security-considerations-for-physical-assets) 中进行了概述。

生成证明的最小 TypeScript 示例可在本提案的 [参考实现部分](#reference-implementation) 中找到。

### 证明限制接口

每个符合该标准的合约可以实现 [提议的证明限制接口](../assets/eip-6956/contracts/IERC6956AttestationLimited.sol)，并遵循以下 [注意事项](#caveats-for-attestationlimited-interface)：

```solidity
// SPDX-License-Identifier: MIT OR CC0-1.0
pragma solidity ^0.8.18;
import "./IERC6956.sol";

/**
 * @title Attestation-limited Asset-Bound NFT
 * @dev See https://eips.ethereum.org/EIPS/eip-6956
 *      Note: The ERC-165 identifier for this interface is 0x75a2e933
 */
interface IERC6956AttestationLimited is IERC6956 {
  enum AttestationLimitPolicy {
    IMMUTABLE,
    INCREASE_ONLY,
    DECREASE_ONLY,
    FLEXIBLE
  }
      
  /// @notice Returns the attestation limit for a particular anchor
  /// @dev MUST return the global attestation limit per default
  ///      and override the global attestation limit in case an anchor-based limit is set
  function attestationLimit(bytes32 anchor) external view returns (uint256 limit);

  /// @notice Returns number of attestations left for a particular anchor
  /// @dev Is computed by comparing the attestationsUsedByAnchor(anchor) and the current attestation limit 
  ///      (current limited emitted via GlobalAttestationLimitUpdate or AttestationLimit events)
  function attestationUsagesLeft(bytes32 anchor) external view returns (uint256 nrTransfersLeft);

  /// @notice Indicates the policy, in which direction attestation limits can be updated (globally or per anchor)
  function attestationLimitPolicy() external view returns (AttestationLimitPolicy policy);

  /// @notice This emits when the global attestation limit is updated
  event GlobalAttestationLimitUpdate(uint256 indexed transferLimit, address updatedBy);

  /// @notice This emits when an anchor-specific attestation limit is updated
  event AttestationLimitUpdate(bytes32 indexed anchor, uint256 indexed tokenId, uint256 indexed transferLimit, address updatedBy);

  /// @dev This emits in the transaction, where attestationUsagesLeft becomes 0
  event AttestationLimitReached(bytes32 indexed anchor, uint256 indexed tokenId, uint256 indexed transferLimit);
}
```

#### 证明限制接口的注意事项

- 必须扩展提议的标准接口
- 必须定义上述列出的证明限制更新策略之一，并通过 `attestationLimitPolicy()` 公开
  - 必须支持不同的更新模式，即固定、仅增加、仅减少、灵活（= 可增加和可减少）
  - 建议具有全局转移限制，可以在代币基础上覆盖（当 `attestationLimitPolicy() != FIXED` 时）
- 必须实现 `attestationLimit(anchor)`，指定锚点可以被转移的总次数。返回值的变化必须反映证明限制策略。
- 必须实现 `attestationUsagesLeft(anchor)`，返回特定锚点剩余的使用次数（即 `attestationLimit(anchor)-attestationsUsedByAnchor[anchor]`）。
### Floatable-Interface

每个符合此扩展的合约可以实现提议的 [Floatable interface](../assets/eip-6956/contracts/IERC6956Floatable.sol)，并受以下 [注意事项](#caveats-for-floatable-interface) 的约束：

```solidity
// SPDX-License-Identifier: MIT OR CC0-1.0
pragma solidity ^0.8.18;
import "./IERC6956.sol";

/**
 * @title Floatable Asset-Bound NFT
 * @notice A floatable Asset-Bound NFT can (temporarily) be transferred without attestation
 * @dev See https://eips.ethereum.org/EIPS/eip-6956
 *      Note: The ERC-165 identifier for this interface is 0xf82773f7
 */
interface IERC6956Floatable is IERC6956 {
  enum FloatState {
    Default, // 0, inherits from floatAll
    Floating, // 1
    Anchored // 2
  }

  /// @notice Indicates that an anchor-specific floating state changed
  event FloatingStateChange(bytes32 indexed anchor, uint256 indexed tokenId, FloatState isFloating, address operator);
  /// @notice Emits when FloatingAuthorization is changed.
  event FloatingAuthorizationChange(Authorization startAuthorization, Authorization stopAuthorization, address maintainer);
  /// @notice Emits, when the default floating state is changed
  event FloatingAllStateChange(bool areFloating, address operator);

  /// @notice Indicates whether an anchored token is floating, namely can be transferred without attestation
  function floating(bytes32 anchor) external view returns (bool);
  
  /// @notice Indicates whether any of OWNER, ISSUER, (ASSET) is allowed to start floating
  function floatStartAuthorization() external view returns (Authorization canStartFloating);
  
  /// @notice Indicates whether any of OWNER, ISSUER, (ASSET) is allowed to stop floating
  function floatStopAuthorization() external view returns (Authorization canStartFloating);

  /**
    * @notice Allows to override or reset to floatAll-behavior per anchor
    * @dev Must throw when newState == Floating and floatStartAuthorization does not authorize msg.sender
    * @dev Must throw when newState == Anchored and floatStopAuthorization does not authorize msg.sender
    * @param anchor The anchor, whose anchored token shall override default behavior
    * @param newState Override-State. If set to Default, the anchor will behave like floatAll
    */
  function float(bytes32 anchor, FloatState newState) external;    
}
```

#### Caveats for Floatable-Interface

如果 `floating(anchor)` 返回 true，则由 `tokenByAnchor(anchor)` 标识的代币必须可以在没有证明的情况下转让，通常通过 `ERC721.isApprovedOrOwner(msg.sender, tokenId)` 授权。

### ValidAnchors-Interface

每个符合此扩展的合约可以实现提议的 [ValidAnchors interface](../assets/eip-6956/contracts/IERC6956ValidAnchors.sol)，并受以下 [注意事项](#caveats-for-validanchors-interface) 的约束：

```solidity
// SPDX-License-Identifier: MIT OR CC0-1.0
pragma solidity ^0.8.18;
import "./IERC6956.sol";

/**
 * @title Anchor-validating Asset-Bound NFT
 * @dev See https://eips.ethereum.org/EIPS/eip-6956
 *      Note: The ERC-165 identifier for this interface is 0x051c9bd8
 */
interface IERC6956ValidAnchors is IERC6956 {
    /**
     * @notice Emits when the valid anchors for the contract are updated.
     * @param validAnchorHash Hash representing all valid anchors. Typically Root of Merkle-Tree
     * @param maintainer msg.sender when updating the hash
     */
    event ValidAnchorsUpdate(bytes32 indexed validAnchorHash, address indexed maintainer);

    /**
     * @notice Indicates whether an anchor is valid in the present contract
     * @dev Typically implemented via MerkleTrees, where proof is used to verify anchor is part of the MerkleTree 
     *      MUST return false when no ValidAnchorsUpdate-event has been emitted yet
     * @param anchor The anchor in question
     * @param proof Proof that the anchor is valid, typically MerkleProof
     * @return isValid True, when anchor and proof can be verified against validAnchorHash (emitted via ValidAnchorsUpdate-event)
     */
    function anchorValid(bytes32 anchor, bytes32[] memory proof) external view returns (bool isValid);        
}
```

#### Caveats for ValidAnchors-Interface

- 必须实现 `validAnchor(anchor, proof)`，当 anchor 有效时返回 true，即 MerkleProof 正确，否则返回 false。

## Rationale

**为什么使用 anchor<>tokenId 映射而不是直接使用 tokenIds？**
特别对于可收藏的用例，特殊或顺序的 tokenIds（例如低数字）具有价值。持有者可能会为获得 tokenId=1 而感到自豪，或者与 tokenId=1 相关的链下资产可能会增值，因为它是首次被认领的。或者发行者可能希望联系前 100 位认领其资产绑定 NFT 的所有者。虽然这些用例在技术上可以通过观察区块链状态变化来覆盖，但我们认为在 tokenIds 中反映顺序是用户友好的方式。请参阅 [安全考虑](#security-considerations) 了解为什么应避免顺序锚点。

**为什么 tokenId=0 和 anchor=0x0 无效？**
出于 gas 效率考虑。这允许省略对代币或锚点存在性的检查和状态变量，因为不存在的键的映射返回 0，且无法与 anchor=0 或 tokenId=0 轻易区分。

**资产通常是批量生产的，目标是具有相同的属性，例如一批汽车备件。为什么要扩展 ERC-721 而不是多代币标准？**
即使（物理）资产是以可替代特性大规模生产的，每个资产都有一个独特的属性/所有权图，因此应以非同质化的方式表示。因此，此 EIP 遵循设计决策，即资产（通过称为锚点的唯一资产标识符表示）和代币始终以 1-1 的方式映射，而不是 1-N，以便代币表示资产的个体属性图。

**为什么有 burnAnchor() 和 approveAnchor()？**
由于无权限的特性，资产绑定的 NFT 甚至可以转移到或从任何地址。这包括任意和随机生成的账户（私钥未知）以及传统上不支持 ERC-721 NFT 的智能合约。因此，拥有资产必须等同于拥有 NFT，这意味着我们还需要通过授权操作的证明来支持 ERC-721 操作，如批准和销毁。

**考虑的实现替代方案** 灵魂绑定的销毁+铸造组合，例如通过共识灵魂绑定代币（[ERC-5484](eip-5484.md)）。被忽略，因为当同一资产通过多个代币在时间上表示时，外观高度可疑。此 EIP 的前身使用了这种方法，并可以在孟买测试网的地址 `0xd04c443913f9ddcfea72c38fed2d128a3ecd719e` 部署找到。

**何时应实现 AttestationLimited-Interface**
自然，当您的用例要求每个资产仅限转让有限次数时。但出于安全原因，请参见 [安全考虑](#security-considerations)。

**为什么有 `IERC6956Floatable.FloatState` 枚举？** 为了允许 gas 高效实现 floatAll()，可以通过基于锚点的浮动在所有组合中被覆盖。（请参见上面关于 tokenId=0 的理由）。

**为什么没有 `floating(tokenId)` 函数？**
这将与许多其他 EIP 中提议的 `isTransferable(tokenId,...)` 机制表现相同（参见例如 [ERC-6454](eip-6454.md)）。此外，可以使用提议的 `floating(anchorByToken(tokenId))`。

**为什么开始和停止有不同的 FloatingAuthorizations？**
根据用例，不同的角色应该能够启动或停止浮动。请注意，对于许多应用程序，发行者可能希望对集合的浮动性进行控制。

### 示例用例和推荐的接口组合

基于占有的用例由标准接口 `IERC6956` 覆盖：资产的持有者拥有资产。占有是一个重要的社会和经济工具：在许多体育比赛中，资产的占有，通常称为“球”，至关重要。占有可能伴随某些义务和特权。对资产的所有权可能伴随权利和利益，也可能承担留置权和义务。例如，拥有的资产可以用作抵押，可以出租，甚至可以产生回报。示例用例包括：

- **基于占有的代币门控：** 拥有限量 T 恤（资产）的俱乐部客人获得一个代币，允许他打开 VIP 休息室的门。

- **基于占有的数字双胞胎：** 一名玩家拥有一双实体运动鞋（资产），并获得一个数字双胞胎（NFT）以在元宇宙中穿戴。

- **稀缺的基于占有的数字双胞胎：** 运动鞋的生产商（资产）决定该产品包括 5 个数字双胞胎（NFT）的限制，以创造稀缺性。

- **可借出的数字双胞胎：** 玩家可以将他的运动鞋代币（NFT）借给元宇宙中的朋友，以便朋友可以跑得更快。

- **防止盗窃的所有权保障：** 如果资产在链下拥有，所有者希望保护锚定的 NFT，即不允许转让以防止盗窃或通过资产轻松恢复 NFT。

- **出售带抵押贷款的房屋：** 所有者持有 NFT 作为所有权证明。DeFi 银行为房屋融资并对 NFT 的转让施加锁定。允许 NFT 的转让需要抵押贷款偿还。链下出售资产（房屋）将是不可能的，因为不再可能为房屋融资。

- **出售带租约的房屋：** 租赁合同对资产的锚定 NFT 施加留置权。旧所有者解除锁定，新所有者购买并再融资房屋。NFT 的转让也将把留置权的义务和利益转移给新所有者。

- **以定金购买全新汽车：** 买方配置一辆汽车并提供定金，汽车将具有一个锚点。在汽车未生产之前，NFT 可以在 NFT 市场上浮动和交易。在资产交付时，NFT 的所有者有权提取汽车并有义务支付全款。

- **通过远期交易购买一桶石油：** 买方购买一桶石油（资产）的远期合约上的石油期权。在到期日，买方有义务提取石油。

下面的用例矩阵显示了示例用例必须实现的扩展和设置（除了 `IERC6956`！）以及相关配置。

请注意，对于下表中列出的 `Lockable`，提议的 EIP 可以扩展为任何已知的扩展 ERC-721 的锁定或留置机制，例如 [ERC-5192](eip-5192.md) 或 [ERC-6982](eip-6982.md)。我们建议在 `_beforeTokenTransfer()` 钩子中验证代币是否被锁定，因为它是从 `safeTransferFrom()` 和 `transferAnchor()` 调用的，因此适合阻止“标准” ERC-721 转让以及提议的基于证明的转让。
| 用例 | approveAuthorization | burnAuthorization | `IERC6956Floatable` | `IERC6956AttestationLimited` | 可锁定 |
|---------------|---|---|---|---|---|
| **管理占有权** |
| 代币门控  | ASSET | ANY | 不兼容 | - | - |
| 数字双胞胎  | ASSET | ANY | 不兼容 | - | - |
| 稀缺数字双胞胎 | ASSET | ANY | 不兼容 | 必需 | - |
| 可借贷数字双胞胎         | OWNER_AND_ASSET | ASSET | 必需 | - | - |
| **管理所有权** |
| 防止盗窃的所有权保障   | OWNER 或 OWNER_AND_ASSET | ANY | 可选 | - | 必需 |
| 以抵押贷款出售房屋  | ASSET  或 OWNER_AND_ASSET | ANY | 可选 | 可选 | 必需 |
| 以租赁出售房屋 | ASSET 或 OWNER_AND_ASSET | ANY | 可选 | 可选 | 必需 |
| 以首付款购买全新汽车 | ASSET 或 OWNER_AND_ASSET | ANY | 可选 | 可选 | 必需 |
| 通过远期交易购买一桶石油 | ASSET 或 OWNER_AND_ASSET | ANY | 可选 | 可选 | 必需 |

图例：

- 必需 ... 我们看不到如何在没有它的情况下实现该用例
- 不兼容 ... 这绝对不能实现，因为它对用例构成安全风险
- 可选 ... 这可以选择性地实现

## 向后兼容性

未发现向后兼容性问题。

该 EIP 完全兼容 ERC-721，并且（在扩展 `IERC6956Floatable` 接口时）对应于众所周知的 ERC-721 行为，增加了通过证明的授权机制。因此，我们建议 - 特别是对于实物资产 - 使用当前 EIP 而不是 ERC-721，并附加为 ERC-721 设计的扩展。

然而，建议将提议标准的实现扩展为一个接口，以指示 NFT 的可转让性以供市场使用。示例包括 [ERC-6454](eip-6454.md) 和 [ERC-5484](eip-5484.md)。

许多 ERC-721 扩展建议在转移方法中添加额外的抛出条件。该标准完全兼容，因为

- 常用的 ERC-721 `_beforeTokenTransfer()` 钩子必须在所有转移中调用，包括证明授权的转移。
- 在参考实现中建议使用 `_beforeAnchorUse()` 钩子，仅在使用证明作为授权时调用。

## 测试用例

测试用例可用：

- 仅实现 [提议的标准接口](../assets/eip-6956/contracts/IERC6956.sol) 的测试用例可以在 [这里](../assets/eip-6956/test/ERC6956.ts) 找到
- 实现 [提议的标准接口](../assets/eip-6956/contracts/IERC6956.sol)、[Floatable 扩展](../assets/eip-6956/contracts/IERC6956Floatable.sol) 、[ValidAnchors 扩展](../assets/eip-6956/contracts/IERC6956ValidAnchors.sol) 和 [AttestationLimited 扩展](../assets/eip-6956/contracts/IERC6956AttestationLimited.sol) 的测试用例可以在 [这里](../assets/eip-6956/test/ERC6956Full.ts) 找到

## 参考实现

- 仅支持 [提议的标准接口](../assets/eip-6956/contracts/IERC6956.sol) 的最小实现可以在 [这里](../assets/eip-6956/contracts/ERC6956.sol) 找到
- 完整实现，支持 [提议的标准接口](../assets/eip-6956/contracts/IERC6956.sol)、[Floatable 扩展](../assets/eip-6956/contracts/IERC6956Floatable.sol) 、[ValidAnchors 扩展](../assets/eip-6956/contracts/IERC6956ValidAnchors.sol) 和 [AttestationLimited 扩展](../assets/eip-6956/contracts/IERC6956AttestationLimited.sol) 可以在 [这里](../assets/eip-6956/contracts/ERC6956Full.sol) 找到
- 使用 ethers 库生成 ATTESTATION 的最小 Typescript 示例可在 [这里](../assets/eip-6956/minimalAttestationSample.ts) 找到

## 安全考虑

**如果资产被盗，这是否意味着小偷控制了 NFT？**
是的。该标准旨在将 NFT 不可分割且无条件地锚定到资产上。这包括反映盗窃，因为 ORACLE 将证明对资产的控制证明已建立。ORACLE 不会证明控制者是否是合法所有者，
请注意，这甚至可能是一个好处。如果小偷（或从小偷那里接收资产的人）与锚进行交互，链上的某个地址与犯罪（直接或其他受害者）相关联将被知晓。这可以成为调查的有价值起点。
还请注意，提议的标准可以与任何锁定机制结合使用，该机制可以暂时或永久锁定基于证明的操作（在铸造后）。

**如何使用 AttestationLimits 避免资金耗尽**
区块链应用中的一个中央安全机制是 gas 费用。Gas 费用确保执行大量交易会受到惩罚，因此所有 DoS 或其他大规模攻击都受到抑制。由于证明授权操作的无权限性质，许多用例将出现，其中资产的发行者（通常也是资产绑定 NFT 的发行者）将为所有交易支付费用 - 与众所周知的 ERC-721 行为相反，在该行为中，发起地址或接收地址支付费用。因此，具有恶意意图的用户可能会让 ORACLE 多次批准控制证明，指定交替的账户地址。这些证明将交给中央 gas 付款人，后者将以无权限的方式执行它们，为每笔交易支付 gas 费用。这有效地耗尽了 gas 付款人的资金，使系统在 gas 付款人无法再支付交易费用时变得不可用。

**为什么建议对序列号进行哈希而不是直接使用？**
使用任何顺序标识符至少可以推断出最低和最高使用过的序列号之间的数字。因此，这提供了市场上资产总数的良好指示。虽然有限数量的资产通常是可取的，但对于大多数行业来说，发布资产的确切生产数量是不希望的，因为这相当于发布每个产品组的销售/收入数字，这通常被视为机密。在供应链中，由于其基于范围的处理能力，序列号通常是强制性的。允许使用物理序列号并仍然模糊实际资产数量的最简单方法是通过对序列号进行哈希/加密。

**为什么需要锚验证，为什么不简单地信任 oracle 只证明有效锚？**
oracle 证明控制证明。由于 ORACLE 必须知道有效锚的默克尔树，因此它也可能出于恶意目的修改默克尔树。因此，需要在链上验证是否使用了原始默克尔树。即使 oracle 被攻破，它也不应有权引入新的锚。这是通过要求 oracle 知道默克尔树，但 updateValidAnchors() 只能由维护者调用来实现的。请注意，oracle 不必是维护者。因此，应在链下小心，以确保攻破一个系统部分不会自动攻破 oracle 和维护者账户。

**为什么使用默克尔树进行锚验证？**
出于安全和 gas 原因。除了有限的集合外，锚通常会随着时间的推移而添加，例如，当生产或发行新批次的资产时。虽然在 gas 方面存储所有可用锚在链上已经无效，但发布所有锚也会暴露资产的总数。当使用锚更新的数据时，甚至可以推断出该资产的生产能力，这通常被视为机密信息。
**假设你有 N 个锚点。如果所有锚定的 NFT 都已铸造，默克尔树有什么用？**  
如果所有锚定的 NFT 都已铸造，这意味着所有锚点都已发布并可以在链上收集。因此，默克尔树可以被重建。虽然这对许多用例来说可能不是问题（所有支持的锚点反正都已铸造），但我们仍然建议在默克尔树中添加一个“盐叶”，其特征在于 ORACLE 永远不会为与该盐叶匹配的 ANCHOR 发出证明。因此，即使所有 N 个锚点都已铸造

### 物理资产的安全考虑

如果资产是物理对象、商品或财产，则必须满足以下附加规范：

#### 物理锚点的 ORACLE

- 发出证明要求 ORACLE
  - 必须证明输入设备（例如智能手机）指定的 `to` 地址与特定物理锚点及其相关物理对象之间的物理接近性。典型的可接受接近性范围为几毫米到几米。
  - 必须在合理怀疑之外验证物理存在，特别是所采用的方法
    - 必须对物理锚点的复制或再生产尝试具有鲁棒性，
    - 必须对欺骗（例如展示攻击）等具有鲁棒性。
  - 必须在假设定义 `to` 地址的方具有恶意意图并试图获取虚假证明的情况下实施，而该方当前或曾经没有访问包含物理锚点的物理对象的权限。

#### 物理资产

- 必须包含一个锚点，作为唯一的物理对象标识符，通常是序列号（明文（不推荐）或哈希（推荐））
- 必须包含一个物理安全设备、标记或任何其他特征，以便通过 ORACLE 证明物理存在以获取证明
- 推荐采用具有不可复制安全特征的锚点技术。
- 一般不推荐采用容易复制的锚点技术（例如条形码、“普通” NFC 芯片等）。复制包括物理和数字复制。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。