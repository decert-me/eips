---
eip: 6617
title: 基于位的权限
description: 基于位的权限和角色系统
author: Chiro (@chiro-hiro), Victor Dusart (@vdusart)
discussions-to: https://ethereum-magicians.org/t/bit-based-permission/13065
status: 审核
type: 标准跟踪
category: ERC
created: 2023-02-27
---

## 摘要

本 EIP 提供了构建基于位的权限和角色系统的标准。每个权限由一个单独的位表示。通过使用 `uint256`，最多可以定义 $256$ 个权限和 $2^{256}$ 个角色。我们能够根据位的顺序指定每个权限的重要性。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按照 RFC 2119 和 RFC 8174 中的描述进行解释。

*注意* 以下规范使用 Solidity `0.8.7`（或更高版本）的语法

参考接口描述如下：

```solidity
pragma solidity ^0.8.7;

/**
    @title EIP-6617 Bit Based Permission
    @dev See https://eips.ethereum.org/EIPS/eip-6617
*/
interface IEIP6617 {

    /**
        MUST trigger when a permission is granted.
        @param _grantor        Grantor of the permission
        @param _permission     Permission that is granted
        @param _user           User who received the permission
    */
    event PermissionGranted(address indexed _grantor, uint256 indexed _permission, address indexed _user);

    /**
        MUST trigger when a permission is revoked.
        @param _revoker        Revoker of the permission
        @param _permission     Permission that is revoked
        @param _user           User who lost the permission
    */
    event PermissionRevoked(address indexed _revoker, uint256 indexed _permission, address indexed _user);

    /**
        @notice Check if user has permission
        @param _user                Address of the user whose permission we need to check
        @param _requiredPermission  The required permission
        @return                     True if the _permission is a superset of the _requiredPermission else False
    */
    function hasPermission(address _user, uint256 _requiredPermission)
        external
        view
        returns (bool);

    /**
        @notice Add permission to user
        @param _user                Address of the user to whom we are going to add a permission
        @param _permissionToAdd     The permission that will be added
        @return                     The new permission with the _permissionToAdd
    */
    function grantPermission(address _user, uint256 _permissionToAdd)
        external
        returns (bool);

    /**
        @notice Revoke permission from user
        @param _user                Address of the user to whom we are going to revoke a permission
        @param _permissionToRevoke  The permission that will be revoked
        @return                     The new permission without the _permissionToRevoke
    */
    function revokePermission(address _user, uint256 _permissionToRevoke)
        external
        returns (bool);
}
```

- 合规合约 MUST 实现 `IEIP6617`
- 合规合约中的权限表示为 `uint256`。权限 MUST 仅占用 `uint256` 的一位，因此 MUST 是 2 的幂。每个权限 MUST 是唯一的，`0` MUST 用于表示无权限。

### 元数据接口

建议合规合约实现可选扩展 `IEIP6617Meta`。

- 他们 SHOULD 为基本权限和主要组合定义名称和描述。

- 他们 SHOULD NOT 为每个可能的权限子组合定义描述。

```solidity
/**
 * @dev Defined the interface of the metadata of EIP6617, MAY NOT be implemented
 */
interface IEIP6617Meta {
    
    /**
        Structure of permission description
        @param _permission     Permission
        @param _name           Name of the permission
        @param _description    Description of the permission
    */
    struct PermissionDescription {
        uint256 permission;
        string name;
        string description;
    }

    /**
        MUST trigger when the description is updated.
        @param _permission     Permission
        @param _name           Name of the permission
        @param _description    Description of the permission
    */
    event UpdatePermissionDescription(uint256 indexed _permission, string indexed _name, string indexed _description);

    /**
        Returns the description of a given `_permission`.
        @param _permission     Permission
    */
    function getPermissionDescription(uint256 _permission) external view returns (PermissionDescription memory description);

    /**
        Return `true` if the description was set otherwise return `false`. It MUST emit `UpdatePermissionDescription` event.
        @param _permission     Permission
        @param _name           Name of the permission
        @param _description    Description of the permission
    */
    function setPermissionDescription(uint256 _permission, string memory _name, string memory _description)
        external
        returns (bool success);
}
```

## 理由

目前，权限和访问控制是通过单一所有者（[ERC-173](./eip-173.md)）或使用 `bytes32` 角色（[ERC-5982](./eip-5982.md)）来执行的。
然而，使用按位和位掩码操作可以实现更高的 gas 效率和灵活性。

### Gas 成本效率

按位操作非常便宜且快速。例如，在权限位掩码上执行 `AND` 按位操作的成本显著低于调用任何数量的 `LOAD` 操作码。

### 灵活性

通过 `uint256` 的 256 位，我们可以创建多达 256 种不同的权限，这导致 $2^{256}$ 种唯一组合（即角色）。
*（角色是多个权限的组合。）* 并非所有角色都必须预定义。

由于权限被定义为无符号整数，我们可以使用二进制或运算符基于多个权限创建新角色。

### 按重要性排序权限

我们可以使用最高有效位表示最重要的权限，权限之间的比较可以很容易地进行，因为它们都是 `uint256`。

### 关联含义

与通过 ERC-5982 管理的访问控制相比，本 EIP 并未提供对权限或角色含义的直接和简单理解。

为了解决这个问题，可以设置元数据接口，将名称和描述与每个权限或角色关联。

## 参考实现

第一个实现可以在这里找到：

- [基本 ERC-6617 实现](../assets/eip-6617/contracts/EIP6617.sol)

## 安全考虑

没有安全考虑。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。