---
eip: 7201
title: 命名空间存储布局
description: 命名空间存储模式中结构体的存储位置约定。
author: Francisco Giordano (@frangio), Hadrien Croubois (@Amxx), Ernesto García (@ernestognw), Eric Lau (@ericglau)
discussions-to: https://ethereum-magicians.org/t/eip-7201-namespaced-storage-layout/14796
status: Final
type: Standards Track
category: ERC
created: 2023-06-20
---

## 摘要

我们定义了 NatSpec 注释 `@custom:storage-location` 来记录存储命名空间及其在 Solidity 或 Vyper 源代码中的存储位置。此外，我们定义了一个公式来从任意标识符推导出位置。该公式的选择是为了防止与 Solidity 和 Vyper 使用的存储布局发生冲突。

## 动机

智能合约语言如 Solidity 和 Vyper 依赖于树形存储布局。该树从槽 0 开始，由连续变量的顺序块组成。哈希用于确保包含映射和动态数组值的块不会发生冲突。这对于大多数合约来说是足够的。然而，它对智能合约开发中使用的各种设计模式提出了挑战。一个例子是模块化设计，其中使用 `DELEGATECALL` 合约从多个合约执行代码，所有这些合约共享相同的存储空间，并且必须仔细协调如何使用它。另一个例子是可升级合约，在升级中添加状态变量可能会很困难，因为这可能会影响现有变量的分配存储位置。

这些模式可以通过在存储空间中分布状态变量来受益，通常是在通过哈希获得的伪随机位置。每个值可以放置在完全不同的位置，但更常见的是，将一起使用的值放在一个 Solidity 结构体中并共同存储。这些伪随机位置可以是遵循与默认存储相同规则的新存储树的根。只要这个伪随机根的构造不属于默认树的一部分，这应该会导致定义独立的空间，彼此之间或与默认空间之间不会发生冲突。

这些存储使用模式对 Solidity 和 Vyper 编译器是不可见的，因为它们没有表示为 Solidity 状态变量。智能合约工具如静态分析器或区块链浏览器通常需要知道合约数据的存储位置。标准化存储布局的位置将允许这些工具正确解释使用这些设计模式的合约。

## 规范

### 前提

一个 _命名空间_ 由一组有序变量组成，其中一些可能是动态数组或映射，其值的布局遵循与默认存储布局相同的规则，但根植于某个不一定是槽 0 的位置。使用命名空间来组织存储的合约被称为使用 _命名空间存储_。

一个 _命名空间 ID_ 是一个字符串，用于标识合约中的命名空间。它不应包含任何空白字符。

### `@custom:storage-location`

合约中的命名空间应实现为结构体类型。这些结构体应使用 NatSpec 标签 `@custom:storage-location <FORMULA_ID>:<NAMESPACE_ID>` 进行注释，其中 `<FORMULA_ID>` 标识用于计算命名空间根存储位置的公式，基于命名空间 ID。 _(注意：自 v0.8.20 起，Solidity 编译器将此注释包含在 AST 中，因此建议在使用此模式时使用此版本作为最低编译器版本。)_ 在合约外部找到的带有此注释的结构体不被视为源代码中任何合约的命名空间。

### 公式

由 `erc7201` 标识的公式定义为 `erc7201(id: string) = keccak256(keccak256(id) - 1) & ~0xff`。在 Solidity 中，这对应于表达式 `keccak256(abi.encode(uint256(keccak256(bytes(id))) - 1)) & ~bytes32(uint256(0xff))`。使用此公式时，注释变为 `@custom:storage-location erc7201:<NAMESPACE_ID>`。例如，`@custom:storage-location erc7201:foobar` 注释了一个 ID 为 `"foobar"` 的命名空间，其根位于 `erc7201("foobar")`。

未来的 EIP 可能会定义具有唯一公式标识符的新公式。建议遵循此 EIP 中设定的约定，并使用格式为 `erc1234` 的标识符。

## 理由

Solidity 和 Vyper 使用的树形存储布局遵循以下语法（根=0）：

$L_{root} := \mathit{root} \mid L_{root} + n \mid \texttt{keccak256}(L_{root}) \mid \texttt{keccak256}(H(k) \oplus L_{root}) \mid \texttt{keccak256}(L_{root} \oplus H(k))$

根的要求是它不应与任何属于 Solidity 和 Vyper 使用的标准存储树的存储位置重叠（根 = 0），也不应是从任何其他命名空间派生的存储树的一部分（另一个根）。这样可以使多个命名空间可以相互使用，并与标准存储布局并存，无论是故意还是意外，而不会发生冲突。公式中的术语 `keccak256(id) - 1` 被选择为一个未被 Solidity 使用的位置，但这并不是最终位置，因为命名空间可以大于 1 槽，并会扩展到 `keccak256(id) + n`，这可能被 Solidity 使用。添加第二个哈希以防止这种情况，并确保命名空间与标准存储完全不相交，假设 keccak256 碰撞抵抗性和数组不异常大。

此外，命名空间位置对齐到 256 作为潜在优化，以预期在 Verkle 状态树迁移后 gas 计划的变化，这可能导致 256 个存储槽组同时变为热。

### 命名

这种模式有时被称为“钻石存储”。这导致它与“钻石代理模式”混淆，尽管它们可以独立使用。此 EIP 选择使用不同的名称以清楚地区分它与代理模式。

## 向后兼容性

未发现向后兼容性问题。

## 参考实现

```solidity
pragma solidity ^0.8.20;

contract Example {
    /// @custom:storage-location erc7201:example.main
    struct MainStorage {
        uint256 x;
        uint256 y;
    }

    // keccak256(abi.encode(uint256(keccak256("example.main")) - 1)) & ~bytes32(uint256(0xff));
    bytes32 private constant MAIN_STORAGE_LOCATION =
        0x183a6125c38840424c4a85fa12bab2ab606c4b6d0e7cc73c0c06ba5300eab500;

    function _getMainStorage() private pure returns (MainStorage storage $) {
        assembly {
            $.slot := MAIN_STORAGE_LOCATION
        }
    }

    function _getXTimesY() internal view returns (uint256) {
        MainStorage storage $ = _getMainStorage();
        return $.x * $.y;
    }
}
```

## 安全考虑

命名空间应避免与其他命名空间或标准 Solidity 或 Vyper 存储布局发生冲突。此 ERC 中定义的公式在假设 keccak256 碰撞抵抗性的情况下保证了这一属性，正如在理由中讨论的那样。

`@custom:storage-location` 是一个 NatSpec 注释，当前编译器不对其施加任何规则或赋予任何含义。合约开发者负责实现该模式并使用注释中声明的命名空间。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。