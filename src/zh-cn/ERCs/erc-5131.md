---
eip: 5131
title: SAFE 认证用于 ENS
description: 使用 ENS 文本记录来促进更安全和更便捷的签名操作。
author: Wilkins Chung (@wwhchung) <wilkins@manifold.xyz>, Jalil Wahdatehagh (@jwahdatehagh), Cry (@crydoteth), Sillytuna (@sillytuna), Cyberpnk (@CyberpnkWin)
discussions-to: https://ethereum-magicians.org/t/eip-5131-ens-subdomain-authentication/9458
status: Stagnant
type: Standards Track
category: ERC
created: 2022-06-03
requires: 137, 181, 634
---

## 摘要
此 EIP 通过以太坊名称服务规范 ([EIP-137](./eip-137.md)) 将一个或多个签名钱包链接起来，以证明对主钱包的控制和资产所有权。

## 动机
向以太坊生态系统中的第三方应用证明资产所有权是很常见的。用户经常签署数据负载以进行身份验证，然后才能获得执行某些操作的访问权限。然而，这种方法——类似于给予第三方对主钱包的根访问权限——既不安全也不方便。

***示例:***
 1. 为了编辑您在 OpenSea 上的个人资料，您必须用您的钱包签署一条消息。
 2. 为了访问 NFT 限制内容，您必须用包含 NFT 的钱包签署一条消息以证明所有权。
 3. 为了获得活动的访问权限，您必须用包含所需 NFT 的钱包签署一条消息以证明所有权。
 4. 为了领取空投，您必须用符合条件的钱包与智能合约进行交互。
 5. 为了证明对 NFT 的所有权，您必须用拥有该 NFT 的钱包签署一个负载。

在上述所有示例中，用户使用钱包本身与 dApp 或智能合约进行交互，这可能是
 - 不方便（如果通过硬件钱包或多重签名控制）
 - 不安全（因为上述操作是只读的，但您正在通过具有写入访问权限的钱包进行签名/交互）

相反，用户应该能够批准多个钱包代表给定钱包进行身份验证。

### 现有方法和解决方案的问题
不幸的是，我们看到许多用户意外签署了恶意负载。结果几乎总是与签名地址相关的资产的重大损失。

此外，许多用户将大量资产保存在“冷存储”中。由于“冷存储”解决方案提供了更高的安全性，我们通常会看到可访问性降低，因为用户自然会增加访问这些钱包所需的障碍。

一些解决方案建议使用专用注册智能合约来创建此链接，或支持新的协议。这在采用的角度上是有问题的，并且尚未为它们创建任何标准。

### 提议：使用以太坊名称服务 (EIP-137)
与其“重新发明轮子”，不如本提案旨在结合广泛采用的以太坊名称服务和 ENS 文本记录功能 ([EIP-634](./eip-634.md))，以实现更安全和更便捷的签名和身份验证，并通过一个或多个辅助钱包提供对主钱包的“只读”访问。

从中，收益是双重的。此 EIP 通过将潜在恶意的签名操作外包给更易于访问的钱包（热钱包），同时能够保持不经常用于签名操作的钱包的预期安全假设，从而为用户提供了更高的安全性。

#### 提高 dApp 交互安全性
许多 dApp 要求用户证明对钱包的控制以获得访问权限。目前，这意味着您必须使用钱包本身与 dApp 进行交互。这是一个安全问题，因为恶意 dApp 或钓鱼网站可能导致钱包的资产因签署恶意负载而受到损害。

然而，如果使用辅助钱包进行这些交互，这种风险将会降低。恶意交互将被限制在辅助钱包中持有的资产上，该钱包可以设置为几乎没有任何价值。

#### 提高多设备访问安全性
为了在多个设备上使用非硬件钱包，您必须将种子短语导入每个设备。每次在新设备上输入种子短语时，钱包被攻破的风险就会增加，因为您增加了知道种子短语的设备的表面面积。

相反，每个设备可以拥有一个独特的钱包，作为主钱包的授权辅助钱包。如果某个设备特定的钱包被攻破或丢失，您可以简单地撤销身份验证授权。

此外，钱包身份验证可以链式连接，以便辅助钱包可以授权一个或多个三级钱包，这些钱包同时具有辅助地址和根主地址的签名权限。这可以让团队中的每个人都有自己的签名者，而主钱包可以通过从根干撤销权限轻松使整个树失效。

#### 提高便利性
许多人使用硬件钱包以获得最大安全性。然而，这通常不方便，因为许多人不想随身携带硬件钱包。

相反，如果您批准一个非硬件钱包用于身份验证活动（例如移动设备），您将能够在不需要随身携带硬件钱包的情况下使用大多数 dApp。

## 规范
本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

让：
 - `mainAddress` 代表我们试图进行身份验证或证明资产所有权的钱包地址。
 - `mainENS` 代表 `mainAddress` 的反向查找 ENS 字符串。
 - `authAddress` 代表我们希望用来签名的地址，以代替 `mainAddress`。
 - `authENS` 代表 `authAddress` 的反向查找 ENS 字符串。
 - `authKey` 代表格式为 `[0-9A-Za-z]+` 的字符串。

如果满足以下所有条件，则证明 `authAddress` 对 `mainAddress` 的控制和对 `mainAddress` 资产的所有权：
 - `mainAddress` 具有 ENS 解析器记录和设置为 `mainENS` 的反向记录。
 - `authAddress` 具有 ENS 解析器记录和设置为 `authENS` 的反向记录。
 - `authENS` 具有格式为 `<authKey>:<mainAddress>` 的 ENS TEXT 记录 `eip5131:vault`。
 - `mainENS` 具有 ENS TEXT 记录 `eip5131:<authKey>`。

### 在单个 ENS 域上设置一个或多个 `authAddress` 记录
`mainAddress` 必须具有配置的 ENS 解析器记录和反向记录。
为了自动发现链接的账户，`authAddress` 应该具有配置的 ENS 解析器记录和反向记录。

1. 选择一个未使用的 `<authKey>`。这可以是格式为 `[0-0A-Za-z]+` 的任何字符串。
2. 在 `mainENS` 上设置 TEXT 记录 `eip5131:<authKey>`，值设置为所需的 `authAddress`。
3. 在 `authENS` 上设置 TEXT 记录 `eip5131:vault`，值设置为 `<authKey>:mainAddress`。

目前此 EIP 不对您可以包含的 `authAddress` 条目的数量施加上限。用户可以使用任意数量的地址重复此过程。

### 通过 `authAddress` 进行 `mainAddress` 的身份验证
如果任何相关的 `authAddress` 是 `msg.sender` 或已签署消息，则证明对 `mainAddress` 的控制和对 `mainAddress` 资产的所有权。
实际上，这将通过执行以下操作来实现：
1. 获取 `authENS` 的解析器
2. 获取 `authENS` 的 `eip5131:vault` TEXT 记录
3. 解析 `<authKey>:<mainAddress>` 以确定 `authKey` 和 `mainAddress`
4. 必须获取 `mainAddress` 的反向 ENS 记录并验证其是否与 `<mainENS>` 匹配
    - 否则，可能会设置其他指向 `mainAddress` 的 ENS 节点（带有授权），并通过这些节点进行身份验证
5. 获取 `mainENS` 的 `eip5131:<authKey>` TEXT 记录并确保其与 `authAddress` 匹配

请注意，此规范允许在合约级别和客户端/服务器端验证签名。它并不限于智能合约，这就是为什么没有提出外部接口定义的原因。

### 撤销 `authAddress`
要撤销 `authAddress` 的权限，请删除 `mainENS` 的 `eip5131:<authKey>` TEXT 记录或将其更新为指向新的 `authAddress`。

## 理由

### 使用 EIP-137
所提议的规范利用 EIP-137，而不是引入另一个注册表范式。这样做的原因是由于 EIP-137 和 ENS 的广泛采用。

然而，EIP-137 的缺点是任何链接的 `authAddress` 必须包含一些 ETH，以便设置 `authENS` 反向记录以及 `eip5131:vault` TEXT 记录。这可以通过一个单独的反向查找注册表来解决，该注册表使 `mainAddress` 能够通过 `authAddress` 签名的消息设置反向记录和 TEXT 记录。

随着 L2 和 ENS Layer 2 功能的出现，即使在跨不同链管理的域中，也可以对链接地址进行链下验证。

### 一对多身份验证关系
该提议的规范允许一（`mainAddress`）对多（`authAddress`）的身份验证关系。即一个 `mainAddress` 可以授权多个 `authAddress` 进行身份验证，但一个 `authAddress` 只能验证自己或单个 `mainAddress`。

这种设计选择的原因是为了简化通过客户端和智能合约代码进行身份验证。您可以确定 `authAddress` 为哪个 `mainAddress` 签名，而无需任何额外的用户输入。

此外，您可以设计用户体验，无需用户交互即可“选择”交互地址，通过显示 `authAddress` 和 `mainAddress` 拥有的资产，并根据用户尝试进行身份验证的资产使用适当的地址。

## 参考实现

### 客户端/服务器端
在 TypeScript 中，使用 ethers.js 的验证函数如下：
```
export interface LinkedAddress {
  ens: string,
  address: string,
}

export async function getLinkedAddress(
  provider: ethers.providers.EnsProvider, address: string
): Promise<LinkedAddress | null> {
  const addressENS = await provider.lookupAddress(address);
  if (!addressENS) return null;

  const vaultInfo = await (await provider.getResolver(addressENS))?.getText('eip5131:vault');
  if (!vaultInfo) return null;

  const vaultInfoArray = vaultInfo.split(':');
  if (vaultInfoArray.length !== 2) {
    throw new Error('EIP5131: Authkey and vault address not configured correctly.');
  }

  const [ authKey, vaultAddress ] = vaultInfoArray;

  const vaultENS = await provider.lookupAddress(vaultAddress);
  if (!vaultENS) {
    throw new Error(`EIP5131: No ENS domain with reverse record set for vault.`);
  };

  const expectedSigningAddress = await (
    await provider.getResolver(vaultENS)
  )?.getText(`eip5131:${authKey}`);

  if (expectedSigningAddress?.toLowerCase() !== address.toLowerCase()) {
    throw new Error(`EIP5131: Authentication mismatch.`);
  };

  return {
    ens: vaultENS,
    address: vaultAddress
  };
}
```

### 合约端

#### 有后端的情况
如果您的应用程序操作一个安全的后端服务器，您可以运行上述客户端/服务器代码，然后将结果与 [EIP-1271](./eip-1271.md) 这样的规范结合使用：`合约的标准签名验证方法`，以便以廉价和安全的方式验证消息签名者确实已获得主地址的身份验证。

#### 无后端（仅 JavaScript）
提供了一个内部函数的参考实现，以验证消息发送者是否与主地址具有身份验证链接。

```
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/// @author: manifold.xyz

/**
 * ENS Registry Interface
 */
interface ENS {
    function resolver(bytes32 node) external view returns (address);
}

/**
 * ENS Resolver Interface
 */
interface Resolver {
    function addr(bytes32 node) external view returns (address);
    function name(bytes32 node) external view returns (string memory);
    function text(bytes32 node, string calldata key) external view returns (string memory);
}

/**
 * Validate a signing address is associtaed with a linked address
 */
library LinkedAddress {
    /**
     * Validate that the message sender is an authentication address for mainAddress
     *
     * @param ensRegistry    Address of ENS registry
     * @param mainAddress     The main address we want to authenticate for.
     * @param mainENSNodeHash The main ENS Node Hash
     * @param authKey         The TEXT record of the authKey we are using for validation
     * @param authENSNodeHash The auth ENS Node Hash
     */
    function validateSender(
        address ensRegistry,
        address mainAddress,
        bytes32 mainENSNodeHash,
        string calldata authKey,
        bytes32 authENSNodeHash
    ) internal view returns (bool) {
        return validate(ensRegistry, mainAddress, mainENSNodeHash, authKey, msg.sender, authENSNodeHash);
    }

    /**
     * Validate that the authAddress is an authentication address for mainAddress
     *
     * @param ensRegistry     Address of ENS registry
     * @param mainAddress     The main address we want to authenticate for.
     * @param mainENSNodeHash The main ENS Node Hash
     * @param authAddress     The address of the authentication wallet
     * @param authENSNodeHash The auth ENS Node Hash
     */
    function validate(
        address ensRegistry,
        address mainAddress,
        bytes32 mainENSNodeHash,
        string calldata authKey,
        address authAddress,
        bytes32 authENSNodeHash
    ) internal view returns (bool) {
        _verifyMainENS(ensRegistry, mainAddress, mainENSNodeHash, authKey, authAddress);
        _verifyAuthENS(ensRegistry, mainAddress, authKey, authAddress, authENSNodeHash);

        return true;
    }

    // *********************
    //   Helper Functions
    // *********************
    function _verifyMainENS(
        address ensRegistry,
        address mainAddress,
        bytes32 mainENSNodeHash,
        string calldata authKey,
        address authAddress
    ) private view {
        // Check if the ENS nodes resolve correctly to the provided addresses
        address mainResolver = ENS(ensRegistry).resolver(mainENSNodeHash);
        require(mainResolver != address(0), "Main ENS not registered");
        require(mainAddress == Resolver(mainResolver).addr(mainENSNodeHash), "Main address is wrong");

        // Verify the authKey TEXT record is set to authAddress by mainENS
        string memory authText = Resolver(mainResolver).text(mainENSNodeHash, string(abi.encodePacked("eip5131:", authKey)));
        require(
            keccak256(bytes(authText)) == keccak256(bytes(_addressToString(authAddress))),
            "Invalid auth address"
        );
    }

    function _verifyAuthENS(
        address ensRegistry,
        address mainAddress,
        string memory authKey,
        address authAddress,
        bytes32 authENSNodeHash
    ) private view {
        // Check if the ENS nodes resolve correctly to the provided addresses
        address authResolver = ENS(ensRegistry).resolver(authENSNodeHash);
        require(authResolver != address(0), "Auth ENS not registered");
        require(authAddress == Resolver(authResolver).addr(authENSNodeHash), "Auth address is wrong");

        // Verify the TEXT record is appropriately set by authENS
        string memory vaultText = Resolver(authResolver).text(authENSNodeHash, "eip5131:vault");
        require(
            keccak256(abi.encodePacked(authKey, ":", _addressToString(mainAddress))) ==
                keccak256(bytes(vaultText)),
            "Invalid auth text record"
        );
    }

    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    function sha3HexAddress(address addr) private pure returns (bytes32 ret) {
        uint256 value = uint256(uint160(addr));
        bytes memory buffer = new bytes(40);
        for (uint256 i = 39; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        return keccak256(buffer);
    }

    function _addressToString(address addr) private pure returns (string memory ptr) {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            ptr := mload(0x40)

            // Adjust mem ptr and keep 32 byte aligned
            // 32 bytes to store string length; address is 42 bytes long
            mstore(0x40, add(ptr, 96))

            // Store (string length, '0', 'x') (42, 48, 120)
            // Single write by offsetting across 32 byte boundary
            ptr := add(ptr, 2)
            mstore(ptr, 0x2a3078)

            // Write string backwards
            for {
                // end is at 'x', ptr is at lsb char
                let end := add(ptr, 31)
                ptr := add(ptr, 71)
            } gt(ptr, end) {
                ptr := sub(ptr, 1)
                addr := shr(4, addr)
            } {
                let v := and(addr, 0xf)
                // if > 9, use ascii 'a-f' (no conditional required)
                v := add(v, mul(gt(v, 9), 39))
                // Add ascii for '0'
                v := add(v, 48)
                mstore8(ptr, v)
            }

            // return ptr to point to length (32 + 2 for '0x' - 1)
            ptr := sub(ptr, 33)
        }

        return string(ptr);
    }
}
```

## 安全考虑
此 EIP 的核心目的是增强安全性，并促进在不需要主钱包的情况下，安全地验证钱包控制和资产所有权的方式，当主钱包持有的资产不需要移动时。将其视为一种“只读”身份验证的方式。

## 版权
通过 [CC0](../LICENSE.md) 放弃版权及相关权利。