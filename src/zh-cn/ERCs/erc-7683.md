---
eip: 7683
title: 跨链意图
description: 跨链交易执行系统的接口。
author: Mark Toda (@marktoda), Matt Rice (@mrice32), Nick Pai (@nicholaspai)
discussions-to: https://ethereum-magicians.org/t/erc-cross-chain-intents-standard/19619
status: 草案
type: 标准跟踪
category: ERC
created: 2024-04-11
---

## 摘要

以下标准允许实现跨链交易执行系统的标准 API。该标准提供了一个通用的 `CrossChainOrder` 结构，以及一个标准的 `ISettlementContract` 智能合约接口。

## 动机

基于意图的系统已成为最终用户跨链交互的主要解决方案，通过抽象化传统桥接的复杂性和时间限制。跨链意图系统的一个关键难点是访问足够的流动性和跨链的活跃填充网络。随着不同链的数量随时间增加，这一挑战可能会加剧。最终结果是用户体验不佳，包括更高的成本、更长的等待时间和更高的失败率。

通过实施标准，跨链意图系统可以互操作并共享基础设施，例如订单传播服务和填充网络，从而通过增加满足用户意图的竞争来改善最终用户体验。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

### CrossChainOrder 结构

合规的跨链订单类型必须能够 ABI 解码为 `CrossChainOrder` 类型。

```solidity
/// @title CrossChainOrder type
/// @notice Standard order struct to be signed by swappers, disseminated to fillers, and submitted to settlement contracts
struct CrossChainOrder {
	/// @dev The contract address that the order is meant to be settled by.
	/// Fillers send this order to this contract address on the origin chain
	address settlementContract;
	/// @dev The address of the user who is initiating the swap,
	/// whose input tokens will be taken and escrowed
	address swapper;
	/// @dev Nonce to be used as replay protection for the order
	uint256 nonce;
	/// @dev The chainId of the origin chain
	uint32 originChainId;
	/// @dev The timestamp by which the order must be initiated
	uint32 initiateDeadline;
	/// @dev The timestamp by which the order must be filled on the destination chain
	uint32 fillDeadline;
	/// @dev Arbitrary implementation-specific data
	/// Can be used to define tokens, amounts, destination chains, fees, settlement parameters,
	/// or any other order-type specific information
	bytes orderData;
}
```

实现该标准的跨链执行系统应创建一个自定义子类型，可以从任意的 `orderData` 字段中解析。这可能包括与交换相关的代币、目标链 ID、履行约束或结算预言机等信息。

### ResolvedCrossChainOrder 结构

合规的跨链订单类型必须能够转换为 `ResolvedCrossChainOrder` 结构。

```solidity
/// @title ResolvedCrossChainOrder type
/// @notice An implementation-generic representation of an order
/// @dev Defines all requirements for filling an order by unbundling the implementation-specific orderData.
/// @dev Intended to improve integration generalization by allowing fillers to compute the exact input and output information of any order
struct ResolvedCrossChainOrder {
	/// @dev The contract address that the order is meant to be settled by.
	address settlementContract;
	/// @dev The address of the user who is initiating the swap
	address swapper;
	/// @dev Nonce to be used as replay protection for the order
	uint256 nonce;
	/// @dev The chainId of the origin chain
	uint32 originChainId;
	/// @dev The timestamp by which the order must be initiated
	uint32 initiateDeadline;
	/// @dev The timestamp by which the order must be filled on the destination chain(s)
	uint32 fillDeadline;

	/// @dev The inputs to be taken from the swapper as part of order initiation
	Input[] swapperInputs;
	/// @dev The outputs to be given to the swapper as part of order fulfillment
	Output[] swapperOutputs;
	/// @dev The outputs to be given to the filler as part of order settlement
	Output[] fillerOutputs;
}

/// @notice Tokens sent by the swapper as inputs to the order
struct Input {
	/// @dev The address of the ERC20 token on the origin chain
	address token;
	/// @dev The amount of the token to be sent
	uint256 amount;
}

/// @notice Tokens that must be receive for a valid order fulfillment
struct Output {
	/// @dev The address of the ERC20 token on the destination chain
	/// @dev address(0) used as a sentinel for the native token
	address token;
	/// @dev The amount of the token to be sent
	uint256 amount;
	/// @dev The address to receive the output tokens
	address recipient;
	/// @dev The destination chain for this output
	uint32 chainId;
}

```

### ISettlementContract 接口

合规的结算合约实现必须实现 `ISettlementContract` 接口：

```solidity
/// @title ISettlementContract
/// @notice Standard interface for settlement contracts
interface ISettlementContract {
	/// @notice Initiates the settlement of a cross-chain order
	/// @dev To be called by the filler
	/// @param order The CrossChainOrder definition
	/// @param signature The swapper's signature over the order
	/// @param fillerData Any filler-defined data required by the settler
	function initiate(CrossChainOrder order, bytes signature, bytes fillerData) external;

	/// @notice Resolves a specific CrossChainOrder into a generic ResolvedCrossChainOrder
	/// @dev Intended to improve standardized integration of various order types and settlement contracts
	/// @param order The CrossChainOrder definition
	/// @param fillerData Any filler-defined data required by the settler
	/// @returns ResolvedCrossChainOrder hydrated order data including the inputs and outputs of the order
	function resolve(CrossChainOrder order, bytes fillerData) external view returns (ResolvedCrossChainOrder);
}
```

## 理由

### 通用 OrderData

一个关键考虑是确保广泛的跨链意图设计能够在同一标准内工作。为此，规范围绕标准的跨链意图 *流* 进行设计，同时允许在该流内的不同实现细节。

标准跨链意图流：

1. 交换者签署一条离线消息，定义其订单的参数
2. 订单传播给填充者
3. 填充者在源链上发起交易
4. 填充者在目标链上填充订单
5. 进行跨链结算过程以结算订单

在此流程中，标准的实施者具有设计灵活性，可以自定义行为，例如：

- 价格决策，例如荷兰拍卖或基于预言机的定价
- 履行约束
- 结算程序。

`orderData` 字段允许实现采用这些行为的任意规范，同时仍然使集成者能够解析订单的主要字段。

此功能也促使了 `resolve` 视图函数和 `ResolvedCrossChainOrder` 类型的出现。解析使集成填充者能够验证和评估订单，而无需对手头的 `orderData` 字段有特定的了解。

### Permit2 的使用

Permit2 并不是该标准所特定要求的，但确实提供了一种高效且简单的方法来构建符合标准的协议。具体而言，permit2 的 `witness` 函数允许交换者通过单个签名同时批准代币转移 *和* 订单本身。这也很好地将代币转移与订单的成功发起结合在一起。

相比之下，标准的批准模型将需要两个单独的签名 - 一个代币批准（可以是 [ERC-2612](./eip-2612.md) 或链上）和一个批准交换条款的签名。这也将代币批准与交换解耦，这意味着已批准的代币可能会由于有缺陷或不受信任的结算合约而在任何时候被提取。

在围绕 Permit2 构建符合标准的结算系统时，应考虑以下事项：

- 订单结构中的 `nonce` 应为 permit2 的 nonce
- 订单结构中的 `initiateDeadline` 应为 permit2 的截止日期
- 在 permit2 调用期间，应使用包括解析的 `orderData` 的完整订单结构作为见证类型。这确保了交换者在签署其订单许可时的最大透明度。

## 安全考虑

<!-- TODO -->

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。