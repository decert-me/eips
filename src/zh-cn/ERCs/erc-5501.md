---
eip: 5501
title: 租赁与委托 NFT - EIP-721 扩展
description: 为 EIP-721 添加一个条件性时间限制的用户角色。该角色可以被委托或借用。
author: Jan Smrža (@smrza), David Rábel (@rabeles11), Tomáš Janča <tomas.janca@jtbstorage.eu>, Jan Bureš (@JohnyX89), DOBBYLABS (@DOBBYLABS)
discussions-to: https://ethereum-magicians.org/t/eip-tbd-rental-delegation-nft-erc-721-extension/10441
status: 停滞
type: 标准跟踪
category: ERC
created: 2022-08-18
requires: 165, 721, 4400, 4907
---

## 摘要
以下标准为 [EIP-721](./eip-721.md) 提出了一个额外的 `user` 角色。该角色授予使用 NFT 的权限，但不能转让或设置用户。它有一个到期时间和一个标志，指示代币是否被借用。`Owner` 可以将 NFT 委托给热钱包使用或借出 NFT。如果代币被借用，甚至连所有者在状态到期之前都无法更改用户，或者双方同意终止。这种方式可以同时保持两个角色的活跃。

## 动机
收藏品、游戏资产、元宇宙、活动门票、音乐、视频、域名、实物表示是许多 NFT 用例中的几个。使用 [EIP-721](./eip-721.md) 时，只有所有者可以获得收益。然而，对于大多数实用程序，区分代币所有者和用户是有益的。例如，音乐或电影可以被租用。元宇宙土地可以被委托使用。

设置用户的两个原因是：

* **委托** - 将用户分配给您的热钱包，以安全地与应用程序交互。在这种情况下，所有者可以随时更改用户。
* **租赁** - 此用例有额外的要求。一旦设定的借贷期限结束，需要终止贷款。这由用户的 `expires` 提供。还需要保护借款人免受所有者重置其状态的影响。因此，必须实现 `isBorrowed` 检查，以在合同到期之前禁用设置用户的选项。

拥有额外用户角色的最常见用例是：

* **委托** - 出于安全原因。
* **游戏** - 您想尝试一款游戏（或特定的游戏资产），但不确定自己是否会喜欢？先租用资产。
* **公会** - 将 NFT 的所有者保持为多重签名钱包，并将用户设置为与公会成员共享私钥的热钱包。
* **活动** - 区分 `ownerOf` 和 `userOf`。每个角色具有不同的访问权限。
* **社交** - 为不同的房间区分角色。例如，所有者具有读 + 写访问权限，而 userOf 仅具有读访问权限。

该提案是对 [EIP-4400](./eip-4400.md) 和 [EIP-4907](./eip-4907.md) 的后续，并引入了额外的借贷升级，包括：

* **NFT 在租赁期间保留在所有者的钱包中**
* **在不终止租赁的情况下列出和出售 NFT**
* **在租赁期间索取所有者的收益**

通过增加 `isBorrowed` 检查构建标准，现在可以创建租赁市场，这些市场可以在没有必要的质押机制的情况下设置 NFT 的用户。在当前标准下，如果在租赁期间代币未被质押，所有者可以通过重复设置用户轻松终止贷款。这通过在代币被借用时禁用该功能来解决，从而为所有者提供额外的好处。他们可以将代币绑定到他们的钱包，这意味着他们仍然可以接收空投、根据代币所有权索取免费铸造或以其他方式使用第三方服务提供的 NFT。他们还可以将 NFT 列出出售。接收空投或免费铸造在以前是可能的，但所有者完全依赖于租赁市场的实施及其自由裁量权。

去中心化应用程序现在可以区分 ownerOf 和 userOf，同时这两种状态可以共存。

## 规范
本文档中的关键字 “MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL” 应按 RFC 2119 中的描述进行解释。

**每个合规的合约 MUST 实现 `IERC5501` 接口。此扩展对于 [EIP-721](./eip-721.md) 合约是 OPTIONAL。**

```solidity
/**
 * @title IERC5501: Rental & Delegation NFT - EIP-721 Extension
 * @notice the EIP-165 identifier for this interface is 0xf808ec37.
 */
interface IERC5501 /* is IERC721 */ {
    /**
     * @dev Emitted when the user of an NFT is modified.
     */
    event UpdateUser(uint256 indexed _tokenId, address indexed _user, uint64 _expires, bool _isBorrowed);

    /**
     * @notice Set the user info of an NFT.
     * @dev User address cannot be zero address.
     * Only approved operator or NFT owner can set the user.
     * If NFT is borrowed, the user info cannot be changed until user status expires.
     * @param _tokenId uint256 ID of the token to set user info for
     * @param _user address of the new user
     * @param _expires Unix timestamp when user info expires
     * @param _isBorrowed flag whether or not the NFT is borrowed
     */
    function setUser(uint256 _tokenId, address _user, uint64 _expires, bool _isBorrowed) external;

    /**
     * @notice Get the user address of an NFT.
     * @dev Reverts if user is not set.
     * @param _tokenId uint256 ID of the token to get the user address for
     * @return address user address for this NFT
     */
    function userOf(uint256 _tokenId) external view returns (address);

    /**
     * @notice Get the user expires of an NFT.
     * @param _tokenId uint256 ID of the token to get the user expires for
     * @return uint64 user expires for this NFT
     */
    function userExpires(uint256 _tokenId) external view returns (uint64);

    /**
     * @notice Get the user isBorrowed of an NFT.
     * @param _tokenId uint256 ID of the token to get the user isBorrowed for
     * @return bool user isBorrowed for this NFT
     */
    function userIsBorrowed(uint256 _tokenId) external view returns (bool);
}
``` 

每个实现 `IERC5501` 接口的合约可以自由定义 `user` 的权限。然而，用户 MUST NOT 被视为 `owner`。他们 MUST NOT 能够执行转移和批准。此外，如果 `userIsBorrowed` 返回 `true` 且 `userExpires` 大于或等于 `block.timestamp`，则必须阻止执行 `setUser`。

当 `user` 被更改时，必须发出 `UpdateUser` 事件。   
`setUser(uint256 _tokenId, address _user, uint64 _expires, bool _isBorrowed)` 函数应该在 `msg.sender` 不是 `owner` 或已批准的操作员时 `revert`。如果代币被借用且状态尚未到期，则必须 `revert`。它可以是 `public` 或 `external`。   
`userOf(uint256 _tokenId)` 函数应该在 `user` 未设置或已过期时 `revert`。   
`userExpires(uint256 _tokenId)` 函数返回用户状态到期的时间戳。   
`userIsBorrowed(uint256 _tokenId)` 函数返回 NFT 是否被借用。   
`supportsInterface` 函数在调用 `0xf808ec37` 时必须返回 `true`。   
在每次 `transfer` 时，如果代币未被借用，则必须重置 `user`。如果代币被借用，则 `user` 必须保持不变。   

**余额扩展是可选的。这提供了查询 `user` 拥有的代币数量的选项。**

```solidity
/**
 * @title IERC5501Balance
 * Extension for ERC5501 which adds userBalanceOf to query how many tokens address is userOf.
 * @notice the EIP-165 identifier for this interface is 0x0cb22289.
 */
interface IERC5501Balance /* is IERC5501 */{
    /**
     * @notice Count of all NFTs assigned to a user.
     * @dev Reverts if user is zero address.
     * @param _user an address for which to query the balance
     * @return uint256 the number of NFTs the user has
     */
    function userBalanceOf(address _user) external view returns (uint256);
}
``` 

`userBalanceOf(address _user)` 函数应该在零地址时 `revert`。 

**可枚举扩展是可选的。这允许迭代用户余额。** 

```solidity
/**
 * @title IERC5501Enumerable
 * This extension for ERC5501 adds the option to iterate over user tokens.
 * @notice the EIP-165 identifier for this interface is 0x1d350ef8.
 */
interface IERC5501Enumerable /* is IERC5501Balance, IERC5501 */ {
    /**
     * @notice Enumerate NFTs assigned to a user.
     * @dev Reverts if user is zero address or _index >= userBalanceOf(_owner).
     * @param _user an address to iterate over its tokens
     * @return uint256 the token ID for given index assigned to _user
     */
    function tokenOfUserByIndex(address _user, uint256 _index) external view returns (uint256);
}
``` 

`tokenOfUserByIndex(address _user, uint256 _index)` 函数应该在零地址时 `revert`，并在索引大于或等于 `user` 余额时 `throw`。 

**可终止扩展是可选的。这允许在双方同意的情况下提前终止租赁。**

```solidity
/**
 * @title IERC5501Terminable
 * This extension for ERC5501 adds the option to terminate borrowing if both parties agree.
 * @notice the EIP-165 identifier for this interface is 0x6a26417e.
 */
interface IERC5501Terminable /* is IERC5501 */ {
    /**
     * @dev Emitted when one party from borrowing contract approves termination of agreement.
     * @param _isLender true for lender, false for borrower
     */
    event AgreeToTerminateBorrow(uint256 indexed _tokenId, address indexed _party, bool _isLender);

    /**
     * @dev Emitted when agreements to terminate borrow are reset.
     */
    event ResetTerminationAgreements(uint256 indexed _tokenId);

    /**
     * @dev Emitted when borrow of token ID is terminated.
     */
    event TerminateBorrow(uint256 indexed _tokenId, address indexed _lender, address indexed _borrower, address _caller);

    /**
     * @notice Agree to terminate a borrowing.
     * @dev Lender must be ownerOf token ID. Borrower must be userOf token ID.
     * If lender and borrower are the same, set termination agreement for both at once.
     * @param _tokenId uint256 ID of the token to set termination info for
     */
    function setBorrowTermination(uint256 _tokenId) external;

    /**
     * @notice Get if it is possible to terminate a borrow agreement.
     * @param _tokenId uint256 ID of the token to get termination info for
     * @return bool, bool first indicates lender agrees, second indicates borrower agrees
     */
    function getBorrowTermination(uint256 _tokenId) external view returns (bool, bool);

    /**
     * @notice Terminate a borrow if both parties agreed.
     * @dev Both parties must have agreed, otherwise revert.
     * @param _tokenId uint256 ID of the token to terminate borrow of
     */
    function terminateBorrow(uint256 _tokenId) external;
}
``` 

当借款人或贷款人同意终止租赁时，必须发出 `AgreeToTerminateBorrow` 事件。   
当代币被借用并转移或调用 `setUser` 和 `terminateBorrow` 函数时，必须发出 `ResetTerminationAgreements` 事件。   
当租赁被终止时，必须发出 `TerminateBorrow` 事件。   
`setBorrowTermination(uint256 _tokenId)` 必须设置来自任一方的协议，无论哪个调用该函数。如果贷款人和借款人是同一地址，则必须同时为双方分配协议。   
`getBorrowTermination(uint256 _tokenId)` 返回双方的协议是否为 `true` 或 `false`。   
`terminateBorrow(uint256 _tokenId)` 函数可以由任何人调用。如果双方的终止协议不为 `true`，则必须 `revert`。此函数应该将 `isBorrowed` 标志从 `true` 更改为 `false`。   
在每次 `transfer` 时，如果代币被借用，则必须重置任一方的终止协议。

## 理由
影响此标准的主要因素是：

* **[EIP-4400](./eip-4400.md) 和 [EIP-4907](./eip-4907.md)**
* **允许在不需要质押或过度抵押的情况下进行借贷，同时所有者保留所有权**
* **保留委托选项**
* **在接口中保持函数数量最小，同时实现所需功能**
* **模块化额外扩展，让开发者选择他们项目所需的功能**
### 名称
额外角色的名称已被选择以适应目的，并保持与 EIP-4907 的兼容性。

### 所有权保留
许多收藏品为其所有者提供空投或各种代币的免费铸造。如果所有者通过将代币质押到合约中借出代币，这基本上是破坏的（除非合约实现了一种方式来至少领取空投代币）。应用程序还可以在其生态系统中为所有者和用户角色提供不同的访问权限和利益。

### 余额和可枚举扩展
由于实现的复杂性，这些被选择为可选扩展，因为一旦用户状态过期，余额会减少，并且没有立即的链上交易来评估这一点。在 `userBalanceOf` 和 `tokenOfUserByIndex` 函数中，必须有一种方法来确定用户状态是否已过期。

### 可终止扩展
如果所有者错误地将一个借用状态的用户设置为一个较大的值，他们将基本上被阻止再次设置该用户。如果双方同意终止用户状态，则此扩展解决了该问题。

### 安全性
一旦应用程序采用用户角色，就可以将所有权委托给热钱包，并与其交互，而无需担心连接到恶意网站。

## 向后兼容性
该标准通过添加扩展函数集与当前的 [EIP-721](./eip-721.md) 兼容。新引入的函数与 EIP-721 中的现有函数类似，这保证了开发者和应用程序的轻松采用。该标准在用户角色及其过期方面也与 [EIP-4907](./eip-4907.md) 有相似之处，这意味着应用程序将能够确定用户是否使用了这两个标准中的任意一个。

## 测试用例
测试用例可以在参考实现中找到：
* [主合约](../assets/eip-5501/test/ERC5501Test.ts)
* [余额扩展](../assets/eip-5501/test/ERC5501BalanceTest.ts)
* [可枚举扩展](../assets/eip-5501/test/ERC5501EnumerableTest.ts)
* [可终止扩展](../assets/eip-5501/test/ERC5501TerminableTest.ts)
* [所有扩展的场景组合](../assets/eip-5501/test/ERC5501CombinedTest.ts)

## 参考实现
参考实现可在此处获得：
* [主合约](../assets/eip-5501/contracts/ERC5501.sol)
* [余额扩展](../assets/eip-5501/contracts/ERC5501Balance.sol)
* [可枚举扩展](../assets/eip-5501/contracts/ERC5501Enumerable.sol)
* [可终止扩展](../assets/eip-5501/contracts/ERC5501Terminable.sol)
* [所有扩展的解决方案组合](../assets/eip-5501/contracts/ERC5501Combined.sol)

## 安全考虑
实施该标准的开发者和应用程序必须考虑他们给予用户和所有者的所有权限。由于所有者和用户同时都是活跃角色，因此必须避免双重支出问题。余额扩展必须以不会导致任何 gas 问题的方式实现。市场应告知用户所列出售的代币是否被借出。 

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。