---
eip: 5521
title: 可引用的 NFT
description: 一个 ERC-721 扩展，用于构建 NFT 之间的引用关系
author: Saber Yu (@OniReimu), Qin Wang <qin.wang@data61.csiro.au>, Shange Fu <shange.fu@monash.edu>, Yilin Sai <yilin.sai@data61.csiro.au>, Shiping Chen <shiping.chen@data61.csiro.au>, Sherry Xu <xiwei.xu@data61.csiro.au>, Jiangshan Yu <jiangshan.yu@monash.edu>
discussions-to: https://ethereum-magicians.org/t/eip-x-erc-721-referable-nft/10310
status: Final
type: Standards Track
category: ERC
created: 2022-08-10
requires: 165, 721
---

## 摘要

该标准是 [ERC-721](./eip-721.md) 的扩展。它提出了两个可引用的指标，引用和被引用，以及一个基于时间的指标 `createdTimestamp`。每个 NFT 之间的关系形成一个有向无环图 (DAG)。该标准允许用户查询、跟踪和分析它们的关系。

![系统架构](../assets/eip-5521/system-arch.png)

## 动机

许多场景需要 NFT 的继承、引用和扩展。例如，艺术家可能会基于之前的 NFT 开发他的 NFT 作品，或者 DJ 可能会通过引用两首流行歌曲来混音他的唱片等。现有 NFT 标准的一个缺口是缺乏 NFT 与其原始创作者之间的既定关系。这一空白使 NFT 孤立，导致每个 NFT 的销售成为一次性交易，从而阻碍创作者随着时间的推移获得其知识产权的全部价值。

从这个意义上说，提出一个可引用的解决方案，使现有 NFT 能够高效查询交叉引用是必要的。通过在 NFT 之间引入引用关系，可以建立一个可持续的经济模型，以激励持续参与创建、使用和推广 NFT。

因此，该标准引入了一个新概念，可引用的 NFT (rNFT)，它可以将静态 NFT 转变为动态可扩展的网络。我们嵌入引用信息，包括 `referring` 和 `referred` 关系，帮助形成基于有向无环图 (DAG) 的 NFT 网络。该结构提供了透明的图形历史记录，并允许用户查询、追踪和分析关系。它可以使 NFT 创作者在不需要重新开始的情况下，基于现有作品进行创作。

一个直观的例子：用户可以通过引用现有的 NFT (A, B) 创建新的 NFT (C, D, E)，而 `referred` 函数则通知原始 NFT (A, B) 其引用情况（例如，A &#8592; D; C &#8592; E; B &#8592; E，和 A &#8592; E）。在这里，`createdTimestamp`（区块级别）作为 NFT (A, B, C, D, E) 创建时间的指示器。

### 关键要点

该标准提供了几个优势：

*清晰的所有权继承*：该标准将静态 NFT 扩展为一个虚拟可扩展的 NFT 网络。艺术家不必孤立地创作作品。所有权继承避免了重复造轮子。

*激励兼容性*：该标准明确了不同 NFT 之间的可引用关系，有助于整合多个上层激励模型，既有利于原始 NFT 拥有者，也有利于新创作者。

*易于集成*：该标准使现有的代币标准或第三方协议更易于集成。例如，rNFT 可以应用于可租赁场景（参见 [ERC-5006](./eip-5006.md)），以建立一个分层租赁市场，在该市场中，多个用户可以在同一时间租用同一 NFT，或者一个用户可以在同一时间段内租用多个 NFT。

*可扩展的互操作性*：该标准支持跨合约引用，为更广泛的公众提供可扩展的采用，具有更强的互操作性。

## 规范

本文档中的关键字 “MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL” 应按 RFC 2119 中的描述进行解释。

- `UpdateNode`：当调用 `setNode` 时发出的事件；
- `safeMint`：铸造一个新的 rNFT；
- `setNode`：设置 rNFT 的引用列表并更新引用列表中的每个 rNFT 的被引用列表；
    - `setNodeReferring`：设置 rNFT 的引用列表；
    - `setNodeReferred`：设置来自不同合约的给定 rNFT 的被引用列表；
        - `setNodeReferredExternal`：设置来自外部合约的给定 rNFT 的被引用列表；
- `referringOf`：获取 rNFT 的引用列表；
- `referredOf`：获取 rNFT 的被引用列表；
- `createdTimestampOf`：获取 rNFT 创建时的时间戳。

该标准的实现者 **MUST** 具备以下所有功能：

```solidity

pragma solidity ^0.8.4;

import "@openzeppelin/contracts/utils/introspection/IERC165.sol";

interface IERC_5521 is IERC165 {

    /// Logged when a node in the rNFT gets referred and changed.
    /// @notice Emitted when the `node` (i.e., an rNFT) is changed.
    event UpdateNode(uint256 indexed tokenId, 
                     address indexed owner, 
                     address[] _address_referringList,
                     uint256[][] _tokenIds_referringList,
                     address[] _address_referredList,
                     uint256[][] _tokenIds_referredList
    );

    /// @notice set the referred list of an rNFT associated with different contract addresses and update the referring list of each one in the referred list. Checking the duplication of `addresses` and `tokenIds` is **RECOMMENDED**.
    /// @param `tokenId` of rNFT being set. `addresses` of the contracts in which rNFTs with `tokenIds` being referred accordingly. 
    /// @requirement 
    /// - the size of `addresses` **MUST** be the same as that of `tokenIds`;
    /// - once the size of `tokenIds` is non-zero, the inner size **MUST** also be non-zero;
    /// - the `tokenId` **MUST** be unique within the same contract;
    /// - the `tokenId` **MUST NOT** be the same as `tokenIds[i][j]` if `addresses[i]` is essentially `address(this)`.
    function setNode(uint256 tokenId, address[] memory addresses, uint256[][] memory tokenIds) external;

    /// @notice get the referring list of an rNFT.
    /// @param `tokenId` of the rNFT being focused, `_address` of contract address associated with the focused rNFT.
    /// @return the referring mapping of the rNFT.
    function referringOf(address _address, uint256 tokenId) external view returns(address[] memory, uint256[][] memory);

    /// @notice get the referred list of an rNFT.
    /// @param `tokenId` of the rNFT being focused, `_address` of contract address associated with the focused rNFT.
    /// @return the referred mapping of the rNFT.
    function referredOf(address _address, uint256 tokenId) external view returns(address[] memory, uint256[][] memory);

    /// @notice get the timestamp of an rNFT when is being created.
    /// @param `tokenId` of the rNFT being focused, `_address` of contract address associated with the focused rNFT.
    /// @return the timestamp of the rNFT when is being created with uint256 format.
    function createdTimestampOf(address _address, uint256 tokenId) external view returns(uint256);
    
    /// @notice check supported interfaces, adhereing to ERC165.
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

interface TargetContract is IERC165 {
    /// @notice set the referred list of an rNFT associated with external contract addresses. 
    /// @param `_tokenIds` of rNFTs associated with the contract address `_address` being referred by the rNFT with `tokenId`.
    /// @requirement
    /// - `_address` **MUST NOT** be the same as `address(this)` where `this` is executed by an external contract where `TargetContract` interface is implemented.
    function setNodeReferredExternal(address _address, uint256 tokenId, uint256[] memory _tokenIds) external;

    function referringOf(address _address, uint256 tokenId) external view returns(address[] memory, uint256[][] memory);

    function referredOf(address _address, uint256 tokenId) external view returns(address[] memory, uint256[][] memory);

    function createdTimestampOf(address _address, uint256 tokenId) external view returns(uint256);
    
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

```

## 理由

### 这个事件信息量够吗？
`UpdateNode`：该事件传播关键信息，包括 rNFT ID、其所有者以及与该 rNFT 相关的引用或被引用的合约地址/ID 列表。这一数据集使利益相关者能够高效管理和导航 rNFT 生态系统中固有的复杂关系网络。

实现者可以自由选择使用结构体（推荐的结构体在参考实现中给出），或多个单独的映射，或其他任何存储机制。无论选择哪种机制，只要其输出能够满足 `UpdateNode` 事件，就不会对合约的行为产生可观察的影响。

### 为什么要 `createdTimestampOf`？
`createdTimestamp`：该标准的一个关键原则是 rNFT 应该引用社区已经接受的内容（参与者已知的基于时间的序列）。因此，rNFT 的全局时间戳至关重要，旨在防止冲突状态（类似于事务处理和区块组织中的并发问题）。我们定义了一个区块级时间戳，其中 `createdTimestamp = block.timestamp`。请注意，由于引用的粒度与区块时间戳相关，因此在同一块内区分两个 rNFT 的顺序是不切实际的。

### 如何进行跨合约引用？
`setNodeReferredExternal`：该函数根据外部合约中的接口验证成功与否有条件地操作。这种选择性调用确保了与现有合约的向后兼容性和集成，只要它们遵循指定的接口。

## 向后兼容性

该标准可以通过添加扩展函数集来完全兼容 [ERC-721](./eip-721.md)。

## 测试用例

测试用例包含在 [ERC_5521.test.js](../assets/eip-5521/ERC_5521.test.js) 中。

## 参考实现

推荐的实现如下所示：

- `Relationship`：一个结构，包含 `referring`、`referred`、`referringKeys`、`referredKeys`、`createdTimestamp` 和其他自定义及可选属性（即不一定包含在标准中），例如 `privityOfAgreement` 记录在可引用 NFT (rNFT) 创建时被引用 NFT 的所有权，或 `profitSharing` 记录 `referring` 的利润分享。
- `referring`：一个出度指标，用于显示该 NFT 引用的用户；
- `referred`：一个入度指标，用于显示引用该 NFT 的用户；
- `referringKeys`：用于事件的出度指标映射转换的辅助工具；
- `referredKeys`：用于事件的入度指标映射转换的辅助工具；
- `createdTimestamp`：一个基于时间的指标，用于比较铸造的时间戳，调用者不应以任何方式编辑。
- `referringOf` 和 `referredOf`：首先，当前的 `referringOf` 和 `referredOf` 允许跨合约查找，而这不能通过直接访问 `_relationship` 来完成。其次，只有在隐私不是问题的情况下，使 `_relationship` 公开可以通过依赖 Solidity 自动生成的 getter 简化合约。然而，如果需要控制数据的可见性，保持状态变量私有并提供特定的 getter 函数将是最佳方法。例如，如果 `_relationship` 包含有关特定用户交互或交易的详细信息或一些私有可扩展参数（在更新版本中，我们特别强调 `Relationship` 可以扩展以满足不同需求），始终将这些数据公开可能会揭示用户的行为模式或偏好，从而导致潜在的隐私泄露。
- `convertMap`：该函数对于检索结构体内的完整映射内容至关重要。即使 `_relationship` 是公开的，getter 仅允许检索特定键的单个值。由于我们需要全面访问所有存储的地址，`convertMap` 是满足我们事件发射要求的必要条件。
```solidity

pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IERC_5521.sol";

contract ERC_5521 is ERC721, IERC_5521, TargetContract {

    struct Relationship {
        mapping (address => uint256[]) referring;
        mapping (address => uint256[]) referred;
        address[] referringKeys;
        address[] referredKeys;
        uint256 createdTimestamp; // unix timestamp when the rNFT is being created

        // extensible parameters
        // ...
    }

    mapping (uint256 => Relationship) internal _relationship;
    address contractOwner = address(0);

    constructor(string memory name_, string memory symbol_) ERC721(name_, symbol_) {
        contractOwner = msg.sender;
    }

    function safeMint(uint256 tokenId, address[] memory addresses, uint256[][] memory _tokenIds) public {
        // require(msg.sender == contractOwner, "ERC_rNFT: Only contract owner can mint");
        _safeMint(msg.sender, tokenId);
        setNode(tokenId, addresses, _tokenIds);
    }

    /// @notice set the referred list of an rNFT associated with different contract addresses and update the referring list of each one in the referred list
    /// @param tokenIds array of rNFTs, recommended to check duplication at the caller's end
    function setNode(uint256 tokenId, address[] memory addresses, uint256[][] memory tokenIds) public virtual override {
        require(
            addresses.length == tokenIds.length,
            "Addresses and TokenID arrays must have the same length"
        );
        for (uint i = 0; i < tokenIds.length; i++) {
            if (tokenIds[i].length == 0) { revert("ERC_5521: the referring list cannot be empty"); }
        }
        setNodeReferring(addresses, tokenId, tokenIds);
        setNodeReferred(addresses, tokenId, tokenIds);
    }

    /// @notice set the referring list of an rNFT associated with different contract addresses 
    /// @param _tokenIds array of rNFTs associated with addresses, recommended to check duplication at the caller's end
    function setNodeReferring(address[] memory addresses, uint256 tokenId, uint256[][] memory _tokenIds) private {
        require(_isApprovedOrOwner(msg.sender, tokenId), "ERC_5521: transfer caller is not owner nor approved");

        Relationship storage relationship = _relationship[tokenId];

        for (uint i = 0; i < addresses.length; i++) {
            if (relationship.referring[addresses[i]].length == 0) { relationship.referringKeys.push(addresses[i]); } // Add the address if it's a new entry
            relationship.referring[addresses[i]] = _tokenIds[i];
        }

        relationship.createdTimestamp = block.timestamp;
        emitEvents(tokenId, msg.sender);
    }

    /// @notice set the referred list of an rNFT associated with different contract addresses 
    /// @param _tokenIds array of rNFTs associated with addresses, recommended to check duplication at the caller's end
    function setNodeReferred(address[] memory addresses, uint256 tokenId, uint256[][] memory _tokenIds) private {
        for (uint i = 0; i < addresses.length; i++) {
            if (addresses[i] == address(this)) {
                for (uint j = 0; j < _tokenIds[i].length; j++) {
                    Relationship storage relationship = _relationship[_tokenIds[i][j]];
                    if (relationship.referred[addresses[i]].length == 0) { relationship.referredKeys.push(addresses[i]); } // Add the address if it's a new entry
                    
                    require(tokenId != _tokenIds[i][j], "ERC_5521: self-reference not allowed");
                    if (relationship.createdTimestamp >= block.timestamp) { revert("ERC_5521: the referred rNFT needs to be a predecessor"); } // Make sure the reference complies with the timing sequence

                    relationship.referred[address(this)].push(tokenId);
                    emitEvents(_tokenIds[i][j], ownerOf(_tokenIds[i][j]));
                }
            } else {
                TargetContract targetContractInstance = TargetContract(addresses[i]);
                bool isSupports = targetContractInstance.supportsInterface(type(TargetContract).interfaceId);
                if (isSupports) {
                    // The target contract supports the interface, safe to call functions of the interface.
                    targetContractInstance.setNodeReferredExternal(address(this), tokenId, _tokenIds[i]);
                }
            }
        }
    }

    /// @notice set the referred list of an rNFT associated with different contract addresses 
    /// @param _tokenIds array of rNFTs associated with addresses, recommended to check duplication at the caller's end
    function setNodeReferredExternal(address _address, uint256 tokenId, uint256[] memory _tokenIds) external {
        for (uint i = 0; i < _tokenIds.length; i++) {
            Relationship storage relationship = _relationship[_tokenIds[i]];
            if (relationship.referred[_address].length == 0) { relationship.referredKeys.push(_address); } // Add the address if it's a new entry

            require(_address != address(this), "ERC_5521: this must be an external contract address");
            if (relationship.createdTimestamp >= block.timestamp) { revert("ERC_5521: the referred rNFT needs to be a predecessor"); } // Make sure the reference complies with the timing sequence

            relationship.referred[_address].push(tokenId);
            emitEvents(_tokenIds[i], ownerOf(_tokenIds[i]));
        }
    }

    /// @notice Get the referring list of an rNFT
    /// @param tokenId The considered rNFT, _address The corresponding contract address
    /// @return The referring mapping of an rNFT
    function referringOf(address _address, uint256 tokenId) external view virtual override(IERC_5521, TargetContract) returns (address[] memory, uint256[][] memory) {
        address[] memory _referringKeys;
        uint256[][] memory _referringValues;

        if (_address == address(this)) {
            require(_exists(tokenId), "ERC_5521: token ID not existed");
            (_referringKeys, _referringValues) = convertMap(tokenId, true);
        } else {
            TargetContract targetContractInstance = TargetContract(_address);
            require(targetContractInstance.supportsInterface(type(TargetContract).interfaceId), "ERC_5521: target contract not supported");
            (_referringKeys, _referringValues) = targetContractInstance.referringOf(_address, tokenId);     
        }      
        return (_referringKeys, _referringValues);
    }

    /// @notice Get the referred list of an rNFT
    /// @param tokenId The considered rNFT, _address The corresponding contract address
    /// @return The referred mapping of an rNFT
    function referredOf(address _address, uint256 tokenId) external view virtual override(IERC_5521, TargetContract) returns (address[] memory, uint256[][] memory) {
        address[] memory _referredKeys;
        uint256[][] memory _referredValues;

        if (_address == address(this)) {
            require(_exists(tokenId), "ERC_5521: token ID not existed");
            (_referredKeys, _referredValues) = convertMap(tokenId, false);
        } else {
            TargetContract targetContractInstance = TargetContract(_address);
            require(targetContractInstance.supportsInterface(type(TargetContract).interfaceId), "ERC_5521: target contract not supported");
            (_referredKeys, _referredValues) = targetContractInstance.referredOf(_address, tokenId);           
        }
        return (_referredKeys, _referredValues);
    }

    /// @notice Get the timestamp of an rNFT when is being created.
    /// @param `tokenId` of the rNFT being focused, `_address` of contract address associated with the focused rNFT.
    /// @return The timestamp of the rNFT when is being created with uint256 format.
    function createdTimestampOf(address _address, uint256 tokenId) external view returns(uint256) {
        uint256 memory createdTimestamp;

        if (_address == address(this)) {
            require(_exists(tokenId), "ERC_5521: token ID not existed");
            Relationship storage relationship = _relationship[tokenId];
            createdTimestamp = relationship.createdTimestamp;
        } else {
            TargetContract targetContractInstance = TargetContract(_address);
            require(targetContractInstance.supportsInterface(type(TargetContract).interfaceId), "ERC_5521: target contract not supported");
            createdTimestamp = targetContractInstance.createdTimestampOf(_address, tokenId);            
        }
        return createdTimestamp;
    }

    /// @dev See {IERC165-supportsInterface}.
    function supportsInterface(bytes4 interfaceId) public view virtual override (ERC721, IERC_5521, TargetContract) returns (bool) {
        return interfaceId == type(IERC_5521).interfaceId
            || interfaceId == type(TargetContract).interfaceId
            || super.supportsInterface(interfaceId);    
    }

    // @notice Emit an event of UpdateNode
    function emitEvents(uint256 tokenId, address sender) private {
        (address[] memory _referringKeys, uint256[][] memory _referringValues) = convertMap(tokenId, true);
        (address[] memory _referredKeys, uint256[][] memory _referredValues) = convertMap(tokenId, false);
        
        emit UpdateNode(tokenId, sender, _referringKeys, _referringValues, _referredKeys, _referredValues);
    }

    // @notice Convert a specific `local` token mapping to a key array and a value array
    function convertMap(uint256 tokenId, bool isReferring) private view returns (address[] memory, uint256[][] memory) {
        Relationship storage relationship = _relationship[tokenId];

        address[] memory returnKeys;
        uint256[][] memory returnValues;

        if (isReferring) {
            returnKeys = relationship.referringKeys;
            returnValues = new uint256[][](returnKeys.length);
            for (uint i = 0; i < returnKeys.length; i++) {
                returnValues[i] = relationship.referring[returnKeys[i]];
            }            
        } else {
            returnKeys = relationship.referredKeys;
            returnValues = new uint256[][](returnKeys.length);
            for (uint i = 0; i < returnKeys.length; i++) {
                returnValues[i] = relationship.referred[returnKeys[i]];
            }
        }
        return (returnKeys, returnValues);
    }
}

```


## 安全考虑

### 时间戳

`createdTimestamp` 仅涵盖区块级时间戳（基于区块头），不支持诸如交易级别的细粒度比较。

### 所有权和引用

所有权的变更与引用关系无关。通常，利润的分配遵循 NFT 创建时的协议，而不论所有权的变更，除非协议中另有规定。

引用一个代币不会默认引用其后代。在仅引用特定子代币的情况下，这意味着合同的私密性将只涉及该特定子代币的所有者。或者，可以从根代币到特定最底层子代币（从根到叶）构建并记录一个引用链，以在 `referring` 中明确定义利润的分配。

### 开放铸造和关系风险

`safeMint` 函数被故意设计为允许无限制的铸造和关系设置，类似于 Google Scholar 等平台中看到的开放引用系统。这个决定促进了强大的灵活性，使任何用户都可以创建和定义 NFT 之间的关系，而无需集中控制。虽然这种设计与系统的开放性意图一致，但它固有地带来了一定的风险。可以创建未经授权或不正确的引用，类似于传统学术引用中可能出现的错误引用。此外，开放的性质可能使系统暴露于恶意行为者的潜在滥用，他们可能操纵关系或膨胀代币供应。重要的是要认识到，这些风险并不被视为设计缺陷，而是有意的权衡，平衡了系统的灵活性与潜在的可靠性问题。

利益相关者应意识到，链上数据完整性保证仅扩展到已记录在区块链上的内容，并不排除链外错误或操控的可能性。因此，用户和集成者在解释和使用该系统提供的关系和其他数据时应谨慎和明智。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。