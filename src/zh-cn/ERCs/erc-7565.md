---
eip: 7565
title: 永续合约 NFT 作为抵押品
description: 将金融资产锁定为 NFT，并将其用作在 DeFi 中借款的抵押品，以促进流动性提供。
author: Hyoungsung Kim (@HyoungsungKim) <hyougnsung@keti.re.kr>, Yong-Suk Park <yspark@keti.re.kr>, Hyun-Sik Kim <hskim@keti.re.kr>
discussions-to: https://ethereum-magicians.org/t/erc-7565-proposal-perpetual-contract-nft-for-defi-composability/16790
status: 草案
type: 标准跟踪
category: ERC
created: 2023-11-27
requires: 721, 4907
---

## 摘要

本 ERC 提议了一种机制，允许一个人（称为“资产拥有者”）将代表锁定存款或资产的 NFT 作为抵押品，以借款。这些 NFT 代表在预定到期后索取基础资产及其任何累积收益的权利。 [^1]

[^1]:
    ```csl-json
    {
        "container-title": "IEEE Access",
        "author": [
            {
                "given": "Hyoungsung",
                "family": "Kim"
            },
            {
                "given": "Hyun-Sik",
                "family": "Kim"
            },
            {
                "given": "Yong-Suk",
                "family": "Park"
            }
        ],
        "DOI": "10.1109/ACCESS.2022.3225884",
        "URL": "https://ieeexplore.ieee.org/document/9967987",
        "type": "article-journal",
        "id": 9967987,
        "citation-label": "9967987",        
        "issued": {
            "date-parts": [
                [
                    2022
                ]
            ]
        },
        "keyword": "Contracts;Nonfungible tokens;Cryptocurrency;Finance;Smart contracts;Blockchains;Financial services;Automated market maker (AMM);blockchain;decentralized exchange (DEX);decentralized finance (DeFi);Ethereum;liquidity pool (LP);non-fungible token (NFT);uniswap",
        "page": "126802-126814",
        "title": "Perpetual Contract NFT as Collateral for DeFi Composability",
        "volume": 10
    }
    ```

## 动机

快速发展的 DeFi 领域引入了各种资产锁定机制，提供了利息和投票权等好处。然而，这个领域面临的一个重大挑战是，在这些资产被锁定时保持流动性。本 ERC 通过提出一种利用 [ERC-721](./eip-721.md) 和 [ERC-4907](./eip-4907.md) 的锁定资产生成利润的方法来解决这一挑战。

在 DeFi 服务中，运行自动化做市商（AMM）时，流动性提供者将资产贡献给池，并获得代表其股份的 NFT。这些 NFT 表示对资产及相关利益的权利，但它们也将资产锁定在池中，常常导致提供者面临流动性挑战。目前的做法要求提供者为紧急流动性需求提取其资产，这对池的流动性产生不利影响，并可能在资产交换过程中增加滑点。

我们的提议允许这些代表流动性池中锁定资产的 NFT 作为抵押品。这种方法使流动性提供者能够在不提取资产的情况下获得临时流动性，从而保持池的流动性水平。此外，它还扩展到更广泛的 DeFi 服务，包括借贷和交易，在这些服务中资产锁定是普遍存在的。通过允许通过 NFT 对锁定资产的表示进行抵押，我们的做法旨在为 DeFi 服务提供多样化的流动性解决方案，惠及生态系统内的多样化用户群体。

我们引入的永续合约 NFT 概念利用了加密货币衍生品市场中的永续期货合约的理念。这些 NFT 代表对永续合约及其抵押品的权利，使其能够有效地用作 DeFi 组合的抵押品。永续合约 NFT 提供了一种新的 NFT 形式，增强了锁定资产的效用，通过在保留资产锁定利益的同时提供流动性，为 DeFi 应用提供了显著优势。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“NOT RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 合约接口

Solidity 接口。

```solidity
    interface IPerpetualContractNFT {

        // Emitted when an NFT is collateralized for obtaining a loan
        event Collateralized(uint256 indexed tokenId, address indexed owner, uint256 loanAmount, uint256 interestRate, uint256 loanDuration);

        // Emitted when a loan secured by an NFT is fully repaid, releasing the NFT from collateral
        event LoanRepaid(uint256 indexed tokenId, address indexed owner);

        // Emitted when a loan defaults, resulting in the transfer of the NFT to the lender
        event Defaulted(uint256 indexed tokenId, address indexed lender);

        // Enables an NFT owner to collateralize their NFT in exchange for a loan
        // @param tokenId The NFT to be used as collateral
        // @param loanAmount The amount of funds to be borrowed
        // @param interestRate The interest rate for the loan
        // @param loanDuration The duration of the loan
        function collateralize(uint256 tokenId, uint256 loanAmount, uint256 interestRate, uint64 loanDuration) external;

        // Enables a borrower to repay their loan and regain ownership of the collateralized NFT
        // @param tokenId The NFT that was used as collateral
        // @param repayAmount The amount of funds to be repaid
        function repayLoan(uint256 tokenId, uint256 repayAmount) external;

        // Allows querying the loan terms for a given NFT
        // @param tokenId The NFT used as collateral
        // @return loanAmount The amount of funds borrowed
        // @return interestRate The interest rate for the loan
        // @return loanDuration The duration of the loan
        // @return loanDueDate The due date for the loan repayment
        function getLoanTerms(uint256 tokenId) external view returns (uint256 loanAmount, uint256 interestRate, uint256 loanDuration, uint256 loanDueDate);

        // Allows querying the current owner of the NFT
        // @param tokenId The NFT in question
        // @return The address of the current owner
        function currentOwner(uint256 tokenId) external view returns (address);

        // View the total amount required to repay the loan for a given NFT
        // @param tokenId The NFT used as collateral
        // @return The total amount required to repay the loan, including interest
        function viewRepayAmount(uint256 tokenId) external view returns (uint256);
    }
```

#### 事件 `Collateralized`

- 当抵押功能成功执行时，必须触发`Collateralized`事件。
- 用法：记录 NFT 作为贷款抵押品使用的事件，捕获贷款金额、利率和贷款期限等重要细节。

#### 事件 `LoanRepaid`

- 当 repayLoan 功能成功执行时，必须触发`LoanRepaid`事件。
- 用法：记录贷款被偿还及相应 NFT 从抵押中释放的事件。

#### 事件 `Defaulted`

- 在贷款违约且 NFT 转移给贷方的情况下，必须触发`Defaulted`事件。
- 用法：用于记录贷款违约及 NFT 转移给贷方的事件。

#### 函数 `collateralize`

- `collateralize`事件应实现为`external`。
- 用法：允许 NFT 拥有者将其 NFT 抵押以获得贷款。

#### 函数 `repayLoan`

- `repayLoan`函数应实现为`external`。
- 用法：使 NFT 拥有者能够偿还其贷款并收回其 NFT。
  
#### 函数 `getLoanTerms`

- `getLoanTerms`函数可以实现为`external` `view`。
- 用法：允许查询给定 NFT 的贷款条款。

#### 函数 `currentOwner`

- `currentOwner`函数可以实现为`external` `view`。
- 用法：允许查询特定 NFT 的当前拥有者。

#### 函数 `viewRepayAmount`

- `viewRepayAmount`函数可以实现为`external` `view`。
- 用法：允许查询特定 NFT 的当前偿还金额。
  
## 理由

### 设计动机

本标准的设计旨在解决 DeFi 领域中特定的挑战，特别是与作为抵押品锁定的资产的流动性和管理相关的挑战。传统的 DeFi 机制通常要求资产持有者将其资产锁定以参与借贷、质押或收益农业等活动，这导致流动性损失。本标准旨在引入一种更灵活的方法，允许资产持有者在其资产被锁定时保留一定的流动性，从而增强 DeFi 产品的效用和吸引力。

### 设计决策

- 双重角色系统（资产拥有者和 DeFi 平台/合约）：在 NFT 拥有者（资产持有者）和利用 NFT 作为抵押品的 DeFi 平台或合约之间建立明确的划分。这一区别简化了权利和责任的管理，增强了清晰度并减少了潜在冲突。

- 在不妨碍资产锁定利益的情况下增强流动性：本标准的一个关键特性是使资产拥有者能够将其代表锁定资产的 NFT 作为抵押品以获得贷款。这种方法使资产拥有者能够在不需要从池或质押程序中提取资产的情况下获得流动性，从而保留相关的利益，如利息累积或投票权。

- 自动化贷款和抵押管理：集成自动化功能以管理抵押 NFT 的条款和条件是一个有意的选择，旨在最小化交易成本和复杂性。

- DeFi 组合性：本标准的战略重点是 DeFi 组合性，特别是资产锁定与抵押服务之间的集成。这种方法旨在简化标准在各种 DeFi 平台和服务中的采用，促进 DeFi 生态系统内的无缝连接。

### 替代设计和相关工作

- 与 [ERC-4907](./eip-4907.md) 的比较：虽然 [ERC-4907](./eip-4907.md) 也引入了 NFT 的双重角色模型（拥有者和用户），但我们的标准专注于 NFT 在金融交易中作为抵押品的使用，偏离了 [ERC-4907](./eip-4907.md) 的租赁导向方法。

- 改进传统抵押方法：与传统 DeFi 抵押相比，后者通常要求完全锁定资产，本标准提出了一种更动态和灵活的模型，允许继续访问流动性。

## 向后兼容性

与 [ERC-721](./eip-721.md) 完全兼容，并与 [ERC-4907](./eip-4907.md) 集成以租赁 NFT。

## 测试用例

```solidity
// SPDX-License-Identifier: CC0-1.0 
pragma solidity ^0.8.0;

import "./PerpetualContractNFT.sol";

contract PerpetualContractNFTDemo is PerpetualContractNFT {

    constructor(string memory name, string memory symbol)
        PerpetualContractNFT(name, symbol)
    {         
    }

    function mint(uint256 tokenId, address to) public {
        _mint(to, tokenId);
    }
}
```

```solidity
import { expect } from "chai";
import { ethers } from "hardhat";

describe("PerpetualContractNFTDemo", function () {
    it("should allow an owner to collateralize an NFT, rent it to a contract, and then have the owner repay the loan", async function () {
        const [owner] = await ethers.getSigners();

        const PerpetualContractNFTDemo = await ethers.getContractFactory("PerpetualContractNFTDemo");
        const demo = await PerpetualContractNFTDemo.deploy("DemoNFT", "DNFT");
        await demo.waitForDeployment();
        expect(demo.target).to.be.properAddress;

        // Mint an NFT to the owner
        await demo.mint(1, owner.address);

        // Owner collateralizes the NFT for a loan
        const loanAmount = ethers.parseUnits("1", "ether"); // 1 Ether in Wei. Use Wei to avoid precision error.
        const interest = 5; // 5% interest
        const expiration = Math.floor(new Date().getTime() / 1000) + 3600; // Expire after 60 minutes (3600 seconds), convert it to seconds because `hours` in solidity converted to seconds
        
        await demo.connect(owner).collateralize(1, loanAmount, interest, expiration); // tokenId, loanAmount, interestRate, loanDuration

        // Check current user of the NFT (should be the contract address)
        expect(await demo.userOf(1)).to.equal(demo.target);

        // Borrower repays the loan to release the NFT
        const repayAmountWei = await demo.connect(owner).viewRepayAmount(1);
        await demo.connect(owner).repayLoan(1, repayAmountWei);
        
        // Check if the NFT is returned to the original owner after the loan is repaid
        expect(await demo.userOf(1)).to.equal("0x0000000000000000000000000000000000000000");
    });
    });
```

在终端中运行：

```console
npx hardhat test
```

## 参考实现

```solidity
// SPDX-License-Identifier: CC0-1.0 
pragma solidity ^0.8.0;

//import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IPerpetualContractNFT.sol";
import "./ERC4907/ERC4907.sol";

contract PerpetualContractNFT is ERC4907, IPerpetualContractNFT {
    struct LoanInfo {
        address borrower;   // Address that borrowed against the NFT
        uint256 loanAmount; // Amount of funds borrowed
        uint256 interestRate; // Interest rate for the loan
        uint64 loanDuration; // Duration of the loan
        uint256 loanStartTime; // Timestamp when the loan starts
    }

    mapping(uint256 => LoanInfo) internal _loans;

    //Constructor to initialize the Perpetual Contract NFT contract with the given name and symbo
    constructor(string memory name_, string memory symbol_)
        ERC4907(name_, symbol_)
    {}

    function collateralize(uint256 tokenId, uint256 loanAmount, uint256 interestRate, uint64 loanDuration) public override {
        require(ownerOf(tokenId) == msg.sender || isApprovedForAll(ownerOf(tokenId), msg.sender) || getApproved(tokenId) == msg.sender, "Not owner nor approved");

        LoanInfo storage info = _loans[tokenId];
        info.borrower = msg.sender;
        // The loan amount should reflect the asset's value as represented by the NFT, considering an appropriate loan-to-value (LTV) ratio.
        info.loanAmount = loanAmount;
        info.interestRate = interestRate;
        info.loanDuration = loanDuration;
        info.loanStartTime = block.timestamp;

        setUser(tokenId, address(this), loanDuration);
        emit Collateralized(tokenId, msg.sender, loanAmount, interestRate, loanDuration);

        // Further logic can be implemented here to manage the lending of assets
    }

    function repayLoan(uint256 tokenId, uint256 repayAmount) public override {
        require(_loans[tokenId].borrower == msg.sender, "Not the borrower.");

        // Calculate the total amount due for repayment
        uint256 totalDue = viewRepayAmount(tokenId);

        // Check if the repayAmount is sufficient to cover at least a part of the total due amount
        require(repayAmount <= totalDue, "Repay amount exceeds total due.");

        // Calculate the remaining loan amount after repayment
        _loans[tokenId].loanAmount = totalDue - repayAmount;

        // Resets the user of the NFT to the default state if the entire loan amount is fully repaid
        if(_loans[tokenId].loanAmount == 0) {
            setUser(tokenId, address(0), 0);
        }

        emit LoanRepaid(tokenId, msg.sender);
    }


    function getLoanTerms(uint256 tokenId) public view override returns (uint256, uint256, uint256, uint256) {
        LoanInfo storage info = _loans[tokenId];
        return (info.loanAmount, info.interestRate, info.loanDuration, info.loanStartTime);
    }

    function currentOwner(uint256 tokenId) public view override returns (address) {
        return ownerOf(tokenId);
    }

    function viewRepayAmount(uint256 tokenId) public view returns (uint256) {
        if (_loans[tokenId].loanAmount == 0) {
            // If the loan amount is zero, there is nothing to repay
            return 0;
        }

        // The interest is calculated on an hourly basis, prorated based on the actual duration for which the loan was held.
        // If the borrower repays before the loan duration ends, they are charged interest only for the time the loan was held.
        // For example, if the annual interest rate is 5% and the borrower repays in half the loan term, they pay only 2.5% interest.
        uint256 elapsed = block.timestamp > (_loans[tokenId].loanStartTime + _loans[tokenId].loanDuration) 
                        ? _loans[tokenId].loanDuration  / 1 hours
                        : (block.timestamp - _loans[tokenId].loanStartTime) / 1 hours;

        // Round up
        // Example: 15/4 = 3.75
        // round((15 + 4 - 1)/4) = 4, round((15/4) = 3)
        uint256 interest = ((_loans[tokenId].loanAmount * _loans[tokenId].interestRate / 100) * elapsed + (_loans[tokenId].loanDuration / 1 hours) - 1) / 
                    (_loans[tokenId].loanDuration / 1 hours);

        // Calculate the total amount due
        uint256 totalDue = _loans[tokenId].loanAmount + interest;

        return totalDue;
    }

    // Additional functions and logic to handle loan defaults, transfers, and other aspects of the NFT lifecycle
}
```

## 安全考虑

<!-- 需要讨论。 -->

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。