---
eip: 4519
title: 与物理资产相关的非同质化代币
description: 表示物理资产的非同质化代币接口，这些代币可以生成或恢复自己的账户并遵循用户。
author: Javier Arcenegui (@Hardblock-IMSE-CNM), Rosario Arjona (@RosarioArjona), Roberto Román <roman@imse-cnm.csic.es>, Iluminada Baturone (@lumi2018)
discussions-to: https://ethereum-magicians.org/t/new-proposal-of-smart-non-fungible-token/7677
status: Final
type: Standards Track
category: ERC
created: 2021-12-03
requires: 165, 721
---

## 摘要

本 EIP 标准化了表示物理资产的非同质化代币接口，例如物联网 (IoT) 设备。这些 NFT 与物理资产相关联，并可以验证这种关联的真实性。它们可以包含物理资产的以太坊地址，允许物理资产签署消息和交易。物理资产可以根据其相应的 NFT 定义的操作模式进行操作。

## 动机

该标准的开发是因为 [EIP-721](./eip-721.md) 仅跟踪所有权（而不是使用权），并且不跟踪资产的以太坊地址。智能资产（如物联网设备）的受欢迎程度正在上升。为了实现安全和可追溯的管理，这些 NFT 可以用于在物理资产、其所有者和用户之间建立安全的通信通道。

## 规范

属性 `addressAsset` 和 `addressUser` 分别是物理资产和用户的以太坊地址。它们是可选属性，但至少应在 NFT 中使用其中一个。如果仅使用属性 `addressUser`，则有两个状态定义代币是否分配给用户。`图 1` 显示了这些状态的流程图。当代币被创建、转移或未分配时，代币状态设置为 `notAssigned`。如果代币分配给有效用户，则状态设置为 `userAssigned`。

![图 1 : 定义了 `addressUser`（且未定义 `addressAsset`）的代币状态流程图](../assets/eip-4519/images/Figure1.jpg)

如果定义了属性 `addressAsset` 但未定义属性 `addressUser`，则有两个状态定义代币是否在等待与所有者的认证，或认证是否已成功完成。`图 2` 显示了这些状态的流程图。当代币被创建或转移给新所有者时，代币状态变为 `waitingForOwner`。在此状态下，代币在等待资产与所有者之间的相互认证。一旦认证成功完成，代币状态变为 `engagedWithOwner`。

![图 2 : 定义了 `addressAsset`（且未定义 `addressUser`）的代币状态流程图](../assets/eip-4519/images/Figure2.jpg)

最后，如果同时定义了属性 `addressAsset` 和 `addressUser`，则 NFT 的状态定义资产是否已与所有者或用户进行交互（`waitingForOwner`、`engagedWithOwner`、`waitingForUser` 和 `engagedWithUser`）。`图 3` 中的流程图显示了所有可能的状态变化。与所有者相关的状态与 `图 2` 中相同。不同之处在于，在状态 `engagedWithOwner` 下，代币可以分配给用户。如果分配给用户（代币处于状态 `engagedWithOwner`、`waitingForUser` 或 `engagedWithUser`），则代币状态变为 `waitingForUser`。一旦资产和用户相互认证，代币状态设置为 `engagedWithUser`，用户便可以使用该资产。

![图 3 : 定义了 `addressUser` 和 `addressUser` 的代币状态流程图](../assets/eip-4519/images/Figure3.jpg)

为了完成代币的所有权转移，新所有者必须与资产进行相互认证过程，该过程在链下与资产进行，在链上与代币进行，使用他们的以太坊地址。类似地，新用户必须与资产进行相互认证过程以完成使用转移。NFT 定义了认证过程的开始和结束。这些认证过程允许为资产与所有者之间、资产与用户之间的安全通信派生新的会话加密密钥。因此，即使新所有者和用户管理资产，资产的可信度也可以被追踪。

当 NFT 被创建或所有权被转移时，代币状态为 `waitingForOwner`。资产将其操作模式设置为 `waitingForOwner`。所有者使用椭圆曲线 secp256k1 和该曲线上的原始元素 P 生成一对密钥：一个私钥 SK<sub>O_A</sub> 和一个公钥 PK<sub>O_A</sub>，使得 PK<sub>O_A</sub> = SK<sub>O_A</sub> * P。为了生成所有者与资产之间的共享密钥 K<sub>O</sub>，使用资产的公钥 PK<sub>A</sub>，如下所示：

K<sub>O</sub> = PK<sub>A</sub> * SK<sub>O_A</sub>

使用函数 `startOwnerEngagement`，PK<sub>O_A</sub> 被保存为属性 `dataEngagement`，K<sub>O</sub> 的哈希值被保存为属性 `hashK_OA`。所有者向资产发送请求参与，资产计算：

K<sub>A</sub> = SK<sub>A</sub> * PK<sub>O_A</sub>

如果一切正常，K<sub>O</sub> 和 K<sub>A</sub> 是相同的，因为：

K<sub>O</sub> = PK<sub>A</sub> * SK<sub>O_A</sub> = (SK<sub>A</sub> * P) * SK<sub>O_A</sub> = SK<sub>A</sub> * (SK<sub>O_A</sub> * P) = SK<sub>A</sub> * PK<sub>O_A</sub>

使用函数 `ownerEngagement`，资产发送 K<sub>A</sub> 的哈希值，如果与 `hashK_OA` 中的数据相同，则代币状态变为 `engagedWithOwner`，并发送事件 `OwnerEngaged`。一旦资产接收到该事件，它将其操作模式更改为 `engagedWithOwner`。此过程在 `图 4` 中显示。从此时起，资产可以由所有者管理，他们可以使用共享密钥进行安全通信。

![图 4: 成功的所有者与资产相互认证过程中的步骤](../assets/eip-4519/images/Figure4.jpg)

如果资产查询以太坊并且其 NFT 的状态为 `waitingForUser`，则资产（假设它是一个电子物理资产）将其操作模式设置为 `waitingForUser`。然后，与用户进行相互认证过程，如同与所有者所做的那样。用户发送与函数 `startUserEngagement` 相关的交易。与 `startOwnerEngagement` 一样，该函数将用户生成的公钥 PK<sub>U_A</sub> 保存为属性 `dataEngagement`，并将 K<sub>U</sub> = PK<sub>A</sub> * SK<sub>U_A</sub> 的哈希值保存为属性 `hashK_UA` 在 NFT 中。

用户发送请求参与，资产计算：

K<sub>A</sub> = SK<sub>A</sub> * PK<sub>U_A</sub>

如果一切正常，K<sub>U</sub> 和 K<sub>A</sub> 是相同的，因为：

K<sub>U</sub> = PK<sub>A</sub> * SK<sub>U_A</sub> = (SK<sub>A</sub> * P) * SK<sub>U_A</sub> = SK<sub>A</sub> * (SK<sub>U_A</sub> * P) = SK<sub>A</sub> * PK<sub>U_A</sub>

使用函数 `userEngagement`，资产发送获得的 K<sub>A</sub> 的哈希值，如果与 `hashK_UA` 中的数据相同，则代币状态变为 `engagedWithUser`，并发送事件 `UserEngaged`。一旦资产接收到该事件，它将其操作模式更改为 `engagedWithUser`。此过程在 `图 5` 中显示。从此时起，资产可以由用户管理，他们可以使用共享密钥进行安全通信。
![图 5：成功的用户和资产相互认证过程中的步骤](../assets/eip-4519/images/Figure5.jpg)

由于建立共享密钥对于安全通信非常重要，NFT 包含属性 `hashK_OA`、`hashK_UA` 和 `dataEngagement`。前两个属性分别定义了资产与其所有者之间以及资产与其用户之间共享的密钥的哈希值。资产、所有者和用户应检查他们是否使用正确的共享密钥。属性 `dataEngagement` 定义了达成协议所需的公共数据。

```solidity
pragma solidity ^0.8.0;
 /// @title EIP-4519 NFT: Extension of EIP-721 Non-Fungible Token Standard. 
///  Note: the EIP-165 identifier for this interface is 0x8a68abe3
 interface EIP-4519 NFT is EIP721/*,EIP165*/{
    /// @dev This emits when the NFT is assigned as utility of a new user.
    ///  This event emits when the user of the token changes.
    ///  (`_addressUser` == 0) when no user is assigned.
    event UserAssigned(uint256 indexed tokenId, address indexed _addressUser);
    
    /// @dev This emits when user and asset finish mutual authentication process successfully.
    ///  This event emits when both the user and the asset prove they share a secure communication channel.
    event UserEngaged(uint256 indexed tokenId);
    
    /// @dev This emits when owner and asset finish mutual authentication process successfully.
    ///  This event emits when both the owner and the asset prove they share a secure communication channel.
    event OwnerEngaged(uint256 indexed tokenId);
    
    /// @dev This emits when it is checked that the timeout has expired.
    ///  This event emits when the timestamp of the EIP-4519 NFT is not updated in timeout.
    event TimeoutAlarm(uint256 indexed tokenId);
    /// @notice This function defines how the NFT is assigned as utility of a new user (if "addressUser" is defined).
    /// @dev Only the owner of the EIP-4519 NFT can assign a user. If "addressAsset" is defined, then the state of the token must be
    /// "engagedWithOwner","waitingForUser" or "engagedWithUser" and this function changes the state of the token defined by "_tokenId" to
    /// "waitingForUser". If "addressAsset" is not defined, the state is set to "userAssigned". In both cases, this function sets the parameter 
    /// "addressUser" to "_addressUser". 
    /// @param _tokenId is the tokenId of the EIP-4519 NFT tied to the asset.
    /// @param _addressUser is the address of the new user.
    function setUser(uint256 _tokenId, address _addressUser) external payable; 
    /// @notice This function defines the initialization of the mutual authentication process between the owner and the asset.
    /// @dev Only the owner of the token can start this authentication process if "addressAsset" is defined and the state of the token is "waitingForOwner".
    /// The function does not change the state of the token and saves "_dataEngagement" 
    /// and "_hashK_OA" in the parameters of the token.
    /// @param _tokenId is the tokenId of the EIP-4519 NFT tied to the asset.
    /// @param _dataEngagement is the public data proposed by the owner for the agreement of the shared key.
    /// @param _hashK_OA is the hash of the secret proposed by the owner to share with the asset.
    function startOwnerEngagement(uint256 _tokenId, uint256 _dataEngagement, uint256 _hashK_OA) external payable;
 
    /// @notice This function completes the mutual authentication process between the owner and the asset.
    /// @dev Only the asset tied to the token can finish this authentication process provided that the state of the token is
    /// "waitingForOwner" and dataEngagement is different from 0. This function compares hashK_OA saved in
    /// the token with hashK_A. If they are equal then the state of the token changes to "engagedWithOwner", dataEngagement is set to 0,
    /// and the event "OwnerEngaged" is emitted.
    /// @param _hashK_A is the hash of the secret generated by the asset to share with the owner.
    function ownerEngagement(uint256 _hashK_A) external payable; 
 
    /// @notice This function defines the initialization of the mutual authentication process between the user and the asset.
    /// @dev Only the user of the token can start this authentication process if "addressAsset" and "addressUser" are defined and
    /// the state of the token is "waitingForUser". The function does not change the state of the token and saves "_dataEngagement" 
    /// and "_hashK_UA" in the parameters of the token.
    /// @param _tokenId is the tokenId of the EIP-4519 NFT tied to the asset.
    /// @param _dataEngagement is the public data proposed by the user for the agreement of the shared key.
    /// @param _hashK_UA is the hash of the secret proposed by the user to share with the asset.
    function startUserEngagement(uint256 _tokenId, uint256 _dataEngagement, uint256 _hashK_UA) external payable;
    
    /// @notice This function completes the mutual authentication process between the user and the asset.
    /// @dev Only the asset tied to the token can finish this authentication process provided that the state of the token is
    /// "waitingForUser" and dataEngagement is different from 0. This function compares hashK_UA saved in
    /// the token with hashK_A. If they are equal then the state of the token changes to "engagedWithUser", dataEngagement is set to 0,
    /// and the event "UserEngaged" is emitted.
    /// @param _hashK_A is the hash of the secret generated by the asset to share with the user.
    function userEngagement(uint256 _hashK_A) external payable; 
 
    /// @notice This function checks if the timeout has expired.
    /// @dev Everybody can call this function to check if the timeout has expired. The event "TimeoutAlarm" is emitted
    /// if the timeout has expired.
    /// @param _tokenId is the tokenId of the EIP-4519 NFT tied to the asset.
    /// @return true if timeout has expired and false in other case.
    function checkTimeout(uint256 _tokenId) external returns (bool);
    
    /// @notice This function sets the value of timeout.
    /// @dev Only the owner of the token can set this value provided that the state of the token is "engagedWithOwner",
    /// "waitingForUser" or "engagedWithUser".
    /// @param _tokenId is the tokenId of the EIP-4519 NFT tied to the asset.
    /// @param _timeout is the value to assign to timeout.
    function setTimeout(uint256 _tokenId, uint256 _timeout) external; 
    
    /// @notice This function updates the timestamp, thus avoiding the timeout alarm.
    /// @dev Only the asset tied to the token can update its own timestamp.
    function updateTimestamp() external; 
    
    /// @notice This function lets obtain the tokenId from an address. 
    /// @dev Everybody can call this function. The code executed only reads from Ethereum.
    /// @param _addressAsset is the address to obtain the tokenId from it.
    /// @return tokenId of the token tied to the asset that generates _addressAsset.
    function tokenFromBCA(address _addressAsset) external view returns (uint256);
    
    /// @notice This function lets know the owner of the token from the address of the asset tied to the token.
    /// @dev Everybody can call this function. The code executed only reads from Ethereum.
    /// @param _addressAsset is the address to obtain the owner from it.
    /// @return owner of the token bound to the asset that generates _addressAsset.
    function ownerOfFromBCA(address _addressAsset) external view returns (address);
    
    /// @notice This function lets know the user of the token from its tokenId.
    /// @dev Everybody can call this function. The code executed only reads from Ethereum.
    /// @param _tokenId is the tokenId of the EIP-4519 NFT tied to the asset.
    /// @return user of the token from its _tokenId.
    function userOf(uint256 _tokenId) external view returns (address);
    
    /// @notice This function lets know the user of the token from the address of the asset tied to the token.
    /// @dev Everybody can call this function. The code executed only reads from Ethereum.
    /// @param _addressAsset is the address to obtain the user from it.
    /// @return user of the token tied to the asset that generates _addressAsset.
    function userOfFromBCA(address _addressAsset) external view returns (address);
    
    /// @notice This function lets know how many tokens are assigned to a user.
    /// @dev Everybody can call this function. The code executed only reads from Ethereum.
    /// @param _addressUser is the address of the user.
    /// @return number of tokens assigned to a user.
    function userBalanceOf(address _addressUser) external view returns (uint256);
    
    /// @notice This function lets know how many tokens of a particular owner are assigned to a user.
    /// @dev Everybody can call this function. The code executed only reads from Ethereum.
    /// @param _addressUser is the address of the user.
    /// @param _addressOwner is the address of the owner.
    /// @return number of tokens assigned to a user from an owner.
    function userBalanceOfAnOwner(address _addressUser, address _addressOwner) external view returns (uint256);
}
```

## 理由

### 认证

该 EIP 使用智能合约来验证相互认证过程，因为智能合约是无信任的。

### 绑定时间

该 EIP 提议包括属性时间戳（用于在以太坊上注册物理资产最后一次检查与其代币的绑定时间）和属性超时（用于注册物理资产再次证明绑定的最大延迟时间）。这些属性避免了恶意的所有者或用户可以无限制地使用资产。

当资产调用 `updateTimestamp` 时，智能合约必须调用 `block.timestamp`，该函数提供自 Unix 纪元以来的当前区块时间戳（以秒为单位）。因此，`timeout` 必须以秒为单位提供。

### 基于 EIP-721

[EIP-721](./eip-721.md) 是最常用的通用 NFT 标准。该 EIP 扩展了 EIP-721，以实现向后兼容。

## 向后兼容性

该标准是 EIP-721 的扩展。它与 EIP-721 标准中提到的两种常用可选扩展（`IERC721Metadata` 和 `IERC721Enumerable`）完全兼容。

## 测试用例

论文中展示的测试用例可在 [这里](../assets/eip-4519/PoC_SmartNFT/README.md) 获取。

## 参考实现

第一版在 **MDPI** 出版社的 **Sensors** 期刊的特刊 **新计算环境中的安全、信任和隐私** 中发表。该论文题为 [通过将 IoT 设备物理绑定到智能非同质代币使用 PUFs 的安全组合](../assets/eip-4519/sensors-21-03119.pdf)，由该 EIP 的同一作者撰写。

## 安全考虑

在该 EIP 中，提出了一种通用系统，用于创建与物理资产绑定的非同质代币。提供了基于当前 EIP-721 NFT 改进的通用观点，例如用户管理机制的实现，这不会影响代币的所有权。物理资产应能够以完全随机的方式生成以太坊地址，以便只有资产能够知道生成以太坊地址的秘密。通过这种方式，可以避免身份盗用，并且资产可以被证明是完全真实的。为了确保这一点，建议只有资产的制造商有能力创建其关联的代币。在 IoT 设备的情况下，设备固件将无法共享和修改秘密。建议资产从与物理不可克隆函数（PUFs）相关的非敏感信息中重建其秘密，而不是存储秘密。尽管已经提出了一种基于椭圆曲线的安全密钥交换协议，但该代币可以与其他类型的密钥交换共存。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。