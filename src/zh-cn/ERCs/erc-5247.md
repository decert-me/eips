---
eip: 5247
title: 智能合约可执行提案接口
description: 创建和执行提案的接口。
author: Zainan Victor Zhou (@xinbenlv)
discussions-to: https://ethereum-magicians.org/t/erc-5247-executable-proposal-standard/9938
status: 审核
type: 标准跟踪
category: ERC
created: 2022-07-13
---

## 摘要

本 EIP 提出了一个“智能合约可执行提案”的接口：提交、记录并可能在链上执行的提案。这些提案包括一系列关于函数调用的信息，包括目标合约地址、要传输的以太值、gas 限制和调用数据。

## 动机

有时需要将要执行的代码与实际执行代码分开。

此 EIP 的一个典型用例是在去中心化自治组织（DAO）中。提案者将创建一个智能提案并进行倡导。成员们随后将选择是否支持该提案并进行投票（参见 `ERC-1202`）。最后，当达成共识时，提案将被执行。

第二个典型用例是可以有一个他们信任的人，例如委托人、受托人或代理人，或任何双边合作形式，其中智能提案将首先被组成、讨论、以某种方式批准，然后付诸执行。

第三个用例是一个人可以向另一个人提出“报价”，可能附带条件。智能提案可以作为报价呈现，第二个人可以选择接受该提案并执行它。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

interface IERC5247 {
    event ProposalCreated(
        address indexed proposer,
        uint256 indexed proposalId,
        address[] targets,
        uint256[] values,
        uint256[] gasLimits,
        bytes[] calldatas,
        bytes extraParams
    );

    event ProposalExecuted(
        address indexed executor,
        uint256 indexed proposalId,
        bytes extraParams
    );

    function createProposal(
        uint256 proposalId,
        address[] calldata targets,
        uint256[] calldata values,
        uint256[] calldata gasLimits,
        bytes[] calldata calldatas,
        bytes calldata extraParams
    ) external returns (uint256 registeredProposalId);

    function executeProposal(uint256 proposalId, bytes calldata extraParams) external;
}
```

## 理由

* 最初，这个接口是 `ERC-1202` 的一部分。然而，提案本身可能在投票之外有许多用例。投票可能不需要以任何特定格式进行提案。因此，我们决定*解耦投票接口和提案接口*。
* 使用数组来表示 `target`、`value`、`calldata`，而不是单个变量，允许提案携带任意长的多个功能调用。
* `createProposal` 中返回 `registeredProposalId`，以便标准可以支持实现决定其提案 ID 的格式。

## 测试用例

一个简单的测试用例可以在以下位置找到

```ts
        it("Should work for a simple case", async function () {
            const { contract, erc721, owner } = await loadFixture(deployFixture);
            const callData1 = erc721.interface.encodeFunctionData("mint", [owner.address, 1]);
            const callData2 = erc721.interface.encodeFunctionData("mint", [owner.address, 2]);
            await contract.connect(owner)
                .createProposal(
                    0,
                    [erc721.address, erc721.address],
                    [0,0],
                    [0,0],
                    [callData1, callData2],
                    []);
            expect(await erc721.balanceOf(owner.address)).to.equal(0);
            await contract.connect(owner).executeProposal(0, []);
            expect(await erc721.balanceOf(owner.address)).to.equal(2);
        });
```

请参见 [testProposalRegistry.ts](../assets/eip-5247/testProposalRegistry.ts) 以获取完整的测试集。

## 参考实现

可以找到一个简单的参考实现。

```solidity
    function createProposal(
        uint256 proposalId,
        address[] calldata targets,
        uint256[] calldata values,
        uint256[] calldata gasLimits,
        bytes[] calldata calldatas,
        bytes calldata extraParams
    ) external returns (uint256 registeredProposalId) {
        require(targets.length == values.length, "GeneralForwarder: targets and values length mismatch");
        require(targets.length == gasLimits.length, "GeneralForwarder: targets and gasLimits length mismatch");
        require(targets.length == calldatas.length, "GeneralForwarder: targets and calldatas length mismatch");
        registeredProposalId = proposalCount;
        proposalCount++;

        proposals[registeredProposalId] = Proposal({
            by: msg.sender,
            proposalId: proposalId,
            targets: targets,
            values: values,
            calldatas: calldatas,
            gasLimits: gasLimits
        });
        emit ProposalCreated(msg.sender, proposalId, targets, values, gasLimits, calldatas, extraParams);
        return registeredProposalId;
    }
    function executeProposal(uint256 proposalId, bytes calldata extraParams) external {
        Proposal storage proposal = proposals[proposalId];
        address[] memory targets = proposal.targets;
        string memory errorMessage = "Governor: call reverted without message";
        for (uint256 i = 0; i < targets.length; ++i) {
            (bool success, bytes memory returndata) = proposal.targets[i].call{value: proposal.values[i]}(proposal.calldatas[i]);
            Address.verifyCallResult(success, returndata, errorMessage);
        }
        emit ProposalExecuted(msg.sender, proposalId, extraParams);
    }
```

请参见 [ProposalRegistry.sol](../assets/eip-5247/ProposalRegistry.sol) 以获取更多信息。

## 安全考虑

需要讨论。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。