---
eip: 3561
title: 信任最小化可升级代理
description: 在指定升级生效之前的延迟代理
author: Sam Porter (@SamPorter1984)
discussions-to: https://ethereum-magicians.org/t/trust-minimized-proxy/5742
status: 停滞
type: 标准跟踪
category: ERC
created: 2021-05-09
---

## 摘要

去除可升级代理的信任对于匿名开发者是必要的。为了实现这一点，必须防止即时和潜在恶意的升级。该 EIP 引入了额外的存储槽，用于可升级代理，假定可以减少与可升级智能合约交互时的信任。由管理员实现的逻辑只能在零信任期允许后，才能成为有效的实现逻辑。

## 动机

利用可升级代理的匿名开发者通常难以赢得社区的信任。

一个更公平、更美好的未来绝对需要一些开发者保持匿名，同时仍然吸引人们对他们提出的解决方案的关注，并同时利用可能的可升级性的好处。

## 规范

该规范是对标准 [EIP-1967](./eip-1967.md) 透明代理设计的补充。
该规范侧重于它添加的槽。所有与信任最小化代理的管理员交互必须发出事件，以使管理员操作可追踪，并且所有管理员操作必须使用 `onlyAdmin()` 修饰符进行保护。

### 下一个逻辑合约地址

存储槽 `0x19e3fabe07b65998b604369d85524946766191ac9434b39e27c424c976493685`（获得方式为 `bytes32(uint256(keccak256('eip3561.proxy.next.logic')) - 1)`）。
所需的实现逻辑地址必须首先定义为下一个逻辑，然后才能作为存储在 EIP-1967 `IMPLEMENTATION_SLOT` 中的实际逻辑实现。
与下一个逻辑合约地址的管理员交互对应于以下方法和事件：

```solidity
// Sets next logic contract address. Emits NextLogicDefined
// If current implementation is address(0), then upgrades to IMPLEMENTATION_SLOT
// immedeatelly, therefore takes data as an argument
function proposeTo(address implementation, bytes calldata data) external IfAdmin
// As soon UPGRADE_BLOCK_SLOT allows, sets the address stored as next implementation
// as current IMPLEMENTATION_SLOT and initializes it.
function upgrade(bytes calldata data) external IfAdmin
// cancelling is possible for as long as upgrade() for given next logic was not called
// emits NextLogicCanceled
function cancelUpgrade() external onlyAdmin;

event NextLogicDefined(address indexed nextLogic, uint earliestArrivalBlock); // important to have
event NextLogicCanceled(address indexed oldLogic);
```

### 升级区块

存储槽 `0xe3228ec3416340815a9ca41bfee1103c47feb764b4f0f4412f5d92df539fe0ee`（获得方式为 `bytes32(uint256(keccak256('eip3561.proxy.next.logic.block')) - 1)`）。
在此区块及之后，可以将下一个逻辑合约地址设置为 EIP-1967 `IMPLEMENTATION_SLOT`，换句话说，可以调用 `upgrade()`。根据零信任期自动更新，在事件 `NextLogicDefined` 中显示为 `earliestArrivalBlock`。

### 提议区块

存储槽 `0x4b50776e56454fad8a52805daac1d9fd77ef59e4f1a053c342aaae5568af1388`（获得方式为 `bytes32(uint256(keccak256('eip3561.proxy.propose.block')) - 1)`）。
定义在何区块后/上可以*提议*下一个逻辑。为了方便，可以手动设置为给定时间的一年。可以设置为最大值以完全封闭代码。
与此槽的管理员交互对应于以下方法和事件：

```solidity
function prolongLock(uint b) external onlyAdmin;
event ProposingUpgradesRestrictedUntil(uint block, uint nextProposedLogicEarliestArrival);
```

### 零信任期

存储槽 `0x7913203adedf5aca5386654362047f05edbd30729ae4b0351441c46289146720`（获得方式为 `bytes32(uint256(keccak256('eip3561.proxy.zero.trust.period')) - 1)`）。
零信任期以区块数表示，只能设置为高于先前值。当其处于默认值（0）时，代理的操作与标准 EIP-1967 透明代理完全相同。在设置零信任期后，强制执行上述所有规范。
与此槽的管理员交互应对应于以下方法和事件：

```solidity
function setZeroTrustPeriod(uint blocks) external onlyAdmin;
event ZeroTrustPeriodSet(uint blocks);
```

### 实现示例

```solidity
pragma solidity >=0.8.0; //important

// EIP-3561 trust minimized proxy implementation https://github.com/ethereum/EIPs/blob/master/EIPS/eip-3561.md
// Based on EIP-1967 upgradeability proxy: https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1967.md

contract TrustMinimizedProxy {
    event Upgraded(address indexed toLogic);
    event AdminChanged(address indexed previousAdmin, address indexed newAdmin);
    event NextLogicDefined(address indexed nextLogic, uint earliestArrivalBlock);
    event ProposingUpgradesRestrictedUntil(uint block, uint nextProposedLogicEarliestArrival);
    event NextLogicCanceled();
    event ZeroTrustPeriodSet(uint blocks);

    bytes32 internal constant ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;
    bytes32 internal constant LOGIC_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;
    bytes32 internal constant NEXT_LOGIC_SLOT = 0x19e3fabe07b65998b604369d85524946766191ac9434b39e27c424c976493685;
    bytes32 internal constant NEXT_LOGIC_BLOCK_SLOT = 0xe3228ec3416340815a9ca41bfee1103c47feb764b4f0f4412f5d92df539fe0ee;
    bytes32 internal constant PROPOSE_BLOCK_SLOT = 0x4b50776e56454fad8a52805daac1d9fd77ef59e4f1a053c342aaae5568af1388;
    bytes32 internal constant ZERO_TRUST_PERIOD_SLOT = 0x7913203adedf5aca5386654362047f05edbd30729ae4b0351441c46289146720;

    constructor() payable {
        require(
            ADMIN_SLOT == bytes32(uint256(keccak256('eip1967.proxy.admin')) - 1) &&
                LOGIC_SLOT == bytes32(uint256(keccak256('eip1967.proxy.implementation')) - 1) &&
                NEXT_LOGIC_SLOT == bytes32(uint256(keccak256('eip3561.proxy.next.logic')) - 1) &&
                NEXT_LOGIC_BLOCK_SLOT == bytes32(uint256(keccak256('eip3561.proxy.next.logic.block')) - 1) &&
                PROPOSE_BLOCK_SLOT == bytes32(uint256(keccak256('eip3561.proxy.propose.block')) - 1) &&
                ZERO_TRUST_PERIOD_SLOT == bytes32(uint256(keccak256('eip3561.proxy.zero.trust.period')) - 1)
        );
        _setAdmin(msg.sender);
    }

    modifier IfAdmin() {
        if (msg.sender == _admin()) {
            _;
        } else {
            _fallback();
        }
    }

    function _logic() internal view returns (address logic) {
        assembly {
            logic := sload(LOGIC_SLOT)
        }
    }

    function _nextLogic() internal view returns (address nextLogic) {
        assembly {
            nextLogic := sload(NEXT_LOGIC_SLOT)
        }
    }

    function _proposeBlock() internal view returns (uint b) {
        assembly {
            b := sload(PROPOSE_BLOCK_SLOT)
        }
    }

    function _nextLogicBlock() internal view returns (uint b) {
        assembly {
            b := sload(NEXT_LOGIC_BLOCK_SLOT)
        }
    }

    function _zeroTrustPeriod() internal view returns (uint ztp) {
        assembly {
            ztp := sload(ZERO_TRUST_PERIOD_SLOT)
        }
    }

    function _admin() internal view returns (address adm) {
        assembly {
            adm := sload(ADMIN_SLOT)
        }
    }

    function _setAdmin(address newAdm) internal {
        assembly {
            sstore(ADMIN_SLOT, newAdm)
        }
    }

    function changeAdmin(address newAdm) external IfAdmin {
        emit AdminChanged(_admin(), newAdm);
        _setAdmin(newAdm);
    }

    function upgrade(bytes calldata data) external IfAdmin {
        require(block.number >= _nextLogicBlock(), 'too soon');
        address logic;
        assembly {
            logic := sload(NEXT_LOGIC_SLOT)
            sstore(LOGIC_SLOT, logic)
        }
        (bool success, ) = logic.delegatecall(data);
        require(success, 'failed to call');
        emit Upgraded(logic);
    }

    fallback() external payable {
        _fallback();
    }

    receive() external payable {
        _fallback();
    }

    function _fallback() internal {
        require(msg.sender != _admin());
        _delegate(_logic());
    }

    function cancelUpgrade() external IfAdmin {
        address logic;
        assembly {
            logic := sload(LOGIC_SLOT)
            sstore(NEXT_LOGIC_SLOT, logic)
        }
        emit NextLogicCanceled();
    }

    function prolongLock(uint b) external IfAdmin {
        require(b > _proposeBlock(), 'can be only set higher');
        assembly {
            sstore(PROPOSE_BLOCK_SLOT, b)
        }
        emit ProposingUpgradesRestrictedUntil(b, b + _zeroTrustPeriod());
    }

    function setZeroTrustPeriod(uint blocks) external IfAdmin {
        // before this set at least once acts like a normal eip 1967 transparent proxy
        uint ztp;
        assembly {
            ztp := sload(ZERO_TRUST_PERIOD_SLOT)
        }
        require(blocks > ztp, 'can be only set higher');
        assembly {
            sstore(ZERO_TRUST_PERIOD_SLOT, blocks)
        }
        _updateNextBlockSlot();
        emit ZeroTrustPeriodSet(blocks);
    }

    function _updateNextBlockSlot() internal {
        uint nlb = block.number + _zeroTrustPeriod();
        assembly {
            sstore(NEXT_LOGIC_BLOCK_SLOT, nlb)
        }
    }

    function _setNextLogic(address nl) internal {
        require(block.number >= _proposeBlock(), 'too soon');
        _updateNextBlockSlot();
        assembly {
            sstore(NEXT_LOGIC_SLOT, nl)
        }
        emit NextLogicDefined(nl, block.number + _zeroTrustPeriod());
    }

    function proposeTo(address newLogic, bytes calldata data) external payable IfAdmin {
        if (_zeroTrustPeriod() == 0 || _logic() == address(0)) {
            _updateNextBlockSlot();
            assembly {
                sstore(LOGIC_SLOT, newLogic)
            }
            (bool success, ) = newLogic.delegatecall(data);
            require(success, 'failed to call');
            emit Upgraded(newLogic);
        } else {
            _setNextLogic(newLogic);
        }
    }

    function _delegate(address logic_) internal {
        assembly {
            calldatacopy(0, 0, calldatasize())
            let result := delegatecall(gas(), logic_, 0, calldatasize(), 0, 0)
            returndatacopy(0, 0, returndatasize())
            switch result
            case 0 {
                revert(0, returndatasize())
            }
            default {
                return(0, returndatasize())
            }
        }
    }
}
```

## 理由

“只需不让这样的合约可升级”这一论点在复杂系统中失效，这些系统可能严重依赖于人类因素，可能以前所未有的方式表现出来。可能不可能在第一次尝试时就正确建模某些系统。将去中心化治理与 EIP-1967 代理结合用于升级管理，可能在某些协议成熟之前成为严重的瓶颈，而数据也未到手。

没有时间延迟的代理在实际升级之前显然是可滥用的。时间延迟可能是不可避免的，即使这意味着缺乏经验的开发者可能没有信心使用它。尽管这是该 EIP 的一个缺点，但在今天的智能合约开发中，拥有这一选项是至关重要的。

## 安全考虑

用户必须确保他们交互的信任最小化代理不允许溢出，理想情况下与上述实现示例中的代码完全相同，并且他们还必须确保零信任期的长度是合理的（至少在通常提前揭示升级的情况下为两周，在大多数情况下至少为一个月）。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。