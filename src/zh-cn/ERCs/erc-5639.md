---
eip: 5639
title: 委托注册
description: 委托权限以实现更安全和更便捷的签名操作。
author: foobar (@0xfoobar), Wilkins Chung (@wwhchung) <wilkins@manifold.xyz>, ryley-o (@ryley-o), Jake Rockland (@jakerockland), andy8052 (@andy8052)
discussions-to: https://ethereum-magicians.org/t/eip-5639-delegation-registry/10949
status: 审核
type: 标准跟踪
category: ERC
created: 2022-09-09
---

## 摘要

本 EIP 描述了委托注册的细节，这是一个提议的协议和 ABI 定义，提供将一个或多个委托钱包链接到一个保险库钱包的能力，以便链接的委托钱包能够证明对保险库钱包的控制和资产所有权。

## 动机

在以太坊生态系统中，向第三方应用程序证明资产所有权是很常见的。用户经常签署数据负载以验证自己的身份，然后才能获得执行某些操作的权限。然而，这种方法——类似于给予第三方对自己主钱包的根访问权限——既不安全也不方便。

***示例：***

 1. 为了编辑您在 OpenSea 上的个人资料，您必须用您的钱包签署一条消息。
 2. 为了访问 NFT 限制内容，您必须用包含 NFT 的钱包签署一条消息以证明所有权。
 3. 为了获得活动的访问权限，您必须用包含所需 NFT 的钱包签署一条消息以证明所有权。
 4. 为了领取空投，您必须用符合条件的钱包与智能合约进行交互。
 5. 为了证明对 NFT 的所有权，您必须用拥有该 NFT 的钱包签署一个负载。

在上述所有示例中，用户使用钱包本身与 dApp 或智能合约进行交互，这可能是

 - 不方便（如果它是通过硬件钱包或多重签名控制的）
 - 不安全（因为上述操作是只读的，但您是通过具有写入权限的钱包进行签名/交互）

相反，用户应该能够批准多个钱包代表给定钱包进行身份验证。

### 现有方法和解决方案的问题

不幸的是，我们看到许多用户意外签署了恶意负载。结果几乎总是与委托地址相关的资产的重大损失。

此外，许多用户将大量资产保存在“冷存储”中。由于“冷存储”解决方案提供了更高的安全性，通常会导致可访问性降低，因为用户自然会增加访问这些钱包所需的障碍。

### 提议：使用委托注册

本提议旨在提供一种机制，允许保险库钱包向委托钱包授予钱包、合约或代币级别的权限。这将实现更安全和更便捷的签名和身份验证，并通过一个或多个辅助钱包提供对保险库钱包的“只读”访问。

从中，收益是双重的。此 EIP 通过将潜在恶意的签名操作外包给更易于访问的钱包（热钱包），同时能够保持不经常用于签名操作的钱包的预期安全假设，从而为用户提供了更高的安全性。

#### 提高 dApp 交互安全性

许多 dApp 要求用户证明对钱包的控制才能获得访问权限。目前，这意味着您必须使用钱包本身与 dApp 进行交互。这是一个安全问题，因为恶意 dApp 或钓鱼网站可能导致钱包的资产因签署恶意负载而受到损害。

然而，如果使用辅助钱包进行这些交互，这种风险将会降低。恶意交互将被限制在辅助钱包中持有的资产，这些资产可以设置为几乎没有价值。

#### 提高多设备访问安全性

为了在多个设备上使用非硬件钱包，您必须将种子短语导入每个设备。每次在新设备上输入种子短语时，钱包被攻破的风险就会增加，因为您增加了知道种子短语的设备的表面面积。

相反，每个设备可以拥有一个独特的钱包，作为主钱包的授权辅助钱包。如果某个设备特定的钱包被攻破或丢失，您可以简单地撤销身份验证授权。

此外，钱包身份验证可以链式连接，以便辅助钱包可以授权一个或多个三级钱包，这些钱包随后对辅助地址以及根主地址具有签名权限。这可以让团队每个人都有自己的签名者，而主钱包可以通过从根干撤销权限轻松使整个树无效。

#### 提高便利性

许多人使用硬件钱包以获得最大安全性。然而，这通常不方便，因为许多人不想随时携带硬件钱包。

相反，如果您批准一个非硬件钱包用于身份验证活动（例如移动设备），您将能够在不需要随身携带硬件钱包的情况下使用大多数 dApp。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

让：

 - `vault` 代表我们试图进行身份验证或证明资产所有权的保险库地址。
 - `delegate` 代表我们希望用来代替 `vault` 进行签名的地址。

**委托注册必须实现 IDelegationRegistry**

```solidity
/**
 * @title An immutable registry contract to be deployed as a standalone primitive
 * @dev New project launches can read previous cold wallet -> hot wallet delegations
 * from here and integrate those permissions into their flow
 */
interface IDelegationRegistry {
    /// @notice Delegation type
    enum DelegationType {
        NONE,
        ALL,
        CONTRACT,
        TOKEN
    }

    /// @notice Info about a single delegation, used for onchain enumeration
    struct DelegationInfo {
        DelegationType type_;
        address vault;
        address delegate;
        address contract_;
        uint256 tokenId;
    }

    /// @notice Info about a single contract-level delegation
    struct ContractDelegation {
        address contract_;
        address delegate;
    }

    /// @notice Info about a single token-level delegation
    struct TokenDelegation {
        address contract_;
        uint256 tokenId;
        address delegate;
    }

    /// @notice Emitted when a user delegates their entire wallet
    event DelegateForAll(address vault, address delegate, bool value);

    /// @notice Emitted when a user delegates a specific contract
    event DelegateForContract(address vault, address delegate, address contract_, bool value);

    /// @notice Emitted when a user delegates a specific token
    event DelegateForToken(address vault, address delegate, address contract_, uint256 tokenId, bool value);

    /// @notice Emitted when a user revokes all delegations
    event RevokeAllDelegates(address vault);

    /// @notice Emitted when a user revoes all delegations for a given delegate
    event RevokeDelegate(address vault, address delegate);

    /**
     * -----------  WRITE -----------
     */

    /**
     * @notice Allow the delegate to act on your behalf for all contracts
     * @param delegate The hotwallet to act on your behalf
     * @param value Whether to enable or disable delegation for this address, true for setting and false for revoking
     */
    function delegateForAll(address delegate, bool value) external;

    /**
     * @notice Allow the delegate to act on your behalf for a specific contract
     * @param delegate The hotwallet to act on your behalf
     * @param contract_ The address for the contract you're delegating
     * @param value Whether to enable or disable delegation for this address, true for setting and false for revoking
     */
    function delegateForContract(address delegate, address contract_, bool value) external;

    /**
     * @notice Allow the delegate to act on your behalf for a specific token
     * @param delegate The hotwallet to act on your behalf
     * @param contract_ The address for the contract you're delegating
     * @param tokenId The token id for the token you're delegating
     * @param value Whether to enable or disable delegation for this address, true for setting and false for revoking
     */
    function delegateForToken(address delegate, address contract_, uint256 tokenId, bool value) external;

    /**
     * @notice Revoke all delegates
     */
    function revokeAllDelegates() external;

    /**
     * @notice Revoke a specific delegate for all their permissions
     * @param delegate The hotwallet to revoke
     */
    function revokeDelegate(address delegate) external;

    /**
     * @notice Remove yourself as a delegate for a specific vault
     * @param vault The vault which delegated to the msg.sender, and should be removed
     */
    function revokeSelf(address vault) external;

    /**
     * -----------  READ -----------
     */

    /**
     * @notice Returns all active delegations a given delegate is able to claim on behalf of
     * @param delegate The delegate that you would like to retrieve delegations for
     * @return info Array of DelegationInfo structs
     */
    function getDelegationsByDelegate(address delegate) external view returns (DelegationInfo[] memory);

    /**
     * @notice Returns an array of wallet-level delegates for a given vault
     * @param vault The cold wallet who issued the delegation
     * @return addresses Array of wallet-level delegates for a given vault
     */
    function getDelegatesForAll(address vault) external view returns (address[] memory);

    /**
     * @notice Returns an array of contract-level delegates for a given vault and contract
     * @param vault The cold wallet who issued the delegation
     * @param contract_ The address for the contract you're delegating
     * @return addresses Array of contract-level delegates for a given vault and contract
     */
    function getDelegatesForContract(address vault, address contract_) external view returns (address[] memory);

    /**
     * @notice Returns an array of contract-level delegates for a given vault's token
     * @param vault The cold wallet who issued the delegation
     * @param contract_ The address for the contract holding the token
     * @param tokenId The token id for the token you're delegating
     * @return addresses Array of contract-level delegates for a given vault's token
     */
    function getDelegatesForToken(address vault, address contract_, uint256 tokenId)
        external
        view
        returns (address[] memory);

    /**
     * @notice Returns all contract-level delegations for a given vault
     * @param vault The cold wallet who issued the delegations
     * @return delegations Array of ContractDelegation structs
     */
    function getContractLevelDelegations(address vault)
        external
        view
        returns (ContractDelegation[] memory delegations);

    /**
     * @notice Returns all token-level delegations for a given vault
     * @param vault The cold wallet who issued the delegations
     * @return delegations Array of TokenDelegation structs
     */
    function getTokenLevelDelegations(address vault) external view returns (TokenDelegation[] memory delegations);

    /**
     * @notice Returns true if the address is delegated to act on the entire vault
     * @param delegate The hotwallet to act on your behalf
     * @param vault The cold wallet who issued the delegation
     */
    function checkDelegateForAll(address delegate, address vault) external view returns (bool);

    /**
     * @notice Returns true if the address is delegated to act on your behalf for a token contract or an entire vault
     * @param delegate The hotwallet to act on your behalf
     * @param contract_ The address for the contract you're delegating
     * @param vault The cold wallet who issued the delegation
     */
    function checkDelegateForContract(address delegate, address vault, address contract_)
        external
        view
        returns (bool);

    /**
     * @notice Returns true if the address is delegated to act on your behalf for a specific token, the token's contract or an entire vault
     * @param delegate The hotwallet to act on your behalf
     * @param contract_ The address for the contract you're delegating
     * @param tokenId The token id for the token you're delegating
     * @param vault The cold wallet who issued the delegation
     */
    function checkDelegateForToken(address delegate, address vault, address contract_, uint256 tokenId)
        external
        view
        returns (bool);
}
```

### 检查委托

dApp 或智能合约将通过检查 checkDelegateForAll 的返回值来检查委托是否已对保险库进行身份验证。

dApp 或智能合约将通过检查 checkDelegateForContract 的返回值来检查委托是否可以对与之相关的合约进行身份验证。

dApp 或智能合约将通过检查 checkDelegateForToken 的返回值来检查委托是否可以对保险库拥有的特定代币进行身份验证。

如果委托具有代币级别的委托、合约级别的委托（针对该代币的合约）或保险库级别的委托，则委托可以对代币进行操作。

如果委托具有合约级别的委托或保险库级别的委托，则委托可以对合约进行操作。

为了节省 gas，如果在智能合约级别执行委托检查，预计 dApp 将向智能合约提供提示，说明委托具有哪个级别的委托，以便智能合约可以使用最节省 gas 的检查方法与委托注册进行验证。

## 理由

### 允许保险库、合约或代币级别的委托

为了支持广泛的委托用例，提议的规范允许保险库委托其控制的所有资产、特定合约的资产或特定代币。这确保了保险库对其资产的安全性具有细粒度控制，并允许围绕仅向与其相关的资产授予第三方钱包有限访问的突现行为。

### 链上枚举

为了支持集成和采用的便利性，本规范选择包括链上委托枚举，并承担与支持枚举相关的额外 gas 成本。链上枚举允许 dApp 前端识别任何连接钱包可以访问的委托，并可以提供 UI 选择器。
没有链上枚举，dApp 将要求用户手动输入保险库，或者需要一种方法来索引所有委托事件。

## 安全考虑

此 EIP 的核心目的是增强安全性，并促进一种更安全的方式来验证钱包控制和资产所有权，当主钱包不需要时，主钱包持有的资产不需要移动。将其视为一种“只读”身份验证的方法。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。