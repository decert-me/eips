---
eip: 3448
title: MetaProxy 标准
description: 一种最小字节码实现，用于创建带有不可变元数据的代理合约
author: pinkiebell (@pinkiebell)
discussions-to: https://ethereum-magicians.org/t/erc-3448-metaproxy-factory/5834
status: Final
type: Standards Track
category: ERC
created: 2021-03-29
---

## 摘要
通过标准化已知的最小字节码代理实现，并支持不可变元数据，该标准允许用户和第三方工具（例如 Etherscan）：
(a) 简单地发现合约将始终以已知方式重定向，以及
(b) 依赖于目标合约的代码行为作为重定向合约的行为，以及
(c) 验证/查看附加的元数据。

工具可以查询重定向地址的字节码，以确定将运行的代码位置及其相关元数据，并可以依赖于关于该代码的表示（已验证的源代码、第三方审计等）。
该实现通过 `DELEGATECALL` 转发所有调用，并将任何（calldata）输入加上字节码末尾的元数据传递给实现合约，然后将返回值中继回调用者。
如果实现合约发生回退，则回退信息将与有效负载数据一起传回。

## 动机
该标准支持在另一个地址克隆确切合约功能并使用不同参数的用例。

## 规范
MetaProxy 合约的确切字节码为：
```
                                              20 bytes target contract address
                                          ----------------------------------------
363d3d373d3d3d3d60368038038091363936013d7300000000000000000000000000000000000000005af43d3d93803e603457fd5bf3
```
其中索引 21 - 41（包含）处的字节被主功能合约的 20 字节地址替换。
此外，MetaProxy 字节码之后的所有内容可以是任意元数据，字节码的最后 32 字节（一个字）必须指示元数据的字节长度。

```
<54 bytes metaproxy> <arbitrary data> <length in bytes of arbitrary data (uint256)>
```

## 理由
该工作的目标如下：
- 以低成本为每个子合约存储不可变元数据，而不是使用存储槽
- 低成本部署克隆
- 处理回退消息的错误返回冒泡

## 向后兼容性
没有向后兼容性问题。

## 测试用例
测试内容包括：
- 无参数调用
- 带参数调用
- 带返回值调用
- 带回退调用（确认回退有效负载被传递）

带有上述测试用例的 Solidity 合约可以在 [EIP 资产目录](../assets/eip-3448/MetaProxyTest.sol) 中找到。

## 参考实现
参考实现可以在 [EIP 资产目录](../assets/eip-3448/MetaProxyFactory.sol) 中找到。

### 部署字节码
部署字节码的注释版本：
```
// PUSH1 11;
// CODESIZE;
// SUB;
// DUP1;
// PUSH1 11;
// RETURNDATASIZE;
// CODECOPY;
// RETURNDATASIZE;
// RETURN;
```

### MetaProxy
MetaProxy 字节码的注释版本：
```
// copy args
// CALLDATASIZE;   calldatasize
// RETURNDATASIZE; 0, calldatasize
// RETURNDATASIZE; 0, 0, calldatasize
// CALLDATACOPY;

// RETURNDATASIZE; 0
// RETURNDATASIZE; 0, 0
// RETURNDATASIZE; 0, 0, 0
// RETURNDATASIZE; 0, 0, 0, 0

// PUSH1 54;       54, 0, 0, 0, 0
// DUP1;           54, 54, 0, 0, 0, 0
// CODESIZE;       codesize, 54, 54, 0, 0, 0, 0
// SUB;            codesize-54, 54, 0, 0, 0, 0
// DUP1;           codesize-54, codesize-54, 54, 0, 0, 0, 0
// SWAP2;          54, codesize-54, codesize-54, 0, 0, 0, 0
// CALLDATASIZE;   calldatasize, 54, codesize-54, codesize-54, 0, 0, 0, 0
// CODECOPY;       codesize-54, 0, 0, 0, 0

// CALLDATASIZE;   calldatasize, codesize-54, 0, 0, 0, 0
// ADD;            calldatasize+codesize-54, 0, 0, 0, 0
// RETURNDATASIZE; 0, calldatasize+codesize-54, 0, 0, 0, 0
// PUSH20 0;       addr, 0, calldatasize+codesize-54, 0, 0, 0, 0 - zero is replaced with shl(96, address())
// GAS;            gas, addr, 0, calldatasize+codesize-54, 0, 0, 0, 0
// DELEGATECALL;   (gas, addr, 0, calldatasize() + metadata, 0, 0) delegatecall to the target contract;
//
// RETURNDATASIZE; returndatasize, retcode, 0, 0
// RETURNDATASIZE; returndatasize, returndatasize, retcode, 0, 0
// SWAP4;          0, returndatasize, retcode, 0, returndatasize
// DUP1;           0, 0, returndatasize, retcode, 0, returndatasize
// RETURNDATACOPY; (0, 0, returndatasize) - Copy everything into memory that the call returned

// stack = retcode, 0, returndatasize # this is for either revert(0, returndatasize()) or return (0, returndatasize())

// PUSH1 _SUCCESS_; push jumpdest of _SUCCESS_
// JUMPI;          jump if delegatecall returned `1`
// REVERT;         (0, returndatasize()) if delegatecall returned `0`
// JUMPDEST _SUCCESS_;
// RETURN;         (0, returndatasize()) if delegatecall returned non-zero (1)
```

### 示例
以下代码片段仅作为建议，并不是该标准的离散部分。

#### 使用 abi.encode 的字节进行代理构造
```solidity
/// @notice MetaProxy construction via abi encoded bytes.
function createFromBytes (
  address a,
  uint256 b,
  uint256[] calldata c
) external payable returns (address proxy) {
  // creates a new proxy where the metadata is the result of abi.encode()
  proxy = MetaProxyFactory._metaProxyFromBytes(address(this), abi.encode(a, b, c));
  require(proxy != address(0));
  // optional one-time setup, a constructor() substitute
  MyContract(proxy).init{ value: msg.value }();
}
```

#### 使用 calldata 的字节进行代理构造
```solidity
/// @notice MetaProxy construction via calldata.
function createFromCalldata (
  address a,
  uint256 b,
  uint256[] calldata c
) external payable returns (address proxy) {
  // creates a new proxy where the metadata is everything after the 4th byte from calldata.
  proxy = MetaProxyFactory._metaProxyFromCalldata(address(this));
  require(proxy != address(0));
  // optional one-time setup, a constructor() substitute
  MyContract(proxy).init{ value: msg.value }();
}
```

#### 从 calldata 和 abi.decode 中检索元数据
```solidity
/// @notice Returns the metadata of this (MetaProxy) contract.
/// Only relevant with contracts created via the MetaProxy standard.
/// @dev This function is aimed to be invoked with- & without a call.
function getMetadataWithoutCall () public pure returns (
  address a,
  uint256 b,
  uint256[] memory c
) {
  bytes memory data;
  assembly {
    let posOfMetadataSize := sub(calldatasize(), 32)
    let size := calldataload(posOfMetadataSize)
    let dataPtr := sub(posOfMetadataSize, size)
    data := mload(64)
    // increment free memory pointer by metadata size + 32 bytes (length)
    mstore(64, add(data, add(size, 32)))
    mstore(data, size)
    let memPtr := add(data, 32)
    calldatacopy(memPtr, dataPtr, size)
  }
  return abi.decode(data, (address, uint256, uint256[]));
}
```

#### 通过对自身的调用检索元数据
```solidity
/// @notice Returns the metadata of this (MetaProxy) contract.
/// Only relevant with contracts created via the MetaProxy standard.
/// @dev This function is aimed to be invoked via a call.
function getMetadataViaCall () public pure returns (
  address a,
  uint256 b,
  uint256[] memory c
) {
  assembly {
    let posOfMetadataSize := sub(calldatasize(), 32)
    let size := calldataload(posOfMetadataSize)
    let dataPtr := sub(posOfMetadataSize, size)
    calldatacopy(0, dataPtr, size)
    return(0, size)
  }
}
```

除了上述示例外，还可以使用 Solidity 结构或任何自定义数据编码。

## 安全考虑
该标准仅涵盖字节码实现，不包括其自身的任何严重副作用。
参考实现仅作为示例。强烈建议根据功能在任何项目中的使用和实现进行研究副作用。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。