---
eip: 7613
title: Puppet Proxy Contract
description: 一个代理合约，如果由其部署者调用，则将调用委托给在 calldata 中指定的实现。
author: Igor Żuk (@CodeSandwich)
discussions-to: https://ethereum-magicians.org/t/eip-7613-puppet-proxy-contract/18482
status: Draft
type: Standards Track
category: ERC
created: 2024-02-04
---

## Abstract

一个 puppet 是一个合约，当被调用时，表现得像一个空账户。它不执行任何操作，也没有 API，除非是由部署它的地址调用。在这种情况下，它将调用委托给在 calldata 中传递给它的地址。这使得部署者能够在 puppet 的上下文中执行他们想要的任何逻辑。

## Motivation

一个 puppet 可以作为其部署者的替代账户。它有一个不同的地址，因此拥有一组独立的资产余额。这使得复杂的会计成为可能，例如，每个协议的用户可以获得自己的地址，以便发送和存储资产。用户可以调用协议合约，协议合约将部署一个新的 puppet，并将其视为分配给用户。如果 puppet 在一个可预测的地址下部署，例如，通过使用用户的地址作为 CREATE2 盐，puppet 甚至可以在资金发送到其地址之前不需要被部署。从现在起，协议将视为发送到 puppet 的所有资产都归用户所有。如果协议需要将资金从 puppet 地址转移出去，它可以调用 puppet，指示其委托给一个将资产转移到任意地址的函数，或进行任意调用以触发对其他合约的批准转移。

puppets 可以作为在将资金加载到协议时的批准转移的替代方案。任何合约和任何钱包都可以将资金转移到分配给用户的 puppet 地址，而无需进行任何批准或调用协议合约。资金可以通过多个交易加载，并可能来自多个来源。为了从另一个协议引导资金，只要能够将资金转移到任意地址，就不需要在第三方合约中进行集成。仅限于普通 [ERC-20](./erc-20.md) 转移且没有任何 web3 功能的钱包可以用于将资金加载到协议中。使用功能齐全的钱包的用户不需要签署可能有害的模糊 calldata 块或批准协议提取他们的代币，他们只需进行转移，这是一种简单的过程，用户体验熟悉。当资金已经存储在分配给用户的 puppet 中时，需要有人调用协议，以便通知其资金已加载。根据协议及其 API，此调用可能是无权限的，可能使用户体验更加便利，支持无 gas 交易或第三方承担 gas 费用。一些协议不需要用户指定需要对加载的资金做什么，或者允许用户提前配置。大多数使用批准转移加载资金的协议可能会受益于使用 puppets。

puppet 的逻辑不需要升级。要改变其行为，部署者需要更改传递给 puppet 的委托地址或传递的 calldata。由单个合约部署的整个 puppet 队列可以通过升级部署它们的合约来升级，而无需使用 beacon。一个不错的技巧是，部署者可以使 puppet 委托给持有部署者自己逻辑的地址，因此 puppet 的逻辑被封装在部署者中。

puppet 无法向任何调用者暴露任何 API，除了部署者。如果第三方需要以某种方式使 puppet 执行某些逻辑，则不能通过直接调用 puppet 来请求。相反，部署者需要暴露一个函数，如果由第三方调用，将调用 puppet，并使其执行所需的逻辑。期望合约暴露某些 API 的机制在 puppet 中不起作用，例如 [ERC-721](./eip-721.md) 的 `safeTransfer`。

本标准将 puppet 定义为用作创建代码的字节块，这使得与许多框架和用多种语言编写的代码库的集成成为可能。具体的工具超出了本标准的范围，但应该很容易创建必要的库和助手以便在实践中使用。所有实现将是互操作的，因为它们将创建相同的 puppets，如果使用 CREATE2，它们将具有所有实现可预测的确定性地址。

由于 puppet 可以在可预测的地址下部署，尽管没有固定的逻辑，在某些情况下它可以作为 CREATE3 的替代品。它还可以通过使用使用 CREATE2 部署的 puppet 来完全替代 CREATE3 工厂，以使用普通的 CREATE 部署任意代码。

部署一个新的 puppet 几乎与部署一个新的克隆代理一样便宜。其整个部署字节码为 66 字节，创建代码为 62 字节。就像克隆代理一样，它可以仅使用 Solidity 的内存临时空间进行部署。部署 puppet 的成本为 45K gas，仅比克隆多 4K。由于字节码未编译，因此可以可靠地在可预测的 CREATE2 地址下部署，而不受编译器版本的影响。

## Specification

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

要进行委托，部署者必须在 calldata 前面添加一个 ABI 编码的地址以进行委托。
地址后的所有数据将逐字传递作为委托 calldata。
如果调用者不是部署者，calldata 短于 32 字节，或者它不以用零填充的地址开头，则 puppet 不会执行任何操作。
这允许部署者向 puppet 进行普通的原生代币转移，
它将有一个空的 calldata，puppet 将接受转移而不进行委托。

puppet 使用以下创建代码进行部署：
```
0x604260126D60203D3D3683113D3560A01C17733D3360147F331817604057823603803D943D373D3D355AF43D82803E903D91604057FD5BF36034525252F3
```

字节码分解：
```
// The creation code.
// [code 1] and [code 2] are parts of the deployed code,
// placed respectively before and after the deployer's address.
// | Opcode used    | Hex value     | Stack content after executing
// Code size and offset in memory
// | PUSH1          | 60 42         | 66
// | PUSH1          | 60 12         | 18 66
// The code before the deployer's address and where it's stored in memory
// | PUSH14         | 6D [code 1]   | [code 1] 18 66
// | RETURNDATASIZE | 3D            | 0 [code 1] 18 66
// The deployer's address and where it's stored in memory
// | CALLER         | 33            | [deployer] 0 [code 1] 18 66
// | PUSH1          | 60 14         | 20 [deployer] 0 [code 1] 18 66
// The code after the deployer's address and where it's stored in memory
// | PUSH32         | 7F [code 2]   | [code 2] 20 [deployer] 0 [code 1] 18 66
// | PUSH1          | 60 34         | 52 [code 2] 20 [deployer] 0 [code 1] 18 66
// Return the entire code
// | MSTORE         | 52            | 20 [deployer] 0 [code 1] 18 66
// | MSTORE         | 52            | 0 [code 1] 18 66
// | MSTORE         | 52            | 18 66
// | RETURN         | F3            |

// The deployed code.
// `deployer` is the deployer's address.
// | Opcode used    | Hex value     | Stack content after executing
// Push some constants
// | PUSH1          | 60 20         | 32
// | RETURNDATASIZE | 3D            | 0 32
// | RETURNDATASIZE | 3D            | 0 0 32
// Do not delegate if calldata shorter than 32 bytes
// | CALLDATASIZE   | 36            | [calldata size] 0 0 32
// | DUP4           | 83            | 32 [calldata size] 0 0 32
// | GT             | 11            | [do not delegate] 0 0 32
// Do not delegate if the first word of calldata is not a zero-padded address
// | RETURNDATASIZE | 3D            | 0 [do not delegate] 0 0 32
// | CALLDATALOAD   | 35            | [first word] [do not delegate] 0 0 32
// | PUSH1          | 60 A0         | 160 [first word] [do not delegate] 0 0 32
// | SHR            | 1C            | [first word upper bits] [do not delegate] 0 0 32
// | OR             | 17            | [do not delegate] 0 0 32
// Do not delegate if not called by the deployer
// | PUSH20         | 73 [deployer] | [deployer] [do not delegate] 0 0 32
// | CALLER         | 33            | [sender] [deployer] [do not delegate] 0 0 32
// | XOR            | 18            | [sender not deployer] [do not delegate] 0 0 32
// | OR             | 17            | [do not delegate] 0 0 32
// Skip to the return if should not delegate
// | PUSH1          | 60 40         | [success branch] [do not delegate] 0 0 32
// | JUMPI          | 57            | 0 0 32
// Calculate the payload size
// | DUP3           | 82            | 32 0 0 32
// | CALLDATASIZE   | 36            | [calldata size] 32 0 0 32
// | SUB            | 03            | [payload size] 0 0 32
// Copy the payload from calldata
// | DUP1           | 80            | [payload size] [payload size] 0 0 32
// | RETURNDATASIZE | 3D            | 0 [payload size] [payload size] 0 0 32
// | SWAP5          | 94            | 32 [payload size] [payload size] 0 0 0
// | RETURNDATASIZE | 3D            | 0 32 [payload size] [payload size] 0 0 0
// | CALLDATACOPY   | 37            | [payload size] 0 0 0
// Delegate call
// | RETURNDATASIZE | 3D            | 0 [payload size] 0 0 0
// | RETURNDATASIZE | 3D            | 0 0 [payload size] 0 0 0
// | CALLDATALOAD   | 35            | [delegate to] 0 [payload size] 0 0 0
// | GAS            | 5A            | [gas] [delegate to] 0 [payload size] 0 0 0
// | DELEGATECALL   | F4            | [success] 0
// Copy return data
// | RETURNDATASIZE | 3D            | [return size] [success] 0
// | DUP3           | 82            | 0 [return size] [success] 0
// | DUP1           | 80            | 0 0 [return size] [success] 0
// | RETURNDATACOPY | 3E            | [success] 0
// Return
// | SWAP1          | 90            | 0 [success]
// | RETURNDATASIZE | 3D            | [return size] 0 [success]
// | SWAP2          | 91            | [success] 0 [return size]
// | PUSH1          | 60 40         | [success branch] [success] 0 [return size]
// | JUMPI          | 57            | 0 [return size]
// | REVERT         | FD            |
// | JUMPDEST       | 5B            | 0 [return size]
// | RETURN         | F3            |
```

## Rationale

puppet 设计的主要目标是低成本和模块化。它应该便宜部署且便宜交互。合约应该是自包含的，易于推理，并且易于用作架构构建块。

puppet 行为可以通过在 Solidity 中使用一些内联 Yul 进行委托来相对容易地实现。这将使字节码变得更大，部署成本更高。它还将根据编译器版本和配置而有所不同，因此在使用 CREATE2 进行可预测地址的部署时将更加棘手。

上述解决方案的问题的一个变通方法是使用克隆代理模式来部署 puppet 实现的副本。这将使每个 puppet 的部署成本略低于本文件中提议的字节码，并且在使用 CREATE2 部署时，克隆的地址将是可预测的。缺点是现在每次调用都会有一个额外的委托，从克隆代理到 puppet 实现地址，这会消耗 gas。这样的解决方案的架构也更复杂，涉及更多合约，并且在任何克隆可以被部署之前需要部署 puppet 实现的初始化步骤。初始化步骤限制了 CREATE2 地址的可预测性，因为克隆代理的创建代码包括实现地址，这会影响部署地址。
另一种选择是使用信标代理模式。安全地进行 Solidity API 调用是一个相对复杂的过程，占用了字节码中非平凡的空间。为了降低木偶的成本，信标代理可能应该与克隆代理一起使用，这将比上述解决方案更复杂且更昂贵。从信标查询委托地址的灵活性不如将其传递在 calldata 中，它需要更新信标的状态以更改地址。

## 向后兼容性

未发现向后兼容性问题。

木偶字节码不使用 PUSH0，因为许多链尚不支持它。

## 测试用例

以下是使用 Foundry 测试工具验证字节码和参考实现库正常工作的测试：

```solidity
pragma solidity ^0.8.0;

import {Test} from "forge-std/Test.sol";
import {Puppet} from "src/Puppet.sol";

contract Logic {
    string public constant ERROR = "Failure called";

    fallback(bytes calldata data) external returns (bytes memory) {
        return abi.encode(data);
    }

    function success(uint256 arg) external payable returns (address, uint256, uint256) {
        return (address(this), arg, msg.value);
    }

    function failure() external pure {
        revert(ERROR);
    }
}

contract PuppetTest is Test {
    address puppet = Puppet.deploy();
    address logic = address(new Logic());

    function logicFailurePayload() internal view returns (bytes memory) {
        return Puppet.delegationCalldata(logic, abi.encodeWithSelector(Logic.failure.selector));
    }

    function call(address target, bytes memory data) internal returns (bytes memory) {
        return call(target, data, 0);
    }

    function call(address target, bytes memory data, uint256 value)
        internal
        returns (bytes memory)
    {
        (bool success, bytes memory returned) = target.call{value: value}(data);
        require(success, "Unexpected revert");
        return returned;
    }

    function testDeployDeterministic() public {
        bytes32 salt = keccak256("Puppet");
        address newPuppet = Puppet.deployDeterministic(salt);
        assertEq(
            newPuppet, Puppet.predictDeterministicAddress(salt, address(this)), "Invalid address"
        );
        assertEq(
            newPuppet, Puppet.predictDeterministicAddress(salt), "Invalid address when no deployer"
        );
        assertEq(newPuppet.code, puppet.code, "Invalid code");
    }

    function testPuppetDelegates() public {
        uint256 arg = 1234;
        bytes memory data = abi.encodeWithSelector(Logic.success.selector, arg);
        bytes memory payload = Puppet.delegationCalldata(logic, data);
        uint256 value = 5678;

        bytes memory returned = call(puppet, payload, value);

        (address thisAddr, uint256 receivedArg, uint256 receivedValue) =
            abi.decode(returned, (address, uint256, uint256));
        assertEq(thisAddr, puppet, "Invalid delegation context");
        assertEq(receivedArg, arg, "Invalid argument");
        assertEq(receivedValue, value, "Invalid value");
    }

    function testPuppetDelegatesWithEmptyCalldata() public {
        bytes memory payload = Puppet.delegationCalldata(logic, "");
        bytes memory returned = call(puppet, payload);
        bytes memory data = abi.decode(returned, (bytes));
        assertEq(data.length, 0, "Delegated with non-empty calldata");
    }

    function testPuppetBubblesRevertPayload() public {
        vm.expectRevert(bytes(Logic(logic).ERROR()));
        call(puppet, logicFailurePayload());
    }

    function testPuppetDoesNothingForNonDeployer() public {
        vm.prank(address(1234));
        call(puppet, logicFailurePayload());
    }

    function testCallingWithCalldataShorterThan32BytesDoesNothing() public {
        address delegateTo = address(uint160(1234) << 8);
        bytes memory payload = abi.encodePacked(bytes31(bytes32(uint256(uint160(delegateTo)))));
        vm.mockCallRevert(delegateTo, "", "Logic called");
        call(puppet, payload);
    }

    function testCallingWithDelegationAddressOver20BytesDoesNothing() public {
        bytes memory payload = logicFailurePayload();
        payload[11] = 0x01;
        call(puppet, payload);
    }

    function testCallingPuppetDoesNothing() public {
        // Forge the calldata, so if puppet uses it to delegate, it will run `Logic.failure`
        uint256 forged = uint256(uint160(address(this))) << 32;
        forged |= uint32(Logic.failure.selector);
        bytes memory payload = abi.encodeWithSignature("abc(uint)", forged);
        call(puppet, payload);
    }

    function testTransferFromDeployerToPuppet() public {
        uint256 amt = 123;
        payable(puppet).transfer(amt);
        assertEq(puppet.balance, amt, "Invalid balance");
    }

    function testTransferToPuppet() public {
        uint256 amt = 123;
        address sender = address(456);
        payable(sender).transfer(amt);
        vm.prank(sender);
        payable(puppet).transfer(amt);
        assertEq(puppet.balance, amt, "Invalid balance");
    }
}
```

## 参考实现

木偶字节码在规范部分中进行了说明。以下是示例辅助库：

```solidity
library Puppet {
    bytes internal constant CREATION_CODE =
        hex"604260126D60203D3D3683113D3560A01C17733D3360147F33181760405782"
        hex"3603803D943D373D3D355AF43D82803E903D91604057FD5BF36034525252F3";
    bytes32 internal constant CREATION_CODE_HASH = keccak256(CREATION_CODE);

    /// @notice Deploy a new puppet.
    /// @return instance The address of the puppet.
    function deploy() internal returns (address instance) {
        bytes memory creationCode = CREATION_CODE;
        assembly {
            instance := create(0, add(creationCode, 32), mload(creationCode))
        }
        require(instance != address(0), "Failed to deploy the puppet");
    }

    /// @notice Deploy a new puppet under a deterministic address.
    /// @param salt The salt to use for the deterministic deployment.
    /// @return instance The address of the puppet.
    function deployDeterministic(bytes32 salt) internal returns (address instance) {
        bytes memory creationCode = CREATION_CODE;
        assembly {
            instance := create2(0, add(creationCode, 32), mload(creationCode), salt)
        }
        require(instance != address(0), "Failed to deploy the puppet");
    }

    /// @notice Calculate the deterministic address for a puppet deployment made by this contract.
    /// @param salt The salt to use for the deterministic deployment.
    /// @return predicted The address of the puppet.
    function predictDeterministicAddress(bytes32 salt) internal view returns (address predicted) {
        return predictDeterministicAddress(salt, address(this));
    }

    /// @notice Calculate the deterministic address for a puppet deployment.
    /// @param salt The salt to use for the deterministic deployment.
    /// @param deployer The address of the deployer of the puppet.
    /// @return predicted The address of the puppet.
    function predictDeterministicAddress(bytes32 salt, address deployer)
        internal
        pure
        returns (address predicted)
    {
        bytes32 hash = keccak256(abi.encodePacked(hex"ff", deployer, salt, CREATION_CODE_HASH));
        return address(uint160(uint256(hash)));
    }

    function delegationCalldata(address delegateTo, bytes memory data)
        internal
        pure
        returns (bytes memory payload)
    {
        return abi.encodePacked(bytes32(uint256(uint160(delegateTo))), data);
    }
}
```

## 安全考虑

字节码在合理的地方被设计成类似于克隆代理，以简化审计。

ABI 编码的委托地址保护部署者不被第三方欺骗，以调用木偶并使其委托给任意地址。这样的情况只有在部署者调用木偶的选择器为 `0x00000000` 的函数时才有可能，而目前没有任何合理命名的函数会产生该选择器。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。