---
eip: 6120
title: 通用代币路由器
description: 一个单一的路由器合约使代币能够以转账并调用的模式发送到应用合约，而不是先批准再调用。
author: Derivable (@derivable-labs), Zergity (@Zergity), Ngo Quang Anh (@anhnq82), BerlinP (@BerlinP), Khanh Pham (@blackskin18), Hal Blackburn (@h4l)
discussions-to: https://ethereum-magicians.org/t/eip-6120-universal-token-router/12142
status: 审核中
type: 标准跟踪
category: ERC
created: 2022-12-12
requires: 20, 165, 721, 1014, 1155
---

## 摘要

ETH 的设计将 *转账并调用* 作为交易的默认行为。不幸的是，[ERC-20](./erc-20.md) 并没有考虑到这种模式，新的标准无法应用于已经部署的代币合约。

应用和路由器合约必须使用 *先批准再调用* 的模式，这需要额外的 $n\times m\times l$ `approve`（或 `permit`）签名，分别对应 $n$ 个合约、$m$ 个代币和 $l$ 个账户。这些授权交易不仅造成了糟糕的用户体验，消耗了大量用户费用和网络存储，还使用户面临严重的安全风险，因为他们通常必须批准未经审计、未验证和可升级的代理合约。*先批准再调用* 的模式也相当容易出错，最近发现了许多与授权相关的漏洞和攻击。

通用代币路由器（UTR）将代币授权与应用逻辑分离，允许任何代币在合约调用中以与 ETH 相同的方式被使用，而无需批准任何其他应用合约。

批准给通用代币路由器的代币只能在其所有者直接签名的交易中被使用，并且它们的代币转移行为是清晰可见的，包括代币类型（ETH、[ERC-20](./erc-20.md)、[ERC-721](./eip-721.md) 或 [ERC-1155](./eip-1155.md)）、`amountIn`、`amountOutMin` 和 `recipient`。

通用代币路由器合约通过 [EIP-1014](./eip-1014.md) SingletonFactory 合约在 `0x8Bd6072372189A12A2889a56b6ec982fD02b0B87` 部署，覆盖所有 EVM 兼容网络。这使得新的代币合约能够将其预配置为受信任的支出者，从而在交互使用过程中消除批准交易的需要。

## 动机

当用户批准他们的代币给一个合约时，他们信任：

* 它只在他们的许可下支出代币（来自 `msg.sender` 或 `ecrecover`）
* 它不使用 `delegatecall`（例如，可升级代理）

通过确保上述相同的安全条件，通用代币路由器可以被所有交互式应用共享，从而节省大多数旧代币的批准交易和**所有**新代币的批准交易。

在此 EIP 之前，当用户签署交易以支出他们批准的代币时，他们完全信任前端代码诚实且正确地构造这些交易。这使他们面临巨大的钓鱼网站风险。

通用代币路由器的函数参数可以作为用户签署交易时的清单。在钱包的支持下，用户可以查看和审查他们预期的代币行为，而不是盲目信任应用合约和前端代码。钓鱼网站将更容易被用户检测和避免。

大多数应用合约已经与通用代币路由器兼容，并可以利用以下好处：

* 安全地与所有其他应用共享用户代币授权。
* 随时更新他们的外围合约。
* 节省路由器合约的开发和安全审计成本。

通用代币路由器在去中心化应用中推广**结果安全**模型，而不是**过程安全**。通过直接查询代币余额变化进行输出验证，即使在与错误或恶意合约交互时，用户交易也可以得到保障。对于非代币结果，应用辅助合约可以提供额外的结果检查功能以验证 UTR 的输出。

## 规范

本文档中的关键字“必须”、“不得”、“要求”、“应”、“不应”、“应该”、“不应该”、“推荐”、“不推荐”、“可以”和“可选”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

UTR 合约的主要接口：

```solidity
interface IUniversalTokenRouter {
    function exec(
        Output[] memory outputs,
        Action[] memory actions
    ) payable;
}
```

### 输出验证

`Output` 定义了预期的代币余额变化以进行验证。

```solidity
struct Output {
    address recipient;
    uint eip;           // token standard: 0 for ETH or EIP number
    address token;      // token contract address
    uint id;            // token id for ERC-721 and ERC-1155
    uint amountOutMin;
}
```

`recipient` 地址的代币余额在 `exec` 函数的开始和结束时被记录。若任何余额变化小于其 `amountOutMin`，交易将以 `INSUFFICIENT_OUTPUT_AMOUNT` 失败。

一个特殊的 ID `ERC_721_BALANCE` 被保留用于 ERC-721，可以在输出操作中使用以验证 `recipient` 地址拥有的所有 ID 的总量。

```solidity
ERC_721_BALANCE = keccak256('UniversalTokenRouter.ERC_721_BALANCE')
```

### 操作

`Action` 定义了代币输入和合约调用。

```solidity
struct Action {
    Input[] inputs;
    address code;       // contract code address
    bytes data;         // contract input data
}
```

操作代码合约必须实现 [ERC-165](./eip-165.md) 接口，ID 为 `0x61206120`，以便被 UTR 调用。此接口检查防止 UTR 直接调用代币 *授权支出* 函数（例如，`transferFrom`）。因此，新的代币合约不得实现此接口 ID。

```solidity
abstract contract NotToken is ERC165 {
    // IERC165-supportsInterface
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return
            interfaceId == 0x61206120 ||
            super.supportsInterface(interfaceId);
    }
}

contract Application is NotToken {
    // this contract can be used with the UTR
}
```

### 输入

`Input` 定义了在执行操作合约之前转移或准备的输入代币。

```solidity
struct Input {
    uint mode;
    address recipient;
    uint eip;           // token standard: 0 for ETH or EIP number
    address token;      // token contract address
    uint id;            // token id for ERC-721 and ERC-1155
    uint amountIn;
}
```

`mode` 取以下值之一：

* `PAYMENT = 0`：挂起代币从 `msg.sender` 转移到 `recipient` 的支付，通过在同一交易中的任何地方调用 `UTR.pay`。
* `TRANSFER = 1`：直接从 `msg.sender` 转移代币到 `recipient`。
* `CALL_VALUE = 2`：记录传递给操作的 `ETH` 数量作为调用 `value`。

`inputs` 参数中的每个输入按顺序处理。为简单起见，重复的 `PAYMENT` 和 `CALL_VALUE` 输入是有效的，但仅使用最后一个 `amountIn` 值。

#### 支付输入

`PAYMENT` 是推荐的模式，适用于使用 *转账回调* 模式的应用合约。例如，闪电贷合约、Uniswap/v3-core、Derivable 等。

对于每个 `Input` 采用 `PAYMENT` 模式，最多可以通过在同一交易中的任何地方调用 `UTR.pay` 将 `amountIn` 的代币从 `msg.sender` 转移到 `recipient`。

```
UTR
 |
 | PAYMENT
 | (payments pended for UTR.pay)
 |
 |                                  Application Contracts
action.code.call ---------------------> |
                                        |
UTR.pay <----------------------- (call) |
                                        |
 | <-------------------------- (return) |
 |
 | (clear all pending payments)
 |
END
```

代币的授权和 `PAYMENT` 本质上是不同的：

* 授权：允许特定的 `spender` 在任何时间将代币转移给任何人。
* `PAYMENT`：仅允许任何人在该交易中将代币转移给特定的 `recipient`。

##### 支出支付

```solidity
interface IUniversalTokenRouter {
    function pay(bytes memory payment, uint amount);
}
```

要调用 `pay`，`payment` 参数必须按如下方式编码：

```solidity
payment = abi.encode(
    payer,      // address
    recipient,  // address
    eip,        // uint256
    token,      // address
    id          // uint256
);
```

`payment` 字节也可以被适配器 UTR 合约用于传递上下文和有效负载，以执行自定义支付逻辑。

##### 丢弃支付

有时，丢弃支付而不是执行转移是有用的，例如，当应用合约想要从 `payment.payer` 销毁其自己的代币时。可以使用以下函数验证支付到调用者的地址并丢弃其中的一部分。

```solidity
interface IUniversalTokenRouter {
    function discard(bytes memory payment, uint amount);
}
```
请参阅**安全考虑**中的[丢弃支付](#discard-payment-1)部分以获取重要的安全说明。

##### 支付生命周期

支付记录在 UTR 存储中，旨在仅在该交易内通过 `input.action` 外部调用进行消费。所有支付存储将在 `UTR.exec` 结束之前被清除。

### 原生代币转移

`UTR` 应该有一个 `receive()` 函数，用于需要转入 ETH 的用户执行逻辑。转入路由器的 `msg.value` 可以在不同操作的多个输入中使用。虽然调用者对 ETH 在路由器内外的移动负全责，但 `exec` 函数应在函数结束之前退还任何剩余的 ETH。

请参阅**安全考虑**中的[重入](#reentrancy)部分以获取有关重入风险和缓解措施的信息。

### 使用示例

#### Uniswap V2 路由器

遗留函数：

```solidity
UniswapV2Router01.swapExactTokensForTokens(
    uint amountIn,
    uint amountOutMin,
    address[] calldata path,
    address to,
    uint deadline
)
```

`UniswapV2Helper01.swapExactTokensForTokens` 是其修改版本，不包含代币转移部分。

此交易由用户签名以执行交换，而不是遗留函数：

```javascript
UniversalTokenRouter.exec([{
    recipient: to,
    eip: 20,
    token: path[path.length-1],
    id: 0,
    amountOutMin,
}], [{
    inputs: [{
        mode: TRANSFER,
        recipient: UniswapV2Library.pairFor(factory, path[0], path[1]),
        eip: 20,
        token: path[0],
        id: 0,
        amountIn: amountIn,
    }],
    code: UniswapV2Helper01.address,
    data: encodeFunctionData("swapExactTokensForTokens", [
        amountIn,
        amountOutMin,
        path,
        to,
        deadline,
    ]),
}])
```

#### Uniswap V3 路由器

遗留路由器合约：

```solidity
contract SwapRouter {
    // this function is called by pool to pay the input tokens
    function pay(
        address token,
        address payer,
        address recipient,
        uint256 value
    ) internal {
        ...
        // pull payment
        TransferHelper.safeTransferFrom(token, payer, recipient, value);
    }
}
```

与 `UTR` 一起使用的辅助合约：

```solidity
contract SwapHelper {
    // this function is called by pool to pay the input tokens
    function pay(
        address token,
        address payer,
        address recipient,
        uint256 value
    ) internal {
        ...
        // pull payment
        bytes memory payment = abi.encode(payer, recipient, 20, token, 0);
        UTR.pay(payment, value);
    }
}
```

此交易由用户签名以使用 `PAYMENT` 模式执行 `exactInput` 功能：

```javascript
UniversalTokenRouter.exec([{
    eip: 20,
    token: tokenOut,
    id: 0,
    amountOutMin: 1,
    recipient: to,
}], [{
    inputs: [{
        mode: PAYMENT,
        eip: 20,
        token: tokenIn,
        id: 0,
        amountIn: amountIn,
        recipient: pool.address,
    }],
    code: SwapHelper.address,
    data: encodeFunctionData("exactInput", [...]),
}])
```

#### 授权适配器

一个简单的非重入 ERC-20 适配器，适用于使用直接授权的应用程序和路由器合约。

```solidity
contract AllowanceAdapter is ReentrancyGuard {
    struct Input {
        address token;
        uint amountIn;
    }

    function approveAndCall(
        Input[] memory inputs,
        address spender,
        bytes memory data,
        address leftOverRecipient
    ) external payable nonReentrant {
        for (uint i = 0; i < inputs.length; ++i) {
            Input memory input = inputs[i];
            IERC20(input.token).approve(spender, input.amountIn);
        }

        (bool success, bytes memory result) = spender.call{value: msg.value}(data);
        if (!success) {
            assembly {
                revert(add(result, 32), mload(result))
            }
        }

        for (uint i = 0; i < inputs.length; ++i) {
            Input memory input = inputs[i];
            // clear all allowance
            IERC20(input.token).approve(spender, 0);
            uint leftOver = IERC20(input.token).balanceOf(address(this));
            if (leftOver > 0) {
                TransferHelper.safeTransfer(input.token, leftOverRecipient, leftOver);
            }
        }
    }
}
```

此交易构造为利用 `UTR` 与 Uniswap V2 路由器交互，而无需批准任何代币：

```javascript
const { data: routerData } = await uniswapRouter.populateTransaction.swapExactTokensForTokens(
    amountIn,
    amountOutMin,
    path,
    to,
    deadline,
)

const { data: adapterData } = await adapter.populateTransaction.approveAndCall(
    [{
        token: path[0],
        amountIn,
    }],
    uniswapRouter.address,
    routerData,
    leftOverRecipient,
)

await utr.exec([], [{
    inputs: [{
        mode: TRANSFER,
        recipient: adapter.address,
        eip: 20,
        token: path[0],
        id: 0,
        amountIn,
    }],
    code: adapter.address,
    data: adapterData,
}])
```

## 理由

`Permit` 类型签名不被支持，因为通用代币路由器的目的是消除所有新代币的交互式 `approve` 签名，以及*大多数*旧代币的签名。

## 向后兼容性

### 代币

旧代币合约（ERC-20、ERC-721 和 ERC-1155）需要为每个账户对通用代币路由器进行一次批准。

新代币合约可以将通用代币路由器预配置为受信任的支出者，交互使用时无需批准交易。

```solidity
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

/**
 * @dev Implementation of the {ERC20} token standard that support a trusted ERC6120 contract as an unlimited spender.
 */
contract ERC20WithUTR is ERC20 {
    address immutable UTR;

    /**
     * @dev Sets the values for {name}, {symbol} and ERC6120's {utr} address.
     *
     * All three of these values are immutable: they can only be set once during
     * construction.
     *
     * @param utr can be zero to disable trusted ERC6120 support.
     */
    constructor(string memory name, string memory symbol, address utr) ERC20(name, symbol) {
        UTR = utr;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        if (spender == UTR && spender != address(0)) {
            return type(uint256).max;
        }
        return super.allowance(owner, spender);
    }

    /**
     * Does not check or update the allowance if `spender` is the UTR.
     */
    function _spendAllowance(address owner, address spender, uint256 amount) internal virtual override {
        if (spender == UTR && spender != address(0)) {
            return;
        }
        super._spendAllowance(owner, spender, amount);
    }
}
```

### 应用程序

唯一与 UTR **不兼容** 的应用程序合约是那些在其内部存储中使用 `msg.sender` 作为受益地址而没有任何所有权转移功能的合约。

所有接受 `recipient`（或 `to`）参数作为受益地址的应用程序合约都可以开箱即用地与 UTR 兼容。

将代币（ERC-20、ERC-721 和 ERC-1155）转移到 `msg.sender` 的应用程序合约需要额外的适配器以将 `recipient` 添加到其函数中。

```solidity
// sample adapter contract for WETH
contract WethAdapter {
    function deposit(address recipient) external payable {
        IWETH(WETH).deposit(){value: msg.value};
        TransferHelper.safeTransfer(WETH, recipient, msg.value);
    }
}
```

可能需要额外的辅助和适配器合约，但它们大多是外围的且不具侵入性。它们不持有任何代币或授权，因此可以频繁更新，对核心应用程序合约的安全影响很小或没有。

## 参考实现

由 Derivable Labs 提供的参考实现，并由 Hacken 进行审计。

```solidity
/// @title The implemetation of the EIP-6120.
/// @author Derivable Labs
contract UniversalTokenRouter is ERC165, IUniversalTokenRouter {
    uint256 constant PAYMENT       = 0;
    uint256 constant TRANSFER      = 1;
    uint256 constant CALL_VALUE    = 2;

    uint256 constant EIP_ETH       = 0;

    uint256 constant ERC_721_BALANCE = uint256(keccak256('UniversalTokenRouter.ERC_721_BALANCE'));

    /// @dev transient pending payments
    mapping(bytes32 => uint256) t_payments;

    /// @dev accepting ETH for user execution (e.g. WETH.withdraw)
    receive() external payable {}

    /// The main entry point of the router
    /// @param outputs token behaviour for output verification
    /// @param actions router actions and inputs for execution
    function exec(
        Output[] memory outputs,
        Action[] memory actions
    ) external payable virtual override {
    unchecked {
        // track the expected balances before any action is executed
        for (uint256 i = 0; i < outputs.length; ++i) {
            Output memory output = outputs[i];
            uint256 balance = _balanceOf(output);
            uint256 expected = output.amountOutMin + balance;
            require(expected >= balance, 'UTR: OUTPUT_BALANCE_OVERFLOW');
            output.amountOutMin = expected;
        }

        address sender = msg.sender;

        for (uint256 i = 0; i < actions.length; ++i) {
            Action memory action = actions[i];
            uint256 value;
            for (uint256 j = 0; j < action.inputs.length; ++j) {
                Input memory input = action.inputs[j];
                uint256 mode = input.mode;
                if (mode == CALL_VALUE) {
                    // eip and id are ignored
                    value = input.amountIn;
                } else {
                    if (mode == PAYMENT) {
                        bytes32 key = keccak256(abi.encode(sender, input.recipient, input.eip, input.token, input.id));
                        t_payments[key] = input.amountIn;
                    } else if (mode == TRANSFER) {
                        _transferToken(sender, input.recipient, input.eip, input.token, input.id, input.amountIn);
                    } else {
                        revert('UTR: INVALID_MODE');
                    }
                }
            }
            if (action.code != address(0) || action.data.length > 0 || value > 0) {
                require(
                    ERC165Checker.supportsInterface(action.code, 0x61206120),
                    "UTR: NOT_CALLABLE"
                );
                (bool success, bytes memory result) = action.code.call{value: value}(action.data);
                if (!success) {
                    assembly {
                        revert(add(result,32),mload(result))
                    }
                }
            }
            // clear all transient storages
            for (uint256 j = 0; j < action.inputs.length; ++j) {
                Input memory input = action.inputs[j];
                if (input.mode == PAYMENT) {
                    // transient storages
                    bytes32 key = keccak256(abi.encodePacked(
                        sender, input.recipient, input.eip, input.token, input.id
                    ));
                    delete t_payments[key];
                }
            }
        }

        // refund any left-over ETH
        uint256 leftOver = address(this).balance;
        if (leftOver > 0) {
            TransferHelper.safeTransferETH(sender, leftOver);
        }

        // verify balance changes
        for (uint256 i = 0; i < outputs.length; ++i) {
            Output memory output = outputs[i];
            uint256 balance = _balanceOf(output);
            // NOTE: output.amountOutMin is reused as `expected`
            require(balance >= output.amountOutMin, 'UTR: INSUFFICIENT_OUTPUT_AMOUNT');
        }
    } }
    
    /// Spend the pending payment. Intended to be called from the input.action.
    /// @param payment encoded payment data
    /// @param amount token amount to pay with payment
    function pay(bytes memory payment, uint256 amount) external virtual override {
        discard(payment, amount);
        (
            address sender,
            address recipient,
            uint256 eip,
            address token,
            uint256 id
        ) = abi.decode(payment, (address, address, uint256, address, uint256));
        _transferToken(sender, recipient, eip, token, id, amount);
    }

    /// Discard a part of a pending payment. Can be called from the input.action
    /// to verify the payment without transferring any token.
    /// @param payment encoded payment data
    /// @param amount token amount to pay with payment
    function discard(bytes memory payment, uint256 amount) public virtual override {
        bytes32 key = keccak256(payment);
        require(t_payments[key] >= amount, 'UTR: INSUFFICIENT_PAYMENT');
        unchecked {
            t_payments[key] -= amount;
        }
    }

    // IERC165-supportsInterface
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return
            interfaceId == type(IUniversalTokenRouter).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    function _transferToken(
        address sender,
        address recipient,
        uint256 eip,
        address token,
        uint256 id,
        uint256 amount
    ) internal virtual {
        if (eip == 20) {
            TransferHelper.safeTransferFrom(token, sender, recipient, amount);
        } else if (eip == 1155) {
            IERC1155(token).safeTransferFrom(sender, recipient, id, amount, "");
        } else if (eip == 721) {
            IERC721(token).safeTransferFrom(sender, recipient, id);
        } else {
            revert("UTR: INVALID_EIP");
        }
    }

    function _balanceOf(
        Output memory output
    ) internal view virtual returns (uint256 balance) {
        uint256 eip = output.eip;
        if (eip == 20) {
            return IERC20(output.token).balanceOf(output.recipient);
        }
        if (eip == 1155) {
            return IERC1155(output.token).balanceOf(output.recipient, output.id);
        }
        if (eip == 721) {
            if (output.id == ERC_721_BALANCE) {
                return IERC721(output.token).balanceOf(output.recipient);
            }
            try IERC721(output.token).ownerOf(output.id) returns (address currentOwner) {
                return currentOwner == output.recipient ? 1 : 0;
            } catch {
                return 0;
            }
        }
        if (eip == EIP_ETH) {
            return output.recipient.balance;
        }
        revert("UTR: INVALID_EIP");
    }
}
```

## 安全考虑

### ERC-165 代币

代币合约绝对**不应**支持 ID 为 `0x61206120` 的 ERC-165 接口，因为它保留给非代币合约以通过 UTR 调用。任何具有接口 ID `0x61206120` 的代币被批准给 UTR 后，任何人都可以在没有任何限制的情况下使用。

### 重入

转移到 UTR 合约的代币将永久丢失，因为没有办法将它们转移出去。需要中间地址来持有代币的应用程序应使用自己的辅助合约，并在安全执行时添加重入保护。

ETH 必须在值在操作调用中被消费之前转移到 UTR 合约（使用 `CALL_VALUE`）。此 ETH 值可以通过在操作代码或恶意代币函数内部的重入调用从 UTR 中抽取。如果用户不转移超过他们将在该交易中消费的 ETH，则此漏洞将无法实现。

```solidity
// transfer 100 in, but spend only 60,
// so at most 40 wei can be exploited in this transaction
UniversalTokenRouter.exec([
    ...
], [{
    inputs: [{
        mode: CALL_VALUE,
        eip: 20,
        token: 0,
        id: 0,
        amountIn: 60,   // spend 60
        recipient: AddressZero,
    }],
    ...
}], {
    value: 100,   // transfer 100 in
})
```

### 丢弃支付

可以通过在调用后查询余额来检查 `pay` 函数的结果，从而以无信任的方式调用 UTR 合约。然而，由于无法验证 `discard` 函数的执行，因此它应仅与受信任的 UTR 合约一起使用。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。