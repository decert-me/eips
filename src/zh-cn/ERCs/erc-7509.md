---
eip: 7509
title: 实体组件系统
description: 表示由数据组件组成的实体，以及对实体组件进行操作的系统。
author: Rickey (@HelloRickey)
discussions-to: https://ethereum-magicians.org/t/a-new-proposal-of-entity-component-system/15665
status: 草案
type: 标准跟踪
category: ERC
created: 2023-09-05
---

## 摘要

本提案定义了一个最小的实体组件系统（ECS）。实体是分配给多个组件（数据）的唯一标识，并通过系统（逻辑）进行处理。
本提案标准化了在智能合约中使用 ECS 的接口规范，提供了一组基本功能，允许用户自由组合和管理多合约应用程序。

## 动机   

ECS 是一种设计模式，通过将数据与行为分离来提高代码的重用性。它通常用于游戏开发。一个最小的 ECS 包括   
**实体**：唯一标识符。   
**组件**：附加到实体的可重用数据容器。   
**系统**：操作实体组件的逻辑。   
**世界**：实体组件系统的容器。   
本提案使用智能合约实现一个易于使用的最小 ECS，消除不必要的复杂性，并进行一些与合约交互行为一致的功能改进。您可以轻松自由地组合组件和系统。
作为智能合约开发者，采用 ECS 的好处包括：

- 采用简单的解耦、封装和模块化设计，使您的游戏或应用程序的架构设计更容易。
- 具有灵活的组合能力，每个实体可以组合不同的组件。您还可以为操作这些新实体的数据定义不同的系统。
- 有利于扩展，两个游戏或应用程序可以通过定义新组件和系统进行交互。
- 可以帮助您的应用程序添加新功能或升级，因为数据和行为是分开的，新功能不会影响您的旧数据。
- 易于管理。当您的应用程序由多个合约组成时，它将帮助您有效管理每个合约的状态。
- 其组件是可重用的，您可以与社区共享您的组件，以帮助他人提高开发效率。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“NOT RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

世界合约是实体、组件合约和系统合约的容器。其核心原则是建立实体与组件合约之间的关系，不同的实体将附加不同的组件，并使用系统合约动态更改组件中实体的数据。

构建基于 ECS 的程序时的常规工作流程：

1. 实现`IWorld`接口以创建世界合约。
2. 调用世界合约的`createEntity()`以创建实体。
3. 实现`IComponent`接口以创建组件合约。
4. 调用世界合约的`registerComponent()`以注册组件合约。
5. 调用世界合约的`addComponent()`以将组件附加到实体。
6. 创建一个系统合约，这是一个没有接口限制的合约，您可以在系统合约中定义任何函数。
7. 调用世界合约的`registerSystem()`以注册系统合约。
8. 运行系统。

### 接口

#### `IWorld.sol`

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity >=0.8.0;

interface IWorld {
    /**
     * Create a new entity.
     * @dev The entity MUST be assigned a unique Id.
     * If the state of the entity is true, it means it is available, and if it is false, it means it is not available.
     * When the state of the entity is false, you cannot add or remove components for the entity.
     * @return New entity id.
     */
    function createEntity() external returns (uint256);

    /**
     * Does the entity exist in the world.
     * @param _entityId is the Id of the entity.
     * @return true exists, false does not exist.
     */
    function entityExists(uint256 _entityId) external view returns (bool);

    /**
     * Get the total number of entities in the world.
     * @return The total number of entities.
     */
    function getEntityCount() external view returns (uint256);

    /**
     * Set the state of an entity.
     * @dev Entity MUST exist.
     * @param _entityId is the Id of the entity.
     * @param _entityState is the state of the entity, true means available, false means unavailable.
     */
    function setEntityState(uint256 _entityId, bool _entityState) external;

    /**
     * Get the state of an entity.
     * @param _entityId Id of the entity.
     * @return The current state of the entity.
     */
    function getEntityState(uint256 _entityId) external view returns (bool);

    /**
     * Register a component to the world.
     * @dev A component MUST be registered with the world before it can be attached to an entity.
     * MUST NOT register the same component to the world repeatedly.
     * It SHOULD be checked that the contract address returned by world() of the component contract is the same as the current world contract.
     * The state of the component is true means it is available, and false means it is not available. When the component state is set to false, it cannot be attached to the entity.
     * @param _componentAddress is the contract address of the component.
     */
    function registerComponent(address _componentAddress) external;

    /**
     * Does the component exist in the world.
     * @param _componentAddress is the contract address of the component.
     * @return true exists, false does not exist.
     */
    function componentExists(address _componentAddress)
        external
        view
        returns (bool);

    /**
     * Get the contract addresses of all components registered in the world.
     * @return Array of contract addresses.
     */
    function getComponents() external view returns (address[] memory);

    /**
     * Set component state.
     * @dev Component MUST exist.
     * @param _componentAddress is the contract address of the component.
     * @param _componentState is the state of the component, true means available, false means unavailable.
     */
    function setComponentState(address _componentAddress, bool _componentState)
        external;

    /**
     * Get the state of a component.
     * @param _componentAddress is the contract address of the component.
     * @return true means available, false means unavailable.
     */
    function getComponentState(address _componentAddress)
        external
        view
        returns (bool);

    /**
     * Attach a component to the entity.
     * @dev Entity MUST be available.Component MUST be available.A component MUST NOT be added to an entity repeatedly.
     * @param _entityId is the Id of the entity.
     * @param _componentAddress is the address of the component to be attached.
     */
    function addComponent(uint256 _entityId, address _componentAddress)
        external;

    /**
     * Whether the entity has a component attached,
     * @dev Entity MUST exist.Component MUST be registered.
     * @param _entityId is the Id of the entity.
     * @param _componentAddress is the component address.
     * @return true is attached, false is not attached
     */
    function hasComponent(uint256 _entityId, address _componentAddress)
        external
        view
        returns (bool);

    /**
     * Remove a component from the entity.
     * @dev Entity MUST be available.The component MUST have been added to the entity before.
     * @param _entityId is the Id of the entity.
     * @param _componentAddress is the address of the component to be removed.
     */
    function removeComponent(uint256 _entityId, address _componentAddress)
        external;

    /**
     * Get the contract addresses of all components attached to the entity.
     * @dev Entity MUST exist.
     * @param _entityId is the Id of the entity.
     * @return An array of contract addresses of the components owned by this entity.
     */
    function getEntityComponents(uint256 _entityId)
        external
        view
        returns (address[] memory);

    /**
     * Register a system to the world.
     * @dev MUST NOT register the same system to the world repeatedly.The system state is true means available, false means unavailable.
     * @param _systemAddress is the contract address of the system.
     */
    function registerSystem(address _systemAddress) external;

    /**
     * Does the system exist in the world.
     * @param _systemAddress is the contract address of the system.
     * @return true exists, false does not exist.
     */
    function systemExists(address _systemAddress) external view returns (bool);

    /**
     * Get the contract addresses of all systems registered in the world.
     * @return Array of contract addresses.
     */
    function getSystems() external view returns (address[] memory);

    /**
     * Set the system State.
     * @dev System MUST exist.
     * @param _systemAddress is the contract address of the system.
     * @param _systemState is the state of the system.
     */
    function setSystemState(address _systemAddress, bool _systemState) external;

    /**
     * Get the state of a system.
     * @param _systemAddress is the contract address of the system.
     * @return The state of the system.
     */
    function getSystemState(address _systemAddress)
        external
        view
        returns (bool);
}
```

#### `IComponent.sol`

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity >=0.8.0;
import "./Types.sol";

interface IComponent {
    /**
     * The world contract address registered by the component.
     * @return world contract address.
     */
    function world() external view returns (address);

    /**
     *Get the data type and get() parameter type of the component
     * @dev SHOULD Import Types Library, which is an enumeration Library containing all data types.
     * Entity data can be stored according to the data type.
     * The get() parameter data type can be used to get entity data.
     * @return the data type array of the entity
     * @return get parameter data type array
     */
    function types()
        external
        view
        returns (Types.Type[] memory, Types.Type[] memory);

    /**
     *Store entity data.
     * @dev entity MUST be available. The system that operates on it MUST be available.
     * The entity has the component attached.
     * @param _entityId is the Id of the entity.
     * @param _data is the data to be stored.
     */
    function set(uint256 _entityId, bytes memory _data) external;

    /**
     *Get the data of the entity according to the entity Id.
     * @param _entityId is the Id of the entity.
     * @return Entity data.
     */
    function get(uint256 _entityId) external view returns (bytes memory);

    /** Get the data of the entity according to the entity Id and parameters.
     * @param _entityId is the Id of the entity.
     * @param _params is an extra parameter, it SHOULD depend on whether you need it.
     * @return Entity data.
     */
    function get(uint256 _entityId, bytes memory _params)
        external
        view
        returns (bytes memory);
}
```

### 库

库 [`Types.sol`](../assets/eip-7509/Types.sol) 包含上述接口中使用的 Solidity 类型的枚举。

## 理由

### 为什么要包含类型信息而不是简单的字节数组？

这是为了确保在使用组件时类型的正确性，以避免潜在的错误和不一致。外部开发者可以根据类型清晰地设置和获取。

### 为什么要区分不存在的实体和状态为 false 的实体？

我们不能仅根据状态判断实体是否实际存在。外部贡献者可以基于实体创建组件。如果他们使用的实体不存在，他们创建的组件可能没有意义。组件创建者应首先检查实体是否存在，如果实体确实存在，即使实体的状态为 false 也有意义。因为他可以在将组件附加到实体之前等待实体状态为 true。

### 为什么`getEntityComponents`函数返回所有组件的地址而不是所有组件 ID？

`getEntityComponents`有两种设计。另一种设计是为组件 ID 和组件地址的存储添加额外的映射。每次调用`addComponent`时，函数的参数是实体 ID 和组件 ID。当用户调用`getEntityComponents`时，它将返回一个组件 ID 的数组，他们使用每个组件 ID 查询组件地址，然后根据每个组件地址查询数据。因为一个实体可能包含许多组件 ID，这将导致用户多次请求组件地址。最终，我们选择直接使用`getEntityComponents`返回实体拥有的所有地址。

### `registerComponent`和`registerSystem`可以提供外部权限吗？

这取决于您的应用程序或游戏的开放性。如果您鼓励开发者参与，他们提交注册的组件和系统的状态应为`false`，并且在使用`setComponentState`和`setSystemState`启用它们之前，您需要检查他们是否提交了恶意代码。

### 何时在组件中使用带有额外参数的`get`？

组件提供两个`get`函数。一个`get`函数只需要传入实体 ID，另一个则有更多的`_params`参数，这些参数将用作获取数据的附加参数。例如，您定义了一个存储与实体等级对应的 HP 的组件。如果您想获取与其等级匹配的实体的 HP，则可以调用带有实体等级作为`_params`的`get`函数。

## 参考实现

请参见[以太坊 ECS 示例](../assets/eip-7509/README.md)

## 安全考虑

除非您想实现特殊功能，否则不要直接向普通用户提供以下方法，这些方法应由合约所有者设置。   
`createEntity()`,
`setEntityState()`,
`addComponent()`,
`removeComponent()`,
`registerComponent()`,
`setComponentState()`,
`registerSystem()`,
`setSystemState()`

不要提供修改实体的函数，除了组件合约中的 set()。并在`set()`中添加检查，以检查实体是否可用以及操作系统是否可用。   

在系统注册到世界中后，它将能够操作世界中所有实体的组件数据。在将所有系统合约注册到世界之前，有必要检查和审计所有系统合约的代码安全性。

如果新版本已弃用某些实体、组件合约和系统合约。需要及时使用`setEntityState()`、`setComponentState()`和`setSystemState()`将其禁用。
## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。