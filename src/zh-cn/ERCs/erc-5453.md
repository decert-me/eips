---
eip: 5453
title: 认可 - 任何函数的许可
description: 一种基于 ERC-5750 的在同一交易中批准函数调用的一般协议。
author: Zainan Victor Zhou (@xinbenlv)
discussions-to: https://ethereum-magicians.org/t/erc-5453-endorsement-standard/10355
status: 最后呼叫
last-call-deadline: 2023-09-27
type: 标准跟踪
category: ERC
created: 2022-08-12
requires: 165, 712, 1271, 5750
---

## 摘要

本 EIP 建立了一种一般协议，用于在同一交易中允许批准函数调用，依赖于 [ERC-5750](./eip-5750.md)。与一些先前的方案（[ERC-2612](./eip-2612.md) 针对 [ERC-20](./eip-20.md)，`ERC-4494` 针对 [ERC-721](./eip-721.md) 通常仅允许单一行为（`transfer` 针对 ERC-20 和 `safeTransferFrom` 针对 ERC-721）和在两笔交易中单一批准者（首先是 `permit(...)` TX，然后是类似 `transfer` 的 TX）不同，本 EIP 提供了一种在同一交易中允许任意行为并聚合来自任意数量批准者的多个批准的方法，允许多签或阈值签名行为。

## 动机

1. 支持与函数调用一起的许可（批准）。
2. 支持来自其他用户的第二次批准。
3. 支持由其他用户支付
4. 支持多签
5. 支持通过认可共同行动的人员
6. 支持累积投票
7. 支持离线签名

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 接口

此处引用的接口和结构如下

```solidity
pragma solidity ^0.8.9;

struct ValidityBound {
    bytes32 functionParamStructHash;
    uint256 validSince;
    uint256 validBy;
    uint256 nonce;
}

struct SingleEndorsementData {
    address endorserAddress; // 32
    bytes sig; // dynamic = 65
}

struct GeneralExtensionDataStruct {
    bytes32 erc5453MagicWord;
    uint256 erc5453Type;
    uint256 nonce;
    uint256 validSince;
    uint256 validBy;
    bytes endorsementPayload;
}

interface IERC5453EndorsementCore {
    function eip5453Nonce(address endorser) external view returns (uint256);
    function isEligibleEndorser(address endorser) external view returns (bool);
}

interface IERC5453EndorsementDigest {
    function computeValidityDigest(
        bytes32 _functionParamStructHash,
        uint256 _validSince,
        uint256 _validBy,
        uint256 _nonce
    ) external view returns (bytes32);

    function computeFunctionParamHash(
        string memory _functionName,
        bytes memory _functionParamPacked
    ) external view returns (bytes32);
}

interface IERC5453EndorsementDataTypeA {
    function computeExtensionDataTypeA(
        uint256 nonce,
        uint256 validSince,
        uint256 validBy,
        address endorserAddress,
        bytes calldata sig
    ) external view returns (bytes memory);
}


interface IERC5453EndorsementDataTypeB {
    function computeExtensionDataTypeB(
        uint256 nonce,
        uint256 validSince,
        uint256 validBy,
        address[] calldata endorserAddress,
        bytes[] calldata sigs
    ) external view returns (bytes memory);
}
```

请参见 [`IERC5453.sol`](../assets/eip-5453/IERC5453.sol)。

### 行为规范

如 [ERC-5750 方法行为的一般可扩展性](./eip-5750.md) 中所述，任何符合要求的方法，其最后一个方法具有 `bytes extraData` 用于扩展行为，可以符合 [ERC-5453](./eip-5453.md)，以指示来自特定用户的许可。

1. 本 EIP 的任何符合要求的方法 MUST 是 [ERC-5750](./eip-5750.md) 兼容的方法。
2. 调用者 MUST 传入最后一个参数 `bytes extraData`，符合在 _接口部分_ 中指定的 `GeneralExtensionDataStruct` 的 Solidity 内存编码布局字节。以下描述基于将 `bytes extraData` 解码为 `GeneralExtensionDataStruct` 时的情况。
3. 在解码的 `GeneralExtensionDataStruct` 的 `extraData` 中，调用者 MUST 将 `GeneralExtensionDataStruct.erc5453MagicWord` 的值设置为 `keccak256("ERC5453-ENDORSEMENT")`。
4. 调用者 MUST 将 `GeneralExtensionDataStruct.erc5453Type` 的值设置为支持的值之一。

```solidity
uint256 constant ERC5453_TYPE_A = 1;
uint256 constant ERC5453_TYPE_B = 2;
```

5. 当 `GeneralExtensionDataStruct.erc5453Type` 的值设置为 `ERC5453_TYPE_A` 时，`GeneralExtensionDataStruct.endorsementPayload` MUST 是 `SingleEndorsementData` 的 ABI 编码字节。
6. 当 `GeneralExtensionDataStruct.erc5453Type` 的值设置为 `ERC5453_TYPE_B` 时，`GeneralExtensionDataStruct.endorsementPayload` MUST 是 `SingleEndorsementData[]`（动态数组）的 ABI 编码字节。

7. 每个 `SingleEndorsementData` MUST 具有 `address endorserAddress;` 和 65 字节的 `bytes sig` 签名。

8. 每个 `bytes sig` MUST 是使用签名者的私钥的 ECDSA（secp256k1）签名，其对应地址为 `endorserAddress`，签名 `validityDigest`，该值是 [EIP-712](./eip-712.md) 的 `ValidityBound` 数据结构的 hashTypeDataV4 的哈希，如下所示：

```solidity
bytes32 validityDigest =
    eip712HashTypedDataV4(
        keccak256(
            abi.encode(
                keccak256(
                    "ValidityBound(bytes32 functionParamStructHash,uint256 validSince,uint256 validBy,uint256 nonce)"
                ),
                functionParamStructHash,
                _validSince,
                _validBy,
                _nonce
            )
        )
    );
```

9. `functionParamStructHash` MUST 按如下方式计算

```solidity
        bytes32 functionParamStructHash = keccak256(
            abi.encodePacked(
                keccak256(bytes(_functionStructure)),
                _functionParamPacked
            )
        );
        return functionParamStructHash;
```

其中

- `_functionStructure` MUST 计算为 `function methodName(type1 param1, type2 param2, ...)`。
- `_functionParamPacked` MUST 计算为 `enc(param1) || enco(param2) ...`

10. 在验证 `endorserAddress == ecrecover(validityDigest, signature)` 或 `EIP1271(endorserAddress).isValidSignature(validityDigest, signature) == ERC1271.MAGICVALUE` 后，单一认可 MUST 被视为有效。
11. 符合要求的方法 MAY 选择对同一 `ERC5453_TYPE_B` 类型的 `endorsementPayload` 施加有效的认可数量的阈值。

12. `validSince` 和 `validBy` 均为包含。实现者 MAY 选择使用区块号或时间戳。实现者 SHOULD 找到一种方法来指示 `validSince` 和 `validBy` 是区块号还是时间戳。

## 理由

1. 我们选择同时拥有 `ERC5453_TYPE_A`（单一认可）和 `ERC5453_TYPE_B`（多重认可，整个合约使用相同的随机数），以便能够平衡更广泛的用例。例如，`ERC-2612` 和 `ERC-4494` 的相同用例可以通过 `ERC5453_TYPE_A` 支持。而阈值批准可以通过 `ERC5453_TYPE_B` 完成。更复杂的批准类型也可以通过定义新的 `ERC5453_TYPE_?` 进行扩展。

2. 我们选择同时包含 `validSince` 和 `validBy` 以允许最大灵活性在过期方面。这也可以通过 EVM 原生支持，如果采用 `ERC-5081`，但 `ERC-5081` 不会很快被采用，因此我们选择在我们的协议中添加这两个数字，以允许智能合约级别的支持。

## 向后兼容性

该设计假设 `bytes calldata extraData` 以最大化未来扩展的灵活性。此假设与 [ERC-721](eip-721.md)、[ERC-1155](eip-1155.md) 和许多其他 ERC 跟踪的 EIP 兼容。那些不兼容的，例如 [ERC-20](./eip-20.md)，也可以更新以支持它，例如使用包装合约或代理升级。

## 参考实现

除了验证认可者签名的指定算法外，我们还提供以下参考实现。

```solidity
pragma solidity ^0.8.9;

import "@openzeppelin/contracts/utils/cryptography/SignatureChecker.sol";
import "@openzeppelin/contracts/utils/cryptography/EIP712.sol";

import "./IERC5453.sol";

abstract contract AERC5453Endorsible is EIP712,
    IERC5453EndorsementCore, IERC5453EndorsementDigest, IERC5453EndorsementDataTypeA, IERC5453EndorsementDataTypeB {
    // ...

    function _validate(
        bytes32 msgDigest,
        SingleEndorsementData memory endersement
    ) internal virtual {
        require(
            endersement.sig.length == 65,
            "AERC5453Endorsible: wrong signature length"
        );
        require(
            SignatureChecker.isValidSignatureNow(
                endersement.endorserAddress,
                msgDigest,
                endersement.sig
            ),
            "AERC5453Endorsible: invalid signature"
        );
    }
    // ...

    modifier onlyEndorsed(
        bytes32 _functionParamStructHash,
        bytes calldata _extensionData
    ) {
        require(_isEndorsed(_functionParamStructHash, _extensionData));
        _;
    }

    function computeExtensionDataTypeB(
        uint256 nonce,
        uint256 validSince,
        uint256 validBy,
        address[] calldata endorserAddress,
        bytes[] calldata sigs
    ) external pure override returns (bytes memory) {
        require(endorserAddress.length == sigs.length);
        SingleEndorsementData[]
            memory endorsements = new SingleEndorsementData[](
                endorserAddress.length
            );
        for (uint256 i = 0; i < endorserAddress.length; ++i) {
            endorsements[i] = SingleEndorsementData(
                endorserAddress[i],
                sigs[i]
            );
        }
        return
            abi.encode(
                GeneralExtensionDataStruct(
                    MAGIC_WORLD,
                    ERC5453_TYPE_B,
                    nonce,
                    validSince,
                    validBy,
                    abi.encode(endorsements)
                )
            );
    }
}

```

请参见 [`AERC5453.sol`](../assets/eip-5453/AERC5453.sol)

### `EndorsableERC721` 的参考实现

这是 `EndorsableERC721` 的参考实现，达到与 `ERC-4494` 相似的行为。

```solidity
pragma solidity ^0.8.9;

contract EndorsableERC721 is ERC721, AERC5453Endorsible {
    //...

    function mint(
        address _to,
        uint256 _tokenId,
        bytes calldata _extraData
    )
        external
        onlyEndorsed(
            _computeFunctionParamHash(
                "function mint(address _to,uint256 _tokenId)",
                abi.encode(_to, _tokenId)
            ),
            _extraData
        )
    {
        _mint(_to, _tokenId);
    }
}
```

请参见 [`EndorsableERC721.sol`](../assets/eip-5453/EndorsableERC721.sol)

### `ThresholdMultiSigForwarder` 的参考实现

这是 `ThresholdMultiSigForwarder` 的参考实现，达到类似于多签阈值批准的远程合约调用，如 Gnosis-Safe 钱包。

```solidity
pragma solidity ^0.8.9;

contract ThresholdMultiSigForwarder is AERC5453Endorsible {
    //...
    function forward(
        address _dest,
        uint256 _value,
        uint256 _gasLimit,
        bytes calldata _calldata,
        bytes calldata _extraData
    )
        external
        onlyEndorsed(
            _computeFunctionParamHash(
                "function forward(address _dest,uint256 _value,uint256 _gasLimit,bytes calldata _calldata)",
                abi.encode(_dest, _value, _gasLimit, keccak256(_calldata))
            ),
            _extraData
        )
    {
        string memory errorMessage = "Fail to call remote contract";
        (bool success, bytes memory returndata) = _dest.call{value: _value}(
            _calldata
        );
        Address.verifyCallResult(success, returndata, errorMessage);
    }

}

```

请参见 [`ThresholdMultiSigForwarder.sol`](../assets/eip-5453/ThresholdMultiSigForwarder.sol)

## 安全考虑

### 重放攻击

重放攻击是一种针对密码认证的攻击类型。从狭义上讲，它通常指的是一种通过重用现有签名对再次签名的消息进行攻击的方式，绕过密码签名验证。任何依赖于本 EIP 的实现必须意识到，此处描述的所有智能认可都是密码签名，且是 _公开_ 的，任何人都可以获得。它们必须预见到在同一智能合约的确切部署中，不仅可能重放交易，还可能在其他类似智能合约的部署中，或在同一合约的另一个 `chainId` 版本中，或任何其他类似攻击面中。`nonce`、`validSince` 和 `validBy` 字段旨在限制攻击面，但可能无法完全消除所有此类攻击的风险，例如，参见 [钓鱼](#phishing) 部分。
### 网络钓鱼

值得指出的是，网络钓鱼是一种特殊形式的重放攻击。对手可以设计另一个智能合约，以便用户被欺骗签署一个看似合法目的的智能背书，但数据设计与目标应用程序匹配

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。