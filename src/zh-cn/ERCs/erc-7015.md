---
eip: 7015
title: NFT 创作者归属
description: 通过加密方式扩展 NFT 的创作者归属。
author: indreams (@strollinghome)
discussions-to: https://ethereum-magicians.org/t/eip-authorship-attribution-for-erc721/14244
status: 审核中
type: 标准跟踪
category: ERC
created: 2023-05-11
requires: 55, 155, 712, 721, 1155
---

## 摘要

本以太坊改进提案旨在解决非同质化代币（NFT）标准的创作者归属问题（[ERC-721](./eip-721.md), [ERC-1155](./eip-1155.md)）。为此，本 EIP 提出了一个机制，NFT 创作者签署 NFT 创建所需的参数，包括 NFT 元数据的哈希以及其他相关信息。签署的参数和签名在部署交易中进行验证和发出，这使得 NFT 能够验证创作者，并使 NFT 平台能够正确归属创作者。此方法确保即使不同的钱包发送部署交易，正确的账户仍被归属为创作者。

## 动机

当前的 NFT 平台假设部署智能合约的钱包是 NFT 的创作者，这导致在不同钱包发送部署交易的情况下出现错误归属。这种情况在使用智能钱包账户和新的合约部署策略（例如，第一个收藏者部署 NFT 合约）时经常发生。该提案旨在通过允许创作者签署 NFT 创建所需的参数来解决此问题，以便任何钱包都可以以可验证的方式发送部署交易，表明谁是创作者。

## 规范

本文档中的关键词“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“应该不”、“推荐”、“可以”和“可选”应按 RFC 2119 中的描述进行解释。

符合 ERC-721 和 ERC-1155 的合约可以实现此 NFT 创作者归属扩展，以提供一个标准事件，在合约创建时发出，定义 NFT 创作者。

本 EIP 利用合约地址可以在合约部署之前预计算的事实。无论 NFT 合约是通过另一个合约（工厂）还是通过 EOA 部署，创作者都可以使用此规范正确归属。

**签名机制**

创作者通过签署与 [EIP-712](./eip-712.md) 兼容的消息来给予同意；所有符合此 EIP 的签名必须包含所有定义的字段。签署的结构可以是任何定义如何创建代币的任意数据；它必须以 EIP-712 兼容格式进行哈希，并具有适当的 EIP-712 域。

以下是一些可以编码到 `structHash`（定义如下）的结构示例：

```solidity
// example struct that can be encoded in `structHash`; defines that a token can be created with a metadataUri and price:

struct TokenCreation {
  string metadataUri;
  uint256 price;
  uint256 nonce;
}
```

**签名验证**

创作者归属通过必须由正在部署的 NFT 合约验证的签名验证来给予，并且 NFT 合约在部署交易期间必须发出一个事件。该事件包括重建签署摘要和验证签名所需的所有必要字段，以确保其与指定的创作者匹配。事件名称为 `CreatorAttribution`，并包括以下字段：

- `structHash`: 部署 NFT 合约的哈希信息（例如名称、符号、管理员等）。这对应于 [EIP-712 hashStruct 定义](./eip-712.md#definition-of-hashstruct) 标准中定义的 `hashStruct` 值。
- `domainName`: 验证签名的合约域名（用于 EIP-712 签名验证）。
- `version`: 验证签名的合约版本（用于 EIP-712 签名验证）
- `creator`: 创作者的账户
- `signature`: 创作者的签名

该事件定义如下：

```solidity
event CreatorAttribution(
  bytes32 structHash,
  string domainName,
  string version,
  address creator,
  bytes signature
);
```

请注意，尽管 `chainId` 参数对于 [EIP-712](./eip-712.md) 签名是必要的，但我们在事件中省略该参数，因为可以通过交易数据推断出来。同样，签名验证的 `verifyingContract` 参数也被省略，因为它必须与交易中的 `emitter` 字段相同。`emitter` 必须是代币。

平台可以通过使用发出的参数重建签名摘要并从 `signature` 参数中恢复签名者来验证创作者归属的有效性。恢复的签名者必须与事件中发出的 `creator` 匹配。如果存在 `CreatorAttribution` 事件并且签名验证正确，则必须将归属给予 `creator`，而不是提交交易的账户。

### 参考实现

#### 示例签名验证器

```solidity
pragma solidity 0.8.20;
import "@openzeppelin/contracts/utils/cryptography/EIP712.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import "@openzeppelin/contracts/interfaces/IERC1271.sol";

abstract contract ERC7015 is EIP712 {
  error Invalid_Signature();
  event CreatorAttribution(
    bytes32 structHash,
    string domainName,
    string version,
    address creator,
    bytes signature
  );

  /// @notice Define magic value to verify smart contract signatures (ERC1271).
  bytes4 internal constant MAGIC_VALUE =
    bytes4(keccak256("isValidSignature(bytes32,bytes)"));

  function _validateSignature(
    bytes32 structHash,
    address creator,
    bytes memory signature
  ) internal {
    if (!_isValid(structHash, creator, signature)) revert Invalid_Signature();
    emit CreatorAttribution(structHash, "ERC7015", "1", creator, signature);
  }

  function _isValid(
    bytes32 structHash,
    address signer,
    bytes memory signature
  ) internal view returns (bool) {
    require(signer != address(0), "cannot validate");

    bytes32 digest = _hashTypedDataV4(structHash);

    // if smart contract is the signer, verify using ERC-1271 smart-contract
    /// signature verification method
    if (signer.code.length != 0) {
      try IERC1271(signer).isValidSignature(digest, signature) returns (
        bytes4 magicValue
      ) {
        return MAGIC_VALUE == magicValue;
      } catch {
        return false;
      }
    }

    // otherwise, recover signer and validate that it matches the expected
    // signer
    address recoveredSigner = ECDSA.recover(digest, signature);
    return recoveredSigner == signer;
  }
}
```

## 理由

通过标准化 `CreatorAttribution` 事件，本 EIP 使平台能够在不依赖隐含假设的情况下确定创作者归属。建立创作者归属的标准使平台能够管理部署合约的复杂方面，同时保留准确的链上创作者信息。这种方法确保了识别 NFT 创作者的更可靠和透明的方法，促进了 NFT 生态系统参与者之间的信任。

[ERC-5375](./eip-5375.md) 尝试解决同样的问题，尽管离线数据提供了更好的向后兼容性，但确保准确和不可变的创作者归属对 NFT 至关重要。标准化的链上创作者归属方法本质上更可靠和安全。

与此提案相比，ERC-5375 不支持为 NFT 集合中的所有代币指定创作者，这是一种普遍的做法，特别是在新兴用例中。

本提案和 ERC-5375 在基于地址的创作者归属方面存在类似的局限性：

> 标准定义了一种协议，以验证某个 *地址* 提供了同意。然而，它并不保证该地址对应于预期的创作者 […]。证明地址与其背后的实体之间的联系超出了本文档的范围。

## 向后兼容性

由于标准要求在 NFT 部署交易期间发出事件，因此现有 NFT 无法实现此标准。

## 安全考虑

利用此提案的潜在攻击可能涉及欺骗创作者无意中签署创作者归属同意消息。因此，创作者必须确保所有签名字段与必要字段相对应，然后再进行签名。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。