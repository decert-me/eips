---
eip: 7741
title: 授权操作员
description: 通过 EIP-712 secp256k1 签名设置操作员
author: Jeroen Offerijns (@hieronx), João Martins (@0xTimepunk)
discussions-to: https://ethereum-magicians.org/t/erc-7741-authorize-operator/20531
status: 草案
type: 标准跟踪
category: ERC
created: 2024-06-03
requires: 712, 1271
---

## 摘要

一组函数，用于通过符合 [EIP-712](./eip-712.md) 类型消息签名规范的签名，启用元交易和与实现操作员模型的合约的原子交互。

## 动机

该标准的主要动机是增强操作员管理的灵活性、安全性和效率。通过利用 EIP-712 签名，该标准允许用户授权操作员，而无需链上交易，从而降低 gas 成本并改善用户体验。这在频繁更改操作员和跨链交互时尤为有利。

此外，该标准旨在：

1. **启用元交易**：允许用户将交易的执行委托给操作员，启用元交易，用户无需持有原生代币即可支付每条链上的 gas 费用。
2. **提高安全性**：利用 EIP-712 标准进行类型数据签名，相较于原始数据签名，提供更安全且用户友好的消息签名方式。
3. **促进互操作性**：提供一个标准化的操作员管理接口，可以在各种保险库协议中采用，促进互操作性并减少开发者的集成复杂性。
4. **简化跨链操作**：简化在不同链上管理操作员的过程，使协议更容易在多链环境中维护一致的操作员权限和交互。

通过解决这些需求，`授权操作员`标准旨在简化去中心化保险库协议中操作员的管理过程，使用户和开发者能够以安全、经济高效和互操作的方式与多个区块链网络中的智能合约进行交互。

## 规范

### 兼容操作员的合约

该签名授权方案适用于任何实现以下接口的合约：

```solidity
  interface IOperator {
    event OperatorSet(address indexed owner, address indexed operator, bool approved);

    function setOperator(address operator, bool approved) external returns (bool);
    function isOperator(address owner, address operator) external returns (bool status);
  }
```

[EIP-6909](./eip-6909.md) 和 [EIP-7540](./eip-7540.md) 已经实现了该接口。

参数的命名是可以互换的，例如 [EIP-6909](./eip-6909.md) 使用 `spender` 而不是 `operator`。

### 方法

#### `authorizeOperator`

授予或撤销 `operator` 代表 `msg.sender` 管理请求的权限，使用 [EIP-712](./eip-712.md) 签名。

如果 `deadline` 已过，必须回滚。

必须使签名的 nonce 无效，以防止消息重放。

如果 `signature` 不是有效的 [EIP-712](./eip-712.md) 签名，必须回滚，并给出输入参数。

必须将操作员状态设置为 `approved` 值。

必须记录 `OperatorSet` 事件。

必须返回 `true`。

```yaml
- name: authorizeOperator
  type: function
  stateMutability: nonpayable

  inputs:
    - name: owner
      type: address
    - name: operator
      type: address
    - name: approved
      type: bool
    - name: nonce
      type: bytes32
    - name: deadline
      type: uint256
    - name: signature
      type: bytes

  outputs:
    - name: success
      type: bool
```

#### `invalidateNonce`

撤销 `msg.sender` 作为 `owner` 的给定 `nonce`。

```yaml
- name: invalidateNonce
  type: function
  stateMutability: nonpayable

  inputs:
    - name: nonce
      type: bytes32
```

#### `authorizations`

返回给定 `nonce` 是否已被 `controller` 使用。

```yaml
- name: authorizations
  type: function
  stateMutability: nonpayable

  inputs:
    - name: controller
      type: address
    - name: nonce
      type: bytes32
  outputs:
    - name: used
      type: bool
```

#### `DOMAIN_SEPARATOR`

返回根据 EIP-712 定义的 `DOMAIN_SEPARATOR`。`DOMAIN_SEPARATOR` 应该对合约和链是唯一的，以防止来自其他域的重放攻击，并满足 EIP-712 的要求，但在其他方面没有限制。

```yaml
- name: DOMAIN_SEPARATOR
  type: function
  stateMutability: nonpayable

  outputs:
    - type: bytes32
```

### [ERC-165](./eip-165.md) 支持

实现该标准的智能合约必须实现 [ERC-165](./eip-165.md) 的 `supportsInterface` 函数。

如果通过 `interfaceID` 参数传递 `0xa9e50872`，合约必须返回常量值 `true`。

## 理由

### 与 [ERC-2612](./eip-2612.md) 的相似性

该规范故意设计得与 [ERC-2612](./eip-2612.md) 密切匹配。这应该简化标准的新集成。

主要区别在于使用 `bytes32` 与 `uint256`，这使得无序 nonce 成为可能。

## 参考实现

```solidity
    // This code snippet is incomplete pseudocode used for example only and is no way intended to be used in production or guaranteed to be secure

    bytes32 public constant AUTHORIZE_OPERATOR_TYPEHASH =
        keccak256("AuthorizeOperator(address controller,address operator,bool approved,bytes32 nonce,uint256 deadline)");

    mapping(address owner => mapping(bytes32 nonce => bool used)) authorizations;

    function DOMAIN_SEPARATOR() public view returns (bytes32) {
      // EIP-712 implementation 
    }

    function isValidSignature(address signer, bytes32 digest, bytes memory signature) internal view returns (bool valid) {
      // ERC-1271 implementation 
    }

    function authorizeOperator(
        address controller,
        address operator,
        bool approved,
        bytes32 nonce,
        uint256 deadline,
        bytes memory signature
    ) external returns (bool success) {
        require(block.timestamp <= deadline, "ERC7540Vault/expired");
        require(controller != address(0), "ERC7540Vault/invalid-controller");
        require(!authorizations[controller][nonce], "ERC7540Vault/authorization-used");

        authorizations[controller][nonce] = true;

        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR(),
                keccak256(abi.encode(AUTHORIZE_OPERATOR_TYPEHASH, controller, operator, approved, nonce, deadline))
            )
        );

        require(SignatureLib.isValidSignature(controller, digest, signature), "ERC7540Vault/invalid-authorization");

        isOperator[controller][operator] = approved;
        emit OperatorSet(controller, operator, approved);

        success = true;
    }
    
    function invalidateNonce(bytes32 nonce) external {
        authorizations[msg.sender][nonce] = true;
    }
```

## 安全考虑

操作员对用户具有重要控制权，签名消息可能导致不良结果。到期日期应设置得尽可能短，以减少未使用签名在后期泄露的机会。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。