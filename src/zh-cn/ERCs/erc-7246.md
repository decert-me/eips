---
eip: 7246
title: Encumber - 拆分所有权与担保
description: 一种令牌接口，允许质押令牌而不转移所有权。
author: Coburn Berry (@coburncoburn), Mykel Pereira (@mykelp), Scott Silver (@scott-silver)
discussions-to: https://ethereum-magicians.org/t/encumber-extending-the-erc-20-token-standard-to-allow-pledging-tokens-without-giving-up-ownership/14849
status: Draft
type: Standards Track
category: ERC
created: 2023-06-27
requires: 20
---

## 摘要

本 ERC 提出了对 [ERC-20](./erc-20.md) 令牌标准的扩展，通过添加 Encumber——允许一个账户授予另一个账户独占权利以移动其余额的一部分。Encumber 是 [ERC-20](./erc-20.md) 允许的更强版本。虽然 [ERC-20](./erc-20.md) 的 approve 授予另一个账户转移指定令牌数量的权限，但 encumber 在确保令牌在需要时可用的同时授予相同的权限。

## 动机

此扩展为 [ERC-20](./erc-20.md) 令牌标准增加了灵活性，适用于需要令牌锁定的用例，但更倾向于保持令牌的实际所有权。此接口也可以以简单的方式适应其他令牌标准，如 [ERC-721](./eip-721.md)。

令牌持有者通常将其令牌转移到智能合约，这些合约将在特定条件下返回令牌。在某些情况下，智能合约实际上并不需要持有令牌，但需要保证在必要时令牌可用。由于允许的机制提供的保证不够强，因此目前保证令牌可用的唯一方法是将令牌转移到智能合约。锁定令牌而不移动它们可以更清楚地表明令牌的权利和所有权。这使得空投和所有权的其他附带好处能够到达真正的所有者。它还增加了另一层安全性，提取一池 [ERC-20](./erc-20.md) 令牌可以通过一次转移完成，而迭代账户以转移被 encumber 的令牌在 gas 使用上将显著更具限制性。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

符合规范的令牌 MUST 实现以下接口

```solidity
/**
 * @dev Interface of the ERC-7246 standard.
 */
interface IERC7246{
    /**
     * @dev Emitted when `amount` tokens are encumbered from `owner` to `taker`.
     */
    event Encumber(address indexed owner, address indexed taker, uint amount);

    /**
     * @dev Emitted when the encumbrance of a `taker` to an `owner` is reduced by `amount`.
     */
    event Release(address indexed owner, address indexed taker, uint amount);

    /**
     * @dev Returns the total amount of tokens owned by `owner` that are currently encumbered.
     * MUST never exceed `balanceOf(owner)`
     *
     * Any function which would reduce balanceOf(owner) below encumberedBalanceOf(owner) MUST revert
     */
    function encumberedBalanceOf(address owner) external returns (uint);

    /**
     * @dev Returns the number of tokens that `owner` has encumbered to `taker`.
     *
     * This value increases when {encumber} or {encumberFrom} are called by the `owner` or by another permitted account.
     * This value decreases when {release} and {transferFrom} are called by `taker`.
     */
    function encumbrances(address owner, address taker) external returns (uint);

    /**
     * @dev Increases the amount of tokens that the caller has encumbered to `taker` by `amount`.
     * Grants to `taker` a guaranteed right to transfer `amount` from the caller's balance by using `transferFrom`.
     *
     * MUST revert if caller does not have `amount` tokens available
     * (e.g. if `balanceOf(caller) - encumbrances(caller) < amount`).
     *
     * Emits an {Encumber} event.
     */
    function encumber(address taker, uint amount) external;

    /**
     * @dev Increases the amount of tokens that `owner` has encumbered to `taker` by `amount`.
     * Grants to `taker` a guaranteed right to transfer `amount` from `owner` using transferFrom
     *
     * The function SHOULD revert unless the owner account has deliberately authorized the sender of the message via some mechanism.
     *
     * MUST revert if `owner` does not have `amount` tokens available
     * (e.g. if `balanceOf(owner) - encumbrances(owner) < amount`).
     *
     * Emits an {Encumber} event.
     */
    function encumberFrom(address owner, address taker, uint amount) external;

    /**
     * @dev Reduces amount of tokens encumbered from `owner` to caller by `amount`
     *
     * Emits a {Release} event.
     */
    function release(address owner, uint amount) external;


    /**
     * @dev Convenience function for reading the unencumbered balance of an address.
     * Trivially implemented as `balanceOf(owner) - encumberedBalanceOf(owner)`
     */
    function availableBalanceOf(address owner) public view returns (uint);
}
```

## 理由

该规范旨在补充和镜像 ERC-20 规范，以便于采用和理解。该规范故意对这一结合的要求最小化，唯一真正的要求是所有者不能转移被 encumber 的令牌。然而，示例实现包括一些关于与 ERC-20 函数连接的决策，值得注意。它的设计旨在对标准 ERC-20 定义的最小干扰。
    - 示例依赖于 `approve` 来促进 `encumberFrom`。该提案允许实现者定义另一种机制，例如 `approveEncumber`，如果需要，可以镜像 ERC-20 允许。
    - `transferFrom(src, dst, amount)` 被编写为首先减少 `encumbrances(src, amount)`，然后再从 `allowance(src, msg.sender)` 中支出。或者，`transferFrom` 可以实现为同时从允许中支出和支出 encumbrances。这将要求 `approve` 检查批准的余额是否不会低于被 encumber 余额所需的金额，并且还要使创建批准成为调用 `encumber` 的前提条件。

可以通过使用 `tokenId` 替代 `amount` 参数来扩展 Encumber 接口以覆盖 ERC-721 令牌，因为它们都是 `uint`。该接口选择了最可能的用例（ERC-20）以保持清晰，即使它与其他格式兼容。

## 向后兼容性

此 EIP 与现有的 [ERC-20](./erc-20.md) 标准向后兼容。实现必须添加功能，以阻止将被 encumber 的令牌转移到另一个账户。

## 参考实现

这可以作为任何基础 [ERC-20](./erc-20.md) 合约的扩展来实现，通过修改转移函数以阻止被 encumber 令牌的转移，并在通过 transferFrom 支出时释放 encumbrances。

``` solidity
// An erc-20 token that implements the encumber interface by blocking transfers.

pragma solidity ^0.8.0;
import {ERC20} from "../lib/openzeppelin-contracts/contracts/token/ERC20/ERC20.sol";
import { IERC7246 } from "./IERC7246.sol";

contract EncumberableERC20 is ERC20, IERC7246 {
    // Owner -> Taker -> Amount that can be taken
    mapping (address => mapping (address => uint)) public encumbrances;

    // The encumbered balance of the token owner. encumberedBalance must not exceed balanceOf for a user
    // Note this means rebasing tokens pose a risk of diminishing and violating this prototocol
    mapping (address => uint) public encumberedBalanceOf;

    address public minter;

    constructor(string memory name, string memory symbol) ERC20(name, symbol) {
        minter = msg.sender;
    }

    function mint(address recipient, uint amount) public {
        require(msg.sender == minter, "only minter");
        _mint(recipient, amount);
    }

    function encumber(address taker, uint amount) external {
        _encumber(msg.sender, taker, amount);
    }

    function encumberFrom(address owner, address taker, uint amount) external {
        require(allowance(owner, msg.sender) >= amount);
       _encumber(owner, taker, amount);
    }

    function release(address owner, uint amount) external {
        _release(owner, msg.sender, amount);
    }

    // If bringing balance and encumbrances closer to equal, must check
    function availableBalanceOf(address a) public view returns (uint) {
        return (balanceOf(a) - encumberedBalanceOf[a]);
    }

    function _encumber(address owner, address taker, uint amount) private {
        require(availableBalanceOf(owner) >= amount, "insufficient balance");
        encumbrances[owner][taker] += amount;
        encumberedBalanceOf[owner] += amount;
        emit Encumber(owner, taker, amount);
    }

    function _release(address owner, address taker, uint amount) private {
        if (encumbrances[owner][taker] < amount) {
          amount = encumbrances[owner][taker];
        }
        encumbrances[owner][taker] -= amount;
        encumberedBalanceOf[owner] -= amount;
        emit Release(owner, taker, amount);
    }

    function transfer(address dst, uint amount) public override returns (bool) {
        // check but dont spend encumbrance
        require(availableBalanceOf(msg.sender) >= amount, "insufficient balance");
        _transfer(msg.sender, dst, amount);
        return true;
    }

    function transferFrom(address src, address dst, uint amount) public override returns (bool) {
        uint encumberedToTaker = encumbrances[src][msg.sender];
        bool exceedsEncumbrance = amount > encumberedToTaker;
        if (exceedsEncumbrance)  {
            uint excessAmount = amount - encumberedToTaker;

            // check that enough enencumbered tokens exist to spend from allowance
           require(availableBalanceOf(src) >= excessAmount, "insufficient balance");

           // Exceeds Encumbrance , so spend all of it
            _spendEncumbrance(src, msg.sender, encumberedToTaker);

            _spendAllowance(src, dst, excessAmount);
        } else {
            _spendEncumbrance(src, msg.sender, amount);
        }

        _transfer(src, dst, amount);
        return true;
    }

    function _spendEncumbrance(address owner, address taker, uint256 amount) internal virtual {
        uint256 currentEncumbrance = encumbrances[owner][taker];
        require(currentEncumbrance >= amount, "insufficient encumbrance");
        uint newEncumbrance = currentEncumbrance - amount;
        encumbrances[owner][taker] = newEncumbrance;
        encumberedBalanceOf[owner] -= amount;
    }
}
```

## 安全考虑

依赖 `balanceOf` 来确定可转移令牌数量的各方应改为依赖 `balanceOf(account) - encumberedBalance(account)`，或者如果实现了，则使用 `availableBalanceOf(account)`。

被 encumber 的余额始终由令牌余额支持的属性可以通过简单地修改 `transfer` 和 `transferFrom` 来实现以阻止转移。如果还有其他可以改变用户余额的函数，例如重基令牌或管理员销毁函数，实施者必须添加额外的保护，以确保这些函数防止将 `balanceOf(account)` 降低到 `encumberedBalanceOf(account)` 之下。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。