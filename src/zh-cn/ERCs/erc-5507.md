---
eip: 5507
title: 可退款代币
description: 为 ERC-20、ERC-721 和 ERC-1155 代币添加退款功能
author: elie222 (@elie222), Gavin John (@Pandapip1)
discussions-to: https://ethereum-magicians.org/t/eip-5507-refundable-nfts/10451
status: Final
type: Standards Track
category: ERC
created: 2022-08-19
requires: 20, 165, 721, 1155
---

## 摘要

该 ERC 为初始代币发行添加退款功能，适用于 [ERC-20](./eip-20.md)、[ERC-721](./eip-721.md) 和 [ERC-1155](./eip-1155.md)。资金在预定时间之前被保留在托管中，直到该时间到达后才能被索取。在预定时间到达之前，用户可以获得他们购买的代币的退款。

## 动机

NFT 和代币领域缺乏问责机制。为了整个生态系统的健康，需要更好的机制来防止 rugpull 事件的发生。提供退款为买家提供了更大的保护，并增加了创作者的合法性。

针对这一特定用例的标准接口带来了某些好处：

- 更好地遵守欧盟“远程销售法规”，该法规要求在线购买的商品（如代币）有 14 天的退款期
- 与各种 NFT 相关应用程序的互操作性，例如投资组合浏览器和市场
  - NFT 市场可以在列表中放置徽章，指示该 NFT 仍然可以退款，并提供退款 NFT 的选项，而不是在市场上列出它们
  - DExes 可以提供退款代币的选项，如果这样做会带来更高的收益
- 更好的钱包确认对话框
  - 钱包可以更好地告知用户正在进行的操作（代币被退款），类似于转账通常有其独特的对话框
  - DAO 可以更好地展示包含退款代币功能的智能提案

## 规范

本文档中的关键字“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“不应该”、“推荐”、“可以”和“可选”应按 RFC 2119 中的描述进行解释。

所有实现必须使用并遵循 [ERC-165](./eip-165.md) 的指示。

### ERC-20 退款扩展
  
```solidity
// SPDX-License-Identifier: CC0-1.0

pragma solidity ^0.8.17;

import "ERC20.sol";
import "ERC165.sol";

/// @notice Refundable ERC-20 tokens
/// @dev    The ERC-165 identifier of this interface is `0xf0ca2917`
interface ERC20Refund is ERC20, ERC165 {
    /// @notice           Emitted when a token is refunded
    /// @dev              Emitted by `refund`
    /// @param  _from     The account whose assets are refunded
    /// @param  _amount   The amount of token (in terms of the indivisible unit) that was refunded
    event Refund(
        address indexed _from,
        uint256 indexed _amount
    );

    /// @notice           Emitted when a token is refunded
    /// @dev              Emitted by `refundFrom`
    /// @param  _sender   The account that sent the refund
    /// @param  _from     The account whose assets are refunded
    /// @param  _amount   The amount of token (in terms of the indivisible unit) that was refunded
    event RefundFrom(
        address indexed _sender,
        address indexed _from,
        uint256 indexed _amount
    );

    /// @notice         As long as the refund is active, refunds the user
    /// @dev            Make sure to check that the user has the token, and be aware of potential re-entrancy vectors
    /// @param  amount  The `amount` to refund
    function refund(uint256 amount) external;

    /// @notice         As long as the refund is active and the sender has sufficient approval, refund the tokens and send the ether to the sender
    /// @dev            Make sure to check that the user has the token, and be aware of potential re-entrancy vectors
    ///                 The ether goes to msg.sender.
    /// @param  from    The user from which to refund the assets
    /// @param  amount  The `amount` to refund
    function refundFrom(address from, uint256 amount) external;

    /// @notice         Gets the refund price
    /// @return _wei    The amount of ether (in wei) that would be refunded for a single token unit (10**decimals indivisible units)
    function refundOf() external view returns (uint256 _wei);
 
    /// @notice         Gets the first block for which the refund is not active
    /// @return block   The first block where the token cannot be refunded
    function refundDeadlineOf() external view returns (uint256 block);
}
```

### ERC-721 退款扩展
  
```solidity
// SPDX-License-Identifier: CC0-1.0

pragma solidity ^0.8.17;

import "ERC721.sol";
import "ERC165.sol";

/// @notice Refundable ERC-721 tokens
/// @dev    The ERC-165 identifier of this interface is `0xe97f3c83`
interface ERC721Refund is ERC721 /* , ERC165 */ {
    /// @notice           Emitted when a token is refunded
    /// @dev              Emitted by `refund`
    /// @param  _from     The account whose assets are refunded
    /// @param  _tokenId  The `tokenId` that was refunded
    event Refund(
        address indexed _from,
        uint256 indexed _tokenId
    );

    /// @notice           Emitted when a token is refunded
    /// @dev              Emitted by `refundFrom`
    /// @param  _sender   The account that sent the refund
    /// @param  _from     The account whose assets are refunded
    /// @param  _tokenId  The `tokenId` that was refunded
    event RefundFrom(
        address indexed _sender,
        address indexed _from,
        uint256 indexed _tokenId
    );

    /// @notice         As long as the refund is active for the given `tokenId`, refunds the user
    /// @dev            Make sure to check that the user has the token, and be aware of potential re-entrancy vectors
    /// @param  tokenId The `tokenId` to refund
    function refund(uint256 tokenId) external;

    /// @notice         As long as the refund is active and the sender has sufficient approval, refund the token and send the ether to the sender
    /// @dev            Make sure to check that the user has the token, and be aware of potential re-entrancy vectors
    ///                 The ether goes to msg.sender.
    /// @param  from    The user from which to refund the token
    /// @param  tokenId The `tokenId` to refund
    function refundFrom(address from, uint256 tokenId) external;

    /// @notice         Gets the refund price of the specific `tokenId`
    /// @param  tokenId The `tokenId` to query
    /// @return _wei    The amount of ether (in wei) that would be refunded
    function refundOf(uint256 tokenId) external view returns (uint256 _wei);
 
    /// @notice         Gets the first block for which the refund is not active for a given `tokenId`
    /// @param  tokenId The `tokenId` to query
    /// @return block   The first block where token cannot be refunded
    function refundDeadlineOf(uint256 tokenId) external view returns (uint256 block);
}
```

#### 可选 ERC-721 批量退款扩展

```solidity
// SPDX-License-Identifier: CC0-1.0;

import "ERC721Refund.sol";

/// @notice Batch Refundable ERC-721 tokens
/// @dev    The ERC-165 identifier of this interface is ``
contract ERC721BatchRefund is ERC721Refund {
    /// @notice           Emitted when one or more tokens are batch refunded
    /// @dev              Emitted by `refundBatch`
    /// @param  _from     The account whose assets are refunded
    /// @param  _tokenId  The `tokenIds` that were refunded
    event RefundBatch(
        address indexed _from,
        uint256[] _tokenIds // This may or may not be indexed
    );

    /// @notice           Emitted when one or more tokens are batch refunded
    /// @dev              Emitted by `refundFromBatch`
    /// @param  _sender   The account that sent the refund
    /// @param  _from     The account whose assets are refunded
    /// @param  _tokenId  The `tokenId` that was refunded
    event RefundFromBatch(
        address indexed _sender,
        address indexed _from,
        uint256 indexed _tokenId
    );
    
    /// @notice           As long as the refund is active for the given `tokenIds`, refunds the user
    /// @dev              Make sure to check that the user has the tokens, and be aware of potential re-entrancy vectors
    ///                   These must either succeed or fail together; there are no partial refunds.
    /// @param  tokenIds  The `tokenId`s to refund
    function refundBatch(uint256[] tokenIds) external;

    /// @notice           As long as the refund is active for the given `tokenIds` and the sender has sufficient approval, refund the tokens and send the ether to the sender
    /// @dev              Make sure to check that the user has the tokens, and be aware of potential re-entrancy vectors
    ///                   The ether goes to msg.sender.
    ///                   These must either succeed or fail together; there are no partial refunds.
    /// @param  from      The user from which to refund the token
    /// @param  tokenIds  The `tokenId`s to refund
    function refundFromBatch(address from, uint256[] tokenIds) external;
}
```

### ERC-1155 退款扩展
  
```solidity
// SPDX-License-Identifier: CC0-1.0

pragma solidity ^0.8.17;

import "ERC1155.sol";
import "ERC165.sol";

/// @notice Refundable ERC-1155 tokens
/// @dev    The ERC-165 identifier of this interface is `0x94029f5c`
interface ERC1155Refund is ERC1155 /* , ERC165 */ {
    /// @notice           Emitted when a token is refunded
    /// @dev              Emitted by `refund`
    /// @param  _from     The account that requested a refund
    /// @param  _tokenId  The `tokenId` that was refunded
    /// @param  _amount   The amount of `tokenId` that was refunded
    event Refund(
        address indexed _from,
        uint256 indexed _tokenId,
        uint256 _amount
    );

    /// @notice           Emitted when a token is refunded
    /// @dev              Emitted by `refundFrom`
    /// @param  _sender   The account that sent the refund
    /// @param  _from     The account whose assets are refunded
    /// @param  _tokenId  The `tokenId` that was refunded
    /// @param  _amount   The amount of `tokenId` that was refunded
    event RefundFrom(
        address indexed _sender,
        address indexed _from,
        uint256 indexed _tokenId
    );

    /// @notice         As long as the refund is active for the given `tokenId`, refunds the user
    /// @dev            Make sure to check that the user has enough tokens, and be aware of potential re-entrancy vectors
    /// @param  tokenId The `tokenId` to refund
    /// @param  amount  The amount of `tokenId` to refund
    function refund(uint256 tokenId, uint256 amount) external;

    /// @notice         As long as the refund is active and the sender has sufficient approval, refund the tokens and send the ether to the sender
    /// @dev            Make sure to check that the user has enough tokens, and be aware of potential re-entrancy vectors
    ///                 The ether goes to msg.sender.
    /// @param  from    The user from which to refund the token
    /// @param  tokenId The `tokenId` to refund
    /// @param  amount  The amount of `tokenId` to refund
    function refundFrom(address from, uint256 tokenId, uint256 amount) external;

    /// @notice         Gets the refund price of the specific `tokenId`
    /// @param  tokenId The `tokenId` to query
    /// @return _wei    The amount of ether (in wei) that would be refunded for a single token
    function refundOf(uint256 tokenId) external view returns (uint256 _wei);

    /// @notice         Gets the first block for which the refund is not active for a given `tokenId`
    /// @param  tokenId The `tokenId` to query
    /// @return block   The first block where the token cannot be refunded
    function refundDeadlineOf(uint256 tokenId) external view returns (uint256 block);
}
```

#### 可选 ERC-1155 批量退款扩展

```solidity
// SPDX-License-Identifier: CC0-1.0;

import "ERC1155Refund.sol";

/// @notice Batch Refundable ERC-1155 tokens
/// @dev    The ERC-165 identifier of this interface is ``
contract ERC1155BatchRefund is ERC1155Refund {
    /// @notice           Emitted when one or more tokens are batch refunded
    /// @dev              Emitted by `refundBatch`
    /// @param  _from     The account that requested a refund
    /// @param  _tokenIds The `tokenIds` that were refunded
    /// @param  _amounts  The amount of each `tokenId` that was refunded
    event RefundBatch(
        address indexed _from,
        uint256[] _tokenIds, // This may or may not be indexed
        uint256[] _amounts
    );

    /// @notice           Emitted when one or more tokens are batch refunded
    /// @dev              Emitted by `refundFromBatch`
    /// @param  _sender   The account that sent the refund
    /// @param  _from     The account whose assets are refunded
    /// @param  _tokenIds The `tokenIds` that was refunded
    /// @param  _amounts  The amount of each `tokenId` that was refunded
    event RefundFromBatch(
        address indexed _sender,
        address indexed _from,
        uint256[] _tokenId, // This may or may not be indexed
        uint256[] _amounts
    );
    
    /// @notice           As long as the refund is active for the given `tokenIds`, refunds the user
    /// @dev              Make sure to check that the user has enough tokens, and be aware of potential re-entrancy vectors
    ///                   These must either succeed or fail together; there are no partial refunds.
    /// @param  tokenIds  The `tokenId`s to refund
    /// @param  amounts   The amount of each `tokenId` to refund
    function refundBatch(uint256[] tokenIds, uint256[] amounts) external;

    /// @notice           As long as the refund is active for the given `tokenIds` and the sender has sufficient approval, refund the tokens and send the ether to the sender
    /// @dev              Make sure to check that the user has the tokens, and be aware of potential re-entrancy vectors
    ///                   The ether goes to msg.sender.
    ///                   These must either succeed or fail together; there are no partial refunds.
    /// @param  from      The user from which to refund the token
    /// @param  tokenIds  The `tokenId`s to refund
    /// @param  amounts   The amount of each `tokenId` to refund
    function refundFromBatch(address from, uint256[] tokenIds, uint256[] amounts external;
}
```

## 理由

`refundDeadlineOf` 使用区块而不是时间戳，因为时间戳不如区块号可靠。

`refund`、`refundOf` 和 `refundDeadlineOf` 的函数名称选择是为了符合 ERC-20、ERC-721 和 ERC-1155 的命名风格。

[ERC-165](./eip-165.md) 是必需的，因为如果没有它，DApp 的自省将变得更加困难。

不支持自定义 ERC-20 代币，因为这无谓地增加了复杂性，而 `refundFrom` 函数在与 DEx 结合时允许此功能。

批量退款是可选的，因为账户抽象将使这些原子操作变得更容易。然而，如果正确实现，它们仍然可能降低 gas 成本。

## 向后兼容性

未发现向后兼容性问题。

## 安全考虑

`refund` 函数存在潜在的重入风险。确保在代币被销毁后**再**执行以太币转账（即遵循检查、效果、交互模式）。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。