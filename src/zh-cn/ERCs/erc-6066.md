---
eip: 6066
title: NFT 签名验证方法
description: 一种在签名实体为 ERC-721 或 ERC-1155 NFT 时验证签名的方法
author: Jack Boyuan Xu (@boyuanx)
discussions-to: https://ethereum-magicians.org/t/eip-6066-signature-validation-method-for-nfts/11943
status: Final
type: Standards Track
category: ERC
created: 2022-11-29
requires: 165, 721, 1155, 1271, 5750
---

## 摘要

虽然 **E**xternally **O**wned **A**ccounts 可以使用 `ecrecover()` 验证签名消息，智能合约可以使用 [ERC-1271](./eip-1271.md) 中概述的规范验证签名，但目前没有标准方法来创建或验证 NFT 的签名。我们提出了一种标准方法，任何人都可以验证 NFT 的签名是否有效。这可以通过在 [ERC-1271](./eip-1271.md) 中最初找到的修改后的签名验证函数实现：`isValidSignature(tokenId, hash, data)`。

## 动机

随着数十亿 ETH 的交易量，**N**on-**F**ungible **T**oken 标准在近年来迅速流行。尽管在链上拥有独特的代币化物品具有深远的影响，NFT 主要用于以头像或个人资料图片的形式代表艺术作品。虽然这对于 [ERC-721](./eip-721.md) 和 [ERC-1155](./eip-1155.md) 代币标准来说绝对不是一个微不足道的用例，但我们认为可以做更多的事情来帮助社区发现 NFT 的替代用途。

NFT 的一个替代用例是用它们来代表组织中的职位。在这种情况下，将签名与可转让的 NFT 绑定，而不是与 EOA 或智能合约绑定变得至关重要。假设存在一个 DAO，利用 NFT 作为代表某些行政职位（即 CEO、COO、CFO 等）的徽章，并进行季度民主选举，可能会替换当前担任这些职位的人。如果现任 COO 之前签署了协议或授权了某些行动，他们的过去签名将保留在曾经担任 COO 的 EOA 身上，而不是 COO 的职位本身，一旦他们被另一个 EOA 替换为新任 COO。当整个 DAO 使用多签钱包时，这是一种缓解此问题的方法，但通常在更复杂的层面上生成签名是有帮助的，以便建立和维护详细的责任分离。将智能合约任命为 COO 而不是 EOA 也是可行的，但这种解决方案带来的复杂性是不必要的。如果 DAO 使用 ENS 来建立其组织层级，这项提案将允许包装的 ENS 子域（即 NFT）生成签名。

## 规范

本文档中的关键字“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“不应该”、“推荐”、“不推荐”、“可以”和“可选”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

```
pragma solidity ^0.8.0;

interface IERC6066 {
    /**
     * @dev MUST return if the signature provided is valid for the provided tokenId and hash
     * @param tokenId   Token ID of the signing NFT
     * @param hash      Hash of the data to be signed
     * @param data      OPTIONAL arbitrary data that may aid verification
     *
     * MUST return the bytes4 magic value 0x12edb34f when function passes.
     * MUST NOT modify state (using STATICCALL for solc < 0.5, view modifier for solc > 0.5)
     * MUST allow external calls
     *
     */
    function isValidSignature(
        uint256 tokenId,
        bytes32 hash,
        bytes calldata data
    ) external view returns (bytes4 magicValue);
}
```

`isValidSignature` 可以调用任意方法来验证给定的签名。

此函数可以由希望使其代币持有者使用其 NFT 签署消息的 [ERC-721](./eip-721.md) 或 [ERC-1155](./eip-1155.md) 合规合约实现。如果签名者是 NFT 的持有者（[ERC-721](./eip-721.md) 或 [ERC-1155](./eip-1155.md)），合规调用者必须调用此方法以支持合约签名。

## 理由

我们故意决定不在此提案中包含签名生成标准，因为这会限制此机制的灵活性，就像 [ERC-1271](./eip-1271.md) 不强制要求智能合约的签名标准一样。我们还决定参考 Gnosis Safe 的合约签名方法，因为它既简单又被证明是足够的。`bytes calldata data` 参数被视为可选，如果需要额外数据进行签名验证，也使此 EIP 符合 [ERC-5750](./eip-5750.md) 的未来兼容性。

## 向后兼容性

此 EIP 与以前的签名验证工作不兼容，因为它不验证任何加密生成的签名。相反，签名仅仅是一个布尔标志，表示同意。这与 Gnosis Safe 的合约签名实现是一致的。

## 参考实现

符合 [ERC-6066](./eip-6066.md) 的 [ERC-721](./eip-721.md) 合规合约的示例实现，具有自定义签名功能：

```
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./interfaces/IERC6066.sol";

contract ERC6066Reference is ERC721, IERC6066 {
    // type(IERC6066).interfaceId
    bytes4 public constant MAGICVALUE = 0x12edb34f;
    bytes4 public constant BADVALUE = 0xffffffff;

    mapping(uint256 => mapping(bytes32 => bool)) internal _signatures;

    error ENotTokenOwner();

    /**
     * @dev Checks if the sender owns NFT with ID tokenId
     * @param tokenId   Token ID of the signing NFT
     */
    modifier onlyTokenOwner(uint256 tokenId) {
        if (ownerOf(tokenId) != _msgSender()) revert ENotTokenOwner();
        _;
    }

    constructor(string memory name_, string memory symbol_)
        ERC721(name_, symbol_)
    {}

    /**
     * @dev SHOULD sign the provided hash with NFT of tokenId given sender owns said NFT
     * @param tokenId   Token ID of the signing NFT
     * @param hash      Hash of the data to be signed
     */
    function sign(uint256 tokenId, bytes32 hash)
        external
        onlyTokenOwner(tokenId)
    {
        _signatures[tokenId][hash] = true;
    }

    /**
     * @dev MUST return if the signature provided is valid for the provided tokenId, hash, and optionally data
     */
    function isValidSignature(uint256 tokenId, bytes32 hash, bytes calldata data)
        external
        view
        override
        returns (bytes4 magicValue)
    {
        // The data parameter is unused in this example
        return _signatures[tokenId][hash] ? MAGICVALUE : BADVALUE;
    }

    /**
     * @dev ERC-165 support
     */
    function supportsInterface(
        bytes4 interfaceId
    ) public view virtual override returns (bool) {
        return
            interfaceId == type(IERC6066).interfaceId ||
            super.supportsInterface(interfaceId);
    }
}
```

## 安全考虑

基于合约的签名的可撤销性质也适用于此 EIP。开发者和用户都应考虑这一点。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。