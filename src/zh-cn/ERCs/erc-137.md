---
eip: 137
title: 以太坊域名服务 - 规范
author: Nick Johnson <arachnid@notdot.net>
status: Final
type: Standards Track
category: ERC
created: 2016-04-04
---

# 摘要

本草案 EIP 描述了以太坊名称服务的细节，这是一种提议的协议和 ABI 定义，提供灵活的短的、可读的人类名称到服务和资源标识符的解析。这允许用户和开发者引用可读且易于记忆的名称，并允许在基础资源（合约、内容地址数据等）发生变化时更新这些名称。

域名的目标是提供稳定的、可读的人类标识符，可用于指定网络资源。通过这种方式，用户可以输入一个易于记忆的字符串，例如 'vitalik.wallet' 或 'www.mysite.swarm'，并被引导到相应的资源。名称和资源之间的映射可能会随着时间的推移而变化，因此用户可能会更改钱包，网站可能会更改主机，或者一个 swarm 文档可能会更新到新版本，而域名不会改变。此外，一个域名不必指定单一资源；不同的记录类型允许同一域名引用不同的资源。例如，浏览器可以通过获取其 A（地址）记录将 'mysite.swarm' 解析为其服务器的 IP 地址，而邮件客户端可以通过获取其 MX（邮件交换）记录将相同的地址解析为邮件服务器。

# 动机

现有的 [规范](https://github.com/ethereum/wiki/wiki/Registrar-ABI) 和 [实现](https://ethereum.gitbooks.io/frontier-guide/content/registrar_services.html) 提供了以太坊中的名称解析的基本功能，但存在几个缺点，这将显著限制其长期的实用性：
- 所有名称的单一全局命名空间，只有一个“集中式”解析器。
- 对委托和子名称/子域的支持有限或没有。
- 只有一种记录类型，且不支持将多个记录副本与域名关联。
- 由于单一的全局实现，不支持多种不同的名称分配系统。
- 职责混淆：名称解析、注册和 whois 信息。

这些功能所允许的用例包括：
- 支持子名称/子域 - 例如，live.mysite.tld 和 forum.mysite.tld。
- 在单一名称下的多个服务，例如托管在 Swarm 中的 DApp、Whisper 地址和邮件服务器。
- 支持 DNS 记录类型，允许区块链托管“传统”名称。这将允许像 Mist 这样的以太坊客户端从区块链名称解析传统网站的地址或电子邮件地址的邮件服务器。
- DNS 网关，通过域名服务公开 ENS 域，提供更简单的方式让传统客户端解析和连接到区块链服务。

特别是前两个用例，可以在当今互联网的 DNS 下随处可见，我们认为它们是名称服务的基本特征，随着以太坊平台的发展和成熟，将继续有用。

本文件的规范部分并未指定提议系统的实现；其目的是记录一个不同解析器实现可以遵循的协议，以促进一致的名称解析。附录提供了解析器合约和库的示例实现，这些应仅视为说明性示例。

同样，本文件并未试图指定域名应如何注册或更新，或系统如何找到负责特定域名的所有者。注册是注册机构的责任，是一个治理问题，必然会在顶级域之间有所不同。

域名记录的更新也可以与解析分开处理。一些系统，例如 swarm，可能需要一个明确定义的接口来更新域名，在这种情况下，我们预期会开发一个标准。

# 规范
## 概述

ENS 系统由三个主要部分组成：
- ENS 注册表
- 解析器
- 注册机构

注册表是一个单一合约，提供从任何注册名称到负责该名称的解析器的映射，并允许名称的所有者设置解析器地址，并创建子域，可能与父域有不同的所有者。

解析器负责执行名称的资源查找 - 例如，返回合约地址、内容哈希或适当的 IP 地址。解析器规范在此定义，并在其他 EIP 中扩展，定义了解析器可以实现的支持解析不同类型记录的方法。

注册机构负责将域名分配给系统的用户，并且是唯一能够更新 ENS 的实体；ENS 注册表中节点的所有者是其注册机构。注册机构可以是合约或外部拥有的账户，但预计根和顶级注册机构至少将作为合约实现。

在 ENS 中解析名称是一个两步过程。首先，使用下面描述的程序对要解析的名称进行哈希，然后调用 ENS 注册表。如果记录存在，注册表将返回其解析器的地址。然后，调用解析器，使用适当的方法请求所需的资源。解析器随后返回所需的结果。

例如，假设您希望找到与 'beercoin.eth' 关联的代币合约的地址。首先，获取解析器：

```javascript
var node = namehash("beercoin.eth");
var resolver = ens.resolver(node);
```

然后，向解析器请求合约的地址：

```javascript
var address = resolver.addr(node);
```

因为 `namehash` 程序仅依赖于名称本身，所以可以预先计算并插入合约中，消除字符串操作的需要，并允许 O(1) 查找 ENS 记录，而不管原始名称中组件的数量。

## 名称语法

ENS 名称必须符合以下语法：

<pre>&lt;domain> ::= &lt;label> | &lt;domain> "." &lt;label>
&lt;label> ::= 任何有效字符串标签，符合 [UTS46](https://unicode.org/reports/tr46/)
</pre>

简而言之，名称由一系列用点分隔的标签组成。每个标签必须是符合 [UTS46](https://unicode.org/reports/tr46/) 的有效规范化标签，选项为 `transitional=false` 和 `useSTD3AsciiRules=true`。对于 Javascript 实现，提供了一个 [库](https://www.npmjs.com/package/idna-uts46)，用于规范化和检查名称。

请注意，虽然名称中允许使用大写和小写字母，但 UTS46 规范化过程在对标签进行哈希之前会将其折叠，因此两个拼写相同但大小写不同的名称将产生相同的 namehash。

标签和域名可以是任意长度，但为了与传统 DNS 兼容，建议将标签限制为每个不超过 64 个字符，完整的 ENS 名称不超过 255 个字符。出于同样的原因，建议标签不以连字符开头或结尾，或以数字开头。

## namehash 算法

在 ENS 中使用之前，名称会使用 'namehash' 算法进行哈希。该算法递归地哈希名称的组成部分，为任何有效输入域生成唯一的固定长度字符串。namehash 的输出称为“节点”。
名称哈希算法的伪代码如下：

```
def namehash(name):
  if name == '':
    return '\0' * 32
  else:
    label, _, remainder = name.partition('.')
    return sha3(namehash(remainder) + sha3(label))
```

非正式地，名称被拆分为标签，每个标签都被哈希。然后，从最后一个组件开始，将先前的输出与标签哈希连接并再次哈希。第一个组件与 32 个 '0' 字节连接。因此，'mysite.swarm' 的处理如下：

```
node = '\0' * 32
node = sha3(node + sha3('swarm'))
node = sha3(node + sha3('mysite'))
```

实现应符合以下名称哈希测试向量：

    namehash('') = 0x0000000000000000000000000000000000000000000000000000000000000000
    namehash('eth') = 0x93cdeb708b7545dc668eb9280176169d1c33cfd8ed6f04690a0bcc88a93fc4ae
    namehash('foo.eth') = 0xde9b09fd7c5f901e23a3f19fecc54828e9c848539801e86591bd9801b019f84f

## 注册表规范

ENS 注册表合约公开以下函数：

```solidity
function owner(bytes32 node) constant returns (address);
```

返回指定节点的所有者（注册人）。

```solidity
function resolver(bytes32 node) constant returns (address);
```

返回指定节点的解析器。

```solidity
function ttl(bytes32 node) constant returns (uint64);
```

返回节点的生存时间（TTL）；即，节点信息可以被缓存的最大持续时间。

```solidity
function setOwner(bytes32 node, address owner);
```

将节点的所有权转移给另一个注册人。此函数只能由 `node` 的当前所有者调用。成功调用此函数会记录事件 `Transfer(bytes32 indexed, address)`。

```solidity
function setSubnodeOwner(bytes32 node, bytes32 label, address owner);
```

创建一个新节点，`sha3(node, label)` 并将其所有者设置为 `owner`，或者如果节点已存在，则更新节点的新所有者。此函数只能由 `node` 的当前所有者调用。成功调用此函数会记录事件 `NewOwner(bytes32 indexed, bytes32 indexed, address)`。

```solidity
function setResolver(bytes32 node, address resolver);
```

设置 `node` 的解析器地址。此函数只能由 `node` 的所有者调用。成功调用此函数会记录事件 `NewResolver(bytes32 indexed, address)`。

```solidity
function setTTL(bytes32 node, uint64 ttl);
```

设置节点的 TTL。节点的 TTL 适用于注册表中的 'owner' 和 'resolver' 记录，以及与关联解析器返回的任何信息。

## 解析器规范

解析器可以实现此处指定的记录类型的任何子集。记录类型规范要求解析器提供多个函数时，解析器必须实现所有或无。解析器必须指定一个抛出异常的回退函数。

解析器有一个强制性函数：

```solidity
function supportsInterface(bytes4 interfaceID) constant returns (bool)
```

`supportsInterface` 函数在 [EIP-165](./eip-165.md) 中有文档说明，如果解析器实现了由提供的 4 字节标识符指定的接口，则返回 true。接口标识符由该接口提供的函数签名哈希的异或组成；在单函数接口的退化情况下，它等于该函数的签名哈希。如果解析器对 `supportsInterface()` 返回 `true`，则必须实现该接口中指定的函数。

`supportsInterface` 必须始终对 `0x01ffc9a7` 返回 true，这是 `supportsInterface` 本身的接口 ID。

当前标准化的解析器接口在下表中指定。

以下接口被定义：

| 接口名称 | 接口哈希 | 规范 |
| --- | --- | --- |
| `addr` | 0x3b3b57de | [合约地址](#addr) |
| `name`      | 0x691f3431   | #181    |
| `ABI`       | 0x2203ab56   | #205    |
| `pubkey`    | 0xc8690233   | #619    |

EIP 可以定义新的接口以添加到此注册表。

### <a name="addr"></a>合约地址接口

希望支持合约地址资源的解析器必须提供以下函数：

```solidity
function addr(bytes32 node) constant returns (address);
```

如果解析器支持 `addr` 查找，但请求的节点没有 addr 记录，则解析器必须返回零地址。

解析 `addr` 记录的客户端必须检查零返回值，并将其视为没有指定解析器的名称 - 即，拒绝向该地址发送资金或与之交互。未能做到这一点可能导致用户意外地将资金发送到 0 地址。

对地址的更改必须触发以下事件：

```solidity
event AddrChanged(bytes32 indexed node, address a);
```
# 附录 A: 注册表实现

```solidity
contract ENS {
    struct Record {
        address owner;
        address resolver;
        uint64 ttl;
    }

    mapping(bytes32=>Record) records;

    event NewOwner(bytes32 indexed node, bytes32 indexed label, address owner);
    event Transfer(bytes32 indexed node, address owner);
    event NewResolver(bytes32 indexed node, address resolver);

    modifier only_owner(bytes32 node) {
        if(records[node].owner != msg.sender) throw;
        _
    }

    function ENS(address owner) {
        records[0].owner = owner;
    }

    function owner(bytes32 node) constant returns (address) {
        return records[node].owner;
    }

    function resolver(bytes32 node) constant returns (address) {
        return records[node].resolver;
    }

    function ttl(bytes32 node) constant returns (uint64) {
        return records[node].ttl;
    }

    function setOwner(bytes32 node, address owner) only_owner(node) {
        Transfer(node, owner);
        records[node].owner = owner;
    }

    function setSubnodeOwner(bytes32 node, bytes32 label, address owner) only_owner(node) {
        var subnode = sha3(node, label);
        NewOwner(node, label, owner);
        records[subnode].owner = owner;
    }

    function setResolver(bytes32 node, address resolver) only_owner(node) {
        NewResolver(node, resolver);
        records[node].resolver = resolver;
    }

    function setTTL(bytes32 node, uint64 ttl) only_owner(node) {
        NewTTL(node, ttl);
        records[node].ttl = ttl;
    }
}
```
# 附录 B: 示例解析器实现
### 内置解析器

最简单的解析器是一个通过实现合约地址资源配置文件而充当其自身名称解析器的合约：

```solidity
contract DoSomethingUseful {
    // Other code

    function addr(bytes32 node) constant returns (address) {
        return this;
    }

    function supportsInterface(bytes4 interfaceID) constant returns (bool) {
        return interfaceID == 0x3b3b57de || interfaceID == 0x01ffc9a7;
    }

    function() {
        throw;
    }
}
```

这样的合约可以直接插入到 ENS 注册表中，从而在简单用例中消除对单独解析器合约的需求。然而，要求在未知函数调用时“抛出”可能会干扰某些类型合约的正常操作。

### 独立解析器

一个基本解析器，实现合约地址配置文件，并仅允许其所有者更新记录：

```solidity
contract Resolver {
    event AddrChanged(bytes32 indexed node, address a);

    address owner;
    mapping(bytes32=>address) addresses;

    modifier only_owner() {
        if(msg.sender != owner) throw;
        _
    }

    function Resolver() {
        owner = msg.sender;
    }

    function addr(bytes32 node) constant returns(address) {
        return addresses[node];    
    }

    function setAddr(bytes32 node, address addr) only_owner {
        addresses[node] = addr;
        AddrChanged(node, addr);
    }

    function supportsInterface(bytes4 interfaceID) constant returns (bool) {
        return interfaceID == 0x3b3b57de || interfaceID == 0x01ffc9a7;
    }

    function() {
        throw;
    }
}
```

部署此合约后，通过更新 ENS 注册表以引用此合约的名称，然后调用 `setAddr()` 使用相同的节点设置它将解析到的合约地址。

### 公共解析器

与上述解析器类似，此合约仅支持合约地址配置文件，但使用 ENS 注册表来确定谁应该被允许更新条目：

```solidity
contract PublicResolver {
    event AddrChanged(bytes32 indexed node, address a);
    event ContentChanged(bytes32 indexed node, bytes32 hash);

    ENS ens;
    mapping(bytes32=>address) addresses;

    modifier only_owner(bytes32 node) {
        if(ens.owner(node) != msg.sender) throw;
        _
    }

    function PublicResolver(address ensAddr) {
        ens = ENS(ensAddr);
    }

    function addr(bytes32 node) constant returns (address ret) {
        ret = addresses[node];
    }

    function setAddr(bytes32 node, address addr) only_owner(node) {
        addresses[node] = addr;
        AddrChanged(node, addr);
    }

    function supportsInterface(bytes4 interfaceID) constant returns (bool) {
        return interfaceID == 0x3b3b57de || interfaceID == 0x01ffc9a7;
    }

    function() {
        throw;
    }
}
```
# 附录 C: 示例注册人实现

此注册人允许用户在没有费用的情况下注册名称，如果他们是第一个请求的人。

```solidity
contract FIFSRegistrar {
    ENS ens;
    bytes32 rootNode;

    function FIFSRegistrar(address ensAddr, bytes32 node) {
        ens = ENS(ensAddr);
        rootNode = node;
    }

    function register(bytes32 subnode, address owner) {
        var node = sha3(rootNode, subnode);
        var currentOwner = ens.owner(node);
        if(currentOwner != 0 && currentOwner != msg.sender)
            throw;

        ens.setSubnodeOwner(rootNode, subnode, owner);
    }
}
```