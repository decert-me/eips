---
eip: 6551
title: 非同质化代币绑定账户
description: 一个由非同质化代币拥有的智能合约账户的接口和注册表
author: Jayden Windle (@jaydenwindle), Benny Giang <bg@futureprimitive.xyz>, Steve Jang, Druzy Downs (@druzydowns), Raymond Huynh (@huynhr), Alanah Lam <alanah@futureprimitive.xyz>, Wilkins Chung (@wwhchung) <wilkins@manifold.xyz>, Paul Sullivan (@sullivph) <paul.sullivan@manifold.xyz>, Auryn Macmillan (@auryn-macmillan), Jan-Felix Schwarz (@jfschwarz), Anton Bukov (@k06a), Mikhail Melnik (@ZumZoom), Josh Weintraub (@jhweintraub) <jhweintraub@gmail.com>, Rob Montgomery (@RobAnon) <rob@revest.finance>, vectorized (@vectorized), Víctor Martínez (@vnmrtz), Adrián Pajares (@0xadrii)
discussions-to: https://ethereum-magicians.org/t/non-fungible-token-bound-accounts/13030
status: 审核中
type: 标准跟踪
category: ERC
created: 2023-02-23
requires: 165, 721, 1167, 1271
---

## 摘要

本提案定义了一个系统，将以太坊账户分配给所有非同质化代币。这些代币绑定账户允许 NFT 拥有资产并与应用程序交互，而无需对现有智能合约或基础设施进行更改。

## 动机

[ERC-721](./eip-721.md)标准促成了非同质化代币应用的爆炸性增长。一些显著的用例包括可繁殖的猫、生成艺术作品和交易流动性头寸。

然而，NFT 无法作为代理或与其他链上资产关联。这一限制使得许多现实世界的非同质化资产难以表示为 NFT。例如：

- 在角色扮演游戏中，基于所采取的行动积累资产和能力的角色
- 由许多同质化和非同质化组件组成的汽车
- 由多个同质化资产组成的投资组合
- 授予进入某个场所的通行证会员卡，并记录过去互动的历史

本提案旨在赋予每个 NFT 与以太坊用户相同的权利。这包括自我保管资产、执行任意操作、控制多个独立账户以及跨多个链使用账户的能力。通过这样做，本提案允许复杂的现实世界资产以一种与以太坊现有所有权模型相似的通用模式表示为 NFT。

这通过定义一个单例注册表来实现，该注册表为所有现有和未来的 NFT 分配唯一的、确定性的智能合约账户地址。每个账户永久绑定到单个 NFT，账户的控制权授予该 NFT 的持有者。

本提案中定义的模式不需要对现有 NFT 智能合约进行任何更改。它还与几乎所有支持以太坊账户的现有基础设施兼容，从链上协议到链下索引器。代币绑定账户与每个现有的链上资产标准兼容，并可以扩展以支持未来创建的新资产标准。

通过赋予每个 NFT 以太坊账户的全部功能，本提案为现有和未来的 NFT 启用了许多新颖的用例。

## 规范

文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“NOT RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 概述

本提案中概述的系统有两个主要组件：

- 代币绑定账户的单例注册表
- 代币绑定账户实现的通用接口

以下图示说明了 NFT、NFT 持有者、代币绑定账户和注册表之间的关系：
![](../assets/eip-6551/diagram.png)

### 注册表

注册表是一个单例合约，作为所有代币绑定账户地址查询的入口点。它有两个功能：

- `createAccount` - 为给定的`implementation`地址创建 NFT 的代币绑定账户
- `account` - 计算给定`implementation`地址的 NFT 的代币绑定账户地址

注册表是无权限的、不可变的，并且没有所有者。注册表的完整源代码可以在[注册表实现](#registry-implementation)部分找到。注册表必须部署在地址`0x000000006551c19487814612e58FE06813775758`，使用 Nick 的工厂(`0x4e59b44847b379578588920cA78FbF26c0B4956C`)和盐`0x0000000000000000000000000000000000000000fd8eb4e1dca713016c518e31`。

注册表可以使用以下交易部署到任何 EVM 兼容链：

```
{
        "to": "0x4e59b44847b379578588920ca78fbf26c0b4956c",
        "value": "0x0",
        "data": "0x0000000000000000000000000000000000000000fd8eb4e1dca713016c518e31608060405234801561001057600080fd5b5061023b806100206000396000f3fe608060405234801561001057600080fd5b50600436106100365760003560e01c8063246a00211461003b5780638a54c52f1461006a575b600080fd5b61004e6100493660046101b7565b61007d565b6040516001600160a01b03909116815260200160405180910390f35b61004e6100783660046101b7565b6100e1565b600060806024608c376e5af43d82803e903d91602b57fd5bf3606c5285605d52733d60ad80600a3d3981f3363d3d373d3d3d363d7360495260ff60005360b76055206035523060601b60015284601552605560002060601b60601c60005260206000f35b600060806024608c376e5af43d82803e903d91602b57fd5bf3606c5285605d52733d60ad80600a3d3981f3363d3d373d3d3d363d7360495260ff60005360b76055206035523060601b600152846015526055600020803b61018b578560b760556000f580610157576320188a596000526004601cfd5b80606c52508284887f79f19b3655ee38b1ce526556b7731a20c8f218fbda4a3990b6cc4172fdf887226060606ca46020606cf35b8060601b60601c60005260206000f35b80356001600160a01b03811681146101b257600080fd5b919050565b600080600080600060a086880312156101cf57600080fd5b6101d88661019b565b945060208601359350604086013592506101f46060870161019b565b94979396509194608001359291505056fea2646970667358221220ea2fe53af507453c64dd7c1db05549fa47a298dfb825d6d11e1689856135f16764736f6c63430008110033",
}
```

注册表必须将每个代币绑定账户部署为 [ERC-1167](./eip-1167.md) 最小代理，并在字节码中附加不可变常量数据。

每个代币绑定账户的已部署字节码必须具有以下结构：

```
ERC-1167 Header               (10 bytes)
<implementation (address)>    (20 bytes)
ERC-1167 Footer               (15 bytes)
<salt (bytes32)>              (32 bytes)
<chainId (uint256)>           (32 bytes)
<tokenContract (address)>     (32 bytes)
<tokenId (uint256)>           (32 bytes)
```

例如，具有实现地址`0xbebebebebebebebebebebebebebebebebebebebe`、盐`0`、链 ID`1`、代币合约`0xcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcf`和代币 ID`123`的代币绑定账户将具有以下已部署字节码：

```
363d3d373d3d3d363d73bebebebebebebebebebebebebebebebebebebebe5af43d82803e903d91602b57fd5bf300000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000001000000000000000000000000cfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcf000000000000000000000000000000000000000000000000000000000000007b
```

每个代币绑定账户代理必须将执行委托给实现`IERC6551Account`接口的合约。

注册表必须使用`create2`操作码部署所有代币绑定账户，以便每个账户地址是确定的。每个代币绑定账户地址应从其实现地址、代币合约地址、代币 ID、链 ID 和盐的唯一组合中派生。

注册表必须实现以下接口：

```solidity
interface IERC6551Registry {
    /**
     * @dev The registry MUST emit the ERC6551AccountCreated event upon successful account creation.
     */
    event ERC6551AccountCreated(
        address account,
        address indexed implementation,
        bytes32 salt,
        uint256 chainId,
        address indexed tokenContract,
        uint256 indexed tokenId
    );

    /**
     * @dev The registry MUST revert with AccountCreationFailed error if the create2 operation fails.
     */
    error AccountCreationFailed();

    /**
     * @dev Creates a token bound account for a non-fungible token.
     *
     * If account has already been created, returns the account address without calling create2.
     *
     * Emits ERC6551AccountCreated event.
     *
     * @return account The address of the token bound account
     */
    function createAccount(
        address implementation,
        bytes32 salt,
        uint256 chainId,
        address tokenContract,
        uint256 tokenId
    ) external returns (address account);

    /**
     * @dev Returns the computed token bound account address for a non-fungible token.
     *
     * @return account The address of the token bound account
     */
    function account(
        address implementation,
        bytes32 salt,
        uint256 chainId,
        address tokenContract,
        uint256 tokenId
    ) external view returns (address account);
}
```

### 账户接口

所有代币绑定账户应通过单例注册表创建。

所有代币绑定账户实现必须实现 [ERC-165](./eip-165.md) 接口检测。

所有代币绑定账户实现必须实现 [ERC-1271](./eip-1271.md) 签名验证。

所有代币绑定账户实现必须实现以下接口：

```solidity
/// @dev the ERC-165 identifier for this interface is `0x6faff5f1`
interface IERC6551Account {
    /**
     * @dev Allows the account to receive Ether.
     *
     * Accounts MUST implement a `receive` function.
     *
     * Accounts MAY perform arbitrary logic to restrict conditions
     * under which Ether can be received.
     */
    receive() external payable;

    /**
     * @dev Returns the identifier of the non-fungible token which owns the account.
     *
     * The return value of this function MUST be constant - it MUST NOT change over time.
     *
     * @return chainId       The chain ID of the chain the token exists on
     * @return tokenContract The contract address of the token
     * @return tokenId       The ID of the token
     */
    function token()
        external
        view
        returns (uint256 chainId, address tokenContract, uint256 tokenId);

    /**
     * @dev Returns a value that SHOULD be modified each time the account changes state.
     *
     * @return The current account state
     */
    function state() external view returns (uint256);

    /**
     * @dev Returns a magic value indicating whether a given signer is authorized to act on behalf
     * of the account.
     *
     * MUST return the bytes4 magic value 0x523e3260 if the given signer is valid.
     *
     * By default, the holder of the non-fungible token the account is bound to MUST be considered
     * a valid signer.
     *
     * Accounts MAY implement additional authorization logic which invalidates the holder as a
     * signer or grants signing permissions to other non-holder accounts.
     *
     * @param  signer     The address to check signing authorization for
     * @param  context    Additional data used to determine whether the signer is valid
     * @return magicValue Magic value indicating whether the signer is valid
     */
    function isValidSigner(address signer, bytes calldata context)
        external
        view
        returns (bytes4 magicValue);
}

```

### 执行接口

所有代币绑定账户必须实现一个执行接口，允许有效的签名者代表账户执行任意操作。对执行接口的支持必须通过账户使用 ERC-165 接口检测进行信号传递。

代币绑定账户可以支持以下执行接口：

```solidity
/// @dev the ERC-165 identifier for this interface is `0x51945447`
interface IERC6551Executable {
    /**
     * @dev Executes a low-level operation if the caller is a valid signer on the account.
     *
     * Reverts and bubbles up error if operation fails.
     *
     * Accounts implementing this interface MUST accept the following operation parameter values:
     * - 0 = CALL
     * - 1 = DELEGATECALL
     * - 2 = CREATE
     * - 3 = CREATE2
     *
     * Accounts implementing this interface MAY support additional operations or restrict a signer's
     * ability to execute certain operations.
     *
     * @param to        The target address of the operation
     * @param value     The Ether value to be sent to the target
     * @param data      The encoded operation calldata
     * @param operation A value indicating the type of operation to perform
     * @return The result of the operation
     */
    function execute(address to, uint256 value, bytes calldata data, uint8 operation)
        external
        payable
        returns (bytes memory);
}
```

## 理由

### 单例注册表

本提案指定了一个单一的、规范的注册表，可以在已知地址上无权限地部署到任何链。它故意不指定可以由多个注册表合约实现的通用接口。这种方法使几个关键属性得以实现。

#### 反事实账户

所有代币绑定账户都是使用 create2 操作码创建的，使账户在创建之前可以存在于反事实状态。这允许代币绑定账户在合约创建之前接收资产。单例账户注册表确保所有代币绑定账户地址使用相同的寻址方案。

#### 无信任部署

一个没有所有者的单一注册表确保任何代币绑定账户的唯一可信合约是实现。这保证了代币持有者可以访问存储在反事实账户中的所有资产，使用一个可信的实现。
在没有规范注册表的情况下，一些代币绑定账户可能会使用拥有或可升级的注册表进行部署。这可能导致存储在反事实账户中的资产丢失，并增加支持此提案的应用程序必须考虑的安全模型范围。

#### 跨链兼容性

具有已知地址的单例注册表使每个代币绑定账户可以存在于多个链上。将 `chainId` 作为 `createAccount` 的参数包含在内，允许代币绑定账户的合约在任何支持的链上以相同地址部署。因此，账户实现能够支持跨链账户执行，其中一个链上的 NFT 可以控制另一个链上的代币绑定账户。

#### 单一入口点

查询账户地址和 `AccountCreated` 事件的单一入口点简化了支持此提案的应用程序中代币绑定账户的索引这一复杂任务。

#### 实现多样性

单例注册表允许多样的账户实现共享一个通用的地址方案。这为开发者提供了显著的自由度，以便以客户端应用程序可以轻松支持的方式实现账户特定功能（例如，委托）以及替代账户模型（例如，临时账户）。

### 注册表与工厂

选择“注册表”一词而不是“工厂”是为了突出合约的规范性质，并强调查询账户地址（这会定期发生）而不是创建账户（每个账户仅发生一次）的行为。

### 可变执行接口

此提案不要求账户实现特定的执行接口以保持兼容，只要它们通过 ERC-165 接口检测信号支持至少一个执行接口。允许账户开发者选择自己的执行接口，使得此提案能够支持现有执行接口的广泛多样性，并保持与未来可能标准化接口的向前兼容性。

### 账户模糊性

上述提案允许 NFT 拥有多个代币绑定账户。在此提案的开发过程中，考虑了其他架构，这些架构将为每个 NFT 分配一个代币绑定账户，使每个代币绑定账户地址成为一个明确的标识符。

然而，这些替代方案存在几个权衡。

首先，由于智能合约的无权限性质，无法强制每个 NFT 限制一个代币绑定账户。任何希望为每个 NFT 利用多个代币绑定账户的人都可以通过部署额外的注册合约来实现。

其次，限制每个 NFT 只能有一个代币绑定账户将要求在此提案中包含一个静态、可信的账户实现。该实现不可避免地会对代币绑定账户的能力施加特定约束。考虑到此提案所启用的未探索用例数量以及多样化账户实现可能为非同质化代币生态系统带来的好处，作者认为在此提案中定义一个规范且受限的实现为时已早。

最后，此提案旨在赋予 NFT 在链上作为代理的能力。在当前实践中，链上代理通常使用多个账户。一个常见的例子是个人使用“热”账户进行日常使用，而使用“冷”账户存储贵重物品。如果链上代理通常使用多个账户，那么 NFT 理应继承相同的能力。

### 代理实现

ERC-1167 最小代理在现有基础设施中得到了良好的支持，并且是一种常见的智能合约模式。此提案使用自定义的 ERC-1167 代理实现部署每个代币绑定账户，该实现将盐、链 ID、代币合约地址和代币 ID 作为 ABI 编码的常量数据附加到合约字节码中。这使得代币绑定账户实现能够轻松查询这些数据，同时确保其保持不变。采取这种方法是为了最大限度地兼容现有基础设施，同时也为智能合约开发者在创建自定义代币绑定账户实现时提供了充分的灵活性。

### 链标识符

此提案使用链 ID 来识别每个 NFT 及其合约地址和代币 ID。代币标识符在单个以太坊链上是全球唯一的，但在多个以太坊链上可能不是唯一的。

## 向后兼容性

此提案旨在与现有的非同质化代币合约最大限度地向后兼容。因此，它不扩展 ERC-721 标准。

此外，此提案不要求注册表在账户创建之前执行 ERC-165 接口检查以确保 ERC-721 兼容性。这最大限度地兼容了在 ERC-721 标准之前存在的非同质化代币合约（例如 CryptoKitties）或仅实现 ERC-721 接口子集的合约（例如 ENS NameWrapper 名称）。它还允许在此提案中描述的系统与半同质化或同质化代币一起使用，尽管这些用例超出了提案的范围。

智能合约作者可以选择在其账户实现中强制执行 ERC-721 的接口检测。

## 参考实现

### 示例账户实现

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/interfaces/IERC1271.sol";
import "@openzeppelin/contracts/utils/cryptography/SignatureChecker.sol";

interface IERC6551Account {
    receive() external payable;

    function token()
        external
        view
        returns (uint256 chainId, address tokenContract, uint256 tokenId);

    function state() external view returns (uint256);

    function isValidSigner(address signer, bytes calldata context)
        external
        view
        returns (bytes4 magicValue);
}

interface IERC6551Executable {
    function execute(address to, uint256 value, bytes calldata data, uint8 operation)
        external
        payable
        returns (bytes memory);
}

contract ERC6551Account is IERC165, IERC1271, IERC6551Account, IERC6551Executable {
    uint256 immutable deploymentChainId = block.chainid;

    uint256 public state;

    receive() external payable {}

    function execute(address to, uint256 value, bytes calldata data, uint8 operation)
        external
        payable
        virtual
        returns (bytes memory result)
    {
        require(_isValidSigner(msg.sender), "Invalid signer");
        require(operation == 0, "Only call operations are supported");

        ++state;

        bool success;
        (success, result) = to.call{value: value}(data);

        if (!success) {
            assembly {
                revert(add(result, 32), mload(result))
            }
        }
    }

    function isValidSigner(address signer, bytes calldata) external view virtual returns (bytes4) {
        if (_isValidSigner(signer)) {
            return IERC6551Account.isValidSigner.selector;
        }

        return bytes4(0);
    }

    function isValidSignature(bytes32 hash, bytes memory signature)
        external
        view
        virtual
        returns (bytes4 magicValue)
    {
        bool isValid = SignatureChecker.isValidSignatureNow(owner(), hash, signature);

        if (isValid) {
            return IERC1271.isValidSignature.selector;
        }

        return bytes4(0);
    }

    function supportsInterface(bytes4 interfaceId) external pure virtual returns (bool) {
        return interfaceId == type(IERC165).interfaceId
            || interfaceId == type(IERC6551Account).interfaceId
            || interfaceId == type(IERC6551Executable).interfaceId;
    }

    function token() public view virtual returns (uint256, address, uint256) {
        bytes memory footer = new bytes(0x60);

        assembly {
            extcodecopy(address(), add(footer, 0x20), 0x4d, 0x60)
        }

        return abi.decode(footer, (uint256, address, uint256));
    }

    function owner() public view virtual returns (address) {
        (uint256 chainId, address tokenContract, uint256 tokenId) = token();
        if (chainId != deploymentChainId) return address(0);

        return IERC721(tokenContract).ownerOf(tokenId);
    }

    function _isValidSigner(address signer) internal view virtual returns (bool) {
        return signer == owner();
    }
}
```

### 注册表实现

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

interface IERC6551Registry {
    /**
     * @dev The registry MUST emit the ERC6551AccountCreated event upon successful account creation.
     */
    event ERC6551AccountCreated(
        address account,
        address indexed implementation,
        bytes32 salt,
        uint256 chainId,
        address indexed tokenContract,
        uint256 indexed tokenId
    );

    /**
     * @dev The registry MUST revert with AccountCreationFailed error if the create2 operation fails.
     */
    error AccountCreationFailed();

    /**
     * @dev Creates a token bound account for a non-fungible token.
     *
     * If account has already been created, returns the account address without calling create2.
     *
     * Emits ERC6551AccountCreated event.
     *
     * @return account The address of the token bound account
     */
    function createAccount(
        address implementation,
        bytes32 salt,
        uint256 chainId,
        address tokenContract,
        uint256 tokenId
    ) external returns (address account);

    /**
     * @dev Returns the computed token bound account address for a non-fungible token.
     *
     * @return account The address of the token bound account
     */
    function account(
        address implementation,
        bytes32 salt,
        uint256 chainId,
        address tokenContract,
        uint256 tokenId
    ) external view returns (address account);
}

contract ERC6551Registry is IERC6551Registry {
    function createAccount(
        address implementation,
        bytes32 salt,
        uint256 chainId,
        address tokenContract,
        uint256 tokenId
    ) external returns (address) {
        assembly {
            // Memory Layout:
            // ----
            // 0x00   0xff                           (1 byte)
            // 0x01   registry (address)             (20 bytes)
            // 0x15   salt (bytes32)                 (32 bytes)
            // 0x35   Bytecode Hash (bytes32)        (32 bytes)
            // ----
            // 0x55   ERC-1167 Constructor + Header  (20 bytes)
            // 0x69   implementation (address)       (20 bytes)
            // 0x5D   ERC-1167 Footer                (15 bytes)
            // 0x8C   salt (uint256)                 (32 bytes)
            // 0xAC   chainId (uint256)              (32 bytes)
            // 0xCC   tokenContract (address)        (32 bytes)
            // 0xEC   tokenId (uint256)              (32 bytes)

            // Silence unused variable warnings
            pop(chainId)

            // Copy bytecode + constant data to memory
            calldatacopy(0x8c, 0x24, 0x80) // salt, chainId, tokenContract, tokenId
            mstore(0x6c, 0x5af43d82803e903d91602b57fd5bf3) // ERC-1167 footer
            mstore(0x5d, implementation) // implementation
            mstore(0x49, 0x3d60ad80600a3d3981f3363d3d373d3d3d363d73) // ERC-1167 constructor + header

            // Copy create2 computation data to memory
            mstore(0x35, keccak256(0x55, 0xb7)) // keccak256(bytecode)
            mstore(0x15, salt) // salt
            mstore(0x01, shl(96, address())) // registry address
            mstore8(0x00, 0xff) // 0xFF

            // Compute account address
            let computed := keccak256(0x00, 0x55)

            // If the account has not yet been deployed
            if iszero(extcodesize(computed)) {
                // Deploy account contract
                let deployed := create2(0, 0x55, 0xb7, salt)

                // Revert if the deployment fails
                if iszero(deployed) {
                    mstore(0x00, 0x20188a59) // `AccountCreationFailed()`
                    revert(0x1c, 0x04)
                }

                // Store account address in memory before salt and chainId
                mstore(0x6c, deployed)

                // Emit the ERC6551AccountCreated event
                log4(
                    0x6c,
                    0x60,
                    // `ERC6551AccountCreated(address,address,bytes32,uint256,address,uint256)`
                    0x79f19b3655ee38b1ce526556b7731a20c8f218fbda4a3990b6cc4172fdf88722,
                    implementation,
                    tokenContract,
                    tokenId
                )

                // Return the account address
                return(0x6c, 0x20)
            }

            // Otherwise, return the computed account address
            mstore(0x00, shr(96, shl(96, computed)))
            return(0x00, 0x20)
        }
    }

    function account(
        address implementation,
        bytes32 salt,
        uint256 chainId,
        address tokenContract,
        uint256 tokenId
    ) external view returns (address) {
        assembly {
            // Silence unused variable warnings
            pop(chainId)
            pop(tokenContract)
            pop(tokenId)

            // Copy bytecode + constant data to memory
            calldatacopy(0x8c, 0x24, 0x80) // salt, chainId, tokenContract, tokenId
            mstore(0x6c, 0x5af43d82803e903d91602b57fd5bf3) // ERC-1167 footer
            mstore(0x5d, implementation) // implementation
            mstore(0x49, 0x3d60ad80600a3d3981f3363d3d373d3d3d363d73) // ERC-1167 constructor + header

            // Copy create2 computation data to memory
            mstore(0x35, keccak256(0x55, 0xb7)) // keccak256(bytecode)
            mstore(0x15, salt) // salt
            mstore(0x01, shl(96, address())) // registry address
            mstore8(0x00, 0xff) // 0xFF

            // Store computed account address in memory
            mstore(0x00, shr(96, shl(96, keccak256(0x00, 0x55))))

            // Return computed account address
            return(0x00, 0x20)
        }
    }
}
```

## 安全考虑

### 防止欺诈

为了实现代币绑定账户的无信任销售，去中心化市场需要实施防止恶意账户所有者欺诈行为的保护措施。

考虑以下潜在骗局：

- 爱丽丝拥有一个 ERC-721 代币 X，该代币拥有代币绑定账户 Y。
- 爱丽丝向账户 Y 存入 10ETH
- 鲍勃通过去中心化市场报价以 11ETH 购买代币 X，假设他将获得存储在账户 Y 中的 10ETH 以及代币
- 爱丽丝从代币绑定账户中提取 10ETH，并立即接受鲍勃的报价
- 鲍勃收到代币 X，但账户 Y 是空的

为了减轻恶意账户所有者的欺诈行为，去中心化市场应在市场层面实施对这些类型骗局的保护。实现此 EIP 的合约也可以实施某些防止欺诈行为的保护措施。

以下是一些需要考虑的缓解策略：

- 将当前代币绑定账户状态附加到市场订单。如果自订单下达以来账户状态发生变化，则视为报价无效。此功能需要在市场层面得到支持。
- 将预期在订单完成时仍保留在代币绑定账户中的资产承诺列表附加到市场订单。如果自订单下达以来任何承诺的资产已从账户中移除，则视为报价无效。这也需要由市场实施。
- 通过外部智能合约将订单提交到去中心化市场，该合约在验证订单签名之前执行上述逻辑。这允许在没有市场支持的情况下实现安全转移。
- 在代币绑定账户实现上实施锁定机制，防止恶意所有者在锁定期间提取账户中的资产。
防止欺诈超出了本提案的范围。

### 所有权循环

如果创建了所有权循环，则所有在代币绑定账户中持有的资产可能会变得无法访问。最简单的例子是将 ERC-721 代币转移到其自己的代币绑定账户。如果发生这种情况，ERC-721 代币和存储在代币绑定账户中的所有资产将永久无法访问，因为代币绑定账户无法执行转移 ERC-721 代币的交易。

在任何 n>0 的代币绑定账户图中都可以引入所有权循环。由于所需的无限搜索空间，链上防止深度>1 的循环是难以执行的，因此超出了本提案的范围。希望采用本提案的应用客户端和账户实现被鼓励实施措施，以限制所有权循环的可能性。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。