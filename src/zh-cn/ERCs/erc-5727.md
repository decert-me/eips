---
eip: 5727
title: 半可替代灵魂绑定代币
description: 一种灵魂绑定代币的接口，也称为徽章或账户绑定代币，可以是可替代的也可以是不可替代的。
author: Austin Zhu (@AustinZhu), Terry Chen <terry.chen@phaneroz.io>
discussions-to: https://ethereum-magicians.org/t/eip-5727-semi-fungible-soulbound-token/11086
status: 草案
type: 标准跟踪
category: ERC
created: 2022-09-28
requires: 165, 712, 721, 3525, 4906, 5192, 5484
---

## 摘要

灵魂绑定代币（SBT）的接口，这是一种不可转让的代币，代表一个人的身份、凭证、隶属关系和声誉。

我们的接口可以以有序的方式处理可替代和不可替代代币的组合。它提供了一组核心方法，可用于管理灵魂绑定代币的生命周期，以及一套丰富的扩展功能，支持 DAO 治理、委托、代币过期和账户恢复。

该接口旨在为灵魂绑定代币系统的开发提供灵活和可扩展的框架。

## 动机

当前的 Web3 生态系统主要集中在金融化、可转让的代币上。然而，越来越需要不可转让的代币来代表独特的个人属性和权利。以太坊社区现有的创建此类代币的尝试缺乏必要的灵活性和可扩展性。我们的接口解决了这一空白，提供了一个多功能和全面的 SBT 解决方案。

我们的接口可以用于表示不可转让的所有权，并提供常见用例的功能，包括但不限于：

- 生命周期管理：强大的工具用于铸造、撤销以及管理 SBT 的订阅和过期。
- DAO 治理和委托：赋予社区驱动的决策和 SBT 管理的操作委托。
- 账户恢复：先进的账户恢复和密钥轮换机制，确保安全性和连续性。
- 代币的多样性：支持可替代和不可替代的 SBT，满足会员卡和忠诚度计划等广泛的用例。
- 代币分组：创新的基于插槽的系统，用于组织 SBT，适合复杂的奖励结构，包括代金券、积分和徽章。
- 可索取的 SBT：简化 SBT 的分发，用于空投、赠品和推荐计划。

该接口不仅丰富了 Web3 生态，还为更去中心化和个性化的数字社会铺平了道路。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

代币通过其 `tokenId` 进行识别，这是一个 256 位的无符号整数。代币还可以具有表示其面值的值。

插槽通过其 `slotId` 进行识别，这是一个 256 位的无符号整数。插槽用于将可替代和不可替代代币分组，从而使代币半可替代。代币一次只能属于一个插槽。

### 核心

核心方法用于管理 SBT 的生命周期。所有半可替代 SBT 实现必须支持这些方法。

```solidity
/**
 * @title ERC5727 Soulbound Token Interface
 * @dev The core interface of the ERC5727 standard.
 */
interface IERC5727 is IERC3525, IERC5192, IERC5484, IERC4906 {
    /**
     * @dev MUST emit when a token is revoked.
     * @param from The address of the owner
     * @param tokenId The token id
     */
    event Revoked(address indexed from, uint256 indexed tokenId);

    /**
     * @dev MUST emit when a token is verified.
     * @param by The address that initiated the verification
     * @param tokenId The token id
     * @param result The result of the verification
     */
    event Verified(address indexed by, uint256 indexed tokenId, bool result);

    /**
     * @notice Get the verifier of a token.
     * @dev MUST revert if the `tokenId` does not exist
     * @param tokenId the token for which to query the verifier
     * @return The address of the verifier of `tokenId`
     */
    function verifierOf(uint256 tokenId) external view returns (address);

    /**
     * @notice Get the issuer of a token.
     * @dev MUST revert if the `tokenId` does not exist
     * @param tokenId the token for which to query the issuer
     * @return The address of the issuer of `tokenId`
     */
    function issuerOf(uint256 tokenId) external view returns (address);

    /**
     * @notice Issue a token in a specified slot to an address.
     * @dev MUST revert if the `to` address is the zero address.
     *      MUST revert if the `verifier` address is the zero address.
     * @param to The address to issue the token to
     * @param tokenId The token id
     * @param slot The slot to issue the token in
     * @param burnAuth The burn authorization of the token
     * @param verifier The address of the verifier
     * @param data Additional data used to issue the token
     */
    function issue(
        address to,
        uint256 tokenId,
        uint256 slot,
        BurnAuth burnAuth,
        address verifier,
        bytes calldata data
    ) external payable;

    /**
     * @notice Issue credit to a token.
     * @dev MUST revert if the `tokenId` does not exist.
     * @param tokenId The token id
     * @param amount The amount of the credit
     * @param data The additional data used to issue the credit
     */
    function issue(
        uint256 tokenId,
        uint256 amount,
        bytes calldata data
    ) external payable;

    /**
     * @notice Revoke a token from an address.
     * @dev MUST revert if the `tokenId` does not exist.
     * @param tokenId The token id
     * @param data The additional data used to revoke the token
     */
    function revoke(uint256 tokenId, bytes calldata data) external payable;

    /**
     * @notice Revoke credit from a token.
     * @dev MUST revert if the `tokenId` does not exist.
     * @param tokenId The token id
     * @param amount The amount of the credit
     * @param data The additional data used to revoke the credit
     */
    function revoke(
        uint256 tokenId,
        uint256 amount,
        bytes calldata data
    ) external payable;

    /**
     * @notice Verify if a token is valid.
     * @dev MUST revert if the `tokenId` does not exist.
     * @param tokenId The token id
     * @param data The additional data used to verify the token
     * @return A boolean indicating whether the token is successfully verified
     */
    function verify(
        uint256 tokenId,
        bytes calldata data
    ) external returns (bool);
}
```

### 扩展

以下所有扩展对于 [ERC-5727](./eip-5727.md) 实现是可选的。实现可以选择实现其中的一些、全部或不实现。

#### 可枚举

此扩展提供方法以枚举所有者的代币。建议与核心接口一起实现。

```solidity
/**
 * @title ERC5727 Soulbound Token Enumerable Interface
 * @dev This extension allows querying the tokens of a owner.
 */
interface IERC5727Enumerable is IERC3525SlotEnumerable, IERC5727 {
    /**
     * @notice Get the number of slots of a owner.
     * @param owner The owner whose number of slots is queried for
     * @return The number of slots of the `owner`
     */
    function slotCountOfOwner(address owner) external view returns (uint256);

    /**
     * @notice Get the slot with `index` of the `owner`.
     * @dev MUST revert if the `index` exceed the number of slots of the `owner`.
     * @param owner The owner whose slot is queried for.
     * @param index The index of the slot queried for
     * @return The slot is queried for
     */
    function slotOfOwnerByIndex(
        address owner,
        uint256 index
    ) external view returns (uint256);

    /**
     * @notice Get the balance of a owner in a slot.
     * @dev MUST revert if the slot does not exist.
     * @param owner The owner whose balance is queried for
     * @param slot The slot whose balance is queried for
     * @return The balance of the `owner` in the `slot`
     */
    function ownerBalanceInSlot(
        address owner,
        uint256 slot
    ) external view returns (uint256);
}
```

#### 元数据

此扩展提供方法以获取代币、插槽和合约本身的元数据。如果需要指定代币、插槽和合约（即 SBT 集合）的外观和属性，建议实现此扩展。

```solidity
/**
 * @title ERC5727 Soulbound Token Metadata Interface
 * @dev This extension allows querying the metadata of soulbound tokens.
 */
interface IERC5727Metadata is IERC3525Metadata, IERC5727 {

}
```

#### 治理

此扩展提供通过投票管理铸造和撤销权限的方法。如果希望依赖一组投票者来决定特定 SBT 的发行，这将非常有用。

```solidity
/**
 * @title ERC5727 Soulbound Token Governance Interface
 * @dev This extension allows issuing of tokens by community voting.
 */
interface IERC5727Governance is IERC5727 {
    enum ApprovalStatus {
        Pending,
        Approved,
        Rejected,
        Removed
    }

    /**
     * @notice Emitted when a token issuance approval is changed.
     * @param approvalId The id of the approval
     * @param creator The creator of the approval, zero address if the approval is removed
     * @param status The status of the approval
     */
    event ApprovalUpdate(
        uint256 indexed approvalId,
        address indexed creator,
        ApprovalStatus status
    );

    /**
     * @notice Emitted when a voter approves an approval.
     * @param voter The voter who approves the approval
     * @param approvalId The id of the approval
     */
    event Approve(
        address indexed voter,
        uint256 indexed approvalId,
        bool approve
    );

    /**
     * @notice Create an approval of issuing a token.
     * @dev MUST revert if the caller is not a voter.
     *      MUST revert if the `to` address is the zero address.
     * @param to The owner which the token to mint to
     * @param tokenId The id of the token to mint
     * @param amount The amount of the token to mint
     * @param slot The slot of the token to mint
     * @param burnAuth The burn authorization of the token to mint
     * @param data The additional data used to mint the token
     */
    function requestApproval(
        address to,
        uint256 tokenId,
        uint256 amount,
        uint256 slot,
        BurnAuth burnAuth,
        address verifier,
        bytes calldata data
    ) external;

    /**
     * @notice Remove `approvalId` approval request.
     * @dev MUST revert if the caller is not the creator of the approval request.
     *      MUST revert if the approval request is already approved or rejected or non-existent.
     * @param approvalId The approval to remove
     */
    function removeApprovalRequest(uint256 approvalId) external;

    /**
     * @notice Approve `approvalId` approval request.
     * @dev MUST revert if the caller is not a voter.
     *     MUST revert if the approval request is already approved or rejected or non-existent.
     * @param approvalId The approval to approve
     * @param approve True if the approval is approved, false if the approval is rejected
     * @param data The additional data used to approve the approval (e.g. the signature, voting power)
     */
    function voteApproval(
        uint256 approvalId,
        bool approve,
        bytes calldata data
    ) external;

    /**
     * @notice Get the URI of the approval.
     * @dev MUST revert if the `approvalId` does not exist.
     * @param approvalId The approval whose URI is queried for
     * @return The URI of the approval
     */
    function approvalURI(
        uint256 approvalId
    ) external view returns (string memory);
}
```

#### 委托

此扩展提供方法以将铸造权委托（撤回委托）给（从）操作员。如果希望允许操作员代表您在特定插槽中铸造代币，这将非常有用。

```solidity
/**
 * @title ERC5727 Soulbound Token Delegate Interface
 * @dev This extension allows delegation of issuing and revocation of tokens to an operator.
 */
interface IERC5727Delegate is IERC5727 {
    /**
     * @notice Emitted when a token issuance is delegated to an operator.
     * @param operator The owner to which the issuing right is delegated
     * @param slot The slot to issue the token in
     */
    event Delegate(address indexed operator, uint256 indexed slot);

    /**
     * @notice Emitted when a token issuance is revoked from an operator.
     * @param operator The owner to which the issuing right is delegated
     * @param slot The slot to issue the token in
     */
    event UnDelegate(address indexed operator, uint256 indexed slot);

    /**
     * @notice Delegate rights to `operator` for a slot.
     * @dev MUST revert if the caller does not have the right to delegate.
     *      MUST revert if the `operator` address is the zero address.
     *      MUST revert if the `slot` is not a valid slot.
     * @param operator The owner to which the issuing right is delegated
     * @param slot The slot to issue the token in
     */
    function delegate(address operator, uint256 slot) external;

    /**
     * @notice Revoke rights from `operator` for a slot.
     * @dev MUST revert if the caller does not have the right to delegate.
     *      MUST revert if the `operator` address is the zero address.
     *      MUST revert if the `slot` is not a valid slot.
     * @param operator The owner to which the issuing right is delegated
     * @param slot The slot to issue the token in
     */

    function undelegate(address operator, uint256 slot) external;

    /**
     * @notice Check if an operator has the permission to issue or revoke tokens in a slot.
     * @param operator The operator to check
     * @param slot The slot to check
     */
    function isOperatorFor(
        address operator,
        uint256 slot
    ) external view returns (bool);
}

```

#### 恢复

此扩展提供从过期所有者处恢复代币的方法。建议使用此扩展，以便用户能够在某些情况下从被盗或旧钱包中检索其代币。签名方案应与 [EIP-712](./eip-712.md) 兼容，以提高可读性和可用性。

```solidity
/**
 * @title ERC5727 Soulbound Token Recovery Interface
 * @dev This extension allows recovering soulbound tokens from an address provided its signature.
 */
interface IERC5727Recovery is IERC5727 {
    /**
     * @notice Emitted when the tokens of `owner` are recovered.
     * @param from The owner whose tokens are recovered
     * @param to The new owner of the tokens
     */
    event Recovered(address indexed from, address indexed to);

    /**
     * @notice Recover the tokens of `owner` with `signature`.
     * @dev MUST revert if the signature is invalid.
     * @param owner The owner whose tokens are recovered
     * @param signature The signature signed by the `owner`
     */
    function recover(address owner, bytes memory signature) external;
}
```

#### 过期

此扩展提供管理代币过期的方法。如果希望在一定时间后使代币过期/失效，这将非常有用。

```solidity
/**
 * @title ERC5727 Soulbound Token Expirable Interface
 * @dev This extension allows soulbound tokens to be expirable and renewable.
 */
interface IERC5727Expirable is IERC5727, IERC5643 {
    /**
     * @notice Set the expiry date of a token.
     * @dev MUST revert if the `tokenId` token does not exist.
     *      MUST revert if the `date` is in the past.
     * @param tokenId The token whose expiry date is set
     * @param expiration The expire date to set
     * @param isRenewable Whether the token is renewable
     */
    function setExpiration(
        uint256 tokenId,
        uint64 expiration,
        bool isRenewable
    ) external;
}
```

## 理由

### 代币存储模型

我们采用半可替代代币存储模型，旨在支持可替代和不可替代代币，灵感来自半可替代代币标准。我们发现这种模型更适合 SBT 的表示，而不是 [ERC-1155](./eip-1155.md) 中使用的模型。

首先，每个插槽可以用于表示不同类别的 SBT。例如，一个 DAO 可以在一个 SBT 集合中拥有会员 SBT、角色徽章、声誉等。

其次，与 [ERC-1155](./eip-1155.md) 不同，其中每个可替代代币的单位完全相同，我们的接口可以帮助区分相似的代币。这是因为来自不同实体的凭证分数不仅在价值上不同，而且在其效果、有效期、来源等方面也不同。然而，它们仍然共享同一个插槽，因为它们都对一个人的信誉、会员资格等有所贡献。

### 恢复机制

为了防止 SBT 的丢失，我们提出了一种恢复机制，允许用户通过提供由其所有者地址签名的签名来恢复其代币。该机制受到 [ERC-1271](./eip-1271.md) 的启发。

由于 SBT 绑定到一个地址，并且旨在代表该地址的身份，不能被分割。因此，每次恢复都应视为所有者所有代币的转移。这就是为什么我们使用 `recover` 函数而不是 `transferFrom` 或 `safeTransferFrom`。

## 向后兼容性

该 EIP 提出了一个新的代币接口，与 [ERC-721](./eip-721.md)、[ERC-3525](./eip-3525.md)、[ERC-4906](./eip-4906.md)、[ERC-5192](./eip-5192.md)、[ERC-5484](./eip-5484.md) 兼容。

该 EIP 也与 [ERC-165](./eip-165.md) 兼容。

## 测试用例

我们的示例实现包括使用 Hardhat 编写的测试用例。

## 参考实现

您可以在 [这里](../assets/eip-5727/ERC5727.sol) 找到我们的参考实现。

## 安全考虑

该 EIP 不涉及代币的一般转移，因此通常不会存在与代币转移相关的安全问题。

然而，用户应注意使用恢复机制的安全风险。如果用户丢失其私钥，所有其灵魂绑定代币将面临被盗的潜在风险。攻击者可以创建签名并恢复受害者的所有 SBT。因此，用户应始终保持其私钥的安全。我们建议开发者实现需要多个签名才能恢复 SBT 的恢复机制。
## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。