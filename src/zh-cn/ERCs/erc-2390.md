---
eip: 2390
title: Geo-ENS
author: James Choncholas (@james-choncholas)
discussions-to: https://github.com/ethereum/EIPs/issues/2959
status: Stagnant
type: Standards Track
category: ERC
created: 2019-11-15
requires: 137, 165, 1062, 1185
---

## 简要总结
GeoENS 为 ENS 带来了地理分割视野的能力。它是 ENS 的 GeoDNS！

## 摘要
本 EIP 指定了一个用于地理分割视野 DNS 的 ENS 解析器接口。
地理分割视野 DNS 返回特定于最终用户位置的资源记录。
这种技术通常被 CDN 用于将流量引导到离用户最近的内容缓存。
地理分割视野解析主要针对存储 DNS 资源记录的 ENS 解析器 [EIP-1185](./eip-1185.md)，尽管该技术也可以用于其他接口，如 IPFS 内容哈希存储 [EIP-1062](./eip-1062.md)。

## 动机
在集中式网络中，传统 GeoDNS 系统（如亚马逊的 Route53）有许多用例。
这些用例包括基于接近度的负载均衡和提供特定于查询地理位置的内容。
不幸的是，ENS 规范没有提供地理特定解析的机制。
ENS 可以响应带有 IP 地址的查询（如 [EIP-1185](./eip-1185.md) 中所述），但没有办法响应地理特定的查询。
本 EIP 提出了一个标准，使 ENS 系统具备地理邻近意识，以实现与 GeoDNS 类似的目的。

GeoENS 能做的不仅仅是基于 DNS 的解决方案。
除了地理分割视野 DNS，GeoENS 还可以用于以下用途：
 - 定位代表现实世界中物理对象的数字资源（如智能合约）。
 - 智能合约管理与特定位置相关的物理对象的访问。
 - ENS + IPFS 网站托管（如 [EIP-1062](./eip-1062.md) 中所述），内容翻译为查询来源的母语。
 - 对具有物理位置的对象进行代币化。

由于 ENS 的去中心化特性，地理特定解析与传统 GeoDNS 不同。
GeoDNS 的工作原理如下。DNS 查询通过其源 IP 地址进行识别。
该 IP 在 MaxMind 的 [GeoIP2](https://www.maxmind.com/en/geoip2-services-and-databases) 数据库中查找，该数据库将 IP 地址映射到位置。
这种定位查询来源的方法容易出错且不可靠。
如果 GeoIP 数据库过时，查询的位置可能与其真实位置大相径庭。
GeoENS 不依赖于数据库，因为用户在查询中包含了位置。

因此，用户可以针对任何位置进行查询，而不仅仅是他们的位置。
传统 DNS 只会返回分配给查询来源的资源。
GeoENS 不将查询的来源与位置相关联，允许从单一位置查询整个地球。

传统 DNS 的另一个缺点是没有办法返回某个特定范围内的服务器列表。
这对于需要发现最低延迟资源的用例至关重要。
GeoENS 允许在特定位置收集资源列表，如 IP 地址。
然后客户端可以自行确定哪个资源具有最低延迟。

最后，公开的 GeoDNS 服务并未对 GeoDNS 查询的地理区域提供细粒度控制。
基于云的 DNS 服务，如 [亚马逊的 Route 53](https://aws.amazon.com/route53/)，仅允许在美国的州级别指定地理区域。
而 GeoENS 则提供 8 个字符的 geohash 分辨率，精度对应于 ±20 米。

## 规范
本 EIP 提出了一个新的接口，以便 ENS 解析器可以记录和检索来自区块链的地理空间信息。
接口更改如下所述，适用于 EIP137 中描述的“地址解析器”，但该想法适用于 EIP1185 和 EIP1062 中描述的任何记录，即 DNS 解析器、文本解析器、ABI 解析器等。

### 什么是 geohash？
[Geohash](https://en.m.wikipedia.org/wiki/Geohash#Algorithm_and_example) 是纬度和经度位的交错，其长度决定了其精度。
Geohash 通常以 base 32 字符编码。

### function setGeoAddr(bytes32 node, string calldata geohash, address addr) external authorised(node)
通过节点和 geohash 设置资源（合约地址、IP、ABI、TEXT 等）。
Geohash 必须对每个地址唯一，并且长度恰好为 8 个字符。
这导致 ±20 米的精度。
写入默认初始化的资源值 `address(0)`，以从解析器中移除资源。

### function geoAddr(bytes32 node, string calldata geohash) external view returns (address[] memory ret)
查询解析器合约以获取特定节点和位置。
返回所有与提供的节点和前缀 geohash 匹配的资源（合约地址、IP 地址、ABI、TEXT 记录等）。
这允许通过精确的 8 个字符的 geohash 查询以返回该位置的内容，或通过小于 8 个字符精度的 geohash 描述的地理边界框进行查询。

可以使用任何类型的 geohash，包括 [Z-order](https://en.wikipedia.org/wiki/Z-order_curve)、[Hilbert](https://en.wikipedia.org/wiki/Hilbert_curve) 或更准确的 [S2 Geometry](https://s2geometry.io/devguide/s2cell_hierarchy.html) 库。
还有一些方法可以使用 geohash 搜索地理数据，而不总是以矩形查询区域结束。
[搜索圆形区域](https://github.com/ashwin711/proximityhash) 稍微复杂一些，因为它需要多个查询。

## 理由
所提议的实现使用稀疏的 [Quadtree](https://dl.acm.org/doi/10.1007/BF00288933) trie 作为资源记录的索引，因为它具有低存储开销和良好的搜索性能。
树的叶节点存储资源记录，而非叶节点表示一个 geohash 字符。
树中深度为 d 的每个节点对应于精度为 d 的 geohash。
树的深度为 8，因为 geohash 的最大精度为 8 个字符。
树的分支因子为 32，因为 geohash 字符的基数为 32。
到达叶节点的路径始终具有深度 8，叶节点包含路径所表示的 geohash 的内容（如 IP 地址）。
该树是稀疏的，因为地球表面 71% 被水覆盖。
该树促进了常见的遍历算法（DFS、BFS），以返回地理边界框内的资源记录列表。

## 向后兼容性
本 EIP 不会引入向后兼容性问题。

## 测试用例
请参见 https://github.com/james-choncholas/resolvers/blob/master/test/TestPublicResolver.js

## 实现
该地址解析器用 Solidity 编写，实施了上述规范。
这里提出的相同想法可以应用于 EIP137 中指定的其他解析器接口。
请注意，geohash 是使用 64 位无符号整数传递和存储的。
使用整数而不是字符串来表示 geohash 在 `geomap` 映射中性能更佳。
为了比较，请参见 https://github.com/james-choncholas/geoens/tree/master/contracts/StringOwnedGeoENSResolver.sol 中低效的字符串实现。
```solidity
pragma solidity ^0.5.0;

import "../ResolverBase.sol";

contract GeoENSResolver is ResolverBase {
    bytes4 constant ERC2390 = 0x8fbcc5ce;
    uint constant MAX_ADDR_RETURNS = 64;
    uint constant TREE_VISITATION_QUEUESZ = 64;
    uint8 constant ASCII_0 = 48;
    uint8 constant ASCII_9 = 57;
    uint8 constant ASCII_a = 97;
    uint8 constant ASCII_b = 98;
    uint8 constant ASCII_i = 105;
    uint8 constant ASCII_l = 108;
    uint8 constant ASCII_o = 111;
    uint8 constant ASCII_z = 122;

    struct Node {
        address data; // 0 if not leaf
        uint256 parent;
        uint256[] children; // always length 32
    }

    // A geohash is 8, base-32 characters.
    // A geomap is stored as tree of fan-out 32 (because
    // geohash is base 32) and height 8 (because geohash
    // length is 8 characters)
    mapping(bytes32=>Node[]) private geomap;

    event GeoENSRecordChanged(bytes32 indexed node, bytes8 geohash, address addr);

    // only 5 bits of ret value are used
    function chartobase32(byte c) pure internal returns (uint8 b) {
        uint8 ascii = uint8(c);
        require( (ascii >= ASCII_0 && ascii <= ASCII_9) ||
                (ascii > ASCII_a && ascii <= ASCII_z));
        require(ascii != ASCII_a);
        require(ascii != ASCII_i);
        require(ascii != ASCII_l);
        require(ascii != ASCII_o);

        if (ascii <= (ASCII_0 + 9)) {
            b = ascii - ASCII_0;

        } else {
            // base32 b = 10
            // ascii 'b' = 0x60
            // note base32 skips the letter 'a'
            b = ascii - ASCII_b + 10;

            // base32 also skips the following letters
            if (ascii > ASCII_i)
                b --;
            if (ascii > ASCII_l)
                b --;
            if (ascii > ASCII_o)
                b --;
        }
        require(b < 32); // base 32 can't be larger than 32
        return b;
    }

    function geoAddr(bytes32 node, bytes8 geohash, uint8 precision) external view returns (address[] memory ret) {
        bytes32(node); // single node georesolver ignores node
        assert(precision <= geohash.length);

        ret = new address[](MAX_ADDR_RETURNS);
        if (geomap[node].length == 0) { return ret; }
        uint ret_i = 0;

        // walk into the geomap data structure
        uint pointer = 0; // not actual pointer but index into geomap
        for(uint8 i=0; i < precision; i++) {

            uint8 c = chartobase32(geohash[i]);
            uint next = geomap[node][pointer].children[c];
            if (next == 0) {
                // nothing found for this geohash.
                // return early.
                return ret;
            } else {
                pointer = next;
            }
        }

        // pointer is now node representing the resolution of the query geohash.
        // DFS until all addresses found or ret[] is full.
        // Do not use recursion because blockchain...
        uint[] memory indexes_to_visit = new uint[](TREE_VISITATION_QUEUESZ);
        indexes_to_visit[0] = pointer;
        uint front_i = 0;
        uint back_i = 1;

        while(front_i != back_i) {
            Node memory cur_node = geomap[node][indexes_to_visit[front_i]];
            front_i ++;

            // if not a leaf node...
            if (cur_node.data == address(0)) {
                // visit all the chilins
                for(uint i=0; i<cur_node.children.length; i++) {
                    // only visit valid children
                    if (cur_node.children[i] != 0) {
                        assert(back_i < TREE_VISITATION_QUEUESZ);
                        indexes_to_visit[back_i] = cur_node.children[i];
                        back_i ++;

                    }
                }
            } else {
                ret[ret_i] = cur_node.data;
                ret_i ++;
                if (ret_i > MAX_ADDR_RETURNS) break;
            }
        }

        return ret;
    }

    // when setting, geohash must be precise to 8 digits.
    function setGeoAddr(bytes32 node, bytes8 geohash, address addr) external authorised(node) {
        bytes32(node); // single node georesolver ignores node

        // create root node if not yet created
        if (geomap[node].length == 0) {
            geomap[node].push( Node({
                data: address(0),
                parent: 0,
                children: new uint256[](32)
            }));
        }

        // walk into the geomap data structure
        uint pointer = 0; // not actual pointer but index into geomap
        for(uint i=0; i < geohash.length; i++) {

            uint8 c = chartobase32(geohash[i]);

            if (geomap[node][pointer].children[c] == 0) {
                // nothing found for this geohash.
                // we need to create a path to the leaf
                geomap[node].push( Node({
                    data: address(0),
                    parent: pointer,
                    children: new uint256[](32)
                }));
                geomap[node][pointer].children[c] = geomap[node].length - 1;
            }
            pointer = geomap[node][pointer].children[c];
        }

        Node storage cur_node = geomap[node][pointer]; // storage = get reference
        cur_node.data = addr;

        emit GeoENSRecordChanged(node, geohash, addr);
    }

    function supportsInterface(bytes4 interfaceID) public pure returns (bool) {
        return interfaceID == ERC2390 || super.supportsInterface(interfaceID);
    }
}
```

## 安全考虑
该合约的功能类似于 ENS 解析器 - 有关安全考虑，请参阅那里。
此外，该合约具有数据隐私的维度。
用户通过 geoAddr 函数查询，指定少于 8 个字符的 geohash，
以定义查询区域。
运行轻客户端的用户会将查询区域泄露给其连接的全节点。
依赖第三方（如 Infura）运行的节点的用户也会泄露
查询区域。
运行自己全节点或可以访问受信任全节点的用户不会泄露任何位置信息。

鉴于大多数位置服务的工作方式，查询区域很可能包含
用户的实际位置。
API 访问、轻节点和全节点之间的差异一直对隐私产生影响，但现在这种影响因
粗粒度用户位置的参与而更加明显。



## 版权
通过 [CC0](../LICENSE.md) 放弃版权及相关权利。