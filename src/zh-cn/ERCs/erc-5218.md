---
eip: 5218
title: NFT 权利管理
description: 创建与 NFT 一同转移的版权许可证的接口。
author: James Grimmelmann (@grimmelm), Yan Ji (@iseriohn), Tyler Kell (@relyt29)
discussions-to: https://ethereum-magicians.org/t/eip-5218-nft-rights-management/9911
status: Stagnant
type: Standards Track
category: ERC
created: 2022-07-11
requires: 721
---



## 摘要

以下标准定义了一个用于管理 NFT 许可证的 API。该标准提供了创建、转移和撤销许可证的基本功能，并确定 NFT 的当前许可状态。该标准不定义许可证的法律细节。相反，它提供了一个结构化框架来记录许可细节。

我们考虑 NFT 创作者希望将与 NFT 相关的作品的版权许可证授予 NFT 持有者的用例。持有有效许可证的用户可以向他人发放子许可证，以执行许可证下授予的权利。许可证可以与 NFT 一同转移，所有子许可证也可以如此。许可证可以根据创作者指定的条件选择性地撤销。 


## 动机

[ERC-721](./eip-721.md) 标准定义了一个 API 来跟踪和转移 NFT 的所有权。然而，当 NFT 代表某些链外资产时，我们需要某种法律有效的机制来*绑定*链上资产（NFT）与链外财产。链外财产的一个重要案例是创作作品，例如图像或音乐文件。最近，大多数涉及创作作品的 NFT 项目都使用许可证来澄清授予 NFT 所有者的法律权利。但这些许可证几乎总是链外的，NFT 本身并未指明适用于它们的许可证，导致对使用与 NFT 相关的作品的权利的不确定性。避免 NFT 中所有版权漏洞并不是一项简单的任务，现有的 EIP 也没有解决 NFT 的权利管理，超出了直接所有权（见 [ERC-721](./eip-721.md)）或租赁（见 [ERC-4907](./eip-4907.md)）的简单案例。

此 EIP 试图提供一个标准，以促进 Web3 世界中 NFT 的权利管理。特别是，[ERC-5218](./eip-5218.md) 智能合约允许所有与 NFT 相关的许可证，包括授予 NFT 所有者的*根许可证*和许可证持有者授予的*子许可证*，通过链上数据进行记录和轻松跟踪。这些许可证可以由人类可读的法律代码、机器可读的摘要（例如 CC REL 中编写的内容）或两者组成。ERC-5218 智能合约通过记录 URI 指向许可证，为用户提供可靠的参考，以了解他们被授予的法律权利，并为 NFT 创作者和审计员检测未经授权的侵权使用提供便利。



## 规范

本文档中的关键词“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“应该不”、“推荐”、“可以”和“可选”应按 RFC 2119 中的描述进行解释。

**每个符合 ERC-5218 的合约*必须*实现 `IERC5218` 接口**：

```solidity
pragma solidity ^0.8.0;

/// @title ERC-5218: NFT Rights Management
interface IERC5218 is IERC721 {

  /// @dev This emits when a new license is created by any mechanism.
  event CreateLicense(uint256 _licenseId, uint256 _tokenId, uint256 _parentLicenseId, address _licenseHolder, string _uri, address _revoker);
 
  /// @dev This emits when a license is revoked. Note that under some
  ///  license terms, the sublicenses may be `implicitly` revoked following the
  ///  revocation of some ancestral license. In that case, your smart contract
  ///  may only emit this event once for the ancestral license, and the revocation
  ///  of all its sublicenses can be implied without consuming additional gas.
  event RevokeLicense(uint256 _licenseId);
 
  /// @dev This emits when the a license is transferred to a new holder. The
  ///  root license of an NFT should be transferred with the NFT in an ERC721
  ///  `transfer` function call. 
  event TransferLicense(uint256 _licenseId, address _licenseHolder);
  
  /// @notice Check if a license is active.
  /// @dev A non-existing or revoked license is inactive and this function must
  ///  return `false` upon it. Under some license terms, a license may become
  ///  inactive because some ancestral license has been revoked. In that case,
  ///  this function should return `false`.
  /// @param _licenseId The identifier for the queried license
  /// @return Whether the queried license is active
  function isLicenseActive(uint256 _licenseId) external view returns (bool);

  /// @notice Retrieve the token identifier a license was issued upon.
  /// @dev Throws unless the license is active.
  /// @param _licenseId The identifier for the queried license
  /// @return The token identifier the queried license was issued upon
  function getLicenseTokenId(uint256 _licenseId) external view returns (uint256);

  /// @notice Retrieve the parent license identifier of a license.
  /// @dev Throws unless the license is active. If a license doesn't have a
  ///  parent license, return a special identifier not referring to any license
  ///  (such as 0).
  /// @param _licenseId The identifier for the queried license
  /// @return The parent license identifier of the queried license
  function getParentLicenseId(uint256 _licenseId) external view returns (uint256);

  /// @notice Retrieve the holder of a license.
  /// @dev Throws unless the license is active.   
  /// @param _licenseId The identifier for the queried license
  /// @return The holder address of the queried license
  function getLicenseHolder(uint256 _licenseId) external view returns (address);

  /// @notice Retrieve the URI of a license.
  /// @dev Throws unless the license is active.   
  /// @param _licenseId The identifier for the queried license
  /// @return The URI of the queried license
  function getLicenseURI(uint256 _licenseId) external view returns (string memory);

  /// @notice Retrieve the revoker address of a license.
  /// @dev Throws unless the license is active.   
  /// @param _licenseId The identifier for the queried license
  /// @return The revoker address of the queried license
  function getLicenseRevoker(uint256 _licenseId) external view returns (address);

  /// @notice Retrieve the root license identifier of an NFT.
  /// @dev Throws unless the queried NFT exists. If the NFT doesn't have a root
  ///  license tethered to it, return a special identifier not referring to any
  ///  license (such as 0).   
  /// @param _tokenId The identifier for the queried NFT
  /// @return The root license identifier of the queried NFT
  function getLicenseIdByTokenId(uint256 _tokenId) external view returns (uint256);
  
  /// @notice Create a new license.
  /// @dev Throws unless the NFT `_tokenId` exists. Throws unless the parent
  ///  license `_parentLicenseId` is active, or `_parentLicenseId` is a special
  ///  identifier not referring to any license (such as 0) and the NFT
  ///  `_tokenId` doesn't have a root license tethered to it. Throws unless the
  ///  message sender is eligible to create the license, i.e., either the
  ///  license to be created is a root license and `msg.sender` is the NFT owner,
  ///  or the license to be created is a sublicense and `msg.sender` is the holder
  ///  of the parent license. 
  /// @param _tokenId The identifier for the NFT the license is issued upon
  /// @param _parentLicenseId The identifier for the parent license
  /// @param _licenseHolder The address of the license holder
  /// @param _uri The URI of the license terms
  /// @param _revoker The revoker address
  /// @return The identifier of the created license
  function createLicense(uint256 _tokenId, uint256 _parentLicenseId, address _licenseHolder, string memory _uri, address _revoker) external returns (uint256);

  /// @notice Revoke a license.
  /// @dev Throws unless the license is active and the message sender is the
  ///  eligible revoker. This function should be used for revoking both root
  ///  licenses and sublicenses. Note that if a root license is revoked, the
  ///  NFT should be transferred back to its creator.
  /// @param _licenseId The identifier for the queried license
  function revokeLicense(uint256 _licenseId) external;
  
  /// @notice Transfer a sublicense.
  /// @dev Throws unless the sublicense is active and `msg.sender` is the license
  ///  holder. Note that the root license of an NFT should be tethered to and
  ///  transferred with the NFT. Whenever an NFT is transferred by calling the
  ///  ERC721 `transfer` function, the holder of the root license should be
  ///  changed to the new NFT owner.
  /// @param _licenseId The identifier for the queried license
  /// @param _licenseHolder The new license holder
  function transferSublicense(uint256 _licenseId, address _licenseHolder) external;
}
```

NFT 的许可证通常具有如下树状结构：

![许可证树](../assets/eip-5218/license-tree.png)

NFT 本身有一个根许可证，授予 NFT 所有者对链接作品的一些权利。NFT 所有者（即根许可证持有者）可以创建子许可证，持有者也可以递归地创建子许可证。

许可证创建、转移和撤销的完整日志*必须*通过事件日志可追溯。因此，所有许可证的创建和转移*必须*发出相应的日志事件。撤销可能会有所不同。此 EIP 的实现可以在函数调用时仅在许可证被撤销时发出 `Revoke` 事件，或者对每个被撤销的许可证都发出事件，这两者都足以追踪所有许可证的状态。如果撤销许可证会自动撤销其下的所有子许可证，则前者的 gas 成本较低，而后者在查询许可证状态时效率较高。实现者应根据其许可证条款进行权衡。

许可证的 `revoker` 可以是许可方、许可证持有者或在满足某些条件时调用 `revokeLicense` 函数的智能合约地址。实现者应谨慎处理授权，并可以通过不硬编码 `licensor` 或 `licenseHolder` 的地址，使 `revoker` 智能合约与转移向后兼容。

许可证的 `URI` 可以指向符合“ERC-5218 元数据 JSON 架构”的 JSON 文件，如下所示，该架构采用了创意共享许可证的“三层”设计：

```json
{
    "title": "License Metadata",
    "type": "object",
    "properties": {
        "legal-code": {
            "type": "string",
            "description": "The legal code of the license."
        },
        "human-readable": {
            "type": "string",
            "description": "The human readable license deed."
        },
        "machine-readable": {
            "type": "string",
            "description": "The machine readable code of the license that can be recognized by software, such as CC REL."
        }
    }
}
```

请注意，此 EIP 不包括更新许可证 URI 的功能，因此许可证条款应默认保持不变。建议将许可证元数据存储在去中心化存储服务上，例如 IPFS，或采用 IPFS 风格的 URI，该 URI 编码了元数据的哈希以进行完整性验证。另一方面，许可证的可更新性（如果在某些情况下需要）可以通过撤销原始许可证并创建新许可证来实现，或者添加更新功能，符合条件的调用者必须在许可证中仔细指定，并在智能合约中安全实现。

`supportsInterface` 方法在调用 `0xac7b5ca9` 时*必须*返回 `true`。

## 理由

此 EIP 旨在允许追踪所有与 NFT 相关的许可证，以促进权利管理。ERC-721 标准仅记录财产，而不记录与 NFT 绑定的法律权利。即使通过可选的 ERC-721 元数据扩展记录许可证，子许可证也无法追踪，这与 Web3 的透明度目标不符。一些实现试图通过铸造 NFT 来表示特定许可证来规避这一限制，例如 BAYC #6068 免版税使用许可证。这并不是一个理想的解决方案，因为不同许可证与 NFT 之间的链接是模糊的。审计员必须调查区块链中的所有 NFT，并检查尚未在子许可证关系方面标准化的元数据。为避免这些问题，此 EIP 将所有与 NFT 相关的许可证记录在树状数据结构中，这与 ERC-721 兼容，并允许高效的可追溯性。

此 EIP 试图默认将 NFT 与版权许可证绑定到创作作品，并不受版权所有权转让的高法律门槛的限制，该转让需要版权所有者的明确签名。为了转移和跟踪版权所有权，可能需要在仔细审查后集成 ERC-5218 和 [ERC-5289](./eip-5289.md)，并实现一个智能合约，该合约原子性地（1）通过 ERC-5289 签署法律合同，以及（2）通过 ERC-5218 转移 NFT 及其版权所有权。两者要么同时发生，要么同时回滚。

## 向后兼容性

该标准与当前的 ERC-721 标准兼容：一个合约可以同时继承 ERC-721 和 ERC-5218。

## 测试用例

测试用例可在 [这里](../assets/eip-5218/contracts/test/Contract.t.sol) 找到。

## 参考实现

参考实现维护以下数据结构：

```solidity
  struct License {
    bool active; // whether the license is active
    uint256 tokenId; // the identifier of the NFT the license is tethered to
    uint256 parentLicenseId; // the identifier of the parent license
    address licenseHolder; // the license holder
    string uri; // the license URI
    address revoker; // the license revoker
  }
  mapping(uint256 => License) private _licenses; // maps from a license identifier to a license object
  mapping(uint256 => uint256) private _licenseIds; // maps from an NFT to its root license identifier
```

每个 NFT 都有一个许可证树，从每个许可证开始，可以通过 `parentLicenseId` 沿路径追溯到根许可证。
在参考实现中，一旦许可证被撤销，所有子许可证也会被撤销。这是以*惰性*的方式实现的，以降低 gas 成本，即仅在 `revokeLicense` 函数调用中显式撤销的许可证上将 `active=false`。因此，`isLicenseActive` 仅在其所有祖先许可证未被撤销时返回 `true`。

对于非根许可证，创建、转让和撤销是直接的：

1. 只有活跃许可证的持有者可以创建子许可证。
2. 只有活跃许可证的持有者可以将其转让给不同的许可证持有者。
3. 只有活跃许可证的撤销者可以撤销它。

根许可证必须与 `ERC-721` 兼容：

1. 当 NFT 被铸造时，许可证授予 NFT 拥有者。
2. 当 NFT 被转让时，许可证持有者更改为 NFT 的新拥有者。
3. 当根许可证被撤销时，NFT 会返回给 NFT 创建者，NFT 创建者可以在之后将其转让给新拥有者，并附带新的许可证。

完整的实现可以在 [这里](../assets/eip-5218/contracts/src/RightsManagement.sol) 找到。

此外，[Token-Bound NFT License](../assets/eip-5218/ic3license/ic3license.pdf) 专门设计用于与此接口配合使用，并提供了 NFT 许可证语言的参考。

## 安全考虑

`IERC5218` 标准的实现者必须彻底考虑他们给予 `licenseHolder` 和 `revoker` 的权限。如果许可证将来要转让给不同的许可证持有者，`revoker` 智能合约不应硬编码 `licenseHolder` 地址，以避免不良场景。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。