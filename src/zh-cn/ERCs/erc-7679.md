---
eip: 7679
title: UserOperation 构建器
description: 构建 UserOperations，而不与特定账户逻辑耦合。
author: Derek Chiang (@derekchiang), Garvit Khatri (@plusminushalf), Fil Makarov (@filmakarov), Kristof Gazso (@kristofgazso), Derek Rein (@arein), Tomas Rocchi (@tomiir), bumblefudge (@bumblefudge)
discussions-to: https://ethereum-magicians.org/t/erc-7679-smart-account-interfaces/19547
status: Draft
type: Standards Track
category: ERC
created: 2024-04-05
requires: 4337
---

## 摘要

不同的 [ERC-4337](./eip-4337.md) 智能账户实现以不同方式编码其签名、nonce 和 calldata。这使得 DApp、钱包和智能账户工具在与智能账户集成时，必须集成特定账户的 SDK，从而引入了供应商锁定，影响了智能账户的采用。

我们提议一种标准方式，让智能账户实现将其特定账户编码逻辑放在链上。可以通过实现接受原始签名、nonce 或 calldata（以及上下文）作为输入的方法，并将其正确格式化输出，从而实现，以便智能账户在验证和执行 User Operation 时可以使用它们。

## 动机

目前，要为智能账户构建 [ERC-4337](./eip-4337.md) UserOperation（简称 UserOp）需要详细了解智能账户实现的工作原理，因为每个实现可以自由地以不同方式编码其 nonce、calldata 和签名。

举个简单的例子，一个账户可能使用名为 `executeFoo` 的执行函数，而另一个账户可能使用名为 `executeBar` 的执行函数。这将导致两个账户之间的 `calldata` 不同，即使它们执行的是相同的调用。

因此，想要为给定智能账户发送 UserOp 的人需要：

* 确定该账户使用的智能账户实现。
* 根据智能账户实现正确编码签名/nonce/calldata，或者使用一个知道如何做到这一点的特定账户 SDK。

在实践中，这意味着大多数 DApp、钱包和 AA 工具今天都与特定的智能账户实现绑定，导致了碎片化和供应商锁定。

## 规范

本文档中的关键字“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“应该不”、“推荐”、“可以”和“可选”应按 RFC 2119 中的描述进行解释。

### UserOp 构建器

为了符合此标准，智能账户实现必须提供一个实现 `IUserOperationBuilder` 接口的“UserOp 构建器”合约，如下所定义：

```solidity
struct Execution {
    address target;
    uint256 value;
    bytes callData;
}

interface IUserOperationBuilder {
    /**
     * @dev Returns the ERC-4337 EntryPoint that the account implementation
     * supports.
     */
    function entryPoint() external view returns (address);
    
    /**
     * @dev Returns the nonce to use for the UserOp, given the context.
     * @param smartAccount is the address of the UserOp sender.
     * @param context is the data required for the UserOp builder to
     * properly compute the requested field for the UserOp.
     */
    function getNonce(
        address smartAccount,
        bytes calldata context
    ) external view returns (uint256);
	
    /**
     * @dev Returns the calldata for the UserOp, given the context and
     * the executions.
     * @param smartAccount is the address of the UserOp sender.
     * @param executions are (destination, value, callData) tuples that
     * the UserOp wants to execute.  It's an array so the UserOp can
     * batch executions.
     * @param context is the data required for the UserOp builder to
     * properly compute the requested field for the UserOp. 
     */
    function getCallData(
        address smartAccount,
        Execution[] calldata executions,
        bytes calldata context
    ) external view returns (bytes memory);
    
    /**
     * @dev Returns a correctly encoded signature, given a UserOp that
     * has been correctly filled out except for the signature field.
     * @param smartAccount is the address of the UserOp sender.
     * @param userOperation is the UserOp.  Every field of the UserOp should
     * be valid except for the signature field.  The "PackedUserOperation"
     * struct is as defined in ERC-4337.
     * @param context is the data required for the UserOp builder to
     * properly compute the requested field for the UserOp.
     */
    function formatSignature(
        address smartAccount,
        PackedUserOperation calldata userOperation,
        bytes calldata context
    ) external view returns (bytes memory signature);
}
```

### 使用 UserOp 构建器

要使用 UserOp 构建器构建 UserOp，构建方应按以下步骤进行：

1. 从账户所有者处获取 `UserOpBuilder` 的地址和 `context`。`context` 对于构建方来说是一个不透明的字节数组。`UserOpBuilder` 实现可能需要 `context` 以正确确定 UserOp 字段。有关更多信息，请参见 [Rationale](#rationale)。
2. 执行 `getNonce` 和 `getCallData` 的多重调用（批量 `eth_call`），并传入 `context` 和执行内容。构建方现在将获得 nonce 和 calldata。
3. 用之前获得的数据填写 UserOp。Gas 值可以随机设置或设置得非常低。此 UserOp 将用于获取用于 gas 估算的虚拟签名。对 UserOp 的哈希进行签名。（有关虚拟签名的详细信息，请参见 [Rationale](#rationale)。有关虚拟签名安全性的详细信息，请参见 [Security Considerations](#security-considerations)）。
4. 调用（通过 `eth_call`）`formatSignature`，传入 UserOp 和 `context`，以获得具有正确格式的虚拟签名的 UserOp。此 UserOp 现在可以用于 gas 估算。
5. 在 UserOp 中，将现有的 gas 值更改为从适当的 gas 估算中获得的值。此 UserOp 必须有效，除了 `signature` 字段。对 UserOp 的哈希进行签名，并将签名放入 UserOp.signature 字段。
6. 调用（通过 `eth_call`）`formatSignature`，传入 UserOp 和 `context`，以获得一个完全有效的 UserOp。
    1. 请注意，UserOp 的字段比 `nonce`、`callData` 和 `signature` 多得多，但构建方如何获得其他字段超出了本文档的范围，因为这三个字段与智能账户实现的依赖性很大。

此时，构建方拥有一个完全有效的 UserOp，可以提交给打包者或随意处理。

### 当账户尚未部署时使用 UserOp 构建器

为了向构建方提供准确的数据，`UserOpBuilder` 在大多数情况下必须调用账户。如果账户尚未部署，这意味着构建方希望为该账户发送第一个 UserOp，则构建方可以按如下方式修改上述流程：

- 除了 `UserOpBuilder` 地址和 `context`，构建方还获取 `factory` 和 `factoryData`，如 ERC-4337 中定义。
- 当调用 UserOp 构建器上的某个视图函数时，构建方可以使用 `eth_call` 部署 `CounterfactualCall` 合约，该合约将部署账户并调用 `UserOpBuilder`（见下文）。
- 在填写 UserOp 时，构建方包括 `factory` 和 `factoryData`。

`CounterfactualCall` 合约应：
- 使用构建方提供的 `factory` 和 `factoryData` 部署账户。
- 如果部署未成功，则回滚。
- 如果账户成功部署，则调用 `UserOpBuilder` 并将 `UserOpBuilder` 返回的数据返回给构建方。

有关 `CounterfactualCall` 合约的更多详细信息，请参见参考实现部分。

## 理由

### 上下文

`context` 是一个字节数组，编码了 UserOp 构建器需要的任何数据，以正确确定 nonce、calldata 和签名。可以推测，`context` 是由账户所有者在钱包软件的帮助下构建的。

在这里，我们概述了 `context` 的一种可能用途：委托。假设账户所有者希望将交易委托给构建方执行。账户所有者可以在 `context` 中编码构建方公钥的签名。我们称账户所有者的这个签名为 `authorization`。

然后，当构建方填写 UserOp 时，它将用其自己的私钥生成的签名填充 `signature` 字段。当它在 UserOp 构建器上调用 `getSignature` 时，UserOp 构建器将从 `context` 中提取 `authorization`，并将其与构建方的签名连接起来。智能账户的实现可以假设会从签名中恢复构建方的公钥，并检查该公钥是否确实由 `authorization` 签名。如果检查成功，智能账户将执行 UserOp，从而允许构建方代表用户执行 UserOp。

### 虚拟签名

“虚拟签名”是指在发送给打包者以估算 gas 的 UserOp 中使用的签名（通过 `eth_estimateUserOperationGas`）。需要虚拟签名，因为在打包者估算 gas 时，尚不存在有效签名，因为有效签名本身依赖于 UserOp 的 gas 值，形成了循环依赖。为了打破循环依赖，使用了虚拟签名。
然而，虚拟签名不仅仅是任何智能账户可以使用的固定值。虚拟签名必须构造得使得 UserOp 使用的 gas 大致与真实签名相同。因此，虚拟签名会根据智能账户用于验证 UserOp 的特定验证逻辑而变化，使其依赖于智能账户的实现。

## 向后兼容性

该 ERC 旨在与所有 ERC-4337 智能账户在 EntryPoint 0.7 版本时向后兼容。

对于针对 EntryPoint 0.6 部署的智能账户，`IUserOperationBuilder` 接口需要进行修改，以便将 `PackedUserOperation` 结构替换为 EntryPoint 0.6 中相应的结构。

## 参考实现

### 反事实调用合约

反事实调用合约的灵感来源于 [ERC-6492](./eip-6492.md)，该合约设计了一种机制来对预部署的（反事实）合约执行 `isValidSignature`（见 [ERC-1271](./eip-1271.md)）。

```solidity
contract CounterfactualCall {
    
    error CounterfactualDeployFailed(bytes error);

    constructor(
        address smartAccount,
        address create2Factory, 
        bytes memory factoryData,
        address userOpBuilder, 
        bytes memory userOpBuilderCalldata
    ) { 
        if (address(smartAccount).code.length == 0) {
            (bool success, bytes memory ret) = create2Factory.call(factoryData);
            if (!success || address(smartAccount).code.length == 0) revert CounterfactualDeployFailed(ret);
        }

        assembly {
            let success := call(gas(), userOpBuilder, 0, add(userOpBuilderCalldata, 0x20), mload(userOpBuilderCalldata), 0, 0)
            let ptr := mload(0x40)
            returndatacopy(ptr, 0, returndatasize())
            if iszero(success) {
                revert(ptr, returndatasize())
            }
            return(ptr, returndatasize())
        }
    }
    
}
```

以下是使用 ethers 和 viem 库调用此合约的示例：

```javascript
// ethers
const nonce = await provider.call({
  data: ethers.utils.concat([
    counterfactualCallBytecode,
    (
      new ethers.utils.AbiCoder()).encode(['address','address', 'bytes', 'address','bytes'], 
      [smartAccount, userOpBuilder, getNonceCallData, factory, factoryData]
    )
  ])
})

// viem
const nonce = await client.call({
  data: encodeDeployData({
    abi: parseAbi(['constructor(address, address, bytes, address, bytes)']),
    args: [smartAccount, userOpBuilder, getNonceCalldata, factory, factoryData],
    bytecode: counterfactualCallBytecode,
  })
})
```

## 安全考虑

### 虚拟签名安全性

由于格式正确的虚拟签名将被公开披露，理论上它可能会被中间人拦截并使用。不过，这种风险和潜在危害非常低，因为在最终 UserOp 提交后，虚拟签名将实际上无法使用（因为两个 UserOps 使用相同的 nonce）。然而，为了减轻这个小问题，建议将用于签名以获取未格式化虚拟签名（上述第 3 步）的 UserOp 填充为非常低的 gas 值。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。