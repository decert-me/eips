---
eip: 6492
title: 预部署合约的签名验证
description: 一种在账户为尚未部署的智能合约时验证签名的方法
author: Ivo Georgiev (@Ivshti), Agustin Aguilar (@Agusx1211)
discussions-to: https://ethereum-magicians.org/t/eip-6492-signature-validation-for-pre-deploy-contracts/12903
status: Final
type: Standards Track
category: ERC
created: 2023-02-10
requires: 1271
---

## 摘要

合约可以通过 [ERC-1271](./eip-1271.md) 签署可验证的消息。

然而，如果合约尚未部署，则无法进行 [ERC-1271](./eip-1271.md) 验证，因为无法在该合约上调用 `isValidSignature` 函数。

我们提出了一种标准方法，供任何合约或链下参与者验证代表给定的反事实合约（尚未部署）的签名是否有效。该标准方法扩展了 [ERC-1271](./eip-1271.md)。

## 动机

随着账户抽象的日益普及，我们发现合约钱包的最佳用户体验通常是推迟合约部署，直到第一次用户交易，从而不让用户在使用其账户之前承担额外的部署步骤。然而，与此同时，许多去中心化应用（dApps）期望签名，不仅用于交互，还用于登录。

因此，合约钱包在其事实上的部署之前签署消息的能力受到限制，而这通常是在第一次交易时完成的。

此外，无法从反事实合约签署消息一直是 [ERC-1271](./eip-1271.md) 的一个限制。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

"验证" 和 "确认" 这两个词可以互换使用。

引用 [ERC-1271](./eip-1271.md)，
> `isValidSignature` 可以调用任意方法来验证给定的签名，这可能是上下文相关的（例如，基于时间或状态）、EOA 相关的（例如，签名者在智能钱包中的授权级别）、签名方案相关的（例如，ECDSA、多重签名、BLS）等。
>
> 该函数应由希望签署消息的合约实现（例如，智能合约钱包、DAO、多重签名钱包等）。希望支持合约签名的应用程序应在签名者为合约时调用此方法。

我们使用相同的 `isValidSignature` 函数，但我们添加了一种新的包装签名格式，签名合约可以在未部署之前使用，以支持验证。

如果检测到包装签名格式，签名验证器必须在尝试调用 `isValidSignature` 之前执行合约部署。

通过检查签名是否以 `magicBytes` 结尾来检测包装格式，该 `magicBytes` 必须定义为 `0x6492649264926492649264926492649264926492649264926492649264926492`。

建议使用此 ERC 与 CREATE2 合约，因为它们的部署地址始终是可预测的。

### 签名方

签名合约通常是合约钱包，但也可以是任何实现 [ERC-1271](./eip-1271.md) 且以反事实方式部署的合约。

- 如果合约已部署，生成正常的 [ERC-1271](./eip-1271.md) 签名
- 如果合约尚未部署，将签名包装如下：`concat(abi.encode((create2Factory, factoryCalldata, originalERC1271Signature), (address, bytes, bytes)), magicBytes)`
- 如果合约已部署但尚未准备好使用 [ERC-1271](./eip-1271.md) 进行验证，将签名包装如下：`concat(abi.encode((prepareTo, prepareData, originalERC1271Signature), (address, bytes, bytes)), magicBytes)`；`prepareTo` 和 `prepareData` 必须包含使合约准备好使用 [ERC-1271](./eip-1271.md) 进行验证所需的交易（例如，调用 `migrate` 或 `update`）

请注意，我们传递 `factoryCalldata` 而不是 `salt` 和 `bytecode`。我们这样做是为了使验证符合任何工厂接口。我们不需要基于 `create2Factory`/`salt`/`bytecode` 计算地址，因为 [ERC-1271](./eip-1271.md) 验证假定我们已经知道正在验证签名的账户地址。

### 验证方

完整的签名验证必须按以下顺序进行：

- 检查签名是否以魔法字节结尾，在这种情况下，先对一个多重调用合约执行 `eth_call`，该合约将首先使用 `factoryCalldata` 调用工厂，并在合约尚未部署时进行部署；然后，像往常一样调用 `contract.isValidSignature`，使用未包装的签名
- 检查地址上是否有合约代码。如果有，按常规通过调用 `isValidSignature` 执行 [ERC-1271](./eip-1271.md) 验证
- 如果 [ERC-1271](./eip-1271.md) 验证失败，并且由于钱包已经有代码而跳过了对 `factory` 的部署调用，则执行 `factoryCalldata` 交易并再次尝试 `isValidSignature`
- 如果地址上没有合约代码，则尝试 `ecrecover` 验证

## 理由

我们认为以允许传递部署数据的方式包装签名是实现此功能的唯一清晰方法，因为它完全与合约无关，但也易于验证。

包装格式以 `magicBytes` 结尾，且以 `0x92` 结尾，这使得它不可能与以 `r,s,v` 格式打包的有效 `ecrecover` 签名发生冲突，因为 `0x92` 不是 `v` 的有效值。为了避免与正常的 [ERC-1271](./eip-1271.md) 冲突，`magicBytes` 本身也相当长（`bytes32`）。

确保正确验证的顺序基于以下规则：

- 检查 `magicBytes` 必须在常规的 [ERC-1271](./eip-1271.md) 检查之前进行，以允许反事实签名在合约部署后仍然有效
- 检查 `magicBytes` 必须在 `ecrecover` 之前进行，以避免尝试通过 `ecrecover` 验证明显可识别的反事实合约签名
- 检查 `ecrecover` 必须在 [ERC-1271](./eip-1271.md) 验证之前进行，因为合约可能使用的签名格式也恰好是某个 EOA 的有效 `ecrecover` 签名，且该 EOA 的地址不同。一个这样的例子是由该 EOA 控制的钱包合约。

我们无法确定为什么在相应的钱包已经有代码的情况下，签名会以 "部署前缀" 编码。这可能是因为签名是在合约部署之前创建的，或者可能是因为合约已部署但尚未准备好验证签名。因此，我们需要尝试这两种选项。

## 向后兼容性

此 ERC 向后兼容于先前的签名验证工作，包括 [ERC-1271](./eip-1271.md)，并允许轻松验证所有签名类型，包括 EOA 签名和类型化数据（[EIP-712](./eip-712.md)）。

### 使用 [ERC-6492](./eip-6492.md) 进行常规合约签名

此 ERC 中描述的包装格式可用于所有合约签名，而不仅仅是普通的 [ERC-1271](./eip-1271.md)。这提供了几个优势：

- 允许快速识别签名类型：得益于魔法字节，您可以立即知道签名是否为合约签名，而无需检查区块链
- 允许地址恢复：您可以仅通过签名使用 `create2Factory` 和 `factoryCalldata` 获取地址，就像 `ecrecover` 一样
## 参考实现

下面您可以找到一个通用验证合约的实现，可以在链上和链下使用，旨在作为单例部署。它可以验证使用此 ERC 签名的签名，[ERC-1271](./eip-1271.md) 和传统的 `ecrecover`。通过扩展，我们还支持 [EIP-712](./eip-712.md)，因为我们验证最终摘要 (`_hash`)。

```solidity
// As per ERC-1271
interface IERC1271Wallet {
  function isValidSignature(bytes32 hash, bytes calldata signature) external view returns (bytes4 magicValue);
}

error ERC1271Revert(bytes error);
error ERC6492DeployFailed(bytes error);

contract UniversalSigValidator {
  bytes32 private constant ERC6492_DETECTION_SUFFIX = 0x6492649264926492649264926492649264926492649264926492649264926492;
  bytes4 private constant ERC1271_SUCCESS = 0x1626ba7e;

  function isValidSigImpl(
    address _signer,
    bytes32 _hash,
    bytes calldata _signature,
    bool allowSideEffects,
    bool tryPrepare
  ) public returns (bool) {
    uint contractCodeLen = address(_signer).code.length;
    bytes memory sigToValidate;
    // The order here is strictly defined in https://eips.ethereum.org/EIPS/eip-6492
    // - ERC-6492 suffix check and verification first, while being permissive in case the contract is already deployed; if the contract is deployed we will check the sig against the deployed version, this allows 6492 signatures to still be validated while taking into account potential key rotation
    // - ERC-1271 verification if there's contract code
    // - finally, ecrecover
    bool isCounterfactual = bytes32(_signature[_signature.length-32:_signature.length]) == ERC6492_DETECTION_SUFFIX;
    if (isCounterfactual) {
      address create2Factory;
      bytes memory factoryCalldata;
      (create2Factory, factoryCalldata, sigToValidate) = abi.decode(_signature[0:_signature.length-32], (address, bytes, bytes));

      if (contractCodeLen == 0 || tryPrepare) {
        (bool success, bytes memory err) = create2Factory.call(factoryCalldata);
        if (!success) revert ERC6492DeployFailed(err);
      }
    } else {
      sigToValidate = _signature;
    }

    // Try ERC-1271 verification
    if (isCounterfactual || contractCodeLen > 0) {
      try IERC1271Wallet(_signer).isValidSignature(_hash, sigToValidate) returns (bytes4 magicValue) {
        bool isValid = magicValue == ERC1271_SUCCESS;

        // retry, but this time assume the prefix is a prepare call
        if (!isValid && !tryPrepare && contractCodeLen > 0) {
          return isValidSigImpl(_signer, _hash, _signature, allowSideEffects, true);
        }

        if (contractCodeLen == 0 && isCounterfactual && !allowSideEffects) {
          // if the call had side effects we need to return the
          // result using a `revert` (to undo the state changes)
          assembly {
           mstore(0, isValid)
           revert(31, 1)
          }
        }

        return isValid;
      } catch (bytes memory err) {
        // retry, but this time assume the prefix is a prepare call
        if (!tryPrepare && contractCodeLen > 0) {
          return isValidSigImpl(_signer, _hash, _signature, allowSideEffects, true);
        }

        revert ERC1271Revert(err);
      }
    }

    // ecrecover verification
    require(_signature.length == 65, 'SignatureValidator#recoverSigner: invalid signature length');
    bytes32 r = bytes32(_signature[0:32]);
    bytes32 s = bytes32(_signature[32:64]);
    uint8 v = uint8(_signature[64]);
    if (v != 27 && v != 28) {
      revert('SignatureValidator: invalid signature v value');
    }
    return ecrecover(_hash, v, r, s) == _signer;
  }

  function isValidSigWithSideEffects(address _signer, bytes32 _hash, bytes calldata _signature)
    external returns (bool)
  {
    return this.isValidSigImpl(_signer, _hash, _signature, true, false);
  }

  function isValidSig(address _signer, bytes32 _hash, bytes calldata _signature)
    external returns (bool)
  {
    try this.isValidSigImpl(_signer, _hash, _signature, false, false) returns (bool isValid) { return isValid; }
    catch (bytes memory error) {
      // in order to avoid side effects from the contract getting deployed, the entire call will revert with a single byte result
      uint len = error.length;
      if (len == 1) return error[0] == 0x01;
      // all other errors are simply forwarded, but in custom formats so that nothing else can revert with a single byte in the call
      else assembly { revert(error, len) }
    }
  }
}

// this is a helper so we can perform validation in a single eth_call without pre-deploying a singleton
contract ValidateSigOffchain {
  constructor (address _signer, bytes32 _hash, bytes memory _signature) {
    UniversalSigValidator validator = new UniversalSigValidator();
    bool isValidSig = validator.isValidSigWithSideEffects(_signer, _hash, _signature);
    assembly {
      mstore(0, isValidSig)
      return(31, 1)
    }
  }
}
```

### 链上验证

对于链上验证，您可以使用两个单独的方法：

- `UniversalSigValidator.isValidSig(_signer, _hash, _signature)`：返回一个布尔值，指示签名是否有效；这是防重入安全的
- `UniversalSigValidator.isValidSigWithSideEffects(_signer, _hash, _signature)`：这与前者等效 - 它不是防重入安全的，但在某些情况下更节省 gas

如果底层调用失败，这两个方法可能会回退。

### 链下验证

`ValidateSigOffchain` 辅助工具允许您在一个 `eth_call` 中执行通用验证，而无需任何预先部署的合约。

以下是如何使用 `ethers` 库进行此操作的示例：

```javascript
const isValidSignature = '0x01' === await provider.call({
  data: ethers.utils.concat([
    validateSigOffchainBytecode,
    (new ethers.utils.AbiCoder()).encode(['address', 'bytes32', 'bytes'], [signer, hash, signature])
  ])
})
```

您还可以使用库来执行通用签名验证，例如 Ambire 的 `signature-validator`。

## 安全考虑

与 [ERC-1271](./eip-1271.md) 相同的考虑适用。

然而，部署合约需要 `CALL` 而不是 `STATICCALL`，这引入了重入问题。在参考实现中，通过使验证方法在有副作用时始终回退，并从回退数据中捕获其实际结果来缓解这一问题。对于重入不是问题的用例，我们提供了 `isValidSigWithSideEffects` 方法。

此外，这个 ERC 可能会更频繁地用于链下验证，因为在许多情况下，在链上验证签名假设钱包已经被部署。

一个超出范围的安全考虑值得一提的是，合约在部署时是否会设置正确的权限，以允许有意义的签名验证。根据设计，这取决于实现，但值得注意的是，由于 CREATE2 的工作原理，改变签名中的字节码或构造函数调用代码将无法提升权限，因为这会改变部署地址，从而导致验证失败。

必须注意，合约账户可以动态更改其身份验证方法。这个问题在这个 EIP 中通过设计得到了缓解 - 即使在验证反事实签名时，如果合约已经部署，我们仍然会调用它，检查当前活动版本的合约。

与签名一样，重放保护应在大多数用例中实现。该提案为此增加了一个额外的维度，因为只要 1) 签名在部署时是有效的 2) 钱包可以在不同网络上使用相同的工厂地址/字节码进行部署，就可能验证已被渲染无效（通过更改授权密钥）的签名。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。