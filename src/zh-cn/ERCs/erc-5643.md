---
eip: 5643
title: 订阅 NFT
description: 为 EIP-721 代币添加基于订阅的功能
author: cygaar (@cygaar)
discussions-to: https://ethereum-magicians.org/t/eip-5643-subscription-nfts/10802
status: Stagnant
type: Standards Track
category: ERC
created: 2022-09-10
requires: 721
---

## 摘要

该标准是 [EIP-721](./eip-721.md) 的扩展。它提出了一个额外的接口，使 NFT 可用作定期、可过期的订阅。该接口包括续订和取消订阅的功能。

## 动机

NFT 通常用作去中心化应用程序上的账户或社区、活动等的会员通行证。然而，目前很少看到具有有限过期日期的 NFT。这种区块链的“永久性”往往导致没有过期日期的会员资格，因此不需要定期付款。然而，对于许多现实世界的应用程序，付费订阅是保持账户或会员资格有效所必需的。

最常见的链上应用程序是以太坊名称服务（ENS），它利用了与下面提议的类似接口。每个域名可以在一定时间内续订，如果不再付款则会过期。一个通用接口将使未来的项目更容易开发基于订阅的 NFT。在当前的 Web2 世界中，用户很难在一个地方查看或管理他们的所有订阅。通过一个通用的订阅标准，单个应用程序将能够轻松确定用户拥有的订阅数量，查看它们的过期时间，并根据请求续订/取消它们。

此外，随着 NFT 交易中二次版权费的消失，创作者将需要新的模型来生成持续收入。对于作为会员或访问通行证的 NFT，转向基于订阅的模型是一种提供收入并迫使发行者持续提供价值的方法。

## 规范

本文档中的关键字“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“应该不”、“推荐”、“可以”和“可选”应按 RFC 2119 中的描述进行解释。

```solidity
interface IERC5643 {
    /// @notice Emitted when a subscription expiration changes
    /// @dev When a subscription is canceled, the expiration value should also be 0.
    event SubscriptionUpdate(uint256 indexed tokenId, uint64 expiration);

    /// @notice Renews the subscription to an NFT
    /// Throws if `tokenId` is not a valid NFT
    /// @param tokenId The NFT to renew the subscription for
    /// @param duration The number of seconds to extend a subscription for
    function renewSubscription(uint256 tokenId, uint64 duration) external payable;

    /// @notice Cancels the subscription of an NFT
    /// @dev Throws if `tokenId` is not a valid NFT
    /// @param tokenId The NFT to cancel the subscription for
    function cancelSubscription(uint256 tokenId) external payable;

    /// @notice Gets the expiration date of a subscription
    /// @dev Throws if `tokenId` is not a valid NFT
    /// @param tokenId The NFT to get the expiration date of
    /// @return The expiration date of the subscription
    function expiresAt(uint256 tokenId) external view returns(uint64);

    /// @notice Determines whether a subscription can be renewed
    /// @dev Throws if `tokenId` is not a valid NFT
    /// @param tokenId The NFT to get the expiration date of
    /// @return The renewability of a the subscription
    function isRenewable(uint256 tokenId) external view returns(bool);
}
```

`expiresAt(uint256 tokenId)` 函数可以实现为 `pure` 或 `view`。

`isRenewable(uint256 tokenId)` 函数可以实现为 `pure` 或 `view`。

`renewSubscription(uint256 tokenId, uint64 duration)` 函数可以实现为 `external` 或 `public`。

`cancelSubscription(uint256 tokenId)` 函数可以实现为 `external` 或 `public`。

每当订阅的到期日期发生更改时，必须发出 `SubscriptionUpdate` 事件。

当使用 `0x8c65f84d` 调用时，`supportsInterface` 方法必须返回 `true`。

## 理由

该标准旨在通过添加实现链上订阅所需的最小功能和事件，使链上订阅尽可能简单。需要注意的是，在此接口中，NFT 本身代表订阅的所有权，不涉及任何其他可替代或不可替代的代币。

### 订阅管理

订阅代表为接收或参与某事而进行的预付款协议。为了促进这些协议，用户必须能够续订或取消他们的订阅，因此需要 `renewSubscription` 和 `cancelSubscription` 函数。还需要知道订阅何时过期 - 用户需要此信息以了解何时续订，应用程序需要此信息以确定订阅 NFT 的有效性。`expiresAt` 函数提供了此功能。最后，订阅可能在过期后无法续订。`isRenewable` 函数为用户和应用程序提供了该信息。

### 简单集成

由于该标准完全符合 EIP-721，现有协议将能够开箱即用地促进订阅 NFT 的转移。只需添加少量功能，协议将能够完全管理订阅的到期，确定订阅是否已过期，并查看是否可以续订。

## 向后兼容性

该标准可以通过添加扩展功能集来完全兼容 EIP-721。

该标准中引入的新功能对现有 EIP-721 接口增加了最小的开销，这应该使开发人员的采用变得简单而快速。

## 测试用例

以下测试需要 Foundry。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import "../src/ERC5643.sol";

contract ERC5643Mock is ERC5643 {
    constructor(string memory name_, string memory symbol_) ERC5643(name_, symbol_) {}

    function mint(address to, uint256 tokenId) public {
        _mint(to, tokenId);
    }
}

contract ERC5643Test is Test {
    event SubscriptionUpdate(uint256 indexed tokenId, uint64 expiration);

    address user1;
    uint256 tokenId;
    ERC5643Mock erc5643;

    function setUp() public {
        tokenId = 1;
        user1 = address(0x1);

        erc5643 = new ERC5643Mock("erc5369", "ERC5643");
        erc5643.mint(user1, tokenId);
    }

    function testRenewalValid() public {
        vm.warp(1000);
        vm.prank(user1);
        vm.expectEmit(true, true, false, true);
        emit SubscriptionUpdate(tokenId, 3000);
        erc5643.renewSubscription(tokenId, 2000);
    }

    function testRenewalNotOwner() public {
        vm.expectRevert("Caller is not owner nor approved");
        erc5643.renewSubscription(tokenId, 2000);
    }

    function testCancelValid() public {
        vm.prank(user1);
        vm.expectEmit(true, true, false, true);
        emit SubscriptionUpdate(tokenId, 0);
        erc5643.cancelSubscription(tokenId);
    }

    function testCancelNotOwner() public {
        vm.expectRevert("Caller is not owner nor approved");
        erc5643.cancelSubscription(tokenId);
    }

    function testExpiresAt() public {
        vm.warp(1000);

        assertEq(erc5643.expiresAt(tokenId), 0);
        vm.startPrank(user1);
        erc5643.renewSubscription(tokenId, 2000);
        assertEq(erc5643.expiresAt(tokenId), 3000);

        erc5643.cancelSubscription(tokenId);
        assertEq(erc5643.expiresAt(tokenId), 0);
    }
}
```

## 参考实现

实现：`ERC5643.sol`

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IERC5643.sol";

contract ERC5643 is ERC721, IERC5643 {
    mapping(uint256 => uint64) private _expirations;

    constructor(string memory name_, string memory symbol_) ERC721(name_, symbol_) {}

    function renewSubscription(uint256 tokenId, uint64 duration) external payable {
        require(_isApprovedOrOwner(msg.sender, tokenId), "Caller is not owner nor approved");

        uint64 currentExpiration = _expirations[tokenId];
        uint64 newExpiration;
        if (currentExpiration == 0) {
            newExpiration = uint64(block.timestamp) + duration;
        } else {
            if (!_isRenewable(tokenId)) {
                revert SubscriptionNotRenewable();
            }
            newExpiration = currentExpiration + duration;
        }

        _expirations[tokenId] = newExpiration;

        emit SubscriptionUpdate(tokenId, newExpiration);
    }

    function cancelSubscription(uint256 tokenId) external payable {
        require(_isApprovedOrOwner(msg.sender, tokenId), "Caller is not owner nor approved");
        delete _expirations[tokenId];
        emit SubscriptionUpdate(tokenId, 0);
    }

    function expiresAt(uint256 tokenId) external view returns(uint64) {
        return _expirations[tokenId];
    }

    function isRenewable(uint256 tokenId) external pure returns(bool) {
        return true;
    }

    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC5643).interfaceId || super.supportsInterface(interfaceId);
    }
}
```

## 安全考虑

该 EIP 标准不影响 NFT 的所有权，因此可以认为是安全的。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。