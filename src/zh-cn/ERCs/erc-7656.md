---
eip: 7656
title: 通用代币关联服务
description: 定义一个与特定 NFT 关联的通用服务注册表
author: Francesco Sullo (@sullof)
discussions-to: https://ethereum-magicians.org/t/variation-to-erc6551-to-deploy-any-kind-of-contract-linked-to-an-nft/19223
status: 审核
type: 标准跟踪
category: ERC
created: 2024-03-15
requires: 165, 1167, 5313, 6551
---

## 摘要

本提案引入了 [ERC-6551](./eip-6551.md) 的一种变体，扩展到所有与非同质化代币（NFT）关联的服务类型，即扩展 NFT 的合约，由单个 NFT 拥有，因此由 NFT 的拥有者拥有。它通过使用通用的函数、错误和事件语言来实现这一目标，并避免与原始提案施加的严格限制相冲突。

## 动机

[ERC-6551](./eip-6551.md) 旨在将智能账户绑定到代币，允许其注册表部署由特定 tokenID 拥有的账户。我们试图通过这一新提案解决的问题是，[ERC-6551](./eip-6551.md) 明确要求通过 `ERC6551Registry` 部署的任何合约都必须实现 `IERC6551Account` 和 `IERC6551Execute`，即它必须是一个账户。这个要求通过接口中函数和事件的命名选择得到了强调。此外，[ERC-6551](./eip-6551.md) 指定 `ERC6551Registry` 智能合约在任何链上以单例形式部署在特定地址。由于服务的集中化，基于此构建的项目容易将通过该注册表部署的任何非账户合约视为垃圾或无效。

通过这一新的 ERC，我们提议一个更通用的注册表，使用通用的函数/事件名称，允许部署任何与 NFT 关联时有意义的合约，从而使合约完全由 NFT 的拥有者控制。与 [ERC-6551](./eip-6551.md) 相比，由于本提案的目标之一是灵活性，因此不期望 `ERC7656Registry` 合约以单例形式部署，允许任何项目根据其需求进行调整；因此，我们要求任何注册表明确支持 `IERC7656Registry` 接口。

扩展注册表的能力以管理实现任何类型服务的合约提供了几个优势：

- **灵活性**：开发者可以允许 NFT 与更广泛的关联合约进行交互，解锁新的用例和功能（借贷系统、资产分配、分级所有权、身份等）
- **兼容性**：通过确保账户类合约仍然可以被识别为账户，该提案保持了与 [ERC-6551](./eip-6551.md) 的向后兼容性。
- **创新**：该提案通过消除与 NFT 关联的合约类型的限制，鼓励 NFT 领域的进一步创新，为纯实用 NFT 开辟了新的可能性。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

接口 `IERC7656Registry` 定义如下：

```solidity
// interfaceId 0xc6bdc908
interface IERC7656Registry {
  /**
   * @notice The registry MUST emit the Created event upon successful contract creation.
   * @param contractAddress The address of the created contract
   * @param implementation The address of the implementation contract
   * @param salt The salt to use for the create2 operation
   * @param chainId The chain id of the chain where the contract is being created
   * @param tokenContract The address of the token contract
   * @param tokenId The id of the token
   */
  event Created(
    address contractAddress,
    address indexed implementation,
    bytes32 salt,
    uint256 chainId,
    address indexed tokenContract,
    uint256 indexed tokenId
  );

  /**
   * The registry MUST revert with CreationFailed error if the create2 operation fails.
   */
  error CreationFailed();

  /**
   * @notice Creates a token linked service for a non-fungible token.
   * If the service has already been created, returns the service address without calling create2.
   * @param implementation The address of the implementation contract
   * @param salt The salt to use for the create2 operation
   * @param chainId The chain id of the chain where the service is being created
   * @param tokenContract The address of the token contract
   * @param tokenId The id of the token
   * Emits Created event.
   * @return service The address of the token linked service
   */
  function create(
    address implementation,
    bytes32 salt,
    uint256 chainId,
    address tokenContract,
    uint256 tokenId
  ) external returns (address service);

  /**
   * @notice Returns the computed token linked service address for a non-fungible token.
   * @param implementation The address of the implementation contract
   * @param salt The salt to use for the create2 operation
   * @param chainId The chain id of the chain where the service is being created
   * @param tokenContract The address of the token contract
   * @param tokenId The id of the token
   * @return service The address of the token linked service
   */
  function compute(
    address implementation,
    bytes32 salt,
    uint256 chainId,
    address tokenContract,
    uint256 tokenId
  ) external view returns (address service);
}
```

任何 `ERC7656Registry` 实现必须支持 `IERC7656Registry` 的接口 ID，即 `0xc6bdc908`。

与 [ERC-6551](./eip-6551.md) 类似，注册表必须将每个与代币关联的服务部署为带有不可变常量数据附加到字节码的 [ERC-1167](./eip-1167.md) 最小代理。

每个代币绑定服务的部署字节码必须具有以下结构：
```
ERC-1167 Header               (10 bytes)
<implementation (address)>    (20 bytes)
ERC-1167 Footer               (15 bytes)
<salt (bytes32)>              (32 bytes)
<chainId (uint256)>           (32 bytes)
<tokenContract (address)>     (32 bytes)
<tokenId (uint256)>           (32 bytes)
```

任何使用 `ERC7656Registry` 创建的合约应实现 `IERC7656Service` 接口：

```solidity
// InterfaceId 0xfc0c546a
interface IERC7656Service {
  /**
  * @notice Returns the token linked to the contract
  * @return chainId The chainId of the token
  * @return tokenContract The address of the token contract
  * @return tokenId The tokenId of the token
  */
  function token() external view returns (uint256 chainId, address tokenContract, uint256 tokenId);
  
}
```

或 `IERC6551Account` 接口或两者兼具。这种灵活性使得 [ERC-6551](./eip-6551.md) 账户与本提案开箱即用兼容。

## 理由

[ERC-7656](./eip-7656.md) 的技术基础集中在可以与 NFT 关联的合约类型的扩展和通用化。选择这种方法是为了应对在 NFT 关联合约的设计和应用中识别出的特定限制和机会。本提案中的关键技术决策包括：

- 通用函数/事件名称：选择采用通用命名约定用于函数、错误和事件是经过深思熟虑的设计决策。此设计使得 [ERC-7656](./eip-7656.md) 注册表能够支持超出单纯账户的广泛合约类型。通过不规定特定角色或功能，我们允许在基于此标准构建的应用程序中实现更大的创新和灵活性。这也简化了接口，使其更适应各种用例。

- 注册表无单例要求：与其前身不同，[ERC-7656](./eip-7656.md) 不要求注册表以单例形式部署。这个决定受到不同项目可能具有独特需求和约束的认识的影响。通过允许注册表的多个实例，项目可以定制和优化注册表的部署，以适应其特定需求，从而增强生态系统的整体多样性和韧性。

- 明确支持 `IERC7656Registry` 接口：要求任何注册表明确支持 `IERC7656Registry` 接口是一个技术决策，旨在确保互操作性和识别性。这个要求促进了对合规注册表的识别和交互，推动了一个更标准化和统一的生态系统。

- 合约关联的灵活性：该提案旨在容纳不仅仅是账户的任何合约，这些合约可以与 NFT 有意义地关联。这个决定源于对 NFT 用例不断演变的技术评估，认识到需要一个能够支持更广泛功能的标准，从复杂的金融工具到身份验证系统。

- 向后兼容性：该提案包括向后兼容性的条款，特别是与 [ERC-6551](./eip-6551.md) 的账户类合约。这一技术选择确保了基于早期标准构建的项目可以过渡到或利用新标准，而无需丢弃现有基础设施或投资。

这些技术决策共同旨在扩大 NFT 关联合约的范围和适用性，赋予开发者更多创新工具，并支持一个不断增长的去中心化应用生态系统。通过解决当前的限制和未来的机会，[ERC-7656](./eip-7656.md) 力求为下一代 NFT 技术奠定一个灵活而稳健的基础。

## 参考实现

`ERC7656Registry` 的参考实现是从 `ERC6551Registry` 变异而来，进行了小幅修改以重命名过于具体的函数，发出不同的事件和错误，以及一个仅对 `IERC7656Registry` interfaceId 返回 true 的 `supportsInterface` 函数。

```solidity
contract ERC7656Registry is IERC7656Registry {
  function create(
    address implementation,
    bytes32 salt,
    uint256 /* chainId */,
    address tokenContract,
    uint256 tokenId
  ) external override returns (address) {
    // solhint-disable-next-line no-inline-assembly
    assembly {
    // Memory Layout:
    // ----
    // 0x00   0xff                           (1 byte)
    // 0x01   registry (address)             (20 bytes)
    // 0x15   salt (bytes32)                 (32 bytes)
    // 0x35   Bytecode Hash (bytes32)        (32 bytes)
    // ----
    // 0x55   ERC-1167 Constructor + Header  (20 bytes)
    // 0x69   implementation (address)       (20 bytes)
    // 0x5D   ERC-1167 Footer                (15 bytes)
    // 0x8C   salt (uint256)                 (32 bytes)
    // 0xAC   chainId (uint256)              (32 bytes)
    // 0xCC   tokenContract (address)        (32 bytes)
    // 0xEC   tokenId (uint256)              (32 bytes)

    // Copy bytecode + constant data to memory
      calldatacopy(0x8c, 0x24, 0x80) // salt, chainId, tokenContract, tokenId
      mstore(0x6c, 0x5af43d82803e903d91602b57fd5bf3) // ERC-1167 footer
      mstore(0x5d, implementation) // implementation
      mstore(0x49, 0x3d60ad80600a3d3981f3363d3d373d3d3d363d73) // ERC-1167 constructor + header

    // Copy create2 computation data to memory
      mstore8(0x00, 0xff) // 0xFF
      mstore(0x35, keccak256(0x55, 0xb7)) // keccak256(bytecode)
      mstore(0x01, shl(96, address())) // registry address
      mstore(0x15, salt) // salt

    // Compute service address
      let computed := keccak256(0x00, 0x55)

    // If the service has not yet been deployed
      if iszero(extcodesize(computed)) {
      // Deploy service contract
        let deployed := create2(0, 0x55, 0xb7, salt)

      // Revert if the deployment fails
        if iszero(deployed) {
          mstore(0x00, 0xd786d393) // `CreationFailed()`
          revert(0x1c, 0x04)
        }

      // Store service address in memory before salt and chainId
        mstore(0x6c, deployed)

      // Emit the Created event
        log4(
          0x6c,
          0x60,
          0xc6989e4f290074742210cbd6491de7ded9cfe2cd247932a53d31005007a6341a,
          implementation,
          tokenContract,
          tokenId
        )

      // Return the service address
        return(0x6c, 0x20)
      }

    // Otherwise, return the computed service address
      mstore(0x00, shr(96, shl(96, computed)))
      return(0x00, 0x20)
    }
  }

  function compute(
    address implementation,
    bytes32 salt,
    uint256 /* chainId */,
    address /* tokenContract */,
    uint256 /* tokenId */
  ) external view override returns (address) {
    // solhint-disable-next-line no-inline-assembly
    assembly {
    // Copy bytecode + constant data to memory
      calldatacopy(0x8c, 0x24, 0x80) // salt, chainId, tokenContract, tokenId
      mstore(0x6c, 0x5af43d82803e903d91602b57fd5bf3) // ERC-1167 footer
      mstore(0x5d, implementation) // implementation
      mstore(0x49, 0x3d60ad80600a3d3981f3363d3d373d3d3d363d73) // ERC-1167 constructor + header

    // Copy create2 computation data to memory
      mstore8(0x00, 0xff) // 0xFF
      mstore(0x35, keccak256(0x55, 0xb7)) // keccak256(bytecode)
      mstore(0x01, shl(96, address())) // registry address
      mstore(0x15, salt) // salt

    // Store computed service address in memory
      mstore(0x00, shr(96, shl(96, keccak256(0x00, 0x55))))

    // Return computed service address
      return(0x00, 0x20)
    }
  }

  /// @dev Returns true if interfaceId is IERC7656Registry's interfaceId
  /// This contract does not explicitly extend IERC165 to keep the bytecode as small as possible
  function supportsInterface(bytes4 interfaceId) external pure returns (bool) {
    return interfaceId == 0xc6bdc908;
  }
}
```

`IERC7656Service` 的实现示例：

```solidity
contract LinkedService is IERC7656Service, EIP5313 {

  function token() public view virtual returns (uint256, address, uint256) {
    bytes memory footer = new bytes(0x60);
     assembly {
      extcodecopy(address(), add(footer, 0x20), 0x4d, 0x60)
    }
    return abi.decode(footer, (uint256, address, uint256));
  }

  function owner() public view virtual override returns (address) {
    (uint256 chainId, address tokenContract_, uint256 tokenId_) = token();
    if (chainId != block.chainid) return address(0);
    return IERC721(tokenContract_).ownerOf(tokenId_);
  }
}
```

## 安全考虑

本提案并未引入任何超出 [ERC-6551](./eip-6551.md) 已解决的安全考虑。
## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。