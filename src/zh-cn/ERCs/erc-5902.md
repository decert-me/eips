---
eip: 5902
title: 智能合约事件钩子
description: 允许合约半自动响应其他合约发出的事件的格式
author: Simon Brown (@orbmis)
discussions-to: https://ethereum-magicians.org/t/idea-smart-contract-event-hooks-standard/11503
status: Stagnant
type: Standards Track
category: ERC
created: 2022-11-09
requires: 712
---

## 摘要

本 EIP 提出了一个标准，用于创建“钩子”，允许智能合约函数在响应由其他合约触发的事件时自动调用，使用公共中继网络作为消息总线。

虽然已经存在许多类似的解决方案，但本提案描述了一种简单而强大的原语，可以被许多应用以开放、无许可和去中心化的方式使用。

它依赖于两个接口，一个用于发布者合约，一个用于订阅者合约。发布者合约发出事件，这些事件被“中继者”捕获，中继者是独立实体，订阅发布者合约上的“钩子”事件，并在发布者合约触发钩子事件时调用相应订阅者合约上的函数。每当中继者使用发布者合约发出的钩子事件的详细信息调用相应的订阅者合约时，订阅者会支付中继者一笔费用。发布者和订阅者合约都在一个中央注册智能合约中注册，中继者可以使用该合约来发现钩子。

## 动机

存在许多用例需要某个链下方监控链并通过广播交易响应链上事件。这些情况通常需要某个链下过程与以太坊节点一起运行，以便订阅智能合约发出的事件，然后执行某些逻辑并随后向网络广播交易。这需要一个以太坊节点和一个开放的 websocket 连接到某个可能只偶尔使用的长时间运行的过程，导致资源的次优使用。

本提案将允许智能合约包含响应事件所需的逻辑，而无需将该逻辑存储在某个链下过程。智能合约可以订阅其他智能合约触发的事件，并且仅在需要时执行所需的逻辑。这种方法适合任何不需要链下计算的合约逻辑，但通常需要链下过程来监控链状态。通过这种方法，订阅者不需要自己的专用链下过程来监控和响应合约事件。相反，一个单一的激励中继者可以代表多个不同的订阅者合约订阅许多不同的事件。

受益于此方案的用例示例包括：

### 抵押贷款协议

抵押贷款协议或稳定币可以在接收到价格预言机更新时发出事件，这将允许借款人自动“补充”他们的开放头寸以避免清算。

例如，Maker 使用“中位数器”智能合约，该合约维护一个允许发布价格更新的价格馈送合约的白名单。每当接收到新的价格更新时，所有馈送价格的中位数会重新计算，并更新中位数值。在这种情况下，中位数器智能合约可以触发一个钩子事件，允许订阅者合约决定重新抵押他们的 CDP。

### 自动化做市商

AMM 流动性池可以在添加或移除流动性时触发钩子事件。这可以允许订阅者智能合约在总池流动性达到某个点时添加或移除流动性。

AMM 可以在交易对内发生交易时触发钩子，发出时间加权价格预言机更新的钩子事件。订阅者可以利用此功能创建一个自动化限价单簿类型的合约，以便在资产现货价格突破预设阈值时买入/卖出代币。

### DAO 投票

钩子事件可以由 DAO 治理合约发出，以表明提案已发布、投票、通过或否决，并允许任何订阅者合约自动做出相应响应。例如，在特定提案通过时执行某个智能合约函数，例如批准支付资金。

### 定时函数调用

可以创建一个调度服务，订阅者可以注册定时函数调用，这可以使用 Unix cron 格式完成，服务可以在不同线程中从智能合约触发事件。订阅者合约可以订阅相应的线程，以便订阅某些时间表（例如，每日、每周、每小时等），甚至可以注册自定义的 cron 时间表。

### 定期支付

服务提供商可以触发钩子事件，允许订阅者合约定期自动支付服务费用。一旦订阅者合约接收到钩子事件，它们可以调用服务提供商合约上的函数以转移到期的资金。

### 通过委托进行协调

钩子事件负载可以包含任何任意数据，这意味着您可以使用诸如 Delegatable 框架之类的东西来签署链下委托，这可以促进一系列授权实体发布有效的钩子事件。您还可以使用诸如 BLS 门限签名之类的东西，以促进多个链下发布者授权触发钩子。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

### 注册发布者

发布者和订阅者合约 **MUST** 在特定注册合约中注册，类似于智能合约在 [ERC-1820](./eip-1820.md) 合约中注册接口的方式。注册合约 **MUST** 使用确定性部署机制，即使用工厂合约和特定盐值。

要注册发布者合约的钩子，必须在注册合约上调用 `registerHook` 函数。需要提供的参数为：

 - (address) 发布者合约地址
 - (uint256) 钩子事件将引用的线程 ID（单个合约可以使用任意数量的线程触发钩子事件，订阅者可以选择订阅哪些线程）
 - (bytes) 与钩子事件相关的公钥（可选）

当在注册合约上调用 `registerHook` 函数时，注册合约 **MUST** 对发布者合约地址进行下游调用，通过调用发布者合约的 `verifyEventHookRegistration` 函数，使用与传递给注册合约的 `registerHook` 函数相同的参数。发布者合约中的 `verifyEventHookRegistration` 函数 **MUST** 返回 true，以指示该合约将允许自己作为发布者添加到注册表中。注册合约 **MUST** 发出 `HookRegistered` 事件，以指示新的发布者合约已被添加。

### 更新钩子

发布者可能希望更新与钩子事件相关的详细信息，或者确实完全移除对钩子事件的支持。注册合约 **MUST** 实现 `updatePublisher` 函数，以允许在注册表中更新现有发布者合约。注册合约 **MUST** 发出 `PublisherUpdated` 事件，以指示发布者合约已被更新。
### 移除 Hook

要移除之前注册的 Hook，必须在 Registry 合约上调用 `removeHook` 函数，使用与 `updateHook` 函数相同的参数。注册合约 **必须** 触发一个 `HookRemoved` 事件，参数与传递给 `removeHook` 函数的相同，并包含 `msg.sender` 值。

### 注册订阅者

要注册一个 Hook 的订阅者，必须在注册合约上调用 `registerSubscriber` 函数，使用以下参数：

 - (address) 发布者合约地址
 - (bytes32) 订阅者合约地址
 - (uint256) 要订阅的线程 ID
 - (uint256) 订阅者愿意支付的更新费用
 - (uint256) 订阅者允许的最大 gas 费用，以防止恶意攻击，或 0 表示没有上限
 - (uint256) 订阅者愿意在费用基础上偿还中继者的最大 gas 价格，或 0 表示没有返还
 - (uint256) 订阅者希望获取更新的链 ID
 - (address) 费用支付的代币地址，或 0x0 表示链的原生资产（例如 ETH、MATIC 等）

订阅者合约 **可以** 在固定费用的基础上实现 gas 退款。如果订阅者选择这样做，则 **应** 指定 `最大 gas` 和 `最大 gas 价格` 参数，以保护自己免受恶意攻击。这是为了防止恶意或粗心的中继者设置过高的 gas 价格，从而耗尽订阅者合约的资金。否则，订阅者合约可以选择设置一个估计足够高以覆盖 gas 费用的费用。

请注意，虽然链 ID 和代币地址在原始规范版本中未包含，但简单地添加这两个参数可以利用中继者进行跨链消息传递，前提是订阅者希望这样做，并且还允许以各种代币支付中继者费用。

### 更新订阅

要更新订阅，必须使用与 `registerSubscriber` 函数相同的一组参数调用 `updateSubscriber` 函数。这可能是为了取消订阅或更改订阅费用。请注意，`updateSubscriber` 函数 **必须** 保持与 `registerSubscriber` 函数相同的 `msg.sender`。

### 移除订阅

要移除之前注册的订阅，必须在 Registry 合约上调用 `removeSubscriber` 函数，使用与 `updateSubscriber` 函数相同的参数，但不包括 `fee` 参数（即发布者和订阅者合约地址及线程 ID）。费用随后将设置为 0，以表示订阅者不再希望获取此订阅的更新。注册合约 **必须** 触发一个 `SubscriptionRemoved` 事件，主题为发布者合约地址、订阅者合约地址和线程 ID。

### 发布事件

发布者合约 **应** 从至少一个函数中触发一个 Hook 事件。发出的事件 **必须** 被称为 `Hook`，并 **必须** 包含以下参数：

 - uint256 (indexed) - threadId
 - uint256 (indexed) - nonce
 - bytes32 digest
 - bytes payload
 - bytes32 checksum

每当发布者合约触发 Hook 事件时，`nonce` 值 **必须** 增加。每个 Hook 事件 **必须** 有一个唯一的 `nonce` 值。`nonce` 属性初始化为 1，但第一次触发的 Hook 事件 **必须** 设置为 2。这是为了防止未初始化的 nonce 变量与显式初始化为零的 nonce 变量之间的歧义。

事件的 `digest` 参数 **必须** 是负载的 keccak256 哈希，`checksum` **必须** 是将 digest 与当前区块高度连接后的 keccak256 哈希，例如：

`bytes32 checksum = keccak256(abi.encodePacked(digest, block.number));`

`Hook` 事件可以通过来自任何 EOA 或外部合约的函数调用触发。这允许在发布者合约中动态创建负载。订阅者合约 **应** 在发布者合约上调用 `verifyEventHook` 函数，以验证接收到的 Hook 负载是否有效。

负载 **可以** 传递给触发 Hook 事件的函数，而不是在发布者合约内部生成，但如果提供了签名，则 **必须** 对负载的哈希进行签名，并强烈建议使用 [EIP-712](./eip-712.md) 标准，并遵循本提案末尾概述的数据结构。此签名 **应** 由订阅者验证，以确保他们获得真实的事件。签名 **必须** 对应于与事件注册的公钥。通过这种方法，签名 **应** 放置在负载的开头（例如，对于具有 r、s、v 属性的 ECDSA 签名，字节 0 到 65）。这种验证方法可用于跨链 Hook 事件，订阅者将无法在另一个链上调用发布者合约的 `verifyHookEvent`。

负载 **必须** 作为字节数组传递给订阅者的 calldata。订阅者智能合约 **应** 将字节数组转换为所需的数据类型。例如，如果负载是 snark 证明，发布者需要将变量序列化为字节数组，而订阅者智能合约需要在另一端对其进行反序列化，例如：

```
struct SnarkProof {
    uint256[2] a;
    uint256[2][2] b;
    uint256[2] c;
    uint256[1] input;
}

SnarkProof memory zkproof = abi.decode(payload, SnarkProof);
```

### 中继者

中继者是独立的参与者，监听发布者智能合约上的 `Hook` 事件。中继者从注册表中检索不同 Hook 的订阅者列表，并监听发布者合约上触发的 Hook 事件。一旦发布者智能合约触发了 Hook 事件，中继者可以决定通过广播一个执行订阅者合约的 `verifyHook` 函数的交易，将 Hook 事件的负载中继到订阅者合约。中继者有动力这样做，因为预计订阅者合约将以 ETH 或其他资产对他们进行报酬。

中继者 **应** 在广播交易之前先在本地模拟交易，以确保订阅者合约有足够的余额支付费用。这要求订阅者合约保持 ETH（或某种资产）的余额，以便支付中继者费用。订阅者合约 **可以** 根据某些逻辑决定撤销交易，这样订阅者合约就可以根据负载中的数据有条件地响应事件。在这种情况下，中继者将本地模拟交易，并决定不将 Hook 事件中继到订阅者合约。

### 验证 Hook 事件

订阅者合约的 `verifyHook` 函数 **应** 包含逻辑，以确保它们正在检索真实的事件。如果 Hook 事件包含签名，则订阅者合约 **应** 创建所需参数的哈希，并 **应** 验证 Hook 事件中的签名是否有效，针对派生的哈希和发布者的公钥（请参见参考实现以获取示例）。Hook 函数 **应** 还验证 Hook 事件的 nonce，并在内部记录，以防止重放攻击。
对于没有签名的 Hook 事件，订阅者合约 **SHOULD** 调用发布者合约上的 `verifyHookEvent` 以验证 Hook 事件的有效性。发布者智能合约 **MUST** 实现 `verifyHookEvent`，该函数接受有效负载的哈希、线程 ID、随机数和与 Hook 事件相关的区块高度，并返回一个布尔值以指示 Hook 事件的真实性。

### 接口

IRegistry.sol

```js
/// @title IRegistry
/// @dev Implements the registry contract
interface IRegistry {
    /// @dev Registers a new hook event by a publisher
    /// @param publisherContract The address of the publisher contract
    /// @param threadId The id of the thread these hook events will be fired on
    /// @param signingKey The public key that corresponds to the signature of externally generated payloads (optional)
    /// @return Returns true if the hook is successfully registered
    function registerHook(
        address publisherContract,
        uint256 threadId,
        bytes calldata signingKey
    ) external returns (bool);

    /// @dev Verifies a hook with the publisher smart contract before adding it to the registry
    /// @param publisherAddress The address of the publisher contract
    /// @param threadId The id of the thread these hook events will be fired on
    /// @param signingKey The public key used to verify the hook signatures
    /// @return Returns true if the hook is successfully verified
    function verifyHook(
        address publisherAddress,
        uint256 threadId,
        bytes calldata signingKey
    ) external returns (bool);

    /// @dev Update a previously registered hook event
    /// @dev Can be used to transfer hook authorization to a new address
    /// @dev To remove a hook, transfer it to the burn address
    /// @param publisherContract The address of the publisher contract
    /// @param threadId The id of the thread these hook events will be fired on
    /// @param signingKey The public key used to verify the hook signatures
    /// @return Returns true if the hook is successfully updated
    function updateHook(
        address publisherContract,
        uint256 threadId,
        bytes calldata signingKey
    ) external returns (bool);

    /// @dev Remove a previously registered hook event
    /// @param publisherContract The address of the publisher contract
    /// @param threadId The id of the thread these hook events will be fired on
    /// @param signingKey The public key used to verify the hook signatures
    /// @return Returns true if the hook is successfully updated
    function removeHook(
        address publisherContract,
        uint256 threadId,
        bytes calldata signingKey
    ) external returns (bool);

    /// @dev Registers a subscriber to a hook event
    /// @param publisherContract The address of the publisher contract
    /// @param subscriberContract The address of the contract subscribing to the event hooks
    /// @param threadId The id of the thread these hook events will be fired on
    /// @param fee The fee that the subscriber contract will pay the relayer
    /// @param maxGas The maximum gas that the subscriber allow to spend, to prevent griefing attacks
    /// @param maxGasPrice The maximum gas price that the subscriber is willing to rebate
    /// @param chainId The chain id that the subscriber wants updates on
    /// @param feeToken The address of the token that the fee will be paid in or 0x0 for the chain's native asset (e.g. ETH)
    /// @return Returns true if the subscriber is successfully registered
    function registerSubscriber(
        address publisherContract,
        address subscriberContract,
        uint256 threadId,
        uint256 fee,
        uint256 maxGas,
        uint256 maxGasPrice,
        uint256 chainId,
        address feeToken
    ) external returns (bool);

    /// @dev Registers a subscriber to a hook event
    /// @param publisherContract The address of the publisher contract
    /// @param subscriberContract The address of the contract subscribing to the event hooks
    /// @param threadId The id of the thread these hook events will be fired on
    /// @param fee The fee that the subscriber contract will pay the relayer
    /// @return Returns true if the subscriber is successfully updated
    function updateSubscriber(
        address publisherContract,
        address subscriberContract,
        uint256 threadId,
        uint256 fee
    ) external returns (bool);

    /// @dev Removes a subscription to a hook event
    /// @param publisherContract The address of the publisher contract
    /// @param subscriberContract The address of the contract subscribing to the event hooks
    /// @param threadId The id of the thread these hook events will be fired on
    /// @return Returns true if the subscriber is subscription removed
    function removeSubscription(
        address publisherContract,
        address subscriberContract,
        uint256 threadId
    ) external returns (bool);
}
```

IPublisher.sol

```js
/// @title IPublisher
/// @dev Implements a publisher contract
interface IPublisher {
    /// @dev Example of a function that fires a hook event when it is called
    /// @param payload The actual payload of the hook event
    /// @param digest Hash of the hook event payload that was signed
    /// @param threadId The thread number to fire the hook event on
    function fireHook(
        bytes calldata payload,
        bytes32 digest,
        uint256 threadId
    ) external;

    /// @dev Adds / updates a new hook event internally
    /// @param threadId The thread id of the hook
    /// @param signingKey The public key associated with the private key that signs the hook events
    function addHook(uint256 threadId, bytes calldata signingKey) external;

    /// @dev Called by the registry contract when registering a hook, used to verify the hook is valid before adding
    /// @param threadId The thread id of the hook
    /// @param signingKey The public key associated with the private key that signs the hook events
    /// @return Returns true if the hook is valid and is ok to add to the registry
    function verifyEventHookRegistration(
        uint256 threadId,
        bytes calldata signingKey
    ) external view returns (bool);

    /// @dev Returns true if the specified hook is valid
    /// @param payloadhash The hash of the hook's data payload
    /// @param threadId The thread id of the hook
    /// @param nonce The nonce of the current thread
    /// @param blockheight The blockheight that the hook was fired at
    /// @return Returns true if the specified hook is valid
    function verifyEventHook(
        bytes32 payloadhash,
        uint256 threadId,
        uint256 nonce,
        uint256 blockheight
    ) external view returns (bool);
}
```

ISubscriber.sol

```js
/// @title ISubscriber
/// @dev Implements a subscriber contract
interface ISubscriber {
    /// @dev Example of a function that is called when a hook is fired by a publisher
    /// @param publisher The address of the publisher contract in order to verify hook event with
    /// @param payload Hash of the hook event payload that was signed
    /// @param threadId The id of the thread this hook was fired on
    /// @param nonce Unique nonce of this hook
    /// @param blockheight The block height at which the hook event was fired
    function verifyHook(
        address publisher,
        bytes calldata payload,
        uint256 threadId,
        uint256 nonce,
        uint256 blockheight
    ) external;
}

```

## 理由

这种设计的理由在于，它允许智能合约开发者编写合约逻辑，以监听和响应其他智能合约中触发的事件，而无需运行某些专用的链下进程来实现这一点。这最适合任何相对不频繁响应其他合约事件的简单智能合约逻辑。

这改进了现有解决方案，以实现发布/订阅设计模式。具体来说，目前有许多服务提供商提供“网络钩子”作为订阅智能合约发出的事件的一种方式，通过在事件发出时调用某个 API 端点，或者提供某种无服务器功能，可以通过某个智能合约事件触发。这种方法非常有效，但确实需要某个 API 端点或无服务器功能始终可用，这可能需要某个专用服务器/进程，而这又需要拥有某个私钥，以及一定数量的 ETH 以重新广播交易，更不用说需要与某个第三方提供商维护账户的要求。

这种方法为不希望有“始终在线”服务器实例的情况提供了更合适的替代方案，例如在不频繁调用的情况下。

该提案结合了一个去中心化的市场驱动中继网络，这一决定基于这样一个事实：这是一个高度可扩展的方法。相反，可以在不诉诸市场驱动的方法的情况下实现此功能，只需定义一个标准，使合约允许其他合约直接订阅。该方法在概念上更简单，但有其缺点，因为它要求发布者合约在其自身状态中记录订阅者，从而增加了数据管理、可升级性等的开销。该方法还要求发布者在每个订阅者合约上调用 `verifyHook` 函数，这将给发布者合约带来潜在的显著 gas 成本。

## 安全考虑

### 恶意攻击

订阅者合约必须信任发布者合约不会触发对它们没有内在兴趣或价值的事件，因为恶意发布者合约可能会发布大量事件，从而耗尽订阅者合约中的 ETH。

### 前置攻击

建议不要仅依赖签名来验证 Hook 事件。发布者和订阅者应意识到，可能会有中继者在事件发布之前，通过检查发布者的交易在内存池中进行中继。正常流程是“触发”交易调用发布者智能合约中的一个函数，该函数触发一个事件，然后被中继者捕获。竞争中的中继者会观察到，可以从公共内存池中的触发交易中提取签名和有效负载，并在触发交易实际包含在区块之前将其简单地中继到订阅者合约。实际上，订阅者合约可能在触发交易被处理之前就处理了事件，这纯粹是基于 gas 费用动态。通过订阅者合约在接收到 Hook 事件时调用发布者合约上的 `verifyEventHook` 函数可以减轻这一风险。

来自前置攻击的另一个风险影响中继者，其中中继者向订阅者合约的交易可能会被内存池中的通用 MEV 搜索者前置。此类 MEV 捕获很可能发生在公共内存池中，因此建议中继者使用私有通道与区块构建者进行沟通，以减轻此问题。

### 中继者竞争

通过向隔离的内存池广播交易，中继者保护自己免受通用 MEV 机器人的前置攻击，但他们的交易仍可能因其他中继者的竞争而失败。如果两个或多个中继者决定开始将 Hook 事件从同一发布者中继到相同的订阅者，则 gas 价格最高的中继交易将优先执行。这将导致其他中继者的交易可能在链上失败，因为它们在同一块中稍后被包含。目前，有某些交易优化服务可以防止交易在链上失败，这将为此问题提供解决方案，尽管这超出了本文档的范围。

### 最优费用

支付给中继者的费用由订阅者自行决定，但设置费用到其最优水平可能并非易事，特别是在考虑到波动的 gas 费用和中继者之间的竞争时。这将导致订阅者将费用设置为一个被认为是“安全”的水平，他们相信这将激励中继者中继 Hook 事件。这不可避免地会导致价格发现不良，订阅者为更新支付过高的费用。

解决此问题的最佳方法是通过拍卖机制，允许中继者相互竞标以获得中继交易的权利，这将保证订阅者为其更新支付最优价格。描述满足这些要求的拍卖机制超出了本提案的范围，但存在一些通用拍卖机制的提案，可以在不引入不必要延迟的情况下促进这一点。其中一个例子是 Flashbots 的 SUAVE，未来可能会有其他类似的提案。

### 没有拍卖的情况下

为了在没有拍卖机制的情况下培养和维护一个可靠的中继者市场，订阅者合约需要实现逻辑，以便在不超过指定限制的情况下返还任何 gas 费用（同时仍允许在正常条件下执行 hook 更新）。

另一种方法是实现一个逻辑条件，检查调用 `verifyHook` 函数的交易的 gas 价格，以确保 gas 价格不会有效地将费用降低到零。这将要求订阅者智能合约对其 `verifyHook` 函数的近似 gas 使用量有一定了解，并检查条件 `minFee >= fee - (gasPrice * gasUsed)` 是否为真。这将减轻竞争性竞标将 _有效_ 中继者费用压至零的风险，确保有一个最低费用，低于该费用有效费用不允许下降。这意味着在交易回退之前可以支付的最高 gas 价格为 `fee - minFee + ε`，其中 `ε ~= 1 gwei`。这将需要仔细估算 `verifyHook` 函数的 gas 成本，并意识到随着合约状态的变化，使用的 gas 可能会发生变化。这种方法的关键见解是，中继者之间的竞争将导致订阅者支付的费用始终是最高的，这就是为什么使用拍卖机制更可取的原因。
### 中继交易批处理

另一个重要的考虑是 Hook 事件的批处理。中继者在逻辑上被激励批量处理 Hook 更新以节省 gas，因为 gas 节省的金额为 21,000 * n，其中 n 是单个中继者在一个区块中处理的 hooks 数量。如果中继者决定将多个 Hook 事件更新批量处理到不同的订阅合约中，通过一个多调用代理合约，那么如果批处理中的任何一个交易在链上失败，则整个批处理在链上的失败风险就会增加。例如，如果中继者 A 批量处理 x 个 Hook 更新，而中继者 B 批量处理 y 个 Hook 更新，则中继者 A 的批处理可能会在中继者 B 的批处理之前被包含在同一个区块中，如果两个批处理都包含至少一个重复项（即相同的 Hook 事件到相同的订阅者），那么这将导致中继者 B 的批处理交易在链上回滚。这是中继者需要考虑的重要因素，这表明中继者应该能够访问某种捆绑模拟服务，以在冲突交易发生之前识别它们。

### 重放攻击

在使用签名验证时，建议使用 [EIP-712](./eip-712.md) 标准，以防止跨网络重放攻击，其中在多个网络上部署的相同合约可以将其 hook 事件推送到其他网络的订阅者，例如，Polygon 上的发布者合约可以触发一个 hook 事件，该事件可以被转发到 Gnosis Chain 上的订阅者合约。虽然用于签署 hook 事件的密钥理想情况下应该是唯一的，但在现实中，这可能并不总是如此。

因此，建议使用 [ERC-721](./eip-712.md) 类型数据签名。在这种情况下，发起 hook 的过程应该根据以下数据结构创建签名：

```js
const domain = [
  { name: "name", type: "string"  },
  { name: "version", type: "string" },
  { name: "chainId", type: "uint256" },
  { name: "verifyingContract", type: "address" },
  { name: "salt", type: "bytes32" }
]
 
const hook = [
  { name: "payload", type: "string" },
  { type: "uint256", name: "nonce" },
  { type: "uint256", name: "blockheight" },
  { type: "uint256", name: "threadId" },
]
 
const domainData = {
  name: "Name of Publisher Dapp",
  version: "1",
  chainId: parseInt(web3.version.network, 10),
  verifyingContract: "0x123456789abcedf....publisher contract address",
  salt: "0x123456789abcedf....random hash unique to publisher contract"
}
 
const message = {
  payload: "bytes array serialized payload"
  nonce: 1,
  blockheight: 999999,
  threadId: 1,
}
 
const eip712TypedData = {
  types: {
    EIP712Domain: domain,
    Hook: hook
  },
  domain: domainData,
  primaryType: "Hook",
  message: message
}
```

注意：请参考参考实现中的单元测试，以获取发布者如何正确构造 hook 事件的示例。

重放攻击也可以发生在触发事件 hook 的同一网络上，只需简单地重新广播之前已广播的事件 hook。因此，订阅合约应该检查接收到的事件 hook 中是否包含 nonce，并在合约的状态中记录该 nonce。如果 hook nonce 无效或已被记录，则交易应回滚。

### 跨链消息

还可以利用 `chainId` 不仅用于防止重放攻击，还可以用于接受来自其他链的消息。在这种用例中，订阅合约应该在与订阅合约部署的同一链上注册，并应将 `chainId` 设置为它希望接收 hook 事件的链。

## 版权

通过 CC0 放弃版权及相关权利。