---
eip: 5827
title: 自动可续期的授权扩展
description: 扩展以启用授权批准的自动续期
author: zlace (@zlace0x), zhongfu (@zhongfu), edison0xyz (@edison0xyz)
discussions-to: https://ethereum-magicians.org/t/eip-5827-auto-renewable-allowance-extension/10392
status: Stagnant
type: Standards Track
category: ERC
created: 2022-10-22
requires: 20, 165
---

## 摘要

此扩展为 [ERC-20](./eip-20.md) 授权添加了可续期的授权机制，其中 `recoveryRate` 定义了每秒恢复到初始最大批准 `amount` 的代币数量。

## 动机

目前，ERC-20 代币支持授权，代币持有者可以允许支出者代表他们支出一定数量的代币。然而，在涉及定期支付的情况下（例如订阅、薪水、定期直接成本平均购买），这并不理想。

许多现有的 DApp 通过请求用户授予大量或无限的授权来规避这一限制。这带来了安全风险，因为恶意 DApp 可以耗尽用户的账户，直到授予的授权额度，而用户可能并不清楚授予授权的影响。

自动可续期的授权使许多传统金融概念得以实现，如信用和借记限额。账户持有者可以指定支出限额，并根据随时间恢复的授权限制对账户的收费金额。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

```solidity
pragma solidity ^0.8.0;

interface IERC5827 /* is ERC20, ERC165 */ {
    /*
     * Note: the ERC-165 identifier for this interface is 0x93cd7af6.
     * 0x93cd7af6 ===
     *   bytes4(keccak256('approveRenewable(address,uint256,uint256)')) ^
     *   bytes4(keccak256('renewableAllowance(address,address)')) ^
     *   bytes4(keccak256('approve(address,uint256)') ^
     *   bytes4(keccak256('transferFrom(address,address,uint256)') ^
     *   bytes4(keccak256('allowance(address,address)') ^
     */

    /**
     * @notice  Thrown when the available allowance is less than the transfer amount.
     * @param   available       allowance available; 0 if unset
     */
    error InsufficientRenewableAllowance(uint256 available);

    /**
     * @notice  Emitted when any allowance is set.
     * @dev     MUST be emitted even if a non-renewable allowance is set; if so, the
     * @dev     `_recoveryRate` MUST be 0.
     * @param   _owner          owner of token
     * @param   _spender        allowed spender of token
     * @param   _value          initial and maximum allowance granted to spender
     * @param   _recoveryRate   recovery amount per second
     */
    event RenewableApproval(
        address indexed _owner,
        address indexed _spender,
        uint256 _value,
        uint256 _recoveryRate
    );

    /**
     * @notice  Grants an allowance of `_value` to `_spender` initially, which recovers over time 
     * @notice  at a rate of `_recoveryRate` up to a limit of `_value`.
     * @dev     SHOULD cause `allowance(address _owner, address _spender)` to return `_value`, 
     * @dev     SHOULD throw when `_recoveryRate` is larger than `_value`, and MUST emit a 
     * @dev     `RenewableApproval` event.
     * @param   _spender        allowed spender of token
     * @param   _value          initial and maximum allowance granted to spender
     * @param   _recoveryRate   recovery amount per second
     */
    function approveRenewable(
        address _spender,
        uint256 _value,
        uint256 _recoveryRate
    ) external returns (bool success);

    /**
     * @notice  Returns approved max amount and recovery rate of allowance granted to `_spender` 
     * @notice  by `_owner`.
     * @dev     `amount` MUST also be the initial approval amount when a non-renewable allowance 
     * @dev     has been granted, e.g. with `approve(address _spender, uint256 _value)`.
     * @param    _owner         owner of token
     * @param   _spender        allowed spender of token
     * @return  amount initial and maximum allowance granted to spender
     * @return  recoveryRate recovery amount per second
     */
    function renewableAllowance(address _owner, address _spender)
        external
        view
        returns (uint256 amount, uint256 recoveryRate);

    /// Overridden ERC-20 functions

    /**
     * @notice  Grants a (non-increasing) allowance of _value to _spender and clears any existing 
     * @notice  renewable allowance.
     * @dev     MUST clear set `_recoveryRate` to 0 on the corresponding renewable allowance, if 
     * @dev     any.
     * @param   _spender        allowed spender of token
     * @param   _value          allowance granted to spender
     */
    function approve(address _spender, uint256 _value)
        external
        returns (bool success);

    /**
    * @notice   Moves `amount` tokens from `from` to `to` using the caller's allowance.
    * @dev      When deducting `amount` from the caller's allowance, the allowance amount used 
    * @dev      SHOULD include the amount recovered since the last transfer, but MUST NOT exceed 
    * @dev      the maximum allowed amount returned by `renewableAllowance(address _owner, address 
    * @dev      _spender)`. 
    * @dev      SHOULD also throw `InsufficientRenewableAllowance` when the allowance is 
    * @dev      insufficient.
    * @param    from            token owner address
    * @param    to              token recipient
    * @param    amount          amount of token to transfer
    */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);

    /**
     * @notice  Returns amount currently spendable by `_spender`.
     * @dev     The amount returned MUST be as of `block.timestamp`, if a renewable allowance 
     * @dev     for the `_owner` and `_spender` is present.
     * @param   _owner         owner of token
     * @param   _spender       allowed spender of token
     * @return  remaining allowance at the current point in time
     */
    function allowance(address _owner, address _spender)
        external
        view
        returns (uint256 remaining);
}
```

基本方法 `approve(address _spender, uint256 _value)` MUST 将 `recoveryRate` 设置为 0。

`allowance()` 和 `transferFrom()` MUST 更新以包含授权恢复逻辑。

`approveRenewable(address _spender, uint256 _value, uint256 _recoveryRate)` MUST 将初始授权金额和最大授权限额（授权可以恢复到的限额）设置为 `_value`。

`supportsInterface(0x93cd7af6)` MUST 返回 `true`。

### 其他接口

**代币代理**

现有的 ERC-20 代币可以将授权执行委托给实现此规范的代理合约。还存在一个额外的查询函数以获取底层的 ERC-20 代币。

```solidity
interface IERC5827Proxy /* is IERC5827 */ {

    /*
     * Note: the ERC-165 identifier for this interface is 0xc55dae63.
     * 0xc55dae63 ===
     *   bytes4(keccak256('baseToken()')
     */

    /**
     * @notice   Get the underlying base token being proxied.
     * @return   baseToken address of the base token
     */
    function baseToken() external view returns (address);
}
```

代理上的 `transfer()` 函数 MUST NOT 触发 `Transfer` 事件（因为底层代币已经这样做）。

**自动过期**

```solidity
interface IERC5827Expirable /* is IERC5827 */ {
    /*
     * Note: the ERC-165 identifier for this interface is 0x46c5b619.
     * 0x46c5b619 ===
     *   bytes4(keccak256('approveRenewable(address,uint256,uint256,uint64)')) ^
     *   bytes4(keccak256('renewableAllowance(address,address)')) ^
     */

    /**
     * @notice  Grants an allowance of `_value` to `_spender` initially, which recovers over time 
     * @notice  at a rate of `_recoveryRate` up to a limit of `_value` and expires at 
     * @notice  `_expiration`.
     * @dev     SHOULD throw when `_recoveryRate` is larger than `_value`, and MUST emit 
     * @dev     `RenewableApproval` event.
     * @param   _spender        allowed spender of token
     * @param   _value          initial allowance granted to spender
     * @param   _recoveryRate   recovery amount per second
     * @param   _expiration     Unix time (in seconds) at which the allowance expires
     */
    function approveRenewable(
        address _spender,
        uint256 _value,
        uint256 _recoveryRate,
        uint64 _expiration
    ) external returns (bool success);

    /**
     * @notice  Returns approved max amount, recovery rate, and expiration timestamp.
     * @return  amount initial and maximum allowance granted to spender
     * @return  recoveryRate recovery amount per second
     * @return  expiration Unix time (in seconds) at which the allowance expires
     */
    function renewableAllowance(address _owner, address _spender)
        external
        view
        returns (uint256 amount, uint256 recoveryRate, uint64 expiration);
}
```

## 理由

可续期的授权可以通过每个时间周期的离散重置来实现。然而，连续的 `recoveryRate` 允许更灵活的用例，不受重置周期的限制，并且可以用更简单的逻辑实现。

## 向后兼容性

现有的 ERC-20 代币合约可以将授权执行委托给实现此规范的代理合约。

## 参考实现

一个最小实现包含在 [这里](../assets/eip-5827/ERC5827.sol)

一个经过审计的、开源的此标准实现作为 `IERC5827Proxy` 可以在 `https://github.com/suberra/funnel-contracts` 找到。

## 安全考虑

此 EIP 引入了一组比 ERC-20 无限授权更严格的约束。然而，当 `_recoveryRate` 设置为较大值时，仍然可以通过多次交易转移大量资金。

不支持 [ERC-5827](./eip-5827.md) 的应用程序可能错误地推断 `allowance(address _owner, address _spender)` 返回的值或包含在 `Approval` 事件中的值是 `_spender` 可以从 `_owner` 支出的最大代币数量。这可能并非如此，例如当 `_owner` 授予 `_spender` 可续期授权时。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。