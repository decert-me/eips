---
eip: 7589
title: 半可替代代币角色
description: 半可替代代币（SFTs）的角色管理。允许账户通过可过期的角色分配共享 SFT 的实用性。
author: Ernani São Thiago (@ernanirst), Daniel Lima (@karacurt)
discussions-to: https://ethereum-magicians.org/t/eip-7589-semi-fungible-token-roles/17967
status: 草稿
type: 标准跟踪
category: ERC
created: 2023-12-28
requires: 165, 1155
---

## 摘要

本标准引入了 SFT（半可替代代币）的角色管理。每个角色分配授予单个用户（受赠人），并会自动过期。角色被定义为 `bytes32`，并具有一个自定义的 `_data` 字段，大小任意，以允许自定义。

## 动机

[ERC-1155](./eip-1155.md) 通过使开发者能够使用单个合约创建可替代和不可替代代币，显著增强了以太坊的代币化能力。虽然 [ERC-1155](./eip-1155.md) 在跟踪所有权方面表现出色，但它仅关注代币余额，忽视了这些代币如何被利用的细微方面。

代币实用性的一个重要方面是访问控制，它决定了谁有权限花费或使用这些代币。在某些情况下，所有者对其余额拥有完全控制权。然而，在许多其他情况下，实用性可以委托（或授予）给其他用户，从而允许实现更复杂的用例。

一个例子是在游戏中，游戏内资产可以通过单个 [ERC-1155](./eip-1155.md) 合约发行，并通过安全的角色管理接口进行租赁。

## 规范

本文档中的关键词 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按照 RFC 2119 和 RFC 8174 中的描述进行解释。

合规合约 MUST 实现以下接口：

```solidity
/// @title ERC-7589 Semi-Fungible Token Roles
/// @dev See https://eips.ethereum.org/EIPS/eip-7589
/// Note: the ERC-165 identifier for this interface is 0xc4c8a71d.
interface IERC7589 /* is IERC165 */ {

    /** Events **/

    /// @notice Emitted when tokens are committed (deposited or frozen).
    /// @param _grantor The owner of the SFTs.
    /// @param _commitmentId The identifier of the commitment created.
    /// @param _tokenAddress The token address.
    /// @param _tokenId The token identifier.
    /// @param _tokenAmount The token amount.
    event TokensCommitted(
        address indexed _grantor,
        uint256 indexed _commitmentId,
        address indexed _tokenAddress,
        uint256 _tokenId,
        uint256 _tokenAmount
    );

    /// @notice Emitted when a role is granted.
    /// @param _commitmentId The commitment identifier.
    /// @param _role The role identifier.
    /// @param _grantee The recipient the role.
    /// @param _expirationDate The expiration date of the role.
    /// @param _revocable Whether the role is revocable or not.
    /// @param _data Any additional data about the role.
    event RoleGranted(
        uint256 indexed _commitmentId,
        bytes32 indexed _role,
        address indexed _grantee,
        uint64 _expirationDate,
        bool _revocable,
        bytes _data
    );

    /// @notice Emitted when a role is revoked.
    /// @param _commitmentId The commitment identifier.
    /// @param _role The role identifier.
    /// @param _grantee The recipient of the role revocation.
    event RoleRevoked(uint256 indexed _commitmentId, bytes32 indexed _role, address indexed _grantee);

    /// @notice Emitted when a user releases tokens from a commitment.
    /// @param _commitmentId The commitment identifier.
    event TokensReleased(uint256 indexed _commitmentId);

    /// @notice Emitted when a user is approved to manage roles on behalf of another user.
    /// @param _tokenAddress The token address.
    /// @param _operator The user approved to grant and revoke roles.
    /// @param _isApproved The approval status.
    event RoleApprovalForAll(address indexed _tokenAddress, address indexed _operator, bool _isApproved);

    /** External Functions **/

    /// @notice Commits tokens (deposits on a contract or freezes balance).
    /// @param _grantor The owner of the SFTs.
    /// @param _tokenAddress The token address.
    /// @param _tokenId The token identifier.
    /// @param _tokenAmount The token amount.
    /// @return commitmentId_ The unique identifier of the commitment created.
    function commitTokens(
        address _grantor,
        address _tokenAddress,
        uint256 _tokenId,
        uint256 _tokenAmount
    ) external returns (uint256 commitmentId_);

    /// @notice Grants a role to `_grantee`.
    /// @param _commitmentId The identifier of the commitment.
    /// @param _role The role identifier.
    /// @param _grantee The recipient the role.
    /// @param _expirationDate The expiration date of the role.
    /// @param _revocable Whether the role is revocable or not.
    /// @param _data Any additional data about the role.
    function grantRole(
        uint256 _commitmentId,
        bytes32 _role,
        address _grantee,
        uint64 _expirationDate,
        bool _revocable,
        bytes calldata _data
    ) external;

    /// @notice Revokes a role.
    /// @param _commitmentId The commitment identifier.
    /// @param _role The role identifier.
    /// @param _grantee The recipient of the role revocation.
    function revokeRole(uint256 _commitmentId, bytes32 _role, address _grantee) external;

    /// @notice Releases tokens back to grantor.
    /// @param _commitmentId The commitment identifier.
    function releaseTokens(uint256 _commitmentId) external;

    /// @notice Approves operator to grant and revoke roles on behalf of another user.
    /// @param _tokenAddress The token address.
    /// @param _operator The user approved to grant and revoke roles.
    /// @param _approved The approval status.
    function setRoleApprovalForAll(address _tokenAddress, address _operator, bool _approved) external;

    /** View Functions **/

    /// @notice Returns the owner of the commitment (grantor).
    /// @param _commitmentId The commitment identifier.
    /// @return grantor_ The commitment owner.
    function grantorOf(uint256 _commitmentId) external view returns (address grantor_);

    /// @notice Returns the address of the token committed.
    /// @param _commitmentId The commitment identifier.
    /// @return tokenAddress_ The token address.
    function tokenAddressOf(uint256 _commitmentId) external view returns (address tokenAddress_);

    /// @notice Returns the identifier of the token committed.
    /// @param _commitmentId The commitment identifier.
    /// @return tokenId_ The token identifier.
    function tokenIdOf(uint256 _commitmentId) external view returns (uint256 tokenId_);

    /// @notice Returns the amount of tokens committed.
    /// @param _commitmentId The commitment identifier.
    /// @return tokenAmount_ The token amount.
    function tokenAmountOf(uint256 _commitmentId) external view returns (uint256 tokenAmount_);

    /// @notice Returns the custom data of a role assignment.
    /// @param _commitmentId The commitment identifier.
    /// @param _role The role identifier.
    /// @param _grantee The recipient the role.
    /// @return data_ The custom data.
    function roleData(
        uint256 _commitmentId,
        bytes32 _role,
        address _grantee
    ) external view returns (bytes memory data_);

    /// @notice Returns the expiration date of a role assignment.
    /// @param _commitmentId The commitment identifier.
    /// @param _role The role identifier.
    /// @param _grantee The recipient the role.
    /// @return expirationDate_ The expiration date.
    function roleExpirationDate(
        uint256 _commitmentId,
        bytes32 _role,
        address _grantee
    ) external view returns (uint64 expirationDate_);

    /// @notice Returns the expiration date of a role assignment.
    /// @param _commitmentId The commitment identifier.
    /// @param _role The role identifier.
    /// @param _grantee The recipient the role.
    /// @return revocable_ Whether the role is revocable or not.
    function isRoleRevocable(
        uint256 _commitmentId,
        bytes32 _role,
        address _grantee
    ) external view returns (bool revocable_);

    /// @notice Checks if the grantor approved the operator for all SFTs.
    /// @param _tokenAddress The token address.
    /// @param _grantor The user that approved the operator.
    /// @param _operator The user that can grant and revoke roles.
    /// @return isApproved_ Whether the operator is approved or not.
    function isRoleApprovedForAll(
        address _tokenAddress,
        address _grantor,
        address _operator
    ) external view returns (bool isApproved_);
}
```

### 单交易扩展

授予角色是一个需要两步的过程，涉及两笔交易。第一步是提交代币，第二步是授予角色。此扩展允许用户在一笔交易中提交代币并授予角色，这在某些用例中是可取的。

```solidity
/// @title ERC-7589 Semi-Fungible Token Roles, optional single transaction extension
/// @dev See https://eips.ethereum.org/EIPS/eip-7589
/// Note: the ERC-165 identifier for this interface is 0x5c3d7d74.
interface ICommitTokensAndGrantRoleExtension /* is IERC7589 */ {
    /// @notice Commits tokens and grant role in a single transaction.
    /// @param _grantor The owner of the SFTs.
    /// @param _tokenAddress The token address.
    /// @param _tokenId The token identifier.
    /// @param _tokenAmount The token amount.
    /// @param _role The role identifier.
    /// @param _grantee The recipient the role.
    /// @param _expirationDate The expiration date of the role.
    /// @param _revocable Whether the role is revocable or not.
    /// @param _data Any additional data about the role.
    /// @return commitmentId_ The identifier of the commitment created.
    function commitTokensAndGrantRole(
        address _grantor,
        address _tokenAddress,
        uint256 _tokenId,
        uint256 _tokenAmount,
        bytes32 _role,
        address _grantee,
        uint64 _expirationDate,
        bool _revocable,
        bytes calldata _data
    ) external returns (uint256 commitmentId_);
}
```

### 角色余额扩展

核心接口允许查询代币承诺的余额，但不允许查询特定用户的余额。要确定授予用户的代币总量，实施需要对授予该用户的所有角色进行求和，同时过滤掉任何过期的角色。

此功能被包含在可选扩展中，因为它并不总是必要的，并且可能会使实现变得更加复杂（增加智能合约风险）。

```solidity
/// @title ERC-7589 Semi-Fungible Token Roles, optional role balance extension
/// @dev See https://eips.ethereum.org/EIPS/eip-7589
/// Note: the ERC-165 identifier for this interface is 0x2f35b73f.
interface IRoleBalanceOfExtension /* is IERC7589 */ {
    /// @notice Returns the sum of all tokenAmounts granted to the grantee for the given role.
    /// @param _role The role identifier.
    /// @param _tokenAddress The token address.
    /// @param _tokenId The token identifier.
    /// @param _grantee The user for which the balance is returned.
    /// @return balance_ The balance of the grantee for the given role.
    function roleBalanceOf(
        bytes32 _role,
        address _tokenAddress,
        uint256 _tokenId,
        address _grantee
    ) external returns (uint256 balance_);
}
```

### 元数据扩展

角色元数据扩展扩展了 SFT 的传统基于 JSON 的元数据架构。因此，支持此功能的 DApp MUST 还必须实现 [ERC-1155](./eip-1155.md) 的元数据扩展。此 JSON 扩展是 **可选** 的，允许开发者提供有关角色的附加信息。

更新的 JSON 架构：
```json
{

  /** Existing ERC-1155 Metadata **/

  "title": "Token Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "Identifies the asset to which this token represents"
    },
    "decimals": {
      "type": "integer",
      "description": "The number of decimal places that the token amount should display - e.g. 18, means to divide the token amount by 1000000000000000000 to get its user representation."
    },
    "description": {
      "type": "string",
      "description": "Describes the asset to which this token represents"
    },
    "image": {
      "type": "string",
      "description": "A URI pointing to a resource with mime type image/* representing the asset to which this token represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
    },
    "properties": {
      "type": "object",
      "description": "Arbitrary properties. Values may be strings, numbers, object or arrays."
    }
  },

  /** Additional fields for ERC-7589 **/

  "roles": [{
    "id": {
      "type": "bytes32",
      "description": "Identifies the role"
    },
    "name": {
      "type": "string",
      "description": "Human-readable name of the role"
    },
    "description": {
      "type": "string",
      "description": "Describes the role"
    },
    "inputs": [{
      "name": {
        "type": "string",
        "description": "Human-readable name of the argument"
      },
      "type": {
        "type": "string",
        "description": "Solidity type, e.g., uint256 or address"
      }
    }]
  }]
}
```

以下代码片段是上述附加字段的示例：

```json
{

  /** Existing ERC-1155 Metadata **/

  "name": "Asset Name",
  "description": "Lorem ipsum...",
  "image": "https:\/\/s3.amazonaws.com\/your-bucket\/images\/{id}.png",
  "properties": {
    "simple_property": "example value",
    "rich_property": {
      "name": "Name",
      "value": "123",
      "display_value": "123 Example Value",
      "class": "emphasis",
      "css": {
        "color": "#ffffff",
        "font-weight": "bold",
        "text-decoration": "underline"
      }
    },
    "array_property": {
      "name": "Name", 
      "value": [1,2,3,4],
      "class": "emphasis"
    }
  },

  /** Additional fields for ERC-7589 **/

  "roles": [
    {
      // keccak256("Player(uint256)")
      "id": "0x70d2dab8c6ff873dc0b941220825d9271fdad6fdb936f6567ffde77d05491cef",
      "name": "Player",
      "description": "The user allowed to use this item in-game.",
      "inputs": [
        {
          "name": "ProfitShare",
          "type": "uint256"
        }
      ]
    }
  ]
}
```

`roles` 数组的属性是建议的，开发者应根据其用例添加任何其他相关信息（例如，表示角色的图像）。

还需要强调 `inputs` 属性的重要性。此字段描述应编码并传递给 `grantRole` 函数的参数，可以包括 `type` 和 `components` 属性以表示数据的格式。建议使用在 Solidity ABI 规范中定义的 `type` 和 `components` 属性。

### 注意事项

* 合规合约 MUST 实现 `IERC7589` 接口。
* 每个角色由 `bytes32` 标识符表示。建议使用角色名称及其参数（如果有） 的 keccak256 哈希作为标识符。例如，`keccak256("Player(uint256)")`。
* 如果 `_tokenAmount` 为零或 `msg.sender` 未获得 `_grantor` 的批准，则 `commitTokens` 函数 MUST 回退。它可以实现为 public 或 external。
* 如果 `_expirationDate` 在过去，或者 `msg.sender` 未获得批准以代表授予者授予角色，则 `grantRole` 函数 MUST 回退。它可以实现为 public 或 external，建议使用 `type(uint64).max` 作为永久角色。
* `revokeRole` 函数应始终允许受赠人撤销角色，可以实现为 public 或 external，并且 MUST 回退如果：
  * 找不到角色分配（未授予角色）。
  * `msg.sender` 未获得授予者或受赠人的批准。
  * `msg.sender` 是授予者或获得授予者的批准，但角色不可撤销或已过期。
* `releaseTokens` 函数可以实现为 public 或 external，并且 MUST 回退如果：
  * 找不到承诺（未提交代币）。
  * `msg.sender` 不是且未获得授予者的批准。
  * 承诺至少有一个未过期的不可撤销角色。
* `setRoleApprovalForAll` 函数可以实现为 public 或 external。
* `grantorOf` 函数可以实现为 pure 或 view，并且 MUST 返回已提交代币的所有者。
* `tokenAddressOf` 函数可以实现为 pure 或 view，并且 MUST 返回已提交代币的地址。
* `tokenIdOf` 函数可以实现为 pure 或 view，并且 MUST 返回已提交代币的标识符。
* `tokenAmountOf` 函数可以实现为 pure 或 view，并且 MUST 返回已提交的代币数量。
* `roleData` 函数可以实现为 pure 或 view，并且 MUST 返回角色分配的自定义数据。
* `roleExpirationDate` 函数可以实现为 pure 或 view，并且 MUST 返回角色分配的过期日期。
* `isRoleRevocable` 函数可以实现为 pure 或 view，并且 MUST 返回授予者是否可以在角色过期之前结束角色分配。
* `isRoleApprovedForAll` 函数可以实现为 pure 或 view，并且 MUST 返回 `_operator` 是否被允许代表 `_grantor` 授予和撤销角色。

> 请注意，“批准”是指允许用户提交代币并代表他人授予/撤销角色。获得批准的用户要么获得了角色批准，要么是目标用户。角色批准与 [ERC-1155](./eip-1155.md) 的批准不同。更多信息可以在 [角色批准](#role-approvals) 部分找到。

## 理由

“代币承诺”作为一种抽象概念，为希望委托其 SFT 控制权的用户提供了强大的工具。代币承诺表示冻结的余额或存入合约的代币，为 SFT 所有者提供了一种标准化和安全的方式来委托其资产的使用。通过 [ERC-7589](./eip-7589.md)，用户获得了一种多功能机制，以抽象安全委托的复杂性，增强半可替代代币的实用性和互操作性。

[ERC-7589](./eip-7589.md) 不是 [ERC-1155](./eip-1155.md) 的扩展。做出这一决定的主要原因是保持标准对任何实现的不可知。这种方法使得标准可以在 SFT 的外部或同一合约中实现，并允许 DApp 使用不可变的 SFT 角色。
### 角色批准

像 [ERC-1155](./eip-1155.md) 一样，[ERC-7589](./eip-7589.md) 允许用户批准操作员代表他们授予和撤销角色。此功能对于互操作性至关重要，因为它使第三方应用程序能够在不需要保管级别批准的情况下管理用户角色。角色批准是核心接口的一部分，符合规范的合约必须实现 `setRoleApprovalForAll` 和 `isRoleApprovedForAll` 函数。

### 自动过期

实现自动过期是为了节省用户的 gas。为了结束角色分配，应用程序应该调用 `roleExpirationDate` 并将其与当前时间戳进行比较，以检查角色是否仍然有效，而不是要求用户始终调用 `revokeRole`。

在 [ERC-7589](./eip-7589.md) 的上下文中，日期表示为 `uint64`。`uint64` 表示的最大 UNIX 时间戳大约是 5840 亿年，这应该足够被视为“永久”。因此，在分配中使用 `type(uint64).max` 表示它永远不会过期。

### 可撤销角色

在某些情况下，授予者可能需要在角色过期之前撤销角色。而在其他情况下，受让人需要确保角色不会被提前撤销（例如，当受让人支付代币以使用它们时）。`_revocable` 参数被包含在 `grantRole` 函数中，正是出于这个原因，它指定授予者是否可以在到期日前撤销角色。无论 `_revocable` 的值如何，受让人始终可以撤销角色，允许接收者消除不希望的分配。

### 自定义数据

`grantRole` 函数的 `_data` 参数对于该 EIP 的标准化至关重要。SFT 有不同的用例，试图在 Solidity 级别接口上覆盖所有用例是不切实际的。因此，加入了一个类型为 `bytes` 的通用参数，允许用户在授予角色时传递任何自定义信息。

例如，web3 游戏通常在将 NFT 委托给玩家时引入利润分享，这由一个 `uint256` 表示。使用 [ERC-7589](./eip-7589.md)，可以简单地将 `uint256` 编码为字节并传递给 `grantRole` 函数。数据验证可以在链上或链下进行，其他合约可以使用 `roleData` 函数查询此信息。

## 向后兼容性

许多 SFT 作为不可变合约部署，这带来了以下挑战：如何为无法修改的 SFT 启用角色管理？该提案通过在提交代币时要求 `tokenAddress` 参数来解决此问题。此要求确保 dApp 可以在 SFT 合约内部实现 [ERC-7589](./eip-7589.md) 或使用独立的外部合约作为不可变 SFT 角色的权威来源。

## 参考实现

请参见 [`ERC7589.sol`](../assets/eip-7589/ERC7589.sol)。

## 安全考虑

与半可替代代币角色集成的开发者应考虑以下实施要点：
* 确保适当的访问控制，以防止未经授权的角色分配或撤销。这在 `commitTokens` 和 `releaseTokens` 中尤其重要，因为它们可能会冻结或转移余额。
* 考虑潜在的攻击向量，例如重入，并确保采取适当的保护措施。
* 在允许用户使用角色分配之前，始终检查到期日期。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。