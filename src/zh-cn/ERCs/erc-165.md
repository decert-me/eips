---
eip: 165
title: 标准接口检测
author: Christian Reitwießner <chris@ethereum.org>, Nick Johnson <nick@ethereum.org>, Fabian Vogelsteller <fabian@lukso.network>, Jordi Baylina <jordi@baylina.cat>, Konrad Feldmeier <konrad.feldmeier@brainbot.com>, William Entriken <github.com@phor.net>
type: 标准跟踪
category: ERC
status: 最终
created: 2018-01-23
requires: 214
---

## 简要总结

创建一种标准方法来发布和检测智能合约实现了哪些接口。

## 摘要

在此，我们标准化以下内容：

1. 如何识别接口
2. 合约如何发布其实现的接口
3. 如何检测合约是否实现了 ERC-165
4. 如何检测合约是否实现了任何给定接口

## 动机

对于一些“标准接口”，如 [ERC-20 代币接口](./erc-20.md)，有时查询合约是否支持该接口以及如果支持，支持哪个版本的接口是有用的，以便调整与合约的交互方式。特别是对于 ERC-20，已经提出了版本标识符。该提案标准化了接口的概念，并标准化了接口的识别（命名）。

## 规范

### 如何识别接口

对于本标准，*接口* 是一组 [以太坊 ABI 定义的函数选择器](https://solidity.readthedocs.io/en/develop/abi-spec.html#function-selector)。这是 [Solidity 接口概念](https://solidity.readthedocs.io/en/develop/abi-spec.html) 的一个子集，以及 `interface` 关键字定义，它还定义了返回类型、可变性和事件。

我们将接口标识符定义为接口中所有函数选择器的 XOR。以下代码示例展示了如何计算接口标识符：

```solidity
pragma solidity ^0.4.20;

interface Solidity101 {
    function hello() external pure;
    function world(int) external pure;
}

contract Selector {
    function calculateSelector() public pure returns (bytes4) {
        Solidity101 i;
        return i.hello.selector ^ i.world.selector;
    }
}
```

注意：接口不允许可选函数，因此，接口标识符将不包括它们。

### 合约如何发布其实现的接口

符合 ERC-165 的合约应实现以下接口（称为 `ERC165.sol`）：

```solidity
pragma solidity ^0.4.20;

interface ERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///  uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

该接口的接口标识符为 `0x01ffc9a7`。您可以通过运行 `bytes4(keccak256('supportsInterface(bytes4)'));` 或使用上述 `Selector` 合约来计算此值。

因此，实现合约将具有一个 `supportsInterface` 函数，该函数返回：

- 当 `interfaceID` 为 `0x01ffc9a7`（EIP165 接口）时返回 `true`
- 当 `interfaceID` 为 `0xffffffff` 时返回 `false`
- 对于该合约实现的任何其他 `interfaceID` 返回 `true`
- 对于任何其他 `interfaceID` 返回 `false`

此函数必须返回一个 bool，并使用最多 30,000 gas。

实现说明，有几种逻辑方法可以实现此函数。请参见示例实现和关于 gas 使用的讨论。

### 如何检测合约是否实现了 ERC-165

1. 源合约对目标地址进行 `STATICCALL`，输入数据为：`0x01ffc9a701ffc9a700000000000000000000000000000000000000000000000000000000`，gas 为 30,000。这对应于 `contract.supportsInterface(0x01ffc9a7)`。
2. 如果调用失败或返回 false，则目标合约不实现 ERC-165。
3. 如果调用返回 true，则进行第二次调用，输入数据为 `0x01ffc9a7ffffffff00000000000000000000000000000000000000000000000000000000`。
4. 如果第二次调用失败或返回 true，则目标合约不实现 ERC-165。
5. 否则，它实现了 ERC-165。

### 如何检测合约是否实现了任何给定接口

1. 如果您不确定合约是否实现了 ERC-165，请使用上述程序进行确认。
2. 如果它不实现 ERC-165，则您需要以传统方式查看它使用了哪些方法。
3. 如果它实现了 ERC-165，则只需调用 `supportsInterface(interfaceID)` 来确定它是否实现了您可以使用的接口。

## 理由

我们尽量保持此规范尽可能简单。此实现也与当前的 Solidity 版本兼容。

## 向后兼容性

上述机制（使用 `0xffffffff`）应适用于大多数在此标准之前的合约，以确定它们不实现 ERC-165。

此外，[ENS](./eip-137.md) 已经实现了此 EIP。

## 测试用例

以下是一个合约，用于检测其他合约实现了哪些接口。来自 @fulldecent 和 @jbaylina。

```solidity
pragma solidity ^0.4.20;

contract ERC165Query {
    bytes4 constant InvalidID = 0xffffffff;
    bytes4 constant ERC165ID = 0x01ffc9a7;

    function doesContractImplementInterface(address _contract, bytes4 _interfaceId) external view returns (bool) {
        uint256 success;
        uint256 result;

        (success, result) = noThrowCall(_contract, ERC165ID);
        if ((success==0)||(result==0)) {
            return false;
        }

        (success, result) = noThrowCall(_contract, InvalidID);
        if ((success==0)||(result!=0)) {
            return false;
        }

        (success, result) = noThrowCall(_contract, _interfaceId);
        if ((success==1)&&(result==1)) {
            return true;
        }
        return false;
    }

    function noThrowCall(address _contract, bytes4 _interfaceId) constant internal returns (uint256 success, uint256 result) {
        bytes4 erc165ID = ERC165ID;

        assembly {
                let x := mload(0x40)               // Find empty storage location using "free memory pointer"
                mstore(x, erc165ID)                // Place signature at beginning of empty storage
                mstore(add(x, 0x04), _interfaceId) // Place first argument directly next to signature

                success := staticcall(
                                    30000,         // 30k gas
                                    _contract,     // To addr
                                    x,             // Inputs are stored at location x
                                    0x24,          // Inputs are 36 bytes long
                                    x,             // Store output over input (saves space)
                                    0x20)          // Outputs are 32 bytes long

                result := mload(x)                 // Load the result
        }
    }
}
```

## 实现

此方法使用 `supportsInterface` 的 `view` 函数实现。任何输入的执行成本为 586 gas。但合约初始化需要存储每个接口（`SSTORE` 为 20,000 gas）。`ERC165MappingImplementation` 合约是通用的且可重用。

```solidity
pragma solidity ^0.4.20;

import "./ERC165.sol";

contract ERC165MappingImplementation is ERC165 {
    /// @dev You must not set element 0xffffffff to true
    mapping(bytes4 => bool) internal supportedInterfaces;

    function ERC165MappingImplementation() internal {
        supportedInterfaces[this.supportsInterface.selector] = true;
    }

    function supportsInterface(bytes4 interfaceID) external view returns (bool) {
        return supportedInterfaces[interfaceID];
    }
}

interface Simpson {
    function is2D() external returns (bool);
    function skinColor() external returns (string);
}

contract Lisa is ERC165MappingImplementation, Simpson {
    function Lisa() public {
        supportedInterfaces[this.is2D.selector ^ this.skinColor.selector] = true;
    }

    function is2D() external returns (bool){}
    function skinColor() external returns (string){}
}
```

以下是 `supportsInterface` 的 `pure` 函数实现。最坏情况下的执行成本为 236 gas，但随着支持的接口数量增加而线性增加。

```solidity
pragma solidity ^0.4.20;

import "./ERC165.sol";

interface Simpson {
    function is2D() external returns (bool);
    function skinColor() external returns (string);
}

contract Homer is ERC165, Simpson {
    function supportsInterface(bytes4 interfaceID) external view returns (bool) {
        return
          interfaceID == this.supportsInterface.selector || // ERC165
          interfaceID == this.is2D.selector
                         ^ this.skinColor.selector; // Simpson
    }

    function is2D() external returns (bool){}
    function skinColor() external returns (string){}
}
```

对于三个或更多支持的接口（包括 ERC165 本身作为必需的支持接口），映射方法（在每种情况下）消耗的 gas 少于纯方法（在最坏情况下）。

## 版本历史
* PR 1640，最终定稿 2019-01-23 -- 这修正了 noThrowCall 测试用例，使用 36 字节而不是之前的 32 字节。之前的代码是一个错误，在 Solidity 0.4.x 中仍然默默有效，但在 Solidity 0.5.0 中引入的新行为破坏了它。此更改在 [#1640](https://github.com/ethereum/EIPs/pull/1640) 中进行了讨论。

* EIP 165，最终定稿 2018-04-20 -- 原始发布版本。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。