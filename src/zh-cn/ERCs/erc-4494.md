---
eip: 4494
title: ERC-721 NFT 的许可
description: ERC-712 签名批准用于 ERC-721 NFT
author: Simon Fremaux (@dievardump), William Schwab (@wschwab)
discussions-to: https://ethereum-magicians.org/t/eip-extending-erc2612-style-permits-to-erc721-nfts/7519/2
status: Stagnant
type: Standards Track
category: ERC
created: 2021-11-25
requires: 165, 712, 721
---

## 摘要
在 [ERC-2612](./eip-2612.md) 中概述的“许可”批准流程通过为 ERC20 代币创建无 gas 批准，已被证明是用户体验的一个非常有价值的进步。该 EIP 将这一模式扩展到 ERC-721 NFT。该 EIP 在很大程度上借鉴了 ERC-2612。

由于 ERC-20 和 ERC-721 代币之间的结构差异，这需要一个单独的 EIP。虽然 ERC-20 许可使用值（被批准的 ERC-20 代币的数量）和基于所有者地址的 nonce，但 ERC-721 许可则专注于 NFT 的 `tokenId`，并基于 NFT 的转移递增 nonce。

## 动机
在 [ERC-2612](./eip-2612.md) 中概述的许可结构允许使用签名消息（按照 [ERC-712](./eip-712.md) 中的结构）来创建批准。而正常的基于批准的提取流程通常涉及两笔交易，一笔用于批准合约，另一笔用于合约提取资产，这样的用户体验较差，常常让新用户感到困惑，许可式流程只需签署一条消息和一笔交易即可。更多信息可以在 [ERC-2612](./eip-2612.md) 中找到。

[ERC-2612](./eip-2612.md) 仅概述了 ERC-20 代币的许可架构。该 ERC 提出了 ERC-721 NFT 的架构，其中也包含一个批准架构，可以从基于签名消息的批准流程中受益。

## 规范
本文档中的关键字“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“应该不”、“推荐”、“可以”和“可选”应按 RFC 2119 中的描述进行解释。

必须向 [ERC-721](./eip-721.md) 添加三个新函数：
```solidity
pragma solidity 0.8.10;

import "./IERC165.sol";

///
/// @dev Interface for token permits for ERC-721
///
interface IERC4494 is IERC165 {
  /// ERC165 bytes to add to interface array - set in parent contract
  ///
  /// _INTERFACE_ID_ERC4494 = 0x5604e225

  /// @notice Function to approve by way of owner signature
  /// @param spender the address to approve
  /// @param tokenId the index of the NFT to approve the spender on
  /// @param deadline a timestamp expiry for the permit
  /// @param sig a traditional or EIP-2098 signature
  function permit(address spender, uint256 tokenId, uint256 deadline, bytes memory sig) external;
  /// @notice Returns the nonce of an NFT - useful for creating permits
  /// @param tokenId the index of the NFT to get the nonce of
  /// @return the uint256 representation of the nonce
  function nonces(uint256 tokenId) external view returns(uint256);
  /// @notice Returns the domain separator used in the encoding of the signature for permits, as defined by EIP-712
  /// @return the bytes32 domain separator
  function DOMAIN_SEPARATOR() external view returns(bytes32);
}
```
其语义如下：

对于所有地址 `spender`、`uint256` 的 `tokenId`、`deadline` 和 `nonce`，以及 `bytes` 的 `sig`，调用 `permit(spender, tokenId, deadline, sig)` 必须在以下条件满足的情况下将 `spender` 设置为 `tokenId` 的批准者，只要 `tokenId` 的所有者仍然持有它，并且必须发出相应的 `Approval` 事件：

* 当前区块时间小于或等于 `deadline`
* `tokenId` 的所有者不是零地址
* `nonces[tokenId]` 等于 `nonce`
* `sig` 是来自 `tokenId` 所有者的有效 `secp256k1` 或 [EIP-2098](./eip-2098.md) 签名：
```
keccak256(abi.encodePacked(
   hex"1901",
   DOMAIN_SEPARATOR,
   keccak256(abi.encode(
            keccak256("Permit(address spender,uint256 tokenId,uint256 nonce,uint256 deadline)"),
            spender,
            tokenId,
            nonce,
            deadline))
));
```
其中 `DOMAIN_SEPARATOR` 必须根据 [EIP-712](./eip-712.md) 定义。`DOMAIN_SEPARATOR` 应该是合约和链的唯一标识，以防止来自其他域的重放攻击，并满足 EIP-712 的要求，但在其他方面没有限制。`DOMAIN_SEPARATOR` 的一个常见选择是：
```
DOMAIN_SEPARATOR = keccak256(
    abi.encode(
        keccak256('EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)'),
        keccak256(bytes(name)),
        keccak256(bytes(version)),
        chainid,
        address(this)
));
```
换句话说，消息是以下 ERC-712 类型结构：
```json
{
  "types": {
    "EIP712Domain": [
      {
        "name": "name",
        "type": "string"
      },
      {
        "name": "version",
        "type": "string"
      },
      {
        "name": "chainId",
        "type": "uint256"
      },
      {
        "name": "verifyingContract",
        "type": "address"
      }
    ],
    "Permit": [
      {
        "name": "spender",
        "type": "address"
      },
      {
        "name": "tokenId",
        "type": "uint256"
      },
      {
        "name": "nonce",
        "type": "uint256"
      },
      {
        "name": "deadline",
        "type": "uint256"
      }
    ],
    "primaryType": "Permit",
    "domain": {
      "name": erc721name,
      "version": version,
      "chainId": chainid,
      "verifyingContract": tokenAddress
  },
  "message": {
    "spender": spender,
    "value": value,
    "nonce": nonce,
    "deadline": deadline
  }
}}
```
此外：
* 特定 `tokenId` 的 `nonce` (`nonces[tokenId]`) 必须在任何 `tokenId` 转移时递增
* `permit` 函数必须检查签名者不是零地址

请注意，在此定义中我们没有提到 `msg.sender`。调用 `permit` 函数的可以是任何地址。

该 EIP 需要 [EIP-165](./eip-165.md)。EIP165 已在 [ERC-721](./eip-721.md) 中要求，但在这里进一步必要，以便注册该 EIP 的接口。这样可以轻松验证 NFT 合约是否实现了该 EIP，从而使其能够相应地进行交互。该 EIP 的接口（如 EIP-165 中定义）为 `0x5604e225`。实现该 EIP 的合约在调用 `supportsInterface` 函数时必须返回 `true`，当传入 `0x5604e225` 时。

## 理由
`permit` 函数足以使 `safeTransferFrom` 交易在不需要额外交易的情况下进行。

该格式避免了对未知代码的任何调用。

`nonces` 映射用于重放保护。

许可的一个常见用例是中继者代表所有者提交许可。在这种情况下，中继方本质上被赋予了提交或保留许可的自由选择。如果这是一个令人担忧的问题，所有者可以通过将截止日期设置为不久的未来的值来限制许可的有效时间。截止日期参数可以设置为 uint(-1)，以创建实际上永不过期的许可。

包含 ERC-712 类型消息是因为它在 [ERC-2612](./eip-2612.md) 中的使用，而该文档又引用了许多钱包提供商的广泛采用。

虽然 ERC-2612 关注于被批准的值，但该 EIP 关注于通过 `permit` 被批准的 NFT 的 `tokenId`。这使得实现 ERC-20（甚至 [ERC-1155](./eip-1155.md)）代币无法实现的灵活性，使单个所有者能够对同一 NFT 给予多个许可。这是可能的，因为每个 ERC-721 代币都是离散的（通常称为非同质化），这使得简单而明确地断言该代币仍然由 `owner` 持有成为可能。

而 ERC-2612 将签名拆分为其 `v,r,s` 组件，该 EIP 选择接受一个可变长度的 `bytes` 数组，以支持 [EIP-2098](./eip-2098) 签名（64 字节），这些签名无法轻易从 `r,s,v` 组件（65 字节）中分离或重建。

## 向后兼容性
已经有一些 ERC-721 合约实现了 `permit` 风格的架构，最著名的是 Uniswap v3。

它们的实现与此处的规范不同在于：
 * `permit` 架构基于 `owner`
 * `nonce` 在创建 `permit` 时递增
 * `permit` 函数必须由 NFT 所有者调用，且该所有者被设置为 `owner`
 * 签名被拆分为 `r,s,v` 而不是 `bytes`

关于设计决策不同的理由在上面详细说明。

## 测试用例

参考实现的基本测试用例可以在 [这里](https://github.com/dievardump/erc721-with-permits/tree/main/test) 找到。

一般来说，测试套件应至少对该 EIP 的任何实现进行以下断言：
* 每次转移后 `nonce` 递增
* `permit` 在正确的 `tokenId` 上批准 `spender`
* NFT 转移后，许可不能再使用
* 过期的许可不能使用

## 参考实现

参考实现已在 [这里](https://github.com/dievardump/erc721-with-permits) 设置。

## 安全考虑

在创建可以在一个函数中使用 `permit` 和转移函数的转移函数时，应格外小心，以确保无效的许可无法以任何方式使用。这对于自动化 NFT 平台尤其相关，因为粗心的实现可能导致大量用户资产的损失。

其余考虑事项已从 [ERC-2612](./eip-2612.md) 中复制，并进行了小幅调整，在这里同样适用：

尽管 `Permit` 的签名者可能有某个特定方希望提交他们的交易，但其他方始终可以在预期方之前抢先调用 `permit`。然而，最终结果对 `Permit` 签名者来说是相同的。
由于 ecrecover 预编译在处理格式错误的消息时会静默失败并返回零地址作为 `signer`，因此确保 `ownerOf(tokenId) != address(0)` 是很重要的，以避免 `permit` 创建对任何没有设置批准的 `tokenId` 的批准。

签名的 `Permit` 消息是可以被审查的。中继方始终可以选择在收到 `Permit` 后不提交它，从而保留提交的选项。`deadline` 参数是对此的一种缓解。如果签名方持有 ETH，他们也可以自己提交 `Permit`，这可能会使之前签署的 `Permit` 无效。

标准的 [ERC-20 批准的竞争条件](https://swcregistry.io/docs/SWC-114) 也适用于 `permit`。

如果 `DOMAIN_SEPARATOR` 包含 `chainId` 并在合约部署时定义，而不是为每个签名重建，则在未来链分裂的情况下可能存在跨链重放攻击的风险。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。