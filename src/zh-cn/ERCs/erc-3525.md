---
eip: 3525
title: 半可替代代币
description: 定义了一种规范，其中具有相同 SLOT 和不同 ID 的 ERC-721 兼容代币是可替代的。
author: Will Wang (@will42w), Mike Meng <myan@solv.finance>, Yi Cai (@YeeTsai) <yee.tsai@gmail.com>, Ryan Chow <ryanchow@solv.finance>, Zhongxin Wu (@Nerverwind), AlvisDu (@AlvisDu)
discussions-to: https://ethereum-magicians.org/t/eip-3525-the-semi-fungible-token
status: Final
type: Standards Track
category: ERC
created: 2020-12-01
requires: 20, 165, 721
---

## 摘要

这是一个半可替代代币的标准。本文件中描述的一组智能合约接口定义了一个与 [ERC-721](./eip-721.md) 兼容的代币标准。该标准引入了一个 `<ID, SLOT, VALUE>` 三元标量模型，表示代币的半可替代结构。它还引入了新的转移模型以及反映代币半可替代性质的批准模型。

代币包含一个 ERC-721 等效的 ID 属性，以识别其作为一个普遍唯一的实体，从而使代币可以在地址之间转移并被批准以 ERC-721 兼容的方式进行操作。

代币还包含一个 `value` 属性，表示代币的定量特性。'value' 属性的含义与 [ERC-20](./erc-20.md) 代币的 'balance' 属性相似。每个代币都有一个 'slot' 属性，确保具有相同槽的两个代币的值被视为可替代的，从而为代币的值属性增加了可替代性。

此 EIP 引入了用于半可替代性的新的代币转移模型，包括同一槽的两个代币之间的值转移和从代币到地址的值转移。

## 动机

代币化是使用和控制加密数字资产的最重要趋势之一。传统上，有两种方法可以做到这一点：可替代代币和不可替代代币。可替代代币通常使用 ERC-20 标准，其中每个资产的单位彼此相同。ERC-20 是操作可替代代币的一种灵活且高效的方法。不可替代代币主要是 ERC-721 代币，这是一种能够根据身份区分数字资产的标准。

然而，两者都有显著的缺点。例如，ERC-20 要求用户为每个单独的数据结构或可定制属性组合创建一个单独的 ERC-20 合约。在实践中，这导致需要创建大量的 ERC-20 合约。另一方面，ERC-721 代币没有提供定量特性，显著削弱了它们的可计算性、流动性和可管理性。例如，如果要使用 ERC-721 创建金融工具，如债券、保险单或归属计划，则没有标准接口可供我们控制其中的价值，这使得例如转移由代币表示的合同中的部分股权变得不可能。

解决这个问题的更直观和简单的方法是创建一种半可替代代币，具有 ERC-20 的定量特性和 ERC-721 的定性属性。这种半可替代代币与 ERC-721 的向后兼容性将有助于利用已经在使用的现有基础设施，并促进更快的采用。

## 规范

本文件中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

**每个 [ERC-3525](./eip-3525.md) 兼容的合约必须实现 ERC-3525、ERC-721 和 [ERC-165](./eip-165.md) 接口**

```solidity
pragma solidity ^0.8.0;

/**
 * @title ERC-3525 Semi-Fungible Token Standard
 * Note: the ERC-165 identifier for this interface is 0xd5358140.
 */
interface IERC3525 /* is IERC165, IERC721 */ {
    /**
     * @dev MUST emit when value of a token is transferred to another token with the same slot,
     *  including zero value transfers (_value == 0) as well as transfers when tokens are created
     *  (`_fromTokenId` == 0) or destroyed (`_toTokenId` == 0).
     * @param _fromTokenId The token id to transfer value from
     * @param _toTokenId The token id to transfer value to
     * @param _value The transferred value
     */
    event TransferValue(uint256 indexed _fromTokenId, uint256 indexed _toTokenId, uint256 _value);

    /**
     * @dev MUST emit when the approval value of a token is set or changed.
     * @param _tokenId The token to approve
     * @param _operator The operator to approve for
     * @param _value The maximum value that `_operator` is allowed to manage
     */
    event ApprovalValue(uint256 indexed _tokenId, address indexed _operator, uint256 _value);
    
    /**
     * @dev MUST emit when the slot of a token is set or changed.
     * @param _tokenId The token of which slot is set or changed
     * @param _oldSlot The previous slot of the token
     * @param _newSlot The updated slot of the token
     */ 
    event SlotChanged(uint256 indexed _tokenId, uint256 indexed _oldSlot, uint256 indexed _newSlot);

    /**
     * @notice Get the number of decimals the token uses for value - e.g. 6, means the user
     *  representation of the value of a token can be calculated by dividing it by 1,000,000.
     *  Considering the compatibility with third-party wallets, this function is defined as
     *  `valueDecimals()` instead of `decimals()` to avoid conflict with ERC-20 tokens.
     * @return The number of decimals for value
     */
    function valueDecimals() external view returns (uint8);

    /**
     * @notice Get the value of a token.
     * @param _tokenId The token for which to query the balance
     * @return The value of `_tokenId`
     */
    function balanceOf(uint256 _tokenId) external view returns (uint256);

    /**
     * @notice Get the slot of a token.
     * @param _tokenId The identifier for a token
     * @return The slot of the token
     */
    function slotOf(uint256 _tokenId) external view returns (uint256);

    /**
     * @notice Allow an operator to manage the value of a token, up to the `_value`.
     * @dev MUST revert unless caller is the current owner, an authorized operator, or the approved
     *  address for `_tokenId`.
     *  MUST emit the ApprovalValue event.
     * @param _tokenId The token to approve
     * @param _operator The operator to be approved
     * @param _value The maximum value of `_toTokenId` that `_operator` is allowed to manage
     */
    function approve(
        uint256 _tokenId,
        address _operator,
        uint256 _value
    ) external payable;

    /**
     * @notice Get the maximum value of a token that an operator is allowed to manage.
     * @param _tokenId The token for which to query the allowance
     * @param _operator The address of an operator
     * @return The current approval value of `_tokenId` that `_operator` is allowed to manage
     */
    function allowance(uint256 _tokenId, address _operator) external view returns (uint256);

    /**
     * @notice Transfer value from a specified token to another specified token with the same slot.
     * @dev Caller MUST be the current owner, an authorized operator or an operator who has been
     *  approved the whole `_fromTokenId` or part of it.
     *  MUST revert if `_fromTokenId` or `_toTokenId` is zero token id or does not exist.
     *  MUST revert if slots of `_fromTokenId` and `_toTokenId` do not match.
     *  MUST revert if `_value` exceeds the balance of `_fromTokenId` or its allowance to the
     *  operator.
     *  MUST emit `TransferValue` event.
     * @param _fromTokenId The token to transfer value from
     * @param _toTokenId The token to transfer value to
     * @param _value The transferred value
     */
    function transferFrom(
        uint256 _fromTokenId,
        uint256 _toTokenId,
        uint256 _value
    ) external payable;


    /**
     * @notice Transfer value from a specified token to an address. The caller should confirm that
     *  `_to` is capable of receiving ERC-3525 tokens.
     * @dev This function MUST create a new ERC-3525 token with the same slot for `_to`, 
     *  or find an existing token with the same slot owned by `_to`, to receive the transferred value.
     *  MUST revert if `_fromTokenId` is zero token id or does not exist.
     *  MUST revert if `_to` is zero address.
     *  MUST revert if `_value` exceeds the balance of `_fromTokenId` or its allowance to the
     *  operator.
     *  MUST emit `Transfer` and `TransferValue` events.
     * @param _fromTokenId The token to transfer value from
     * @param _to The address to transfer value to
     * @param _value The transferred value
     * @return ID of the token which receives the transferred value
     */
    function transferFrom(
        uint256 _fromTokenId,
        address _to,
        uint256 _value
    ) external payable returns (uint256);
}
```

槽的枚举扩展是可选的。这允许您的合约发布其完整的 `SLOT` 列表并使其可发现。

```solidity
pragma solidity ^0.8.0;

/**
 * @title ERC-3525 Semi-Fungible Token Standard, optional extension for slot enumeration
 * @dev Interfaces for any contract that wants to support enumeration of slots as well as tokens 
 *  with the same slot.
 * Note: the ERC-165 identifier for this interface is 0x3b741b9e.
 */
interface IERC3525SlotEnumerable is IERC3525 /* , IERC721Enumerable */ {

    /**
     * @notice Get the total amount of slots stored by the contract.
     * @return The total amount of slots
     */
    function slotCount() external view returns (uint256);

    /**
     * @notice Get the slot at the specified index of all slots stored by the contract.
     * @param _index The index in the slot list
     * @return The slot at `index` of all slots.
     */
    function slotByIndex(uint256 _index) external view returns (uint256);

    /**
     * @notice Get the total amount of tokens with the same slot.
     * @param _slot The slot to query token supply for
     * @return The total amount of tokens with the specified `_slot`
     */
    function tokenSupplyInSlot(uint256 _slot) external view returns (uint256);

    /**
     * @notice Get the token at the specified index of all tokens with the same slot.
     * @param _slot The slot to query tokens with
     * @param _index The index in the token list of the slot
     * @return The token ID at `_index` of all tokens with `_slot`
     */
    function tokenInSlotByIndex(uint256 _slot, uint256 _index) external view returns (uint256);
}
```

槽级别的批准是可选的。这允许任何希望支持槽批准的合约，使操作员能够管理具有相同槽的代币。

```solidity
pragma solidity ^0.8.0;

/**
 * @title ERC-3525 Semi-Fungible Token Standard, optional extension for approval of slot level
 * @dev Interfaces for any contract that wants to support approval of slot level, which allows an
 *  operator to manage one's tokens with the same slot.
 *  See https://eips.ethereum.org/EIPS/eip-3525
 * Note: the ERC-165 identifier for this interface is 0xb688be58.
 */
interface IERC3525SlotApprovable is IERC3525 {
    /**
     * @dev MUST emit when an operator is approved or disapproved to manage all of `_owner`'s
     *  tokens with the same slot.
     * @param _owner The address whose tokens are approved
     * @param _slot The slot to approve, all of `_owner`'s tokens with this slot are approved
     * @param _operator The operator being approved or disapproved
     * @param _approved Identify if `_operator` is approved or disapproved
     */
    event ApprovalForSlot(address indexed _owner, uint256 indexed _slot, address indexed _operator, bool _approved);

    /**
     * @notice Approve or disapprove an operator to manage all of `_owner`'s tokens with the
     *  specified slot.
     * @dev Caller SHOULD be `_owner` or an operator who has been authorized through
     *  `setApprovalForAll`.
     *  MUST emit ApprovalSlot event.
     * @param _owner The address that owns the ERC-3525 tokens
     * @param _slot The slot of tokens being queried approval of
     * @param _operator The address for whom to query approval
     * @param _approved Identify if `_operator` would be approved or disapproved
     */
    function setApprovalForSlot(
        address _owner,
        uint256 _slot,
        address _operator,
        bool _approved
    ) external payable;

    /**
     * @notice Query if `_operator` is authorized to manage all of `_owner`'s tokens with the
     *  specified slot.
     * @param _owner The address that owns the ERC-3525 tokens
     * @param _slot The slot of tokens being queried approval of
     * @param _operator The address for whom to query approval
     * @return True if `_operator` is authorized to manage all of `_owner`'s tokens with `_slot`,
     *  false otherwise.
     */
    function isApprovedForSlot(
        address _owner,
        uint256 _slot,
        address _operator
    ) external view returns (bool);
}
```


### ERC-3525 代币接收者

如果智能合约希望在接收到来自其他地址的值时被通知，则应实现 `IERC3525Receiver` 接口中的所有函数，在实现中可以决定是否接受或拒绝转移。有关详细信息，请参见“转移规则”。

```solidity
 pragma solidity ^0.8.0;

/**
 * @title ERC-3525 token receiver interface
 * @dev Interface for a smart contract that wants to be informed by ERC-3525 contracts when receiving values from ANY addresses or ERC-3525 tokens.
 * Note: the ERC-165 identifier for this interface is 0x009ce20b.
 */
interface IERC3525Receiver {
    /**
     * @notice Handle the receipt of an ERC-3525 token value.
     * @dev An ERC-3525 smart contract MUST check whether this function is implemented by the recipient contract, if the
     *  recipient contract implements this function, the ERC-3525 contract MUST call this function after a 
     *  value transfer (i.e. `transferFrom(uint256,uint256,uint256,bytes)`).
     *  MUST return 0x009ce20b (i.e. `bytes4(keccak256('onERC3525Received(address,uint256,uint256,
     *  uint256,bytes)'))`) if the transfer is accepted.
     *  MUST revert or return any value other than 0x009ce20b if the transfer is rejected.
     * @param _operator The address which triggered the transfer
     * @param _fromTokenId The token id to transfer value from
     * @param _toTokenId The token id to transfer value to
     * @param _value The transferred value
     * @param _data Additional data with no specified format
     * @return `bytes4(keccak256('onERC3525Received(address,uint256,uint256,uint256,bytes)'))` 
     *  unless the transfer is rejected.
     */
    function onERC3525Received(address _operator, uint256 _fromTokenId, uint256 _toTokenId, uint256 _value, bytes calldata _data) external returns (bytes4);

}
```

### 代币操作

#### 场景

**_转移:_**

除了 ERC-721 兼容的代币转移方法外，此 EIP 引入了两种新的转移模型：从 ID 到 ID 的值转移，以及从 ID 到地址的值转移。

```solidity
function transferFrom(uint256 _fromTokenId, uint256 _toTokenId, uint256 _value) external payable;
	
function transferFrom(uint256 _fromTokenId, address _to, uint256 _value) external payable returns (uint256 toTokenId_);
```

第一个允许从一个代币（由 `_fromTokenId` 指定）到另一个代币（由 `_toTokenId` 指定）在同一槽内进行值转移，导致 `_value` 从源代币的值中减去并添加到目标代币的值中；

第二个允许从一个代币（由 `_fromTokenId` 指定）到一个地址（由 `_to` 指定）进行值转移，值实际上是转移到该地址拥有的代币中，并且应返回目标代币的 ID。有关此方法的进一步解释，请参见“设计决策”部分。

#### 规则

**_批准规则:_**

此 EIP 提供四种批准函数，表示不同级别的批准，可以描述为全级别批准、槽级别批准、代币 ID 级别批准以及值级别批准。

- `setApprovalForAll`，与 ERC-721 兼容，应该表示全级别的批准，这意味着授权的操作员能够管理所有代币，包括其值，所有者拥有的代币。
- `setApprovalForSlot`（可选）应该表示槽级别的批准，这意味着授权的操作员能够管理所有具有指定槽的代币，包括其值，所有者拥有的代币。
- 代币 ID 级别的 `approve` 函数，与 ERC-721 兼容，应该表示授权的操作员能够管理仅指定的代币 ID，包括其值，所有者拥有的代币。
- 值级别的 `approve` 函数，应该表示授权的操作员能够管理所有者拥有的指定代币的指定最大值。
- 对于任何批准函数，调用者必须是所有者或已获得更高权限的批准。

**_transferFrom 规则:_**

- `transferFrom(uint256 _fromTokenId, uint256 _toTokenId, uint256 _value)` 函数，应该表示从一个代币到另一个代币的值转移，遵循以下规则：

  - 除非 `msg.sender` 是 `_fromTokenId` 的所有者、授权的操作员或已获得整个代币或至少其 `_value` 的批准的操作员，否则必须回滚。
  - 如果 `_fromTokenId` 或 `_toTokenId` 是零代币 ID 或不存在，则必须回滚。
  - 如果 `_fromTokenId` 和 `_toTokenId` 的槽不匹配，则必须回滚。
  - 如果 `_value` 超过 `_fromTokenId` 的值或其对操作员的授权，则必须回滚。
  - 如果 _toTokenId 的所有者是智能合约，则必须检查 `onERC3525Received` 函数，如果该函数存在，必须在值转移后调用此函数，如果结果不等于 0x009ce20b，则必须回滚。
  - 必须发出 `TransferValue` 事件。
- `transferFrom(uint256 _fromTokenId, address _to, uint256 _value)` 函数，该函数将值从一个代币 ID 转移到一个地址，必须遵循以下规则：

  - 必须找到一个由地址 `_to` 拥有的 ERC-3525 代币，或者创建一个新的 ERC-3525 代币，使用与 `_fromTokenId` 相同的槽，以接收转移的值。
  - 除非 `msg.sender` 是 `_fromTokenId` 的所有者、授权的操作员或已被批准整个代币或至少 `_value` 的操作员，否则必须回滚。
  - 如果 `_fromTokenId` 是零代币 ID 或不存在，必须回滚。
  - 如果 `_to` 是零地址，必须回滚。
  - 如果 `_value` 超过 `_fromTokenId` 的值或其对操作员的授权，必须回滚。
  - 如果 _to 地址是智能合约，必须检查 `onERC3525Received` 函数，如果该函数存在，必须在值转移后调用此函数，如果结果不等于 0x009ce20b，必须回滚；
  - 必须发出 `Transfer` 和 `TransferValue` 事件。


### 元数据

#### 元数据扩展

ERC-3525 元数据扩展与 ERC-721 元数据扩展兼容。

此可选接口可以通过 ERC-165 标准接口检测来识别。

```solidity
pragma solidity ^0.8.0;

/**
 * @title ERC-3525 Semi-Fungible Token Standard, optional extension for metadata
 * @dev Interfaces for any contract that wants to support query of the Uniform Resource Identifier
 *  (URI) for the ERC-3525 contract as well as a specified slot. 
 *  Because of the higher reliability of data stored in smart contracts compared to data stored in 
 *  centralized systems, it is recommended that metadata, including `contractURI`, `slotURI` and 
 *  `tokenURI`, be directly returned in JSON format, instead of being returned with a url pointing 
 *  to any resource stored in a centralized system. 
 *  See https://eips.ethereum.org/EIPS/eip-3525
 * Note: the ERC-165 identifier for this interface is 0xe1600902.
 */
interface IERC3525Metadata is
    IERC3525 /* , IERC721Metadata */
{
    /**
     * @notice Returns the Uniform Resource Identifier (URI) for the current ERC-3525 contract.
     * @dev This function SHOULD return the URI for this contract in JSON format, starting with
     *  header `data:application/json;`.
     *  See https://eips.ethereum.org/EIPS/eip-3525 for the JSON schema for contract URI.
     * @return The JSON formatted URI of the current ERC-3525 contract
     */
    function contractURI() external view returns (string memory);

    /**
     * @notice Returns the Uniform Resource Identifier (URI) for the specified slot.
     * @dev This function SHOULD return the URI for `_slot` in JSON format, starting with header
     *  `data:application/json;`.
     *  See https://eips.ethereum.org/EIPS/eip-3525 for the JSON schema for slot URI.
     * @return The JSON formatted URI of `_slot`
     */
    function slotURI(uint256 _slot) external view returns (string memory);
}
```

#### ERC-3525 元数据 URI JSON 架构

这是上述提到的“ERC-3525 元数据 JSON 架构，用于 `contractURI()`”。

```json
{
  "title": "Contract Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "Contract Name"
    },
    "description": {
      "type": "string",
      "description": "Describes the contract"
    },
    "image": {
      "type": "string",
      "description": "Optional. Either a base64 encoded imgae data or a URI pointing to a resource with mime type image/* representing what this contract represents."
    },
    "external_link": {
      "type": "string",
      "description": "Optional. A URI pointing to an external resource."
    },
    "valueDecimals": {
      "type": "integer",
      "description": "The number of decimal places that the balance should display - e.g. 18, means to divide the token value by 1000000000000000000 to get its user representation."
    }
  }
}
```

这是上述提到的“ERC-3525 元数据 JSON 架构，用于 `slotURI(uint)`”。

```json
{
  "title": "Slot Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "Identifies the asset category to which this slot represents"
    },
    "description": {
      "type": "string",
      "description": "Describes the asset category to which this slot represents"
    },
    "image": {
      "type": "string",
      "description": "Optional. Either a base64 encoded imgae data or a URI pointing to a resource with mime type image/* representing the asset category to which this slot represents."
    },
    "properties": {
      "type": "array",
      "description": "Each item of `properties` SHOULD be organized in object format, including name, description, value, order (optional), display_type (optional), etc."
      "items": {
        "type": "object",
        "properties": {
          "name": {
            "type": "string",
            "description": "The name of this property."
          },
          "description": {
            "type": "string",
            "description": "Describes this property."
          }
          "value": {
            "description": "The value of this property, which may be a string or a number."
          },
          "is_intrinsic": {
            "type": "boolean",
            "description": "According to the definition of `slot`, one of the best practice to generate the value of a slot is utilizing the `keccak256` algorithm to calculate the hash value of multi properties. In this scenario, the `properties` field should contain all the properties that are used to calculate the value of `slot`, and if a property is used in the calculation, is_intrinsic must be TRUE."
          },
          "order": {
            "type": "integer",
            "description": "Optional, related to the value of is_intrinsic. If is_intrinsic is TRUE, it must be the order of this property appeared in the calculation method of the slot."
          },
          "display_type": {
            "type": "string",
            "description": "Optional. Specifies in what form this property should be displayed."
          }
        }
      }
    }
  }
}
```


这是上述提到的“ERC-3525 元数据 JSON 架构，用于 `tokenURI(uint)`”。

```json
{
  "title": "Token Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "Identifies the asset to which this token represents"
    },
    "description": {
      "type": "string",
      "description": "Describes the asset to which this token represents"
    },
    "image": {
      "type": "string",
      "description": "Either a base64 encoded imgae data or a URI pointing to a resource with mime type image/* representing the asset to which this token represents."
    },
    "balance": {
      "type": "integer",
      "description": "THe value held by this token."
    },
    "slot": {
      "type": "integer",
      "description": "The id of the slot that this token belongs to."
    },
    "properties": {
      "type": "object",
      "description": "Arbitrary properties. Values may be strings, numbers, objects or arrays. Optional, you can use the same schema as the properties section of ERC-3525 Metadata JSON Schema for slotURI(uint) if you need a better description attribute."
    }
  }
}
```


## 理由

### 元数据生成

此代币标准旨在表示半可替代资产，这些资产更适合金融工具而非收藏品或游戏内物品。为了最大限度地提高数字资产的透明度和安全性，我们强烈建议所有实现应直接从合约代码生成元数据，而不是提供离线服务器 URL。

### 设计决策：从代币到地址的值转移

代币的“值”是代币的一个属性，并不与地址相关联，因此将值转移到一个地址实际上是将其转移到该地址拥有的代币，而不是地址本身。

从实现的角度来看，从代币到地址转移值的过程可以如下进行：（1）为接收者的地址创建一个新代币，（2）从“源代币”转移值到新代币。因此，这种方法并不完全独立于 ID 到 ID 的转移方法，可以视为对上述过程的语法糖包装。

在特殊情况下，如果目标地址拥有一个或多个与源代币具有相同槽值的代币，则此方法将有替代实现如下：（1）找到一个由该地址拥有的与源代币具有相同槽值的代币，（2）将值转移到找到的代币。

上述两种实现都应视为符合此标准。

维护 ID 到地址转移功能的目的是最大限度地提高与大多数钱包应用程序的兼容性，因为对于大多数代币标准，代币转移的目标是地址。这种语法包装将帮助钱包应用程序轻松实现从代币到任何地址的值转移功能。

### 设计决策：通知/接受机制而不是“安全转移”

ERC-721 和一些后来的代币标准引入了“安全转移”模型，以更好地控制转移代币时的“安全性”，该机制将不同的转移模式（安全/不安全）的选择留给发送者，并可能导致一些潜在问题：

1. 在大多数情况下，发送者不知道如何在两种转移方法（安全/不安全）之间进行选择；
2. 如果发送者调用 `safeTransferFrom` 方法，如果接收合约未实现回调函数，则转移可能会失败，即使该合约能够接收和处理代币没有问题。

此 EIP 定义了一种简单的“检查、通知和响应”模型，以更好地灵活性和简洁性：

1. 不需要额外的 `safeTransferFrom` 方法，所有调用者只需调用一种转移；
2. 所有 ERC-3525 合约必须检查接收合约上 `onERC3525Received` 的存在，并在存在时调用该函数；
3. 任何智能合约都可以实现 `onERC3525Received` 函数，以便在接收值后被通知；如果转移被接受，则该函数必须返回 0x009ce20b（即 `bytes4(keccak256('onERC3525Received(address,uint256,uint256,uint256,bytes)'))`），如果转移被拒绝，则返回任何其他值。

对于此通知/接受机制有一个特殊情况：由于 ERC-3525 允许从地址转移值到自身，当实现 `onERC3525Received` 的智能合约将值转移到自身时，`onERC3525Received` 也会被调用。这允许合约在自值转移和从其他地址接收值之间实现不同的接受规则。

### 设计决策：不同批准模型之间的关系

为了与 ERC-721 语义兼容以及代币值操作的灵活性，我们决定定义一些批准级别之间的关系如下：

1. 对一个 ID 的批准将导致被批准的操作员能够部分转移该 ID 的值；这将简化对一个 ID 的值批准。然而，代币的总值批准不应导致被批准的操作员能够转移代币实体。
2. `setApprovalForAll` 将导致能够部分转移任何代币的值，以及能够批准将任何代币的值部分转移给第三方；这将简化对地址拥有的所有代币的值转移和批准。

## 向后兼容性

如开头所述，此 EIP 与 ERC-721 向后兼容。

## 参考实现

- [ERC-3525 实现](../assets/eip-3525/contracts/ERC3525.sol)

## 安全考虑

值级别批准和槽级别批准（可选）与 ERC-721 批准模型隔离，因此批准值不应影响 ERC-721 级别的批准。此 EIP 的实现必须遵循这一原则。

由于此 EIP 与 ERC-721 兼容，任何能够持有和操作标准 ERC-721 代币的钱包和智能合约都不会因不兼容的标准实现而面临 ERC-3525 代币的资产损失风险。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。