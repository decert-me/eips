---
eip: 6220
title: 可组合的 NFT 利用可装备部件
description: 通过固定和插槽部件装备的可组合非同质化代币接口。
author: Bruno Škvorc (@Swader), Cicada (@CicadaNCR), Steven Pineda (@steven2308), Stevan Bogosavljevic (@stevyhacker), Jan Turk (@ThunderDeliverer)
discussions-to: https://ethereum-magicians.org/t/eip-6220-composable-nfts-utilizing-equippable-parts/12289
status: Final
type: Standards Track
category: ERC
created: 2022-12-20
requires: 165, 721, 5773, 6059
---

## 摘要

可组合的 NFT 利用可装备部件标准扩展了 [ERC-721](./eip-721.md)，允许 NFT 通过装备选择性地添加部件。

代币可以通过从每个 NFT 实例的目录中挑选部件列表进行组合，并能够将其他 NFT 装备到插槽中，这些插槽也在目录中定义。目录包含可以组合 NFT 的部件。

本提案引入了两种类型的部件：插槽类型部件和固定类型部件。插槽类型部件允许其他 NFT 集合装备到其中，而固定部件是具有自己元数据的完整组件。

将部件装备到 NFT 中不会生成新代币，而是添加另一个组件，以便在检索代币时进行渲染。

## 动机

随着 NFT 在以太坊生态系统中成为广泛使用的代币形式，并用于各种用例，现在是时候为它们标准化额外的实用性。代币能够装备其他代币并从一组可用部件中组合，允许更大的实用性、可用性和向前兼容性。

自 [ERC-721](./eip-721.md) 发布以来的四年中，额外功能的需求导致了无数扩展。此 EIP 在以下方面改进了 ERC-721：

- [组合](#composing)
- [代币进展](#token-progression)
- [功绩跟踪](#merit-tracking)
- [可证明的数字稀缺性](#provable-digital-scarcity)

### 组合

NFT 可以协同工作以创建更大的构造。在此提案之前，可以通过检查与给定账户关联的所有兼容 NFT 并不加区分地使用（如果有多个 NFT 意图在同一插槽中使用，这可能导致意外结果），或者通过保持自定义的部件账本进行组合（无论是在智能合约中还是在链下数据库中）。本提案建立了一个可组合 NFT 的标准化框架，其中单个 NFT 可以选择哪些部件应成为整体的一部分，信息在链上。以这种方式组合 NFT 允许对基础 NFT 进行几乎无限的自定义。一个例子可能是电影 NFT。一些部件，如片尾字幕，应是固定的。其他部件，如场景，应是可互换的，以便可以替换各种版本（基础版本、扩展版、周年纪念版等）。

### 代币进展

随着代币在其存在的各个阶段的进展，它可以获得或被授予各种部件。这可以用游戏来解释。一个角色可以通过利用此提案的 NFT 表示，并能够装备通过游戏活动获得的装备，随着其在游戏中的进一步进展，可以获得更好的物品。与其拥有多个 NFT 来表示其进展中收集的物品，不如解锁可装备的部件，NFT 所有者可以决定装备哪些物品，哪些物品保留在库存中（未装备），而无需集中方的介入。

### 功绩跟踪

可装备的 NFT 还可以用于跟踪功绩。一个例子是学术功绩。在这种情况下，可装备的 NFT 将代表一种学术成就的数字档案，所有者可以装备他们的文凭、发表的文章和奖项供所有人查看。

### 可证明的数字稀缺性

当前大多数 NFT 项目只是模拟稀缺性。即使代币的供应有限，这些代币的实用性（如果有的话）也是无限的。例如，您可以使用相同的钱包和相同的 NFT 登录 500 个不同的游戏实例。然后，您可以同时将同一顶帽子装备到 500 个不同的游戏角色上，因为其视觉表现只是一个客户端机制。

本提案增加了强制执行的能力，即如果一顶帽子装备在一个角色上（通过发送到该角色并然后装备），则不能装备到另一个角色上。这提供了真正的数字稀缺性。

## 规范

本文件中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

### 可装备代币

可装备代币核心智能合约的接口。

```solidity
/// @title EIP-6220 Composable NFTs utilizing Equippable Parts
/// @dev See https://eips.ethereum.org/EIPS/eip-6220
/// @dev Note: the ERC-165 identifier for this interface is 0x28bc9ae4.

pragma solidity ^0.8.16;

import "./IERC5773.sol";

interface IERC6220 is IERC5773 /*, ERC165 */ {
    /**
     * @notice Used to store the core structure of the `Equippable` component.
     * @return assetId The ID of the asset equipping a child
     * @return childAssetId The ID of the asset used as equipment
     * @return childId The ID of token that is equipped
     * @return childEquippableAddress Address of the collection to which the child asset belongs to
     */
    struct Equipment {
        uint64 assetId;
        uint64 childAssetId;
        uint256 childId;
        address childEquippableAddress;
    }

    /**
     * @notice Used to provide a struct for inputing equip data.
     * @dev Only used for input and not storage of data.
     * @return tokenId ID of the token we are managing
     * @return childIndex Index of a child in the list of token's active children
     * @return assetId ID of the asset that we are equipping into
     * @return slotPartId ID of the slot part that we are using to equip
     * @return childAssetId ID of the asset that we are equipping
     */
    struct IntakeEquip {
        uint256 tokenId;
        uint256 childIndex;
        uint64 assetId;
        uint64 slotPartId;
        uint64 childAssetId;
    }

    /**
     * @notice Used to notify listeners that a child's asset has been equipped into one of its parent assets.
     * @param tokenId ID of the token that had an asset equipped
     * @param assetId ID of the asset associated with the token we are equipping into
     * @param slotPartId ID of the slot we are using to equip
     * @param childId ID of the child token we are equipping into the slot
     * @param childAddress Address of the child token's collection
     * @param childAssetId ID of the asset associated with the token we are equipping
     */
    event ChildAssetEquipped(
        uint256 indexed tokenId,
        uint64 indexed assetId,
        uint64 indexed slotPartId,
        uint256 childId,
        address childAddress,
        uint64 childAssetId
    );

    /**
     * @notice Used to notify listeners that a child's asset has been unequipped from one of its parent assets.
     * @param tokenId ID of the token that had an asset unequipped
     * @param assetId ID of the asset associated with the token we are unequipping out of
     * @param slotPartId ID of the slot we are unequipping from
     * @param childId ID of the token being unequipped
     * @param childAddress Address of the collection that a token that is being unequipped belongs to
     * @param childAssetId ID of the asset associated with the token we are unequipping
     */
    event ChildAssetUnequipped(
        uint256 indexed tokenId,
        uint64 indexed assetId,
        uint64 indexed slotPartId,
        uint256 childId,
        address childAddress,
        uint64 childAssetId
    );

    /**
     * @notice Used to notify listeners that the assets belonging to a `equippableGroupId` have been marked as
     *  equippable into a given slot and parent
     * @param equippableGroupId ID of the equippable group being marked as equippable into the slot associated with
     *  `slotPartId` of the `parentAddress` collection
     * @param slotPartId ID of the slot part of the catalog into which the parts belonging to the equippable group
     *  associated with `equippableGroupId` can be equipped
     * @param parentAddress Address of the collection into which the parts belonging to `equippableGroupId` can be
     *  equipped
     */
    event ValidParentEquippableGroupIdSet(
        uint64 indexed equippableGroupId,
        uint64 indexed slotPartId,
        address parentAddress
    );

    /**
     * @notice Used to equip a child into a token.
     * @dev The `IntakeEquip` stuct contains the following data:
     *  [
     *      tokenId,
     *      childIndex,
     *      assetId,
     *      slotPartId,
     *      childAssetId
     *  ]
     * @param data An `IntakeEquip` struct specifying the equip data
     */
    function equip(
        IntakeEquip memory data
    ) external;

    /**
     * @notice Used to unequip child from parent token.
     * @dev This can only be called by the owner of the token or by an account that has been granted permission to
     *  manage the given token by the current owner.
     * @param tokenId ID of the parent from which the child is being unequipped
     * @param assetId ID of the parent's asset that contains the `Slot` into which the child is equipped
     * @param slotPartId ID of the `Slot` from which to unequip the child
     */
    function unequip(
        uint256 tokenId,
        uint64 assetId,
        uint64 slotPartId
    ) external;

    /**
     * @notice Used to check whether the token has a given child equipped.
     * @dev This is used to prevent from transferring a child that is equipped.
     * @param tokenId ID of the parent token for which we are querying for
     * @param childAddress Address of the child token's smart contract
     * @param childId ID of the child token
     * @return bool The boolean value indicating whether the child token is equipped into the given token or not
     */
    function isChildEquipped(
        uint256 tokenId,
        address childAddress,
        uint256 childId
    ) external view returns (bool);

    /**
     * @notice Used to verify whether a token can be equipped into a given parent's slot.
     * @param parent Address of the parent token's smart contract
     * @param tokenId ID of the token we want to equip
     * @param assetId ID of the asset associated with the token we want to equip
     * @param slotId ID of the slot that we want to equip the token into
     * @return bool The boolean indicating whether the token with the given asset can be equipped into the desired
     *  slot
     */
    function canTokenBeEquippedWithAssetIntoSlot(
        address parent,
        uint256 tokenId,
        uint64 assetId,
        uint64 slotId
    ) external view returns (bool);

    /**
     * @notice Used to get the Equipment object equipped into the specified slot of the desired token.
     * @dev The `Equipment` struct consists of the following data:
     *  [
     *      assetId,
     *      childAssetId,
     *      childId,
     *      childEquippableAddress
     *  ]
     * @param tokenId ID of the token for which we are retrieving the equipped object
     * @param targetCatalogAddress Address of the `Catalog` associated with the `Slot` part of the token
     * @param slotPartId ID of the `Slot` part that we are checking for equipped objects
     * @return struct The `Equipment` struct containing data about the equipped object
     */
    function getEquipment(
        uint256 tokenId,
        address targetCatalogAddress,
        uint64 slotPartId
    ) external view returns (Equipment memory);

    /**
     * @notice Used to get the asset and equippable data associated with given `assetId`.
     * @param tokenId ID of the token for which to retrieve the asset
     * @param assetId ID of the asset of which we are retrieving
     * @return metadataURI The metadata URI of the asset
     * @return equippableGroupId ID of the equippable group this asset belongs to
     * @return catalogAddress The address of the catalog the part belongs to
     * @return partIds An array of IDs of parts included in the asset
     */
    function getAssetAndEquippableData(uint256 tokenId, uint64 assetId)
        external
        view
        returns (
            string memory metadataURI,
            uint64 equippableGroupId,
            address catalogAddress,
            uint64[] calldata partIds
        );
}
```

### 目录

包含可装备部件的目录接口。目录是可装备的固定和插槽部件的集合，并不局限于单个集合，而是可以支持任意数量的 NFT 集合。

```solidity
/**
 * @title ICatalog
 * @notice An interface Catalog for equippable module.
 * @dev Note: the ERC-165 identifier for this interface is 0xd912401f.
 */

pragma solidity ^0.8.16;

interface ICatalog /* is IERC165 */ {
    /**
     * @notice Event to announce addition of a new part.
     * @dev It is emitted when a new part is added.
     * @param partId ID of the part that was added
     * @param itemType Enum value specifying whether the part is `None`, `Slot` and `Fixed`
     * @param zIndex An uint specifying the z value of the part. It is used to specify the depth which the part should
     *  be rendered at
     * @param equippableAddresses An array of addresses that can equip this part
     * @param metadataURI The metadata URI of the part
     */
    event AddedPart(
        uint64 indexed partId,
        ItemType indexed itemType,
        uint8 zIndex,
        address[] equippableAddresses,
        string metadataURI
    );

    /**
     * @notice Event to announce new equippables to the part.
     * @dev It is emitted when new addresses are marked as equippable for `partId`.
     * @param partId ID of the part that had new equippable addresses added
     * @param equippableAddresses An array of the new addresses that can equip this part
     */
    event AddedEquippables(
        uint64 indexed partId,
        address[] equippableAddresses
    );

    /**
     * @notice Event to announce the overriding of equippable addresses of the part.
     * @dev It is emitted when the existing list of addresses marked as equippable for `partId` is overwritten by a new
     *  one.
     * @param partId ID of the part whose list of equippable addresses was overwritten
     * @param equippableAddresses The new, full, list of addresses that can equip this part
     */
    event SetEquippables(uint64 indexed partId, address[] equippableAddresses);

    /**
     * @notice Event to announce that a given part can be equipped by any address.
     * @dev It is emitted when a given part is marked as equippable by any.
     * @param partId ID of the part marked as equippable by any address
     */
    event SetEquippableToAll(uint64 indexed partId);

    /**
     * @notice Used to define a type of the item. Possible values are `None`, `Slot` or `Fixed`.
     * @dev Used for fixed and slot parts.
     */
    enum ItemType {
        None,
        Slot,
        Fixed
    }

    /**
     * @notice The integral structure of a standard RMRK catalog item defining it.
     * @dev Requires a minimum of 3 storage slots per catalog item, equivalent to roughly 60,000 gas as of Berlin hard fork
     *  (April 14, 2021), though 5-7 storage slots is more realistic, given the standard length of an IPFS URI. This
     *  will result in between 25,000,000 and 35,000,000 gas per 250 assets--the maximum block size of Ethereum
     *  mainnet is 30M at peak usage.
     * @return itemType The item type of the part
     * @return z The z value of the part defining how it should be rendered when presenting the full NFT
     * @return equippable The array of addresses allowed to be equipped in this part
     * @return metadataURI The metadata URI of the part
     */
    struct Part {
        ItemType itemType; //1 byte
        uint8 z; //1 byte
        address[] equippable; //n Collections that can be equipped into this slot
        string metadataURI; //n bytes 32+
    }

    /**
     * @notice The structure used to add a new `Part`.
     * @dev The part is added with specified ID, so you have to make sure that you are using an unused `partId`,
     *  otherwise the addition of the part vill be reverted.
     * @dev The full `IntakeStruct` looks like this:
     *  [
     *          partID,
     *      [
     *          itemType,
     *          z,
     *          [
     *               permittedCollectionAddress0,
     *               permittedCollectionAddress1,
     *               permittedCollectionAddress2
     *           ],
     *           metadataURI
     *       ]
     *   ]
     * @return partId ID to be assigned to the `Part`
     * @return part A `Part` to be added
     */
    struct IntakeStruct {
        uint64 partId;
        Part part;
    }

    /**
     * @notice Used to return the metadata URI of the associated catalog.
     * @return string Base metadata URI
     */
    function getMetadataURI() external view returns (string memory);

    /**
     * @notice Used to return the `itemType` of the associated catalog
     * @return string `itemType` of the associated catalog
     */
    function getType() external view returns (string memory);

    /**
     * @notice Used to check whether the given address is allowed to equip the desired `Part`.
     * @dev Returns true if a collection may equip asset with `partId`.
     * @param partId The ID of the part that we are checking
     * @param targetAddress The address that we are checking for whether the part can be equipped into it or not
     * @return bool The status indicating whether the `targetAddress` can be equipped into `Part` with `partId` or not
     */
    function checkIsEquippable(uint64 partId, address targetAddress)
        external
        view
        returns (bool);

    /**
     * @notice Used to check if the part is equippable by all addresses.
     * @dev Returns true if part is equippable to all.
     * @param partId ID of the part that we are checking
     * @return bool The status indicating whether the part with `partId` can be equipped by any address or not
     */
    function checkIsEquippableToAll(uint64 partId) external view returns (bool);

    /**
     * @notice Used to retrieve a `Part` with id `partId`
     * @param partId ID of the part that we are retrieving
     * @return struct The `Part` struct associated with given `partId`
     */
    function getPart(uint64 partId) external view returns (Part memory);

    /**
     * @notice Used to retrieve multiple parts at the same time.
     * @param partIds An array of part IDs that we want to retrieve
     * @return struct An array of `Part` structs associated with given `partIds`
     */
    function getParts(uint64[] calldata partIds)
        external
        view
        returns (Part[] memory);
}
```

## 理由

在设计提案时，我们考虑了以下问题：

1. **为什么我们使用目录而不是支持直接 NFT 装备？**\
如果 NFT 可以直接装备到其他 NFT 中而没有任何监督，结果的复合体将是不可预测的。目录允许对部件进行预验证，以便生成按预期组合的复合体。目录的另一个好处是能够定义可重用的固定部件。
2. **为什么我们提议两种类型的部件？**\
一些对所有代币相同的部件，不适合由单独的 NFT 表示，因此可以由固定部件表示。这减少了所有者钱包的杂乱，并引入了一种有效的方式来传播与 NFT 相关的重复资产。\
插槽部件允许将 NFT 装备到其中。这提供了在验证不相关集合能够正确组合后，将不相关的 NFT 集合装备到基础 NFT 的能力。\
拥有两种部件可以支持众多用例，并且由于提案不强制使用两者，因此可以根据需要以任何配置应用。
3. **为什么不包括获取所有装备部件的方法？**\
获取所有部件可能不是所有实现者所需的操作。此外，它可以作为扩展添加，使用钩子实现，或可以使用索引器进行模拟。
4. **目录是否应限制为一次支持一个 NFT 集合，还是能够支持任意数量的集合？**\
由于目录的设计方式与使用它的用例无关，因此支持尽可能广泛的重用是有意义的。拥有一个支持多个集合的目录可以在部署时优化操作并降低 gas 费用，同时设置固定和插槽部件。

### 固定部件

固定部件在目录中定义并包含。它们具有自己的元数据，并且在 NFT 的生命周期中不应更改。

固定部件不能被替换。

固定部件的好处在于，它们代表可装备的部件，可以被任意数量的代币在任意数量的集合中装备，并且只需定义一次。
### 插槽部分

插槽部分在目录中定义和包含。它们没有自己的元数据，而是支持将选定的 NFT 集合装备到其中。然而，装备到插槽中的代币包含自己的元数据。这允许由其所有者控制的基础 NFT 的可装备可修改内容。由于它们可以装备到任何数量的代币和任何数量的集合中，因此它们允许通过审核哪些 NFT 可以被给定插槽装备，从而可靠地组合最终代币，并且可以重复使用多次。

## 向后兼容性

可装备代币标准已与 [ERC-721](./eip-721.md) 兼容，以利用可用于 ERC-721 实现的强大工具，并确保与现有 ERC-721 基础设施的兼容性。

## 测试用例

测试包含在 [`equippableFixedParts.ts`](../assets/eip-6220/test/equippableFixedParts.ts) 和 [`equippableSlotParts.ts`](../assets/eip-6220/test/equippableSlotParts.ts) 中。

要在终端中运行它们，可以使用以下命令：

```
cd ../assets/eip-6220
npm install
npx hardhat test
```

## 参考实现

请参见 [`EquippableToken.sol`](../assets/eip-6220/contracts/EquippableToken.sol)。

## 安全考虑

与 [ERC-721](./eip-721.md) 相同的安全考虑适用：任何函数中可能存在隐藏逻辑，包括销毁、添加资源、接受资源等。

处理未审计合同时请谨慎。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。