---
eip: 6381
title: 公共非同质化代币表情库
description: 使用 Unicode 表情符号对任何非同质化代币进行反应。
author: Bruno Škvorc (@Swader), Steven Pineda (@steven2308), Stevan Bogosavljevic (@stevyhacker), Jan Turk (@ThunderDeliverer)
discussions-to: https://ethereum-magicians.org/t/eip-6381-emotable-extension-for-non-fungible-tokens/12710
status: 最终
type: 标准跟踪
category: ERC
created: 2023-01-22
requires: 165
---

## 摘要

公共非同质化代币表情库标准为 [ERC-721](./eip-721.md) 和 [ERC-1155](./eip-1155.md) 提供了增强的互动功能，允许对 NFT 进行表情反应。

该提案引入了在一个公共非门控的库智能合约中使用 Unicode 标准化表情符号对 NFT 进行反应的能力，该合约在所有网络中都可以在相同地址访问。

## 动机

随着 NFT 在以太坊生态系统中成为广泛使用的代币形式，并用于各种用例，现在是时候为它们标准化额外的功能了。任何人都能与 NFT 互动的能力为拥有 NFT 引入了互动的方面，并解锁了基于反馈的 NFT 机制。

该 ERC 在以下领域为基于 [ERC-721](./eip-721.md) 的代币引入了新的功能：

- [互动性](#interactivity)
- [基于反馈的演变](#feedback-based-evolution)
- [估值](#valuation)

### 互动性

对 NFT 进行表情反应的能力为拥有 NFT 引入了互动的方面。这可以反映出表情者（对 NFT 表情的人）的钦佩，或者可以是代币所有者执行某个操作的结果。在代币上积累表情可以增加其独特性和/或价值。

### 基于反馈的演变

对 NFT 的标准化链上反应允许基于反馈的演变。

当前的解决方案要么是专有的，要么是链下的，因此容易受到操控和不信任。能够在链上跟踪互动可以确保信任和对特定代币的客观评估。当达到某些表情阈值时，设计代币以演变可以激励与代币集合的互动。

### 估值

当前的 NFT 市场严重依赖于代币之前的销售价格、列出代币的最低价格以及市场提供的稀缺性数据。没有实时的钦佩或特定代币的吸引力的指示。用户能够对代币进行表情反应增加了潜在买家和卖家根据代币所收集的印象来评估代币价值的可能性。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

```solidity
/// @title ERC-6381 Emotable Extension for Non-Fungible Tokens
/// @dev See https://eips.ethereum.org/EIPS/eip-6381
/// @dev Note: the ERC-165 identifier for this interface is 0xd9fac55a.

pragma solidity ^0.8.16;

interface IERC6381 /*is IERC165*/ {
    /**
     * @notice Used to notify listeners that the token with the specified ID has been emoted to or that the reaction has been revoked.
     * @dev The event MUST only be emitted if the state of the emote is changed.
     * @param emoter Address of the account that emoted or revoked the reaction to the token
     * @param collection Address of the collection smart contract containing the token being emoted to or having the reaction revoked
     * @param tokenId ID of the token
     * @param emoji Unicode identifier of the emoji
     * @param on Boolean value signifying whether the token was emoted to (`true`) or if the reaction has been revoked (`false`)
     */
    event Emoted(
        address indexed emoter,
        address indexed collection,
        uint256 indexed tokenId,
        bytes4 emoji,
        bool on
    );

    /**
     * @notice Used to get the number of emotes for a specific emoji on a token.
     * @param collection Address of the collection containing the token being checked for emoji count
     * @param tokenId ID of the token to check for emoji count
     * @param emoji Unicode identifier of the emoji
     * @return Number of emotes with the emoji on the token
     */
    function emoteCountOf(
        address collection,
        uint256 tokenId,
        bytes4 emoji
    ) external view returns (uint256);

    /**
     * @notice Used to get the number of emotes for a specific emoji on a set of tokens.
     * @param collections An array of addresses of the collections containing the tokens being checked for emoji count
     * @param tokenIds An array of IDs of the tokens to check for emoji count
     * @param emojis An array of unicode identifiers of the emojis
     * @return An array of numbers of emotes with the emoji on the tokens
     */
    function bulkEmoteCountOf(
        address[] memory collections,
        uint256[] memory tokenIds,
        bytes4[] memory emojis
    ) external view returns (uint256[] memory);

    /**
     * @notice Used to get the information on whether the specified address has used a specific emoji on a specific
     *  token.
     * @param emoter Address of the account we are checking for a reaction to a token
     * @param collection Address of the collection smart contract containing the token being checked for emoji reaction
     * @param tokenId ID of the token being checked for emoji reaction
     * @param emoji The ASCII emoji code being checked for reaction
     * @return A boolean value indicating whether the `emoter` has used the `emoji` on the token (`true`) or not
     *  (`false`)
     */
    function hasEmoterUsedEmote(
        address emoter,
        address collection,
        uint256 tokenId,
        bytes4 emoji
    ) external view returns (bool);

    /**
     * @notice Used to get the information on whether the specified addresses have used specific emojis on specific
     *  tokens.
     * @param emoters An array of addresses of the accounts we are checking for reactions to tokens
     * @param collections An array of addresses of the collection smart contracts containing the tokens being checked
     *  for emoji reactions
     * @param tokenIds An array of IDs of the tokens being checked for emoji reactions
     * @param emojis An array of the ASCII emoji codes being checked for reactions
     * @return An array of boolean values indicating whether the `emoter`s has used the `emoji`s on the tokens (`true`)
     *  or not (`false`)
     */
    function haveEmotersUsedEmotes(
        address[] memory emoters,
        address[] memory collections,
        uint256[] memory tokenIds,
        bytes4[] memory emojis
    ) external view returns (bool[] memory);

    /**
     * @notice Used to get the message to be signed by the `emoter` in order for the reaction to be submitted by someone
     *  else.
     * @param collection The address of the collection smart contract containing the token being emoted at
     * @param tokenId ID of the token being emoted
     * @param emoji Unicode identifier of the emoji
     * @param state Boolean value signifying whether to emote (`true`) or undo (`false`) emote
     * @param deadline UNIX timestamp of the deadline for the signature to be submitted
     * @return The message to be signed by the `emoter` in order for the reaction to be submitted by someone else
     */
    function prepareMessageToPresignEmote(
        address collection,
        uint256 tokenId,
        bytes4 emoji,
        bool state,
        uint256 deadline
    ) external view returns (bytes32);

    /**
     * @notice Used to get multiple messages to be signed by the `emoter` in order for the reaction to be submitted by someone
     *  else.
     * @param collections An array of addresses of the collection smart contracts containing the tokens being emoted at
     * @param tokenIds An array of IDs of the tokens being emoted
     * @param emojis An arrau of unicode identifiers of the emojis
     * @param states An array of boolean values signifying whether to emote (`true`) or undo (`false`) emote
     * @param deadlines An array of UNIX timestamps of the deadlines for the signatures to be submitted
     * @return The array of messages to be signed by the `emoter` in order for the reaction to be submitted by someone else
     */
    function bulkPrepareMessagesToPresignEmote(
        address[] memory collections,
        uint256[] memory tokenIds,
        bytes4[] memory emojis,
        bool[] memory states,
        uint256[] memory deadlines
    ) external view returns (bytes32[] memory);

    /**
     * @notice Used to emote or undo an emote on a token.
     * @dev Does nothing if attempting to set a pre-existent state.
     * @dev MUST emit the `Emoted` event is the state of the emote is changed.
     * @param collection Address of the collection containing the token being emoted at
     * @param tokenId ID of the token being emoted
     * @param emoji Unicode identifier of the emoji
     * @param state Boolean value signifying whether to emote (`true`) or undo (`false`) emote
     */
    function emote(
        address collection,
        uint256 tokenId,
        bytes4 emoji,
        bool state
    ) external;

    /**
     * @notice Used to emote or undo an emote on multiple tokens.
     * @dev Does nothing if attempting to set a pre-existent state.
     * @dev MUST emit the `Emoted` event is the state of the emote is changed.
     * @dev MUST revert if the lengths of the `collections`, `tokenIds`, `emojis` and `states` arrays are not equal.
     * @param collections An array of addresses of the collections containing the tokens being emoted at
     * @param tokenIds An array of IDs of the tokens being emoted
     * @param emojis An array of unicode identifiers of the emojis
     * @param states An array of boolean values signifying whether to emote (`true`) or undo (`false`) emote
     */
    function bulkEmote(
        address[] memory collections,
        uint256[] memory tokenIds,
        bytes4[] memory emojis,
        bool[] memory states
    ) external;

    /**
     * @notice Used to emote or undo an emote on someone else's behalf.
     * @dev Does nothing if attempting to set a pre-existent state.
     * @dev MUST emit the `Emoted` event is the state of the emote is changed.
     * @dev MUST revert if the lengths of the `collections`, `tokenIds`, `emojis` and `states` arrays are not equal.
     * @dev MUST revert if the `deadline` has passed.
     * @dev MUST revert if the recovered address is the zero address.
     * @param emoter The address that presigned the emote
     * @param collection The address of the collection smart contract containing the token being emoted at
     * @param tokenId IDs of the token being emoted
     * @param emoji Unicode identifier of the emoji
     * @param state Boolean value signifying whether to emote (`true`) or undo (`false`) emote
     * @param deadline UNIX timestamp of the deadline for the signature to be submitted
     * @param v `v` value of an ECDSA signature of the message obtained via `prepareMessageToPresignEmote`
     * @param r `r` value of an ECDSA signature of the message obtained via `prepareMessageToPresignEmote`
     * @param s `s` value of an ECDSA signature of the message obtained via `prepareMessageToPresignEmote`
     */
    function presignedEmote(
        address emoter,
        address collection,
        uint256 tokenId,
        bytes4 emoji,
        bool state,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @notice Used to bulk emote or undo an emote on someone else's behalf.
     * @dev Does nothing if attempting to set a pre-existent state.
     * @dev MUST emit the `Emoted` event is the state of the emote is changed.
     * @dev MUST revert if the lengths of the `collections`, `tokenIds`, `emojis` and `states` arrays are not equal.
     * @dev MUST revert if the `deadline` has passed.
     * @dev MUST revert if the recovered address is the zero address.
     * @param emoters An array of addresses of the accounts that presigned the emotes
     * @param collections An array of addresses of the collections containing the tokens being emoted at
     * @param tokenIds An array of IDs of the tokens being emoted
     * @param emojis An array of unicode identifiers of the emojis
     * @param states An array of boolean values signifying whether to emote (`true`) or undo (`false`) emote
     * @param deadlines UNIX timestamp of the deadline for the signature to be submitted
     * @param v An array of `v` values of an ECDSA signatures of the messages obtained via `prepareMessageToPresignEmote`
     * @param r An array of `r` values of an ECDSA signatures of the messages obtained via `prepareMessageToPresignEmote`
     * @param s An array of `s` values of an ECDSA signatures of the messages obtained via `prepareMessageToPresignEmote`
     */
    function bulkPresignedEmote(
        address[] memory emoters,
        address[] memory collections,
        uint256[] memory tokenIds,
        bytes4[] memory emojis,
        bool[] memory states,
        uint256[] memory deadlines,
        uint8[] memory v,
        bytes32[] memory r,
        bytes32[] memory s
    ) external;
}
```

### 预签名表情的消息格式

由 `emoter` 签名的消息格式如下，以便由其他人提交反应：

```solidity
keccak256(
        abi.encode(
            DOMAIN_SEPARATOR,
            collection,
            tokenId,
            emoji,
            state,
            deadline
        )
    );
```

生成待签名消息时传递的值为：

- `DOMAIN_SEPARATOR` - 表情库智能合约的域分隔符
- `collection` - 包含被表情的代币的集合地址
- `tokenId` - 被表情的代币 ID
- `emoji` - 表情符号的 Unicode 标识符
- `state` - 布尔值，表示是否进行表情（`true`）或撤销表情（`false`）
- `deadline` - 提交签名的截止时间的 UNIX 时间戳

`DOMAIN_SEPARATOR` 的生成方式如下：

```solidity
keccak256(
        abi.encode(
            "ERC-6381: Public Non-Fungible Token Emote Repository",
            "1",
            block.chainid,
            address(this)
        )
    );
```

每个部署了表情库智能合约的链将具有不同的 `DOMAIN_SEPARATOR` 值，因为链 ID 是不同的。

### 预定的表情库地址

表情库智能合约的地址设计为类似于其所服务的功能。它以 `0x311073` 开头，这是 `EMOTE` 的抽象表示。地址为：

```
0x31107354b61A0412E722455A771bC462901668eA
```

## 理由

在设计提案时，我们考虑了以下问题：

1. **提案是否支持自定义表情，还是仅支持 Unicode 指定的表情？**\
该提案仅接受 Unicode 标识符，这是一个 `bytes4` 值。这意味着虽然我们鼓励实施者使用标准化表情添加反应，但不在 Unicode 标准覆盖范围内的值可以用于自定义表情。唯一的缺点是，显示反应的界面必须知道渲染什么样的图像，这样的添加可能仅限于其制作的界面或市场。
2. **提案是否应该使用表情来传达 NFT 的印象，还是使用其他方法？**\
印象可以使用用户提供的字符串或数值来完成，但我们决定使用表情，因为它们是传达印象和情感的成熟手段。
3. **提案是否应该建立一个可表情的扩展或一个公共利益库？**\
最初我们打算创建一个可表情的扩展，以便与任何符合 ERC-721 的代币一起使用。然而，我们意识到，如果它是一个公共利益的可表情代币库，提案将更有用。这样，可以反应的代币不仅是新的，还有在提案之前就存在的旧代币。\
根据这一决定，我们决定为库智能合约计算一个确定性地址。这样，任何 NFT 集合都可以使用该库，而无需在给定链上搜索地址。
4. **我们应该仅包含单一操作，还是仅包含多重操作，或两者都包含？**\
我们考虑过仅包含单一操作，用户只能对单个代币进行单一表情反应，但我们决定同时包含单一操作和多重操作。这样，用户可以选择是否要对单个代币或多个代币同时进行表情或撤销表情。\
这一决定是基于提案的长期可行性。根据网络的 gas 成本和集合中的代币数量，用户可以选择最具成本效益的表情方式。
5. **我们应该添加代表他人进行表情的能力吗？**\
虽然在起草提案时我们并不打算将其作为提案的一部分，但我们意识到这将是一个有用的功能。这样，用户可以代表他人进行表情，例如，如果他们自己无法做到，或者如果表情是通过链下活动获得的。
6. **我们如何确保代表他人进行表情是合法的？**\
我们可以向提案中添加委托；当用户将其表情权委托给他人时，委托人可以代表他们进行表情。然而，这将为提案增加很多复杂性和额外逻辑。\
使用 ECDSA 签名，我们可以确保用户已同意代表他们进行表情。这样，用户可以用表情的参数签署一条消息，签名可以由其他人提交。
7. **我们应该在对代币进行反应时添加链 ID 作为参数吗？**\
在提案讨论过程中，有人建议我们可以在对代币进行反应时添加链 ID 作为参数。这将允许用户在一个链上对另一个链的代币进行表情反应。\
我们决定不这样做，因为我们认为这个额外的参数很少会被使用，并且会增加反应交易的额外成本。如果集合智能合约希望利用链上表情对其包含的代币进行记录，则需要在同一链上记录反应。集成该提案的市场和钱包也将依赖于反应存在于同一链上，因为如果支持链 ID 参数，这将意味着他们需要查询在所有部署了库的链上的库智能合约，以获取给定代币的反应。\
此外，如果集合创建者希望用户在不同链上记录他们的反应，他们仍然可以引导用户这样做。库不会验证被反应的代币是否存在，这在理论上意味着您可以对不存在的代币或尚不存在的代币进行反应。在另一个链上以相同地址存在不同集合的可能性非常低，因此用户可以使用另一个链上集合的地址进行反应，而不太可能无意中对另一个集合的代币进行反应。
## 向后兼容性

Emote 仓库标准与 [ERC-721](./eip-721.md) 完全兼容，并且与 ERC-721 的实现工具以及现有的 ERC-721 基础设施兼容。

## 测试用例

测试包含在 [`emotableRepository.ts`](../assets/eip-6381/test/emotableRepository.ts) 中。

要在终端中运行它们，可以使用以下命令：

```
cd ../assets/eip-6381
npm install
npx hardhat test
```

## 参考实现

请参见 [`EmotableRepository.sol`](../assets/eip-6381/contracts/EmotableRepository.sol)。

## 安全考虑

该提案不设想处理用户的任何形式的资产，因此在与 Emote 仓库交互时，资产不应处于风险之中。

使用 ECDSA 签名代表他人进行情感表达的能力引入了重放攻击的风险，而待签名消息的格式对此进行了保护。待签名消息中使用的 `DOMAIN_SEPARATOR` 是唯一的，特定于其部署的链的仓库智能合约。这意味着该签名在任何其他链上都是无效的，部署在这些链上的 Emote 仓库应在尝试重放攻击时撤销操作。

另一个需要考虑的事项是预签名消息的重用能力。由于消息包含签名有效性截止日期，因此在截止日期到达之前，消息可以被重用任意次数。该提案仅允许对特定代币的给定表情符号进行单次反应，因此预签名消息不能被滥用以增加代币的反应计数。然而，如果使用该仓库的服务依赖于在某些操作后撤销反应的能力，则有效的预签名消息可以用于重新对代币进行反应。我们建议使用该仓库的服务与预签名消息结合使用时，使用在合理短的时间内使预签名消息失效的截止日期。

处理未审计合约时请谨慎。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。