---
eip: 5095
title: 本金代币
description: 本金代币（零息代币）可在未来的时间戳兑换为单一的基础 EIP-20 代币。
author: Julian Traversa (@JTraversa), Robert Robbins (@robrobbins), Alberto Cuesta Cañada (@alcueca)
discussions-to: https://ethereum-magicians.org/t/eip-5095-principal-token-standard/9259
status: 停滞
type: 标准跟踪
category: ERC
created: 2022-05-01
requires: 20, 2612
---

## 摘要

本金代币代表在未来时间戳对基础 [EIP-20](./eip-20.md) 代币的所有权。

本规范是对 [EIP-20](./eip-20.md) 代币的扩展，提供了存入和提取代币的基本功能，以及读取余额和 [EIP-2612](./eip-2612.md) 规范，提供基于 [EIP-712](./eip-712.md) 签名的授权。

## 动机

本金代币缺乏标准化，导致开发空间难以导航和多样化的实现方案。

主要示例包括剥离未来收益的收益代币化平台，留下本金代币，以及利用本金代币作为借贷媒介的固定利率货币市场。

这种实现的不一致性使得在应用层和钱包层的集成变得困难，而这两者是该领域增长的关键催化剂。
开发者目前被期望为每个本金代币实现单独的适配器，以及为其池合约和许多情况下为其托管合约实现适配器，浪费了大量开发资源。

## 规范

所有本金代币（PTs）必须实现 [EIP-20](./eip-20.md) 以代表未来基础赎回的所有权。
如果 PT 是不可转让的，它可以在调用 `transfer` 或 `transferFrom` 时回退。
[EIP-20](./eip-20.md) 操作 `balanceOf`、`transfer`、`totalSupply` 等在本金代币余额上操作。

所有本金代币必须实现 [EIP-20](./eip-20.md) 的可选元数据扩展。
`name` 和 `symbol` 函数应以某种方式反映基础代币的 `name` 和 `symbol`，以及来源协议，在收益代币化协议的情况下，反映来源货币市场。

所有本金代币可以实现 [EIP-2612](./eip-2612.md) 以改善在各种集成中批准 PT 的用户体验。

### 定义：

- underlying: 本金代币在到期时可赎回的代币。
  由相应的 [EIP-20](./eip-20.md) 合约定义单位。
- maturity: 本金代币到期的时间戳（unix）。本金代币在此时间戳或之后可赎回基础代币。
- fee: 本金代币向用户收取的基础代币或本金代币的金额。费用可以在赎回或到期后收益时存在。
- slippage: 广告赎回价值与 PT 赎回的经济现实之间的任何差异，这些差异未被费用考虑在内。

### 方法

#### `underlying`

本金代币用于会计和赎回的基础代币地址。

必须是 EIP-20 代币合约。

必须 _不_ 回退。

```yaml
- name: underlying
  type: function
  stateMutability: view

  inputs: []

  outputs:
    - name: underlyingAddress
      type: address
```

#### `maturity`

本金代币可以赎回其基础存款的 unix 时间戳（uint256）。

必须 _不_ 回退。

```yaml
- name: maturity
  type: function
  stateMutability: view

  inputs: []

  outputs:
    - name: timestamp
      type: uint256
```

#### `convertToUnderlying`

在理想情况下，满足所有条件的情况下，将提供的 PT 数量兑换为的基础代币数量。

在到期之前，返回的基础代币数量就像 PT 在到期时一样。

必须不包括任何赎回时收取的费用。

必须不显示任何取决于调用者的变化。

在执行实际赎回时，必须不反映滑点或其他链上条件。

除非由于不合理的大输入导致整数溢出，否则必须不回退。

必须向下取整至 0。

此计算可能不反映“每用户”每本金代币的价格，而应反映“平均用户”的每本金代币价格，即平均用户在兑换时应期望看到的价格。

```yaml
- name: convertToUnderlying
  type: function
  stateMutability: view

  inputs:
    - name: principalAmount
      type: uint256

  outputs:
    - name: underlyingAmount
      type: uint256
```

#### `convertToPrincipal`

在理想情况下，满足所有条件的情况下，本金代币合约为提供指定的基础代币数量而请求赎回的本金代币数量。

必须不包括任何费用。

必须不显示任何取决于调用者的变化。

在执行实际兑换时，必须不反映滑点或其他链上条件。

除非由于不合理的大输入导致整数溢出，否则必须不回退。

必须向下取整至 0。

此计算可能不反映“每用户”每本金代币的价格，而应反映“平均用户”的每本金代币价格，即平均用户在赎回时应期望看到的价格。

```yaml
- name: convertToPrincipal
  type: function
  stateMutability: view

  inputs:
    - name: underlyingAmount
      type: uint256

  outputs:
    - name: principalAmount
      type: uint256
```

#### `maxRedeem`

可以通过 `redeem` 调用从 `holder` 余额中赎回的最大本金代币数量。

必须返回可以从 `holder` 通过 `redeem` 转移的最大本金代币数量，并且不会导致回退，且不得高于实际接受的最大值（如有必要应低估）。

必须考虑全球和用户特定的限制，例如如果赎回完全禁用（即使是暂时的），则必须返回 0。

必须不回退。

```yaml
- name: maxRedeem
  type: function
  stateMutability: view

  inputs:
    - name: holder
      type: address

  outputs:
    - name: maxPrincipalAmount
      type: uint256
```

#### `previewRedeem`

允许链上或链下用户在当前区块下模拟其赎回的效果，考虑当前链上条件。

必须返回尽可能接近且不超过在同一交易中 `redeem` 调用将获得的确切基础代币数量。即：如果在同一交易中调用 `redeem`，则应返回与 `previewRedeem` 相同或更多的 `underlyingAmount`。

必须不考虑如 maxRedeem 返回的赎回限制，并且应始终假设赎回将被接受，无论用户是否拥有足够的本金代币等。

必须包括赎回费用。集成者应意识到赎回费用的存在。

必须不因本金代币合约特定的用户/全局限制而回退。可能因其他条件而回退，这些条件也会导致 `redeem` 回退。

请注意，`convertToUnderlying` 和 `previewRedeem` 之间的任何不利差异应视为每本金代币价格的滑点或其他类型的条件。

```yaml
- name: previewRedeem
  type: function
  stateMutability: view

  inputs:
    - name: principalAmount
      type: uint256

  outputs:
    - name: underlyingAmount
      type: uint256
```

#### `redeem`

在到期时或之后，从 `from` 中销毁正好 `principalAmount` 的本金代币，并将 `underlyingAmount` 的基础代币发送到 `to`。

接口和其他合约不得期望资金保管存在。虽然通过本金代币合约进行本金代币的托管赎回对集成者非常有用，但某些协议可能会发现给予本金代币本身保管会破坏其向后兼容性。

必须发出 `Redeem` 事件。

必须支持一种赎回流程，其中本金代币直接从 `holder` 中销毁，`holder` 是 `msg.sender` 或 `msg.sender` 对 `holder` 的本金代币具有 EIP-20 授权。
可以支持一种额外的流程，其中本金代币在 `redeem` 执行之前转移到本金代币合约，并在 `redeem` 期间进行核算。
如果无法赎回全部的 `principalAmount`（由于达到提款限额、滑点、持有者没有足够的本金代币等），则必须回滚。

请注意，一些实现可能需要在进行提款之前预先请求本金代币。这些方法应单独执行。

```yaml
- name: redeem
  type: function
  stateMutability: nonpayable

  inputs:
    - name: principalAmount
      type: uint256
    - name: to
      type: address
    - name: from
      type: address

  outputs:
    - name: underlyingAmount
      type: uint256
```

#### `maxWithdraw`

可以从 `holder` 的本金代币余额中通过 `withdraw` 调用赎回的基础资产的最大金额。

必须返回可以从 `holder` 通过 `withdraw` 赎回的最大基础代币数量，并且不会导致回滚，这个数量不得高于实际接受的最大值（如有必要应低估）。

必须考虑全球和用户特定的限制，例如如果提款完全被禁用（即使是暂时的），则必须返回 0。

必须不回滚。

```yaml
- name: maxWithdraw
  type: function
  stateMutability: view

  inputs:
    - name: holder
      type: address

  outputs:
    - name: maxUnderlyingAmount
      type: uint256
```

#### `previewWithdraw`

允许链上或链下用户在当前区块下模拟其提款的效果，考虑当前链上条件。

必须返回尽可能接近且不少于在同一交易中 `withdraw` 调用时将被销毁的本金代币的确切数量。即，如果在同一交易中调用 `withdraw`，则应返回与 `previewWithdraw` 相同或更少的 `principalAmount`。

必须不考虑如 `maxWithdraw` 返回的提款限制，并且应始终假设提款将被接受，无论用户是否有足够的本金代币等。

必须包括提款费用。集成者应意识到提款费用的存在。

必须不因本金代币合约特定的用户/全球限制而回滚。可能因其他条件导致 `withdraw` 回滚而回滚。

请注意，`convertToPrincipal` 和 `previewWithdraw` 之间的任何不利差异应视为每个本金代币的价格滑点或其他类型的条件。

```yaml
- name: previewWithdraw
  type: function
  stateMutability: view

  inputs:
    - name: underlyingAmount
      type: uint256

  outputs:
    - name: principalAmount
      type: uint256
```

#### `withdraw`

从 `holder` 销毁 `principalAmount` 并将确切的 `underlyingAmount` 基础代币发送给 `receiver`。

必须发出 `Redeem` 事件。

必须支持一种提款流程，其中本金代币直接从 `holder` 销毁，`holder` 是 `msg.sender` 或 `msg.sender` 对 `holder` 的本金代币具有 [EIP-20](./eip-20.md) 授权。
可能支持一种额外的流程，其中本金代币在 `withdraw` 执行之前转移到本金代币合约，并在 `withdraw` 期间进行核算。

如果无法提取全部的 `underlyingAmount`（由于达到提款限额、滑点、持有者没有足够的本金代币等），则必须回滚。

请注意，一些实现可能需要在进行提款之前预先请求本金代币合约。这些方法应单独执行。

```yaml
- name: withdraw
  type: function
  stateMutability: nonpayable

  inputs:
    - name: underlyingAmount
      type: uint256
    - name: receiver
      type: address
    - name: holder
      type: address

  outputs:
    - name: principalAmount
      type: uint256
```

### 事件

#### Redeem

`from` 已经用 `principalAmount` 的本金代币兑换了 `underlyingAmount` 的基础代币，并将该基础代币转移给 `to`。

必须在本金代币被销毁并从合约中提取基础代币时发出。

```yaml
- name: Redeem
  type: event

  inputs:
    - name: from
      indexed: true
      type: address
    - name: to
      indexed: true
      type: address
    - name: amount
      indexed: false
      type: uint256
```

## 理由

本金代币接口旨在为集成者优化，提供核心最小接口以及可选接口以实现向后兼容。诸如基础资产的会计和管理等细节故意未指定，因为预计本金代币在链上被视为黑箱，并在使用前在链下进行检查。

[EIP-20](./eip-20.md) 被强制执行，因为诸如代币授权和余额计算等实现细节直接延续。这种标准化使本金代币与所有 [EIP-20](./eip-20.md) 用例立即兼容，此外还兼容 EIP-5095。

所有本金代币在到期时均可赎回，唯一的差异是到期后是否会产生进一步的收益。鉴于赎回的普遍性，`redeem` 的存在允许集成者在公开市场上购买本金代币，并在仅知道本金代币地址的情况下将其赎回以获得固定收益。

该 EIP 在设计上大量借鉴了 [EIP-4626](./eip-4626.md)，因为从技术上讲，本金代币可以被描述为收益承载金库的一个子集，扩展了 `maturity` 变量并对实现施加了限制。然而，扩展 [EIP-4626](./eip-4626.md) 将迫使 PT 实现包含不必要的（即 `mint` 和 `deposit`）方法，这些方法与 PT 解决的业务案例无关。也可以说，部分赎回（通过 `withdraw` 实现）在 PT 中是罕见的。

PT 在精确的秒数到期，但考虑到智能合约的反应性，不能有标记到期的事件，因为没有保证在到期时或之后会有任何活动。在到期后的第一笔交易中发出事件以通知到期将是不精确且昂贵的。相反，建议集成者使用第一次 `Redeem` 事件，或自行跟踪每个 PT 预计到期的时间。

## 向后兼容性

该 EIP 完全向后兼容 [EIP-20](./eip-20.md) 规范，并且与其他标准没有已知的兼容性问题。
对于不使用 EIP-5095 的本金代币的生产实现，可以开发和使用包装适配器，或实现包装代币。

## 参考实现

```
// SPDX-License-Identifier: MIT
pragma solidity 0.8.14;

import {ERC20} from "yield-utils-v2/contracts/token/ERC20.sol";
import {MinimalTransferHelper} from "yield-utils-v2/contracts/token/MinimalTransferHelper.sol";

contract ERC5095 is ERC20 {
    using MinimalTransferHelper for ERC20;

    /* EVENTS
     *****************************************************************************************************************/

    event Redeem(address indexed from, address indexed to, uint256 underlyingAmount);

    /* MODIFIERS
     *****************************************************************************************************************/

    /// @notice A modifier that ensures the current block timestamp is at or after maturity.
    modifier afterMaturity() virtual {
        require(block.timestamp >= maturity, "BEFORE_MATURITY");
        _;
    }

    /* IMMUTABLES
     *****************************************************************************************************************/

    ERC20 public immutable underlying;
    uint256 public immutable maturity;

    /* CONSTRUCTOR
     *****************************************************************************************************************/

    constructor(
        string memory name_,
        string memory symbol_,
        uint8 decimals_,
        ERC20 underlying_,
        uint256 maturity_
    ) ERC20(name_, symbol_, decimals_) {
        underlying = underlying_;
        maturity = maturity_;
    }

    /* CORE FUNCTIONS
     *****************************************************************************************************************/

    /// @notice Burns an exact amount of principal tokens in exchange for an amount of underlying.
    /// @dev This reverts if before maturity.
    /// @param principalAmount The exact amount of principal tokens to be burned.
    /// @param from The owner of the principal tokens to be redeemed.  If not msg.sender then must have prior approval.
    /// @param to The address to send the underlying tokens.
    /// @return underlyingAmount The total amount of underlying tokens sent.
    function redeem(
        uint256 principalAmount,
        address from,
        address to
    ) public virtual afterMaturity returns (uint256 underlyingAmount) {
        _decreaseAllowance(from, principalAmount);

        // Check for rounding error since we round down in previewRedeem.
        require((underlyingAmount = _previewRedeem(principalAmount)) != 0, "ZERO_ASSETS");

        _burn(from, principalAmount);

        emit Redeem(from, to, principalAmount);

        _transferOut(to, underlyingAmount);
    }

    /// @notice Burns a calculated amount of principal tokens in exchange for an exact amount of underlying.
    /// @dev This reverts if before maturity.
    /// @param underlyingAmount The exact amount of underlying tokens to be received.
    /// @param from The owner of the principal tokens to be redeemed.  If not msg.sender then must have prior approval.
    /// @param to The address to send the underlying tokens.
    /// @return principalAmount The total amount of underlying tokens redeemed.
    function withdraw(
        uint256 underlyingAmount,
        address from,
        address to
    ) public virtual afterMaturity returns (uint256 principalAmount) {
        principalAmount = _previewWithdraw(underlyingAmount); // No need to check for rounding error, previewWithdraw rounds up.

        _decreaseAllowance(from, principalAmount);

        _burn(from, principalAmount);

        emit Redeem(from, to, principalAmount);

        _transferOut(to, underlyingAmount);
    }

    /// @notice An internal, overridable transfer function.
    /// @dev Reverts on failed transfer.
    /// @param to The recipient of the transfer.
    /// @param amount The amount of the transfer.
    function _transferOut(address to, uint256 amount) internal virtual {
        underlying.safeTransfer(to, amount);
    }

    /* ACCOUNTING FUNCTIONS
     *****************************************************************************************************************/

    /// @notice Calculates the amount of underlying tokens that would be exchanged for a given amount of principal tokens.
    /// @dev Before maturity, it converts to underlying as if at maturity.
    /// @param principalAmount The amount principal on which to calculate conversion.
    /// @return underlyingAmount The total amount of underlying that would be received for the given principal amount..
    function convertToUnderlying(uint256 principalAmount) external view returns (uint256 underlyingAmount) {
        return _convertToUnderlying(principalAmount);
    }

    function _convertToUnderlying(uint256 principalAmount) internal view virtual returns (uint256 underlyingAmount) {
        return principalAmount;
    }

    /// @notice Converts a given amount of underlying tokens to principal exclusive of fees.
    /// @dev Before maturity, it converts to principal as if at maturity.
    /// @param underlyingAmount The total amount of underlying on which to calculate the conversion.
    /// @return principalAmount The amount principal tokens required to provide the given amount of underlying.
    function convertToPrincipal(uint256 underlyingAmount) external view returns (uint256 principalAmount) {
        return _convertToPrincipal(underlyingAmount);
    }

    function _convertToPrincipal(uint256 underlyingAmount) internal view virtual returns (uint256 principalAmount) {
        return underlyingAmount;
    }

    /// @notice Allows user to simulate redemption of a given amount of principal tokens, inclusive of fees and other
    /// current block conditions.
    /// @dev This reverts if before maturity.
    /// @param principalAmount The amount of principal that would be redeemed.
    /// @return underlyingAmount The amount of underlying that would be received.
    function previewRedeem(uint256 principalAmount) external view afterMaturity returns (uint256 underlyingAmount) {
        return _previewRedeem(principalAmount);
    }

    function _previewRedeem(uint256 principalAmount) internal view virtual returns (uint256 underlyingAmount) {
        return _convertToUnderlying(principalAmount); // should include fees/slippage
    }

    /// @notice Calculates the maximum amount of principal tokens that an owner could redeem.
    /// @dev This returns 0 if before maturity.
    /// @param owner The address for which the redemption is being calculated.
    /// @return maxPrincipalAmount The maximum amount of principal tokens that can be redeemed by the given owner.
    function maxRedeem(address owner) public view returns (uint256 maxPrincipalAmount) {
        return block.timestamp >= maturity ? _balanceOf[owner] : 0;
    }

    /// @notice Allows user to simulate withdraw of a given amount of underlying tokens.
    /// @dev This reverts if before maturity.
    /// @param underlyingAmount The amount of underlying tokens that would be withdrawn.
    /// @return principalAmount The amount of principal tokens that would be redeemed.
    function previewWithdraw(uint256 underlyingAmount) external view afterMaturity returns (uint256 principalAmount) {
        return _previewWithdraw(underlyingAmount);
    }

    function _previewWithdraw(uint256 underlyingAmount) internal view virtual returns (uint256 principalAmount) {
        return _convertToPrincipal(underlyingAmount); // should include fees/slippage
    }

    /// @notice Calculates the maximum amount of underlying tokens that can be withdrawn by a given owner.
    /// @dev This returns 0 if before maturity.
    /// @param owner The address for which the withdraw is being calculated.
    /// @return maxUnderlyingAmount The maximum amount of underlying tokens that can be withdrawn by a given owner.
    function maxWithdraw(address owner) public view returns (uint256 maxUnderlyingAmount) {
        return _previewWithdraw(maxRedeem(owner));
    }
}

```

## 安全考虑

完全无权限的用例可能会受到恶意实现的攻击，这些实现仅符合该 EIP 中的接口，但不符合规范，未能实现适当的保管功能，但提供通过二级市场购买本金代币的能力。

建议所有集成者在集成之前审查每个实现，以寻找可能导致用户存款损失的潜在方式。

`convertToUnderlying` 方法是用于显示目的的估算，
并不需要提供其上下文所暗示的基础资产的 _确切_ 数量。

正如许多标准中常见的那样，强烈建议尽可能镜像基础代币的 `decimals`，以消除可能的混淆来源，并简化前端和其他链下用户的集成。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。