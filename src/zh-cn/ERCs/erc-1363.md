---
eip: 1363
title: 可支付代币
author: Vittorio Minacori (@vittominacori)
discussions-to: https://github.com/ethereum/eips/issues/1363
status: 最终
type: 标准跟踪
category: ERC
created: 2018-08-30
requires: 20, 165
---

## 简单总结
定义了一个代币接口，用于支持在 `transfer` 或 `transferFrom` 之后执行接收者代码，或在 `approve` 之后执行支出者代码的 [ERC-20](./erc-20.md) 代币。

## 摘要
代币合约和与代币一起工作的合约可以实现的标准函数，以使代币可支付。

`transferAndCall` 和 `transferFromAndCall` 将在 `ERC1363Receiver` 合约上调用 `onTransferReceived`。

`approveAndCall` 将在 `ERC1363Spender` 合约上调用 `onApprovalReceived`。

## 动机
在 [ERC-20](./erc-20.md) 转账或批准（即进行支付）后，没有办法执行代码，因此要进行操作，必须发送另一个交易并支付两次 GAS。

该提案旨在简化代币支付，并在不使用任何其他监听器的情况下工作。它允许在单个交易中在转账或批准后进行回调。

有许多提议的以太坊智能合约可以接受 [ERC-20](./erc-20.md) 支付。

示例可能包括
* 创建一个可支付的代币众筹
* 用代币出售服务
* 支付发票
* 进行订阅

出于这些原因，它被命名为 **“可支付代币”**。

无论如何，您可以将其用于特定的实用程序或任何其他需要在接收转账或批准后执行回调的目的。

该提案的灵感来源于 [ERC-721](./eip-721.md) 的 `onERC721Received` 和 `ERC721TokenReceiver` 行为。

## 规范
实现合约 **必须** 实现 [ERC-1363](./eip-1363.md) 接口，以及 [ERC-20](./erc-20.md) 和 [ERC-165](./eip-165.md) 接口。

```solidity
pragma solidity ^0.8.0;

interface ERC1363 /* is ERC20, ERC165 */ {
  /*
   * Note: the ERC-165 identifier for this interface is 0xb0202a11.
   * 0xb0202a11 ===
   *   bytes4(keccak256('transferAndCall(address,uint256)')) ^
   *   bytes4(keccak256('transferAndCall(address,uint256,bytes)')) ^
   *   bytes4(keccak256('transferFromAndCall(address,address,uint256)')) ^
   *   bytes4(keccak256('transferFromAndCall(address,address,uint256,bytes)')) ^
   *   bytes4(keccak256('approveAndCall(address,uint256)')) ^
   *   bytes4(keccak256('approveAndCall(address,uint256,bytes)'))
   */

  /**
   * @notice Transfer tokens from `msg.sender` to another address and then call `onTransferReceived` on receiver
   * @param to address The address which you want to transfer to
   * @param value uint256 The amount of tokens to be transferred
   * @return true unless throwing
   */
  function transferAndCall(address to, uint256 value) external returns (bool);

  /**
   * @notice Transfer tokens from `msg.sender` to another address and then call `onTransferReceived` on receiver
   * @param to address The address which you want to transfer to
   * @param value uint256 The amount of tokens to be transferred
   * @param data bytes Additional data with no specified format, sent in call to `to`
   * @return true unless throwing
   */
  function transferAndCall(address to, uint256 value, bytes memory data) external returns (bool);

  /**
   * @notice Transfer tokens from one address to another and then call `onTransferReceived` on receiver
   * @param from address The address which you want to send tokens from
   * @param to address The address which you want to transfer to
   * @param value uint256 The amount of tokens to be transferred
   * @return true unless throwing
   */
  function transferFromAndCall(address from, address to, uint256 value) external returns (bool);


  /**
   * @notice Transfer tokens from one address to another and then call `onTransferReceived` on receiver
   * @param from address The address which you want to send tokens from
   * @param to address The address which you want to transfer to
   * @param value uint256 The amount of tokens to be transferred
   * @param data bytes Additional data with no specified format, sent in call to `to`
   * @return true unless throwing
   */
  function transferFromAndCall(address from, address to, uint256 value, bytes memory data) external returns (bool);

  /**
   * @notice Approve the passed address to spend the specified amount of tokens on behalf of msg.sender
   * and then call `onApprovalReceived` on spender.
   * @param spender address The address which will spend the funds
   * @param value uint256 The amount of tokens to be spent
   * @return true unless throwing
   */
  function approveAndCall(address spender, uint256 value) external returns (bool);

  /**
   * @notice Approve the passed address to spend the specified amount of tokens on behalf of msg.sender
   * and then call `onApprovalReceived` on spender.
   * @param spender address The address which will spend the funds
   * @param value uint256 The amount of tokens to be spent
   * @param data bytes Additional data with no specified format, sent in call to `spender`
   * @return true unless throwing
   */
  function approveAndCall(address spender, uint256 value, bytes memory data) external returns (bool);
}

interface ERC20 {
  function totalSupply() external view returns (uint256);
  function balanceOf(address account) external view returns (uint256);
  function transfer(address recipient, uint256 amount) external returns (bool);
  function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
  function allowance(address owner, address spender) external view returns (uint256);
  function approve(address spender, uint256 amount) external returns (bool);
  event Transfer(address indexed from, address indexed to, uint256 value);
  event Approval(address indexed owner, address indexed spender, uint256 value);
}

interface ERC165 {
  function supportsInterface(bytes4 interfaceId) external view returns (bool);
}
```

希望通过 `transferAndCall` 或 `transferFromAndCall` 接受代币支付的合约 **必须** 实现以下接口：

```solidity
/**
 * @title ERC1363Receiver interface
 * @dev Interface for any contract that wants to support `transferAndCall` or `transferFromAndCall`
 *  from ERC1363 token contracts.
 */
interface ERC1363Receiver {
  /*
   * Note: the ERC-165 identifier for this interface is 0x88a7ca5c.
   * 0x88a7ca5c === bytes4(keccak256("onTransferReceived(address,address,uint256,bytes)"))
   */

  /**
   * @notice Handle the receipt of ERC1363 tokens
   * @dev Any ERC1363 smart contract calls this function on the recipient
   * after a `transfer` or a `transferFrom`. This function MAY throw to revert and reject the
   * transfer. Return of other than the magic value MUST result in the
   * transaction being reverted.
   * Note: the token contract address is always the message sender.
   * @param operator address The address which called `transferAndCall` or `transferFromAndCall` function
   * @param from address The address which are token transferred from
   * @param value uint256 The amount of tokens transferred
   * @param data bytes Additional data with no specified format
   * @return `bytes4(keccak256("onTransferReceived(address,address,uint256,bytes)"))`
   *  unless throwing
   */
  function onTransferReceived(address operator, address from, uint256 value, bytes memory data) external returns (bytes4);
}
``` 

希望通过 `approveAndCall` 接受代币支付的合约 **必须** 实现以下接口：

```solidity
/**
 * @title ERC1363Spender interface
 * @dev Interface for any contract that wants to support `approveAndCall`
 *  from ERC1363 token contracts.
 */
interface ERC1363Spender {
  /*
   * Note: the ERC-165 identifier for this interface is 0x7b04a2d0.
   * 0x7b04a2d0 === bytes4(keccak256("onApprovalReceived(address,uint256,bytes)"))
   */

  /**
   * @notice Handle the approval of ERC1363 tokens
   * @dev Any ERC1363 smart contract calls this function on the recipient
   * after an `approve`. This function MAY throw to revert and reject the
   * approval. Return of other than the magic value MUST result in the
   * transaction being reverted.
   * Note: the token contract address is always the message sender.
   * @param owner address The address which called `approveAndCall` function
   * @param value uint256 The amount of tokens to be spent
   * @param data bytes Additional data with no specified format
   * @return `bytes4(keccak256("onApprovalReceived(address,uint256,bytes)"))`
   *  unless throwing
   */
  function onApprovalReceived(address owner, uint256 value, bytes memory data) external returns (bytes4);
}
``` 

## 理由
选择使用 `transferAndCall`、`transferFromAndCall` 和 `approveAndCall` 源于 [ERC-20](./erc-20.md) 的命名。它们旨在强调它们具有与 `transfer`、`transferFrom` 和 `approve` 相同的行为，并增加了接收者或支出者的回调。

## 向后兼容性
该提案也受到 [ERC-223](https://github.com/ethereum/EIPs/issues/223) 和 [ERC-677](https://github.com/ethereum/EIPs/issues/677) 的启发，但它采用了 [ERC-721](./eip-721.md) 的方法，因此不会覆盖 [ERC-20](./erc-20.md) 的 `transfer` 和 `transferFrom` 方法，并定义了要实现的接口 ID，以保持 [ERC-20](./erc-20.md) 的向后兼容性。

## 安全考虑
`approveAndCall` 和 `transferFromAndCall` 方法可能会受到标准 [ERC-20](./erc-20.md) 的 `approve` 和 `transferFrom` 方法的相同问题的影响。

使用 `approveAndCall` 方法更改授权带来了风险，即某人可能会因不幸的交易排序而同时使用旧的和新的授权。

缓解这种竞争条件的一个可能解决方案是首先将支出者的授权减少到 0，然后再设置所需的值（[EIP-20#issuecomment-263524729](https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729)）。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。