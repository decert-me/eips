---
eip: 1444
title: 使用信号转文本的本地化消息传递
author: Brooklyn Zelenka (@expede), Jennifer Cooper (@jenncoop)
discussions-to: https://ethereum-magicians.org/t/eip-1444-localized-messaging-with-signal-to-text/
status: Stagnant
type: Standards Track
category: ERC
created: 2018-09-23
---

## 简单总结

一种将机器代码转换为任何语言和措辞的人类可读文本的方法。

## 摘要

一个链上系统，通过将机器高效代码转换为任何语言或措辞的人类可读字符串来提供用户反馈。该系统不强制规定语言列表，而是允许用户创建、分享和使用他们选择的本地化文本。

## 动机

在许多情况下，最终用户需要来自智能合约的反馈或指令。直接暴露数字代码并不能提供良好的用户体验或开发者体验。如果以太坊要成为一个真正全球化的系统，供专家和普通人使用，那么需要尽可能多的系统来提供交易过程中发生的事情的反馈。

返回一个硬编码的字符串（通常是英语）只服务于全球人口的一小部分。该标准提出了一种方法，允许用户创建、注册、分享和使用去中心化的翻译集合，从而实现更丰富的消息传递，具有更丰富的文化和语言多样性。

有几种机器高效的方式来表示意图、状态、状态转换和其他语义信号，包括布尔值、枚举和 [ERC-1066 代码](./eip-1066.md)。通过为这些信号提供人类可读的消息，开发者体验得到了增强，因为返回的信息更易于理解且具有更多上下文（例如 `revert`）。最终用户体验得到了增强，因为提供的文本可以传播到用户界面。

## 规范

### 合约架构

两种类型的合约：`LocalizationPreferences` 和 `Localization`。

`LocalizationPreferences` 合约作为 `tx.origin` 的代理。

```diagram
                                                                   +--------------+
                                                                   |              |
                                                          +------> | Localization |
                                                          |        |              |
                                                          |        +--------------+
                                                          |
                                                          |
+-----------+          +-------------------------+        |        +--------------+
|           |          |                         | <------+        |              |
| Requestor | <------> | LocalizationPreferences | <-------------> | Localization |
|           |          |                         | <------+        |              |
+-----------+          +-------------------------+        |        +--------------+
                                                          |
                                                          |
                                                          |        +--------------+
                                                          |        |              |
                                                          +------> | Localization |
                                                                   |              |
                                                                   +--------------+
```

### `Localization`

一个持有代码与其文本表示简单映射的合约。

```solidity
interface Localization {
  function textFor(bytes32 _code) external view returns (string _text);
}
```

#### `textFor`

获取本地化的文本表示。

```solidity
function textFor(bytes32 _code) external view returns (string _text);
```

### `LocalizationPreferences`

一个代理合约，允许用户设置他们首选的 `Localization`。文本查找委托给用户首选的合约。

必须提供一个填充了所有键的后备 `Localization`。如果用户指定的 `Localization` 没有明确设置本地化（即 `textFor` 返回 `""`），则 `LocalizationPreferences` 必须重新委托给后备 `Localization`。

```solidity
interface LocalizationPreferences {
  function set(Localization _localization) external returns (bool);
  function textFor(bytes32 _code) external view returns (bool _wasFound, string _text);
}
```

#### `set`

注册用户首选的 `Localization`。注册用户应被视为 `tx.origin`。

```solidity
function set(Localization _localization) external;
```

#### `textFor`

检索用户首选的 `Localization` 合约中找到的代码的文本。

第一个返回值（`bool _wasFound`）表示该文本是否可从该 `Localization` 获取，或者是否使用了后备。如果在此上下文中使用了后备，则 `textFor` 的第一个返回值必须设置为 `false`，否则为 `true`。

```solidity
function textFor(bytes32 _code) external view returns (bool _wasFound, string _text);
```

### 字符串格式

所有字符串必须编码为 [UTF-8](https://www.ietf.org/rfc/rfc3629.txt)。

```solidity
"Špeĉiäl chârãçtérs are permitted"
"As are non-Latin characters: アルミ缶の上にあるみかん。"
"Emoji are legal: 🙈🙉🙊🎉"
"Feel free to be creative: (ﾉ◕ヮ◕)ﾉ*:･ﾟ✧"
```

### 模板

允许使用模板字符串，并且必须遵循 [ANSI C `printf`](https://pubs.opengroup.org/onlinepubs/009696799/utilities/printf.html) 约定。

```solidity
"Satoshi's true identity is %s"
```

具有两个或更多参数的文本应使用 POSIX 参数字段扩展。

```solidity
"Knock knock. Who's there? %1$s. %1$s who? %2$s!"
```

## 理由

### `bytes32` 键

`bytes32` 非常高效，因为它是 EVM 的基本字长。考虑到元素的巨大数量（card(A) > 1.1579 × 10<sup>77</sup>），它几乎可以嵌入任何实际信号、枚举或状态。在应用程序的键长于 `bytes32` 的情况下，可以对该长键进行哈希，以将该值映射到正确的宽度。

使用小于 `bytes32` 的数据类型（例如 [ERC-1066](./eip-1066.md) 中的 `bytes1`）的设计可以直接嵌入到更大的宽度中。这是将较小集合映射到较大集合的简单一对一映射。

### 本地与全局和单例

该规范选择不强制使用单一全局注册表，而是允许任何合约和用例部署自己的系统。这允许更多的灵活性，并且不限制社区选择使用单例 `LocalizationPreference` 合约来处理常见用例，分享不同代理之间的 `Localization`，在 `Localization` 之间委托翻译，等等。

有许多实际用途的公认单例。例如，旨在相对通用并直接集成到更广泛生态系统（钱包、框架、调试器等）中的代码翻译将希望拥有一个单一的 `LocalizationPreference`。

与其为不同的用例和代码分散多个 `LocalizationPreference`，不如设想一个全球的“注册表的注册表”。虽然这种方法允许对所有用例中的所有翻译进行统一查找，但它与去中心化和自由的精神相悖。这样的系统还增加了查找的复杂性，要求第一次就正确编写代码（或增加可升级合约的开销），并需要考虑用例冲突与“统一”或集中编号系统的关系。此外，查找应轻量级（特别是在查找 revert 文本的情况下）。

出于这些原因，该规范选择了更去中心化、轻量级、自由的方法，代价是链上可发现性。仍然可以编译注册表，但很难强制执行，并且超出了该规范的范围。

### 链下存储

一个非常可行的替代方案是将文本存储在链下，并在链上指向翻译，并发出或返回一个 `bytes32` 代码供其他方进行查找。很难保证链下资源将可用，并且需要其他系统（如 Web 服务器）进行代码到文本的匹配。这也与 `revert` 消息不兼容。

### ASCII 与 UTF-8 与 UTF-16

UTF-8 是撰写时使用最广泛的编码。它直接嵌入 ASCII，同时为大多数自然语言、表情符号和特殊字符提供字符。

有关更多信息，请参见 [UTF-8 Everywhere Manifesto](https://utf8everywhere.org/)。

### 当未找到文本时

向请求者返回空字符串完全违背了本地化系统的目的。处理缺失文本的两个选项是：

1. 在首选语言中返回一个通用的“未找到文本”消息
2. 在另一种语言中返回实际消息

#### 通用选项

该设计选择不使用通用的后备文本。它不会向用户提供任何有用的信息，除了可能联系 `Localization` 维护者（如果存在且更新是可能的）。

#### 后备选项

本提案中概述的设计是提供一种常用语言的文本（例如英语或普通话）。首先，如果用户尚未设置首选项，则将路由到该语言。其次，用户可能对该语言有某种程度的熟练度，或者至少能够使用自动翻译服务。
知道文本通过 `textFor` 的第一个返回字段布尔值回退要简单得多，而不是事后尝试语言检测。这些信息对于某些 UI 情况是有用的。例如，在某些情况下可能希望解释为什么本地化回退。

### 去中心化文本众包

为了让以太坊获得广泛采用，用户必须能够以他们最舒适的语言、措辞和细节水平与之互动。与传统的集中式应用程序强加固定翻译集不同，这个 EIP 提供了一种让任何人创建、策划和使用翻译的方法。这使得大众能够提供文化和语言上多样化的信息，从而实现更广泛和更分散的信息获取。

### `printf` 风格格式字符串

C 风格的 `printf` 模板已经成为事实上的标准。它们在大多数语言中具有广泛的兼容性（无论是在标准库还是第三方库中）。这使得消费程序能够以较低的开发者开销插入字符串变得更加容易。

#### 参数字段

POSIX 参数字段扩展很重要，因为不同语言的单词顺序并不相同。参数字段使得在不同本地化中重用和重新排列参数成为可能。

```solidity
("%1$s is an element with the atomic number %2$d!", "Mercury", 80);
// => "Mercury is an element with the atomic number 80!"
```

#### 简化本地化

本地化文本不需要使用所有参数，可以简单地忽略某些值。这对于不向用户暴露更多技术信息非常有用，否则这些信息可能会让用户感到困惑。

```ruby
#!/usr/bin/env ruby

sprintf("%1$s é um elemento", "Mercurio", 80)
# => "Mercurio é um elemento"
```

```clojure
#!/usr/bin/env clojure

(format "Element #%2$s" "Mercury" 80)
;; => Element #80
```

### 插值策略

请注意，强烈建议将模板字符串*原样*返回，参数作为多个返回值或 `event` 中的字段，实际的插值在链外完成。

```solidity
event AtomMessage {
  bytes32 templateCode;
  bytes32 atomCode;
  uint256 atomicNumber;
}
```

```javascript
#!/usr/bin/env node

var printf = require('printf');

const { returnValues: { templateCode, atomCode, atomicNumber } } = eventResponse;

const template = await AppText.textFor(templateCode);
// => "%1$s ist ein Element mit der Ordnungszahl %2$d!"

const atomName = await PeriodicTableText.textFor(atomCode);
// => "Merkur"

printf(template, atomName, 80);
// => "Merkur ist ein Element mit der Ordnungszahl 80!"
```

### 未指定行为

该规范未指定：

* 对默认 `Localization` 的公共或私有访问
* 谁可以设置文本
  * 部署者
  * `onlyOwner`
  * 任何人
  * 白名单用户
  * 等等
* 何时设置文本
  * `constructor`
  * 任何时间
  * 写入空槽，但不覆盖现有文本
  * 等等

这些故意留空。每种情况都有很多可能性，限制任何一种完全超出了本提案的范围。

## 实现

```solidity
pragma solidity ^0.4.25;

contract Localization {
  mapping(bytes32 => string) private dictionary_;

  constructor() public {}

  // Currently overwrites anything
  function set(bytes32 _code, string _message) external {
    dictionary_[_code] = _message;
  }

  function textFor(bytes32 _code) external view returns (string _message) {
    return dictionary_[_code];
  }
}

contract LocalizationPreference {
  mapping(address => Localization) private registry_;
  Localization public defaultLocalization;

  bytes32 private empty_ = keccak256(abi.encodePacked(""));

  constructor(Localization _defaultLocalization) public {
    defaultLocalization = _defaultLocalization;
  }

  function set(Localization _localization) external returns (bool) {
    registry_[tx.origin] = _localization;
    return true;
  }

  function get(bytes32 _code) external view returns (bool, string) {
    return get(_code, tx.origin);
  }

  // Primarily for testing
  function get(bytes32 _code, address _who) public view returns (bool, string) {
    string memory text = getLocalizationFor(_who).textFor(_code);

    if (keccak256(abi.encodePacked(text)) != empty_) {
      return (true, text);
    } else {
      return (false, defaultLocalization.textFor(_code));
    }
  }

  function getLocalizationFor(address _who) internal view returns (Localization) {
    if (Localization(registry_[_who]) == Localization(0)) {
      return Localization(defaultLocalization);
    } else {
      return Localization(registry_[tx.origin]);
    }
  }
}
```

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。