---
eip: 7160
title: ERC-721 多元数据扩展
description: 每个代币多个元数据 URI，并可选择固定一个主 URI。
author: 0xG (@0xGh), Marco Peyfuss (@mpeyfuss)
discussions-to: https://ethereum-magicians.org/t/erc721-multi-metadata-extension/14629
status: Final
type: Standards Track
category: ERC
created: 2023-06-09
requires: 165, 721
---

## 摘要

本 EIP 提出了对 [ERC-721](./eip-721.md) 标准的扩展，以支持每个代币多个元数据 URI。它引入了一个新的接口 `IERC721MultiMetadata`，提供访问与代币相关的元数据 URI 的方法，包括固定 URI 索引和所有元数据 URI 的列表。该扩展旨在与现有的 `ERC721Metadata` 实现向后兼容。

## 动机

当前的 [ERC-721](./eip-721.md) 标准允许每个代币有一个元数据 URI，使用 `ERC721Metadata` 实现。然而，有些用例需要多个元数据 URI。以下是一些示例用例：

- 一个代币代表一组（循环）资产，每个资产都有单独的元数据
- 代币元数据的链上修订历史
- 附加具有不同纵横比的元数据，以便在所有屏幕上正确显示
- 动态和不断演变的元数据
- 协作和多艺术家代币

该扩展通过引入多元数据支持的概念来实现这些用例。

拥有多元数据标准的主要原因是，dapp 和市场没有机制来推断和显示所有代币 URI。为市场提供一种标准方式，让收藏者能够固定/取消固定其中一个元数据选项，也使得这一功能的快速和简单采用成为可能。

## 规范

本文档中的关键字 “MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL” 应按 RFC 2119 中的描述进行解释。

**多元数据扩展对于 [ERC-721](./eip-721.md) 合同是可选的，建议与 [ERC-4906](./eip-4906.md) 标准一起使用（如果实现）**。

```solidity
/// @title EIP-721 Multi-Metdata Extension
/// @dev The ERC-165 identifier for this interface is 0x06e1bc5b.
interface IERC7160 {

  /// @dev This event emits when a token uri is pinned and is
  ///  useful for indexing purposes.
  event TokenUriPinned(uint256 indexed tokenId, uint256 indexed index);

  /// @dev This event emits when a token uri is unpinned and is
  ///  useful for indexing purposes.
  event TokenUriUnpinned(uint256 indexed tokenId);

  /// @notice Get all token uris associated with a particular token
  /// @dev If a token uri is pinned, the index returned SHOULD be the index in the string array
  /// @dev This call MUST revert if the token does not exist
  /// @param tokenId The identifier for the nft
  /// @return index An unisgned integer that specifies which uri is pinned for a token (or the default uri if unpinned)
  /// @return uris A string array of all uris associated with a token
  /// @return pinned A boolean showing if the token has pinned metadata or not
  function tokenURIs(uint256 tokenId) external view returns (uint256 index, string[] memory uris, bool pinned);

  /// @notice Pin a specific token uri for a particular token
  /// @dev This call MUST revert if the token does not exist
  /// @dev This call MUST emit a `TokenUriPinned` event
  /// @dev This call MAY emit a `MetadataUpdate` event from ERC-4096
  /// @param tokenId The identifier of the nft
  /// @param index The index in the string array returned from the `tokenURIs` function that should be pinned for the token
  function pinTokenURI(uint256 tokenId, uint256 index) external;

  /// @notice Unpin metadata for a particular token
  /// @dev This call MUST revert if the token does not exist
  /// @dev This call MUST emit a `TokenUriUnpinned` event
  /// @dev This call MAY emit a `MetadataUpdate` event from ERC-4096
  /// @dev It is up to the developer to define what this function does and is intentionally left open-ended
  /// @param tokenId The identifier of the nft
  function unpinTokenURI(uint256 tokenId) external;

  /// @notice Check on-chain if a token id has a pinned uri or not
  /// @dev This call MUST revert if the token does not exist
  /// @dev Useful for on-chain mechanics that don't require the tokenURIs themselves
  /// @param tokenId The identifier of the nft
  /// @return pinned A bool specifying if a token has metadata pinned or not
  function hasPinnedTokenURI(uint256 tokenId) external view returns (bool pinned);
}
```

当使用 `pinTokenUri` 函数固定代币 URI 时，必须发出 `TokenUriPinned` 事件。

当使用 `unpinTokenUri` 函数取消固定代币 URI 时，必须发出 `TokenUriUnpinned` 事件。

在 ERC-721 元数据扩展中定义的 `tokenURI` 函数必须在代币有固定 URI 时返回该固定 URI。

在 ERC-721 元数据扩展中定义的 `tokenURI` 函数必须在代币有未固定 URI 时返回默认 URI。

`supportsInterface` 方法在调用 `0x06e1bc5b` 时必须返回 `true`。

实现向代币添加或移除 URI 的功能必须与本标准分开实现。建议在添加或移除 URI 时发出 [ERC-4906](./eip-4906.md) 中定义的事件之一。

请参见 [实现](#reference-implementation) 部分以获取示例。

## 理由

使用与 [ERC-721](./eip-721.md) 相似的术语，以保持获取元数据的熟悉性。引入固定和取消固定元数据的概念，因为 NFT 拥有者可能希望选择显示哪一部分元数据。起初，我们考虑将固定和取消固定的操作留给每个开发者，但意识到标准接口的引入使得 dApp 可以轻松实现对多元数据代币的通用支持。

我们最初考虑 `tokenURIs` 函数是否应该仅返回一个字符串数组，但添加了额外信息，以便您可以在一次调用中获取所需的所有信息，而不是可能需要三次调用。固定的 URI 应作为代币的主 URI，而元数据 URI 列表可用于访问代币内各个资产的元数据。dApp 可以将这些呈现为画廊或媒体轮播。

本规范中包含的 `TokenUriPinned` 和 `TokenUriUnpinned` 事件可供 dApp 用于索引要显示的元数据。这可以消除链上调用，并可以使用事件驱动架构。

本标准建议在从代币添加或移除 URI 时使用 [ERC-4906](./eip-4906.md)，因为该事件已经得到了广泛的 dApp 支持，并且它正是所需的 - 向 dApp 发出代币元数据已更新的警报。我们不希望可能导致 dApp 问题的重复事件。第三方监听此事件后，可以调用 `tokenURIs` 函数以获取更新的元数据。

## 向后兼容性

该扩展旨在与现有的 [ERC-721](./eip-721.md) 合同向后兼容。`tokenURI` 方法的实现必须返回固定的代币 URI（如果已固定）或某个默认 URI（如果未固定）。

## 参考实现

可以提供 `IERC721MultiMetadata` 接口的开源参考实现，演示如何扩展现有的 [ERC-721](./eip-721.md) 合同以支持多元数据功能。该参考实现可以作为开发者在自己合同中实现扩展的指南。

```solidity
// SPDX-License-Identifier: CC0-1.0

pragma solidity ^0.8.19;

import {ERC721} from "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";
import {IERC4906} from "@openzeppelin/contracts/interfaces/IERC4906.sol";
import {IERC7160} from "./IERC7160.sol";

contract MultiMetadata is ERC721, Ownable, IERC7160, IERC4906 {
  mapping(uint256 => string[]) private _tokenURIs;
  mapping(uint256 => uint256) private _pinnedURIIndices;
  mapping(uint256 => bool) private _hasPinnedTokenURI;

  constructor(string memory _name, string memory _symbol) ERC721(_name, _symbol) Ownable() {
    _mint(msg.sender, 1);
  }

  // @notice Returns the pinned URI index or the last token URI index (length - 1).
  function _getTokenURIIndex(uint256 tokenId) internal view returns (uint256) {
    return _hasPinnedTokenURI[tokenId] ? _pinnedURIIndices[tokenId] : _tokenURIs[tokenId].length - 1;
  }

  // @notice Implementation of ERC721.tokenURI for backwards compatibility.
  // @inheritdoc ERC721.tokenURI
  function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
    _requireMinted(tokenId);

    uint256 index = _getTokenURIIndex(tokenId);
    string[] memory uris = _tokenURIs[tokenId];
    string memory uri = uris[index];

    // Revert if no URI is found for the token.
    require(bytes(uri).length > 0, "ERC721: not URI found");
    return uri;
  }

  /// @inheritdoc IERC721MultiMetadata.tokenURIs
  function tokenURIs(uint256 tokenId) external view returns (uint256 index, string[] memory uris, bool pinned) {
    _requireMinted(tokenId);
    return (_getTokenURIIndex(tokenId), _tokenURIs[tokenId], _hasPinnedTokenURI[tokenId]);
  }

  /// @inheritdoc IERC721MultiMetadata.pinTokenURI
  function pinTokenURI(uint256 tokenId, uint256 index) external {
    require(msg.sender == ownerOf(tokenId), "Unauthorized");
    _pinnedURIIndices[tokenId] = index;
    _hasPinnedTokenURI[tokenId] = true;
    emit TokenUriPinned(tokenId, index);
  }

  /// @inheritdoc IERC721MultiMetadata.unpinTokenURI
  function unpinTokenURI(uint256 tokenId) external {
    require(msg.sender == ownerOf(tokenId), "Unauthorized");
    _pinnedURIIndices[tokenId] = 0;
    _hasPinnedTokenURI[tokenId] = false;
    emit TokenUriUnpinned(tokenId);
  }

  /// @inheritdoc IERC721MultiMetadata.hasPinnedTokenURI
  function hasPinnedTokenURI(uint256 tokenId) external view returns (bool pinned) {
    return _hasPinnedTokenURI[tokenId];
  }

  /// @notice Sets a specific metadata URI for a token at the given index.
  function setUri(uint256 tokenId, uint256 index, string calldata uri) external onlyOwner {
    if (_tokenURIs[tokenId].length > index) {
      _tokenURIs[tokenId][index] = uri;
    } else {
      _tokenURIs[tokenId].push(uri);
    }

    emit MetadataUpdate(tokenId);
  }

  // Overrides supportsInterface to include IERC721MultiMetadata interface support.
  function supportsInterface(bytes4 interfaceId) public view virtual override(IERC165, ERC721) returns (bool) {
    return (
      interfaceId == type(IERC7160).interfaceId ||
      super.supportsInterface(interfaceId)
    );
  }
}
```

## 安全考虑

在指定状态改变事件的访问控制时应谨慎，例如允许将 URI 添加到代币的事件，以及本标准中指定的事件：`pinTokenUri` 和 `unpinTokenUri` 函数。这由开发者指定，因为每个应用程序可能有不同的要求来允许固定和取消固定。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。