---
eip: 7291
title: 目的绑定货币
description: 一个扩展 ERC-1155 的接口，用于实现目的绑定货币，这是一种可编程支付和货币的混合形式。
author: Orchid-Dev (@proj-orchid-straitsx), Victor Liew (@alcedo), Wong Tse Jian (@wongtsejian), Jacob Shan (@Jacobshan429), Chin Sin Ong (@chinsinong)
discussions-to: https://ethereum-magicians.org/t/eip-7291-purpose-bound-money/14973
status: 草案
type: 标准跟踪
category: ERC
created: 2023-06-24
requires: 165, 173, 1155
---

## 摘要

本提案概述了一个智能合约接口，该接口基于 [ERC-1155](./eip-1155.md) 标准实现目的绑定货币 (PBM) 概念：

- PBM 由 PBM 包装器和它所包装的数字货币代币组成。数字货币代币（例如稳定币、中央银行数字货币、代币化银行存款等）作为价值储存（简称为 "sov"）。因此，数字货币代币（也称为 "sovToken"）**应**：
  - 是良好的价值储存；
  - 是合适的计量单位；以及
  - 是交换媒介。
- PBM 是持有工具，具有自包含的编程逻辑，可以在不涉及中介的情况下在两方之间转移。它结合了以下概念：
  - 可编程支付 - 一旦满足预定义条件，自动执行支付；以及
  - 可编程货币 - 在交换媒介中嵌入规则的可能性，这些规则定义或限制其使用。
- 一旦条件满足，sovToken 被释放，并再次变为无约束。PBM 可以被视为数字现金券，因为它对付款方如何使用 PBM 施加了限制，但对商家/兑换者没有施加任何限制，因为 PBM 在支付给商家/兑换者时解包并释放基础数字货币。这类似于实体现金券，付款方被限制只能从发行者指定的商家处购买，但接受券的商家可以将实体券与发行者兑换成法定货币。

在此 EIP 中，我们提出一个模块化结构，包括一个与 [ERC-20](./erc-20.md) 兼容的 sovToken 作为数字货币，一个与 ERC-1155 兼容的智能合约作为 PBM 包装器，一个地址列表智能合约作为 PBM 逻辑，以及一个 PBM 代币管理智能合约来管理代币注册和检索。

## 动机

本提案旨在防止技术碎片化，从而导致缺乏互操作性。通过开放 PBM 规范，它为新参与者提供了轻松和免费的访问现有市场标准的机会，促进不同平台、钱包、支付系统和通道之间的互操作性。这将降低新参与者的进入成本，促进充满活力的支付生态，并防止围墙花园和垄断的形成，最终导致更高效、更实惠的服务和更好的用户体验。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按照 RFC 2119 和 RFC 8174 的描述进行解释。

### 定义

- **sovToken** - 一种 ERC-20 或 ERC-20 兼容的数字货币（例如 [ERC-777](./eip-777.md)、[ERC-1363](./eip-1363.md)），作为价值储存代币（即支持 PBM 代币的抵押品）。

- **PBM 包装器** - 一个符合 ERC-1155 的智能合约，通过指定一个或多个必须满足的条件（在本提案后续部分称为 PBM 业务逻辑）来包装 sovToken。PBM 包装器可以设计为模块化，具有核心、插件和钩子组件（详见 PBM 架构部分）。PBM 包装器智能合约与采用的钩子智能合约一起验证条件是否已满足，然后解包基础的 sovToken。

- **PBM 代币** - sovToken 及其 PBM 包装器统称为 PBM 代币。PBM 代币表示为 ERC-1155 代币。

- **PBM 创建者** 定义 PBM 包装器的条件以创建 PBM 代币。

- **PBM 钱包** - 加密钱包，可以是由私钥控制的 EOA（外部拥有账户），也可以是智能合约钱包。

- **商家 / 兑换者** - 在本提案的上下文中，商家或兑换者广义上定义为 PBM 代币的最终接收者或终端，这些代币本质上是指向或目的绑定于此。商家/兑换者的身份将在 PBM 地址列表中指定（该列表可以是 PBM 包装器智能合约的一部分或注册为 PBM 钩子的独立 PBM 地址列表智能合约）。

### 概述

- PBM **应** 遵循“包装”或“包装”的定义，以在其生命周期阶段根据 PBM 业务逻辑对代币进行绑定。

- PBM **应** 遵循“解包”或“解包”的定义，以在其生命周期阶段根据 PBM 业务逻辑释放代币。

- 有效的 PBM 代币 **必须** 包含一个基础的 sovToken 和 PBM 包装器。

  - sovToken 可以在 PBM 代币创建时或稍后进行包装。

  - sovToken 可以实现为任何广泛接受的 ERC-20 兼容代币，例如 ERC-20、ERC-777 或 ERC-1363。

- PBM 包装器 **必须** 提供一个机制，以便所有交易方在允许 PBM 代币解包之前验证所有必要条件是否已满足。有关详细信息，请参见可审计性部分。必要条件可以在 PBM 包装器内实现，或在单独的 PBM 钩子智能合约中实现。

- PBM 包装器 **必须** 确保解包的 sovToken 的目标地址在商家/兑换者地址的白名单中，并且在解包基础 sovToken 之前不在黑名单中。

- PBM 代币 **必须** 在完全解包和使用后被销毁。

- PBM 代币 **应** 具有由 PBM 创建者决定的到期时间。

  - 对于不需要到期时间的情况，到期时间 **应** 设置为无限。

- 本提案定义了 PBM 应包含的基本规范。对该基本规范的扩展可以作为单独的规范实施。

### PBM 架构

在此 EIP 中，我们提出一个模块化的 PBM 架构，具有三个不同的组件（核心、插件和钩子）：

- **核心组件** 包含所有 PBM 应具备的基本功能和验证检查。核心组件包括 sovToken 和 PBM 包装器，包含核心逻辑（例如，验证商家/兑换者地址的白名单逻辑、在转移到白名单地址时解包的逻辑、铸造和销毁 PBM 的逻辑）以及一个代币管理器，允许代币注册和检索。此外，PBM 包装器 **可以** 包含与插件和钩子接口的逻辑。
- **插件组件** 实现仅特定 PBM 可能需要的附加功能（例如，调用外部应用程序编程接口以验证特定 PBM 条件是否满足的逻辑、跟踪 PBM 使用模式的逻辑）。
- **钩子组件** 实现某些 PBM 可能需要的附加验证检查（例如，检查到期、每日支出限额、商品和服务限额等）。
例如，PBM 创建者可能希望确保只有 50% 的 PBM A 系列可以在超市消费，而 PBM B 系列在超市消费的比例没有限制。他可以实现一个插件智能合约来跟踪 PBM 用户在超市的消费情况。他还将实现一个钩子来验证用户所持有的 PBM A 系列中少于 50% 的代币是在超市消费的。这使他能够对 PBM A 和 B 系列使用相同的通用 PBM 包装和 sovToken。此外，PBM A 系列将注册插件模块和钩子模块以进行额外的数据跟踪和验证。

### 可审计性

PBM Wrapper **应**提供公众易于访问的机制，以验证解包 PBM 的智能合约逻辑。这些机制可以被交易方和/或白名单第三方的自动验证或异步用户验证所利用。

由于 PBM 条件的履行可能会受到审计，以确保所有交易方之间的信任，以下证据应记录以支持审计：

- 接口/事件的发出 **应**允许对交易历史、代币类型和代币余额进行细粒度重建
- 源代码 **应**在区块链浏览器上经过验证并正式发布。

### 可替代性

PBM Wrapper **应**能够包装多种类型的兼容 sovTokens（例如，同一个 PBM Wrapper 应能够包装 USDC 和 XSGD）。由同一 PBM Wrapper 包装的 sovTokens 可能是可替代的，也可能不是。该标准不强制规定实现必须如何做到这一点。

### PBM 代币详情

ERC-1155 多代币标准使每个代币 ID 对应于一个唯一的、可配置的代币类型。所有促进 PBM 的业务或展示逻辑的基本细节 **必须** 为每种代币类型定义。为此目的的强制字段在 `struct PBMToken`（如下）中列出。未来的提案可以根据需要定义额外的可选状态变量。一旦代币详情被定义，它 **必须** 是不可变的。

代币详情示例：

```solidity
pragma solidity ^0.8.0;

abstract contract IPBMRC1_TokenManager {
    /// @dev Mapping of each ERC-1155 tokenId to its corresponding PBM Token details.
    mapping (uint256 => PBMToken) internal tokenTypes ;

    /// @notice A PBM token MUST include compulsory state variables (name, faceValue, expiry, and uri) to adhere to this standard.
    /// @dev Represents all the details corresponding to a PBM tokenId.
    struct PBMToken {
        // Name of the token.
        string name;

        // Value of the underlying wrapped ERC20-compatible sovToken. Additional information on the `faceValue` can be specified by
        // adding the optional variables: `currencySymbol` or `tokenSymbol` as indicated below
        uint256 faceValue;

        // Time after which the token will be rendered useless (expressed in Unix Epoch time).
        uint256 expiry;

        // Metadata URI for ERC-1155 display purposes.
        string uri;

        // OPTIONAL: Indicates if the PBM token can be transferred to a non merchant/redeemer wallet.
        bool isTransferable;

        // OPTIONAL: Determines whether the PBM will be burned or revoked upon expiry, under certain predefined conditions, or at the owner's discretion.
        bool burnable;

        // OPTIONAL: Number of decimal places for the token.
        uint8 decimals;

        // OPTIONAL: The address of the creator of this PBM type on this smart contract. This field is optional because the creator is msg.sender by default.
        address creator;

        // OPTIONAL: The smart contract address of the sovToken.
        address tokenAddress;

        // OPTIONAL: The running balance of the PBM Token type that has been minted.
        uint256 totalSupply;

        // OPTIONAL: An ISO4217 three-character alphabetic code may be needed for the faceValue in multicurrency PBM use cases.
        string currencySymbol;

        // OPTIONAL: An abbreviation for the PBM token name may be assigned.
        string tokenSymbol;

        // Add other optional state variables below...
    }
}
```

实现者可以选择在 PBM 合约部署时定义所有代币类型。如有需要，他们还可以公开一个外部函数，以便在稍后创建新的 PBM 代币。
所有创建的代币类型 **应** 发出 `NewPBMTypeCreated` 事件。

```solidity
    /// @notice Creates a new PBM Token type with the provided data.
    /// @dev The caller of createPBMTokenType shall be responsible for setting the creator address.
    /// Example of uri can be found in [`sample-uri`](../assets/eip-7291/sample-uri/stx-10-static)
    /// Must emit {NewPBMTypeCreated}
    /// @param _name Name of the token.
    /// @param _faceValue Value of the underlying wrapped ERC20-compatible sovToken.
    /// @param _tokenExpiry Time after which the token will be rendered useless (expressed in Unix Epoch time).
    /// @param _tokenURI Metadata URI for ERC-1155 display purposes
    function createPBMTokenType(
        string memory _name,
        uint256 _faceValue,
        uint256 _tokenExpiry,
        string memory _tokenURI
    ) external virtual returns (uint256 tokenId_);

    /// @notice Emitted when a new Purpose-Bound Token (PBM) type is created within the contract.
    /// @param tokenId The unique identifier for the newly created PBM token type.
    /// @param tokenName A human-readable string representing the name of the newly created PBM token type.
    /// @param amount The initial supply of the newly created PBM token type.
    /// @param expiry The timestamp at which the newly created PBM token type will expire.
    /// @param creator The address of the account that created the new PBM token type.
    event NewPBMTypeCreated(uint256 tokenId, string tokenName, uint256 amount, uint256 expiry, address creator);

```

标准的实现者 **必须** 定义一个方法来检索 PBM 代币详情

```solidity
    /// @notice Retrieves the details of a PBM Token type given its tokenId.
    /// @dev This function fetches the PBMToken struct associated with the tokenId and returns it.
    /// @param tokenId The identifier of the PBM token type.
    /// @return pbmToken_ A PBMToken struct containing all the details of the specified PBM token type.
    function getTokenDetails(uint256 tokenId) external virtual view returns(PBMToken memory pbmToken_);
```

### PBM 地址列表

必须指定一个针对 PBM 解包的目标地址列表。该列表可以通过初始化函数作为包含各种业务逻辑元素的复合合约的一部分提供，或者可以直接作为状态变量编码在 PBM 智能合约中。

```solidity
pragma solidity ^0.8.0;

/// @title PBM Address list Interface.
/// @notice The PBM address list stores and manages whitelisted merchants/redeemers and blacklisted address for the PBMs
interface IPBMAddressList {

    /// @notice Checks if the address is one of the blacklisted addresses
    /// @param _address The address to query
    /// @return bool_ True if address is blacklisted, else false
    function isBlacklisted(address _address) external returns (bool bool_) ;

    /// @notice Checks if the address is one of the whitelisted merchant/redeemer addresses
    /// @param _address The address to query
    /// @return bool_ True if the address is in merchant/redeemer whitelist and is NOT a blacklisted address, otherwise false.
    function isMerchant(address _address) external returns (bool bool_) ;

    /// @notice Event emitted when the Merchant/Redeemer List is edited
    /// @param action Tags "add" or "remove" for action type
    /// @param addresses An array of merchant wallet addresses that was just added or removed from Merchant/Redeemer whitelist
    /// @param metadata Optional comments or notes about the added or removed addresses.
    event MerchantList(string action, address[] addresses, string metadata);

    /// @notice Event emitted when the Blacklist is edited
    /// @param action Tags "add" or "remove" for action type
    /// @param addresses An array of wallet addresses that was just added or removed from address blacklist
    /// @param metadata Optional comments or notes about the added or removed addresses.
    event Blacklist(string action, address[] addresses, string metadata);
}

```

### PBMRC1 - 基础接口

该接口包含实现预加载 PBM 所需的基本功能。

```solidity
pragma solidity ^0.8.0;

/// LIST OF EVENTS TO BE EMITTED
/// A database or explorer may listen to events and be able to provide indexed and categorized searches
/// @title PBM Specification interface
/// @notice The PBM (purpose bound money) allows us to add logical requirements on the use of sovTokens.
/// The PBM acts as wrapper around the sovTokens and implements the necessary business logic.
/// @dev PBM deployer must assign an overall owner to the smart contract. If fine grain access controls are required, EIP-5982 can be used on top of ERC173
interface IPBMRC1 is IERC173, IERC5679Ext1155 {

    /// @notice Initialise the contract by specifying an underlying ERC20-compatible token address,
    /// contract expiry, and the PBM address list.
    /// @param _sovToken The address of the underlying sovToken.
    /// @param _expiry The contract-wide expiry timestamp (in Unix epoch time).
    /// @param _pbmWrapperLogic This address should point to a smart contract that contains conditions governing a PBM;
    /// such as purpose-bound conditions (ie: an address list determining whether a PBM is permitted to be transferred to or to be unwrapped)
    /// and other relevant business logic, effectively implementing an inversion of control.
    function initialise(address _sovToken, uint256 _expiry, address _pbmWrapperLogic) external;

    /// @notice Returns the Uniform Resource Identifier (URI) metadata information for the PBM with the corresponding tokenId
    /// @dev URIs are defined in RFC 3986.
    /// The URI MUST point to a JSON file that conforms to the "ERC-1155 Metadata URI JSON Schema".
    /// Developer may choose to adhere to the ERC1155Metadata_URI extension interface if necessary.
    /// The URI is not expected to be immutable.
    /// @param tokenId The id for the PBM in query
    /// @return Returns the metadata URI string for the PBM
    function uri(uint256 tokenId) external  view returns (string memory);

    /**
        @notice Creates a PBM copy ( ERC1155 NFT ) of an existing PBM token type.
        @dev See {IERC5679Ext1155} on further implementation notes
        @param receiver The wallet address to which the created PBMs need to be transferred to
        @param tokenId The identifier of the PBM token type to be copied.
        @param amount The number of the PBMs that are to be created
        @param data Additional data with no specified format, based on eip-5750

        This function will transfer the underlying token from the caller into the PBM smart contract.
        IMPT: Before minting, the caller should approve the contract address to spend sovTokens on behalf of the caller.
            This can be done by calling the `approve` or `increaseMinterAllowance` functions of the ERC-20 contract and specifying `_spender` to be the PBM contract address.
            Ref : https://eips.ethereum.org/EIPS/eip-20

        WARNING: Any contracts that externally call these safeMint() and safeMintBatch() functions should implement some sort of reentrancy guard procedure
        (such as OpenZeppelin's ReentrancyGuard) or a Checks-effects-interactions pattern.

        As per ERC-5679 standard: When the token is being minted, the transfer events MUST be emitted as if the token in the `amount` for EIP-1155
        and `tokenId` being _id for EIP-1155 were transferred from address 0x0 to the recipient address identified by receiver.
        The total supply MUST increase accordingly.

        MUST Emits {TokenWrap} event as the underlying sovToken is wrapped by PBM wrapper smart contract during minting.

        Requirements:
        - contract must not be paused
        - tokens must not be expired
        - `tokenId` should be a valid id that has already been created
        - caller should have the necessary amount of the sovTokens required to mint
        - caller should have approved the PBM contract to spend the sovTokens
        - receiver should not be blacklisted
     */
    function safeMint(address receiver, uint256 tokenId, uint256 amount, bytes calldata data) external;

    /**
        @notice Creates multiple PBM copies (ERC1155 NFT) of an existing PBM token type.
        @dev See {IERC5679Ext1155}.
        @param receiver The wallet address to which the created PBMs need to be transferred to
        @param tokenIds The identifier of the PBM token type
        @param amounts The number of the PBMs that are to be created
        @param data Additional data with no specified format, based on eip-5750

        This function will transfer the underlying token from the caller into the PBM smart contract.
        IMPT: Before minting, the caller should approve the contract address to spend sovTokens on behalf of the caller.
            This can be done by calling the `approve` or `increaseMinterAllowance` functions of the ERC-20 contract and specifying `_spender` to be the PBM contract address.
            Ref : https://eips.ethereum.org/EIPS/eip-20

        WARNING: Any contracts that externally call these safeMint() and safeMintBatch() functions should implement some sort of reentrancy guard procedure
        (such as OpenZeppelin's ReentrancyGuard) or a Checks-effects-interactions pattern.

        As per ERC-5679 standard: When the token is being minted, the transfer events MUST be emitted as if the token in the `amount` for EIP-1155
        and `tokenId` being _id for EIP-1155 were transferred from address 0x0 to the recipient address identified by receiver.
        The total supply MUST increase accordingly.

        MUST Emits {TokenWrap} event as the underlying sovToken is wrapped by PBM wrapper smart contract during minting.

        Requirements:
        - contract must not be paused
        - tokens must not be expired
        - `tokenIds` should all be valid ids that have already been created
        - `tokenIds` and `amounts` list need to have the same number of values
        - caller should have the necessary amount of the sovTokens required to mint
        - caller should have approved the PBM contract to spend the sovTokens
        - receiver should not be blacklisted
     */
    function safeMintBatch(address receiver, uint256[] calldata tokenIds, uint256[] calldata amounts, bytes calldata data) external;

    /**
        @notice Burns a PBM token. Upon burning of the tokens, the underlying wrapped token (if any) should be handled.
        @dev Destroys `amount` tokens of token type `tokenId` from `from`
        @dev See {IERC5679Ext1155}

        @param from The originating wallet address of the PBMs to be burned
        @param tokenId The identifier of the PBM token type
        @param amount The amount of the PBMs that are to be burned
        @param data Additional data with no specified format, based on eip-5750

        MUST Emits {TransferSingle} event.
        MUST Emits {TokenUnwrapForPBMBurn} event if the underlying wrapped token is moved out of the PBM smart contract.

        Requirements:
        - `from` cannot be the zero address.
        - `from` must have at least `amount` tokens of token type `tokenId`.

     */
    function burn(address from, uint256 tokenId, uint256 amount, bytes calldata data) external;

    /**
        @notice Burns multiple PBM token. Upon burning of the tokens, the underlying wrapped token (if any) should be handled.
        @dev Destroys `amount` tokens of token type `tokenId` from `from`
        @dev See {IERC5679Ext1155}

        @param from The originating wallet address of the PBMs to be burned
        @param tokenIds The identifier of the PBM token types
        @param amounts The amount of the PBMs that are to be burned for each tokenId in _tokenIds
        @param data Additional data with no specified format, based on eip-5750

        Must Emits {TransferSingle} event.
        Must Emits {TokenUnwrapForPBMBurn} event if the underlying wrapped token is moved out of the PBM smart contract.

        Requirements:
        - `from` cannot be the zero address.
        - `from` must have at least amount specified in `_amounts` of the corresponding token type tokenId in `_tokenIds` array.
     */
    function burnBatch(address from, uint256[] calldata tokenIds, uint256[] calldata amounts, bytes calldata data) external;

    /// @notice Transfers the PBM(NFT) from one wallet to another.
    /// @dev This function extends the ERC-1155 standard in order to allow the PBM token to be freely transferred between wallet addresses due to
    /// widespread support accross wallet providers. Specific conditions and restrictions on whether a pbm can be moved across addresses can be incorporated in this function.
    /// Unwrap logic MAY also be placed within this function to be called.
    /// @param from The account from which the PBM (NFT) is moving from
    /// @param to The account which is receiving the PBM (NFT)
    /// @param id The identifier of the PBM token type
    /// @param amount The number of (quantity) the PBM type that are to be transferred of the PBM type
    /// @param data To record any data associated with the transaction, can be left blank if none
    function safeTransferFrom(address from, address to, uint256 id, uint256 amount, bytes memory data) external;

    /// @notice Transfers the PBM(NFT)(s) from one wallet to another.
    /// @dev This function extends the ERC-1155 standard in order to allow the PBM token to be freely transferred between wallet addresses due to
    /// widespread support accross wallet providers.  Specific conditions and restrictions on whether a pbm can be moved across addresses can be incorporated in this function.
    /// Unwrap logic MAY also be placed within this function to be called.
    /// If the receiving wallet is a whitelisted /redeemer wallet address, the PBM(NFT)(s) will be burnt and the underlying sovTokens will be transferred to the merchant/redeemer wallet instead.
    /// @param from The account from which the PBM (NFT)(s) is moving from
    /// @param to The account which is receiving the PBM (NFT)(s)
    /// @param ids The identifiers of the different PBM token type
    /// @param amounts The number of (quantity) the different PBM types that are to be created
    /// @param data To record any data associated with the transaction, can be left blank if none.
    function safeBatchTransferFrom(address from, address to, uint256[] memory ids, uint256[] memory amounts, bytes memory data) external;

    /// @notice Unwraps the underlying ERC-20 compatible tokens to an intended end point (ie: merchant/redeemer) upon fulfilling the required PBM conditions.
    /// @dev Add implementation specific logic for the conditions under which a PBM processes and transfers the underlying tokens here.
    /// e.g. If the receving wallet is a whitelisted merchant/redeemer wallet address, the PBM (NFT) MUST be burnt and the underlying sovTokens
    /// will unwrapped to be transferred to the merchant/redeemer wallet.
    /// MUST emit the event {TokenUnwrapForTarget} on success
    /// @param from The account currently holding the PBM
    /// @param to The account receiving the PBM (NFT)
    /// @param tokenId The identifier of the PBM token type
    /// @param amount The quantity of the PBM type involved in this transaction
    /// @param data Additional data without a specified format, based on EIP-5750
    function unwrap(address from, address to, uint256 tokenId, uint256 amount, bytes memory data) internal;

    /// @notice Allows the creator of a PBM token type to retrieve all locked-up underlying sovTokens within that PBM.
    /// @dev Ensure that only the creator of the PBM token type or the contract owner can call this function.
    /// Validate the token state and existence, handle PBM token burning if necessary, safely transfer the remaining sovTokens to the originator,
    /// MUST emit {PBMrevokeWithdraw} upon a successful revoke.
    /// @param tokenId The identifier of the PBM token type
    /// Requirements:
    /// - `tokenId` should be a valid identifier for an existing PBM token type.
    /// - The caller must be either the creator of the token type or the smart contract owner.
    function revokePBM(uint256 tokenId) external;

    /// @notice Emitted when a PBM type creator withdraws the underlying sovTokens from all the remaining expired PBMs
    /// @param beneficiary the address ( PBM type creator ) which receives the sovToken
    /// @param PBMTokenId The identifiers of the different PBM token type
    /// @param sovToken The address of the underlying sovToken
    /// @param sovTokenValue The number of underlying sovTokens transferred
    event PBMrevokeWithdraw(address beneficiary, uint256 PBMTokenId, address sovToken, uint256 sovTokenValue);

    /// @notice Emitted when the underlying tokens are unwrapped and transferred to a specific purpose-bound address.
    /// This event signifies the end of the PBM lifecycle, as all necessary conditions have been met to release the underlying tokens to the recipient (whitelisted merchant/redeemer with non-blacklisted wallet address).
    /// If there are multiple different underlying tokens involved in a single unwrap operation, this event should be emitted for each underlying token.
    /// @param from The address from which the PBM tokens are being unwrapped.
    /// @param to The purpose-bound address receiving the unwrapped underlying tokens.
    /// @param tokenIds An array containing the identifiers of the unwrapped PBM token types.
    /// @param amounts An array containing the quantities of the corresponding unwrapped PBM tokens.
    /// @param sovToken The address of the underlying sovToken.
    /// @param sovTokenValue The amount of unwrapped underlying sovTokens transferred.
    event TokenUnwrapForTarget(address from, address to, uint256[] tokenIds, uint256[] amounts, address sovToken, uint256 sovTokenValue);

    /// @notice Emitted when PBM tokens are burned, resulting in the unwrapping of the underlying tokens for the designated recipient.
    /// This event is required if there is an unwrapping of the underlying tokens during the PBM (NFT) burning process.
    /// If there are multiple different underlying tokens involved in a single unwrap operation, this event should be emitted for each underlying token.
    /// @param from The address from which the PBM tokens are being burned.
    /// @param to The address receiving the unwrapped underlying tokens.
    /// @param tokenIds An array containing the identifiers of the burned PBM token types.
    /// @param amounts An array containing the quantities of the corresponding burned PBM tokens.
    /// @param sovToken The address of the underlying sovToken.
    /// @param sovTokenValue The amount of unwrapped underlying sovTokens transferred.
    event TokenUnwrapForPBMBurn(address from, address to, uint256[] tokenIds, uint256[] amounts, address sovToken, uint256 sovTokenValue);

    /// Indicates the wrapping of an token into the PBM smart contract.
    /// @notice Emitted when underlying tokens are wrapped within the PBM smart contract.
    /// If there are multiple different underlying tokens involved in a single wrap operation, this event should be emitted for each underlying token.
    /// This event signifies the beginning of the PBM lifecycle, as tokens are now managed by the conditions within the PBM contract.
    /// @param from The address initiating the token wrapping process, and
    /// @param tokenIds An array containing the identifiers of the token types being wrapped.
    /// @param amounts An array containing the quantities of the corresponding wrapped tokens.
    /// @param sovToken The address of the underlying sovToken.
    /// @param sovTokenValue The amount of wrapped underlying sovTokens transferred.
    event TokenWrap(address from, uint256[] tokenIds, uint256[] amounts,address sovToken, uint256 sovTokenValue);
}

```

### 扩展

#### PBMRC1 - 代币接收器

智能合约 **必须** 实现 PBMRC1_TokenReceiver 接口中的所有功能，以订阅 PBM 解包回调。

```solidity
pragma solidity ^0.8.0;

/// @notice Smart contracts MUST implement the ERC-165 `supportsInterface` function and signify support for the `PBMRC1_TokenReceiver` interface to accept callbacks.
/// It is optional for a receiving smart contract to implement the `PBMRC1_TokenReceiver` interface
/// @dev WARNING: Reentrancy guard procedure, Non delegate call, or the check-effects-interaction pattern must be adhere to when calling an external smart contract.
/// The interface functions MUST only be called at the end of the `unwrap` function.
interface PBMRC1_TokenReceiver {
    /**
        @notice Handles the callback from a PBM smart contract upon unwrapping
        @dev An PBM smart contract MUST call this function on the token recipient contract, at the end of a `unwrap` if the
        receiver smart contract supports type(PBMRC1_TokenReceiver).interfaceId
        @param _operator  The address which initiated the transfer (either the address which previously owned the token or the address authorised to make transfers on the owner's behalf) (i.e. msg.sender)
        @param _from      The address which previously owned the token
        @param _id        The ID of the token being unwrapped
        @param _value     The amount of tokens being transferred
        @param _data      Additional data with no specified format
        @return           `bytes4(keccak256("onPBMRC1Unwrap(address,address,uint256,uint256,bytes)"))`
    */
    function onPBMRC1Unwrap(address _operator, address _from, uint256 _id, uint256 _value, bytes calldata _data) external returns(bytes4);

    /**
        @notice Handles the callback from a PBM smart contract upon unwrapping a batch of tokens
        @dev An PBM smart contract MUST call this function on the token recipient contract, at the end of a `unwrap` if the
        receiver smart contract supports type(PBMRC1_TokenReceiver).interfaceId

        @param _operator  The address which initiated the transfer (either the address which previously owned the token or the address authorised to make transfers on the owner's behalf) (i.e. msg.sender)
        @param _from      The address which previously owned the token
        @param _id        The ID of the token being unwrapped
        @param _value     The amount of tokens being transferred
        @param _data      Additional data with no specified format
        @return           `bytes4(keccak256("onPBMRC1BatchUnwrap(address,address,uint256,uint256,bytes)"))`
    */
    function onPBMRC1BatchUnwrap(address _operator, address _from, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external returns(bytes4);
}

```

#### PBMRC2 - 非预加载 PBM 接口

**非预加载** PBM 扩展是合规智能合约的可选项。这允许合约在稍后日期将基础 sovToken 绑定到 PBM，而不是在铸造过程中。

合规合约 **必须** 实现以下接口：

```solidity
pragma solidity ^0.8.0;

/**
 *  @dev This interface extends IPBMRC1, adding functions for working with non-preloaded PBMs.
 *  Non-preloaded PBMs are minted as empty containers without any underlying tokens of value,
 *  allowing the loading of the underlying token to happen at a later stage.
 */
interface PBMRC2_NonPreloadedPBM is IPBMRC1 {

  /// @notice This function extends IPBMRC1 to mint PBM tokens as empty containers without underlying tokens of value.
  /// @dev The loading of the underlying token of value can be done by calling the `load` function. The function parameters should be identical to IPBMRC1
  function safeMint(address receiver, uint256 tokenId, uint256 amount, bytes calldata data) external;

  /// @notice This function extends IPBMRC1 to mint PBM tokens as empty containers without underlying tokens of value.
  /// @dev The loading of the underlying token of value can be done by calling the `load` function. The function parameters should be identical to IPBMRC1
  function safeMintBatch(address to, uint256[] calldata ids, uint256[] calldata amounts, bytes calldata data) external;

  /// @notice Wrap an amount of sovTokens into the PBM
  /// @dev function will pull sovTokens from msg.sender
  /// Approval must be given to the PBM smart contract in order to for the pbm to pull money from msg.sender
  /// underlying data structure must record how much the msg.sender has been loaded into the PBM.
  /// Emits {TokenLoad} event.
  /// @param amount    The amount of sovTokens to be loaded
  function load(uint256 amount) external;

  /// @notice Retrieves the balance of the underlying sovToken associated with a specific PBM token type and user address.
  /// This function provides a way to check the amount of the underlying token that a user has loaded into a particular PBM token.
  /// @param user The address of the user whose underlying token balance is being queried.
  /// @return The balance of the underlying sovToken associated with the specified PBM token type and user address.
  function underlyingBalanceOf(address user) external view returns (uint256);

  /// @notice Unloads all of the underlying token belonging to the caller from the PBM smart contract.
  /// @dev The underlying token that belongs to the caller (msg.sender) will be removed and transferred
  /// back to the caller.
  /// Emits {TokenUnload} event.
  /// @param amount The quantity of the corresponding tokens to be unloaded.
  /// Amount should not exceed the amount that the caller has originally loaded into the PBM smart contract.
  function unload(uint256 amount) external;

  /// @notice Emitted when an underlying token is loaded into a PBM
  /// @param caller Address by which sovToken is taken from.
  /// @param to Address by which the token is loaded and assigned to
  /// @param amount The quantity of tokens to be loaded
  /// @param sovToken The address of the underlying sovToken.
  /// @param sovTokenValue The amount of underlying sovTokens loaded
  event TokenLoad(address caller, address to, uint256 amount, address sovToken, uint256 sovTokenValue);

  /// @notice Emitted when an underlying token is unloaded from a PBM.
  /// This event indicates the process of releasing the underlying token from the PBM smart contract.
  /// @param caller The address initiating the token unloading process.
  /// @param from The address from which the token is being unloaded and removed from.
  /// @param amount The quantity of the corresponding unloaded tokens.
  /// @param sovToken The address of the underlying sovToken.
  /// @param sovTokenValue The amount of unloaded underlying sovTokens transferred.
  event TokenUnload(address caller, address from, uint256 amount, address sovToken, uint256 sovTokenValue);
}

```

## 理由

### 为什么 sovToken **必须** 兼容 ERC-20？

由于 PBM 被设想为具有货币功能，它必须是具有稳定价值的可替代代币。目前，市场上主要的稳定币主要基于 ERC-20 接口。ERC-20 或 ERC-20 兼容代币是现有钱包、DeFi 应用程序最广泛支持的，并且也被协议设计如 [ERC-4337](./eip-4337.md) 使用，更重要的是，它们是可替代代币的事实标准。

关于 [ERC-721](./eip-721.md) 和 ERC-1155 兼容代币：

- ERC-721 不适合，因为它是非可替代代币的标准，无法满足货币的功能。
- 虽然 ERC-1155 代币可以用于可替代代币，但我们决定不包括它，因为市场上缺乏 ERC-1155 稳定币。要求 PBM 接口同时支持 ERC-20 兼容和 ERC-1155 兼容的 sovToken 会使 PBM 接口复杂化，而没有增加太多实际效用。此外，基础 ERC-1155 不支持小数，但这并不是致命问题，因为可以有变通办法。然而，如果未来稳定币市场发生变化，可以考虑修订。

### 为什么 PBM Wrapper **必须** 兼容 ERC-1155？

本文扩展了 ERC-1155 标准，以便现有钱包提供商能够轻松采用。目前，大多数钱包提供商能够支持和显示 ERC-20、ERC-1155 和 ERC-721 标准。不扩展这些标准的实现将要求钱包提供商构建自定义用户界面和接口逻辑，这会增加实施成本并延长上市时间。

我们提案的核心目标是标准化 PBM 的实现。因此，我们调查了现有的接口标准，并决定基于 ERC-1155 标准为 PBM 代币构建，原因如下：

- ERC-1155 允许单个合约支持多种代币。这对于 PBM 用例非常有用，因为单个合约可以支持不同面额、到期日和业务逻辑的代币的发行。
- ERC-1155 还支持批量转移，而 ERC-20 不支持，这在需要将代币空投给大量接收者时可以节省 gas。
- ERC-1155 能够支持半可替代代币，这对于 PBM 用例非常有用，因为 PBM 可以在到期后转换为收藏品。
- ERC-1155 允许在钱包发行者的 UI 上可视化 PBM 代币。

### 为什么 PBM **必须** 确保解包的 sovToken 的目标地址在商户/兑换者地址的白名单中，并且在解包基础 sovToken 之前不在黑名单中？

为什么我们需要白名单？

- 白名单是强制要求，因为 PBM 是有目的的，即只有在所有条件满足并且转移给白名单中的某人时，才应解包。
- 在某些实现中，开发者可以定义白名单地址是动态在运行时确定的，例如钱包地址中存在 NFT，或依赖于预言机等。

为什么我们需要黑名单？

- 黑名单是强制要求，以确保因各种原因被禁止的账户（例如，地址所有者重新注册了新账户，地址所有者因投诉或执法原因被暂停/撤回/驱逐等）。

为什么我们不能只拥有白名单或黑名单？

- 虽然仅拥有白名单可以获得相同的效果，但为了禁止一个人而反复将白名单重新部署到区块链上并不高效。
- 使用黑名单来实施目的明确的货币并不实际，因为您需要有一个所有要排除的地址的列表，并在每次创建新账户时更新它。
为什么需要目的地？

- 这实际上构成了我们所提议的核心 - PBM 只有在转移到预先批准的目的地时才能被解包。
- PBM 可以自由转移，但允许解包 PBM 并接收基础 sovToken 的目标必须有限，以将其与由智能合约包装的普通稳定币区分开来。

### 商业逻辑包含什么？

- 一般来说，商业逻辑可以分为核心逻辑、插件逻辑和钩子逻辑：
  - 核心逻辑包含基本功能和验证检查，应包含在 PBM Wrapper 合约中。
  - 插件和钩子逻辑可以包含在独立的智能合约模块中，并由 PBM Wrapper 合约注册。插件逻辑通过添加功能扩展核心逻辑，例如自定义数据收集、额外的管理功能等。
  - 钩子逻辑实现额外的验证检查，这些检查仅适用于一部分 PBM。
- “PBM 商业逻辑”可以包含访问控制逻辑、PBM 解包逻辑、与非区块链 IT 系统集成的 API 逻辑。
- 由于 PBM 可以用于各种用例，从政府发放代币、购物券、预付代币、奖励积分代币、目的绑定捐赠代币、学校津贴代币等，每个用例都有单独的商业逻辑，因此故意未定义，以便实现作者可以拥有最大的灵活性。

### 为什么选择推送交易模型？

- 该标准坚持推送交易模型，其中 PBM 的转移在发送方发起。现代钱包可以通过在 ERC-1155 `safeTransfer` 函数中嵌入解包逻辑来支持所需的 PBM 逻辑。

### 可定制性

每个 ERC-1155 PBM 代币将映射到一个基础的 `PBMToken` 数据结构，实施者可以根据商业逻辑自由定制。

通过将基础 ERC-1155 代币模型与附加数据结构映射，可以在同一智能合约中灵活管理多种代币类型，并为每种代币类型附加多个条件解包逻辑，从而降低 gas 成本，因为不需要为每种代币类型部署多个智能合约。

1. 为了保持简单，该标准*故意*省略了不增加 PBM 定义和概念的函数或事件。

2. 本 EIP 不对访问控制或函数执行条件做出假设。PBM 创建者有责任确定每个特定 PBM 商业流程中涉及的各种角色。

3. 提议的 PBM 架构*故意*模块化，以实现更大的可定制性和智能合约的重用性。

4. 与 PBM 标准相关的元数据未包含在标准中。如有必要，可以通过单独的元数据扩展接口创建相关元数据，例如来自 ERC-721 的 `ERC721Metadata`。有关实现示例，请参阅 Opensea 的元数据标准。

5. 为了允许未来的可扩展性，**建议**开发者阅读并采用构建方法行为通用可扩展性的规范（[ERC-5750](./eip-5750.md)）。

## 向后兼容性

该接口旨在与 ERC-1155 兼容。

## 参考实现

参考实现可以在 [`README.md`](../assets/eip-7291/README.md) 中找到。

## 安全考虑

- 智能合约中使用的所有内容都是公开可见的，包括标记为 `private` 的局部变量和状态变量。

- 由于 gas 限制，循环中没有固定迭代次数的情况必须谨慎使用。

- 切勿使用 tx.origin 来检查授权。应使用 `msg.sender` 来检查授权。

- 如果库代码作为 `delegatecall` 的一部分使用，请确保库代码是无状态的，以防止恶意行为者通过 `delegatecall` 更改合约中的状态。

- 恶意行为者可能会尝试抢先交易。由于交易在被挖掘之前需要一些时间，攻击者可以观察交易池并发送交易，使其在原始交易之前被包含在区块中。此机制可能被滥用以重新排序交易以攻击者的利益。可以使用承诺方案来防止抢先交易。

- 不要使用 block.timestamp 作为熵和随机数的来源。

- 在链下签署消息，并要求合约在执行函数之前需要该签名是一种有用的技术。然而，恶意行为者可以利用相同的签名多次执行函数。如果签署者的意图是仅批准一次交易，这可能是有害的。为了防止签名重放，消息应使用 nonce 和合约地址进行签名。

- 恶意用户可能会尝试：

  - 通过重入进行双重支出。
  - 克隆现有的 PBM 代币以进行双重支出；
  - 创建没有基础 sovToken 的无效 PBM 代币；或
  - 通过包装欺诈/无效/毫无价值的 sovTokens 来伪造 PBM 代币的面值。

- 为了一致性，当合约被暂停或由于涉嫌欺诈活动或错误转账而限制用户的代币转移时，必须对用户的 PBM 代币解包请求施加相应的限制。

- 应进行安全审计和测试，以验证解包逻辑是否按预期工作，或是否实施了涉及调用外部智能合约的复杂商业逻辑，以防止重入攻击和其他形式的调用链攻击。

- 本 EIP 依赖于代币实现的安全和准确的记账行为。

  - 遵循该标准的合约应密切监控每个用户在代币消费或铸造过程中的余额变化。

  - PBM Wrapper 必须精心设计，以确保有效控制铸造新代币的权限。未能确保铸造权限可能导致欺诈性发行和未经授权的总代币供应膨胀。

  - 每个 PBM 代币与智能合约持有的相应基础 sovToken 数量的映射需要仔细的会计和审计。

  - 对于销毁代币的权限的访问控制应仔细设计。通常，只有以下两个角色有权销毁代币：

    - 角色 1. 在 PBM 到期之前，只有具有非黑名单钱包地址的白名单商家/兑换者被允许解包和销毁他们持有的代币。
    - 角色 2. 在 PBM 到期后：
      - 具有非黑名单钱包地址的白名单商家/兑换者被允许解包和销毁他们持有的代币；以及
      - PBM 所有者被允许销毁仍在非白名单商家/兑换者手中的未使用 PBM 代币，以检索基础 sovTokens。

  - 然而，我们确实认识到可能还有其他用例，其中第三种角色可能有权销毁。实施者在设计 PBM 代币销毁的访问控制时应谨慎。

- 建议采用与 ERC-20 兼容的代币标准。此类兼容代币的示例包括实现 ERC-777 或 ERC-1363 的代币。然而，由于其简单性，ERC-20 仍然是最广泛接受的，因为对其安全性有很高的信心。
## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。