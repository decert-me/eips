---
eip: 6997
title: 带有交易验证步骤的 ERC-721
description: 一种新的验证步骤用于转账和批准调用，以防止钱包被盗时的安全措施。
author: Eduard López i Fina (@eduardfina)
discussions-to: https://ethereum-magicians.org/t/erc721-with-a-validation-step/14071
status: Review
type: Standards Track
category: ERC
created: 2023-05-07
requires: 721
---

## 摘要

该标准是 [ERC-721](./eip-721.md) 的扩展。它定义了新的验证功能，以避免钱包被盗：每个 `transfer` 或 `approve` 将被锁定，等待验证。

## 动机

区块链的力量同时也是它的弱点：赋予用户对其数据的完全责任。

目前存在许多 NFT 被盗的案例，而当前的 NFT 防盗方案，如将 NFT 转移到冷钱包，使得 NFT 使用起来不方便。

在每个 `transfer` 和 `approve` 之前进行验证步骤，将为智能合约开发者提供创建安全 NFT 防盗方案的机会。

一个实现示例是一个系统，其中一个验证者地址负责验证所有智能合约交易。

该地址将连接到一个 dApp，用户可以在其中查看其 NFT 的验证请求并接受正确的请求。

仅赋予该地址验证交易的权力，将使系统更加安全，盗贼必须同时拥有用户地址和验证者地址才能盗取 NFT。

## 规范

本文档中的关键词 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

符合 [ERC-721](./eip-721.md) 的合约 MAY 实现此 EIP。

所有更改 NFT 所有权的操作，如 `transferFrom`/`safeTransferFrom`，SHALL 创建一个待验证的 `TransferValidation` 并发出 `ValidateTransfer`，并且 SHALL NOT 转移 NFT 的所有权。

所有启用管理 NFT 的批准操作，如 `approve`/`setApprovalForAll`，SHALL 创建一个待验证的 `ApprovalValidation` 并发出 `ValidateApproval`，并且 SHALL NOT 启用批准。

当转账由已批准的账户而非所有者调用时，必须直接执行，而无需验证。这是为了适应所有当前市场所需的直接移动 NFT 的批准。

在验证 `TransferValidation` 或 `ApprovalValidation` 时，valid 字段必须设置为 true，并且必须不再验证。

验证 `TransferValidation` 的操作 SHALL 更改 NFT 的所有权或启用批准。

验证 `ApprovalValidation` 的操作 SHALL 启用批准。

### 合约接口

```solidity
 interface IERC6997 {

    struct TransferValidation {
        // The address of the owner.
        address from;
        // The address of the receiver.
        address to;
        // The token Id.
        uint256 tokenId;
        // Whether is a valid transfer.
        bool valid;
    }

    struct ApprovalValidation {
        // The address of the owner.
        address owner;
        // The approved address.
        address approve;
        // The token Id.
        uint256 tokenId;
        // Wether is a total approvement.
        bool approveAll;
        // Whether is a valid approve.
        bool valid;
    }

    /**
     * @dev Emitted when a new transfer validation has been requested.
     */
    event ValidateTransfer(address indexed from, address to, uint256 indexed tokenId, uint256 indexed transferValidationId);

    /**
    * @dev Emitted when a new approval validation has been requested.
    */
    event ValidateApproval(address indexed owner, address approve, uint256 tokenId, bool indexed approveAll, uint256 indexed approvalValidationId);

    /**
     * @dev Returns true if this contract is a validator ERC721.
     */
    function isValidatorContract() external view returns (bool);

    /**
     * @dev Returns the transfer validation struct using the transfer ID.
     *
     */
    function transferValidation(uint256 transferId) external view returns (TransferValidation memory);

    /**
    * @dev Returns the approval validation struct using the approval ID.
    *
    */
    function approvalValidation(uint256 approvalId) external view returns (ApprovalValidation memory);

    /**
     * @dev Return the total amount of transfer validations created.
     *
     */
    function totalTransferValidations() external view returns (uint256);

    /**
     * @dev Return the total amount of transfer validations created.
     *
     */
    function totalApprovalValidations() external view returns (uint256);
}
  ```

`isValidatorContract()` 函数必须实现为 `public`。

`transferValidation(uint256 transferId)` 函数可以实现为 `public` 或 `external`。

`approvalValidation(uint256 approveId)` 函数可以实现为 `public` 或 `external`。

`totalTransferValidations()` 函数可以实现为 `pure` 或 `view`。

`totalApprovalValidations()` 函数可以实现为 `pure` 或 `view`。

## 理由

### 普遍性

该标准仅定义验证函数，而不定义它们的使用方式。它将验证定义为内部，并让用户决定如何管理它们。

一个示例可以是将地址验证者连接到 dApp，以便用户可以管理他们的验证。

该验证者可以用于所有 NFT 或仅用于某些用户。

它也可以作为现有 ERC-721 的包装智能合约使用，允许与现有 NFT 进行 1/1 转换。

### 可扩展性

该标准仅定义验证函数，但不定义必须如何进行验证的系统。第三方协议可以根据自己的意愿定义如何调用这些函数。

## 向后兼容性

该标准是 [ERC-721](./eip-721.md) 的扩展，兼容所有操作，除了 `transferFrom`/`safeTransferFrom`/`approve`/`setApprovalForAll`。

这些操作将被重写，以创建验证请求，而不是转移 NFT 的所有权或启用批准。

## 参考实现

```solidity
// SPDX-License-Identifier: CC0-1.0

pragma solidity ^0.8.0;

import "./IERC6997.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";

/**
 * @dev Implementation of ERC6997
 */
contract ERC6997 is IERC6997, ERC721 {

    // Mapping from transfer ID to transfer validation
    mapping(uint256 => TransferValidation) private _transferValidations;

    // Mapping from approval ID to approval validation
    mapping(uint256 => ApprovalValidation) private _approvalValidations;

    // Total number of transfer validations
    uint256 private _totalTransferValidations;

    // Total number of approval validations
    uint256 private _totalApprovalValidations;

    /**
     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.
     */
    constructor(string memory name_, string memory symbol_) ERC721(name_, symbol_){
    }

    /**
    * @dev Returns true if this contract is a validator ERC721.
    */
    function isValidatorContract() public pure returns (bool) {
        return true;
    }

    /**
     * @dev Returns the transfer validation struct using the transfer ID.
     *
     */
    function transferValidation(uint256 transferId) public view override returns (TransferValidation memory) {
        require(transferId < _totalTransferValidations, "ERC6997: invalid transfer ID");
        TransferValidation memory v = _transferValidation(transferId);

        return v;
    }

    /**
     * @dev Returns the approval validation struct using the approval ID.
     *
     */
    function approvalValidation(uint256 approvalId) public view override returns (ApprovalValidation memory) {
        require(approvalId < _totalApprovalValidations, "ERC6997: invalid approval ID");
        ApprovalValidation memory v = _approvalValidation(approvalId);

        return v;
    }

    /**
     * @dev Return the total amount of transfer validations created.
     *
     */
    function totalTransferValidations() public view override returns (uint256) {
        return _totalTransferValidations;
    }

    /**
     * @dev Return the total amount of approval validations created.
     *
     */
    function totalApprovalValidations() public view override returns (uint256) {
        return _totalApprovalValidations;
    }

    /**
     * @dev Returns the transfer validation of the `transferId`. Does NOT revert if transfer doesn't exist
     */
    function _transferValidation(uint256 transferId) internal view virtual returns (TransferValidation memory) {
        return _transferValidations[transferId];
    }

    /**
     * @dev Returns the approval validation of the `approvalId`. Does NOT revert if transfer doesn't exist
     */
    function _approvalValidation(uint256 approvalId) internal view virtual returns (ApprovalValidation memory) {
        return _approvalValidations[approvalId];
    }

    /**
     * @dev Validate the transfer using the transfer ID.
     *
     */
    function _validateTransfer(uint256 transferId) internal virtual {
        TransferValidation memory v = transferValidation(transferId);
        require(!v.valid, "ERC6997: the transfer is already validated");

        address from = v.from;
        address to = v.to;
        uint256 tokenId = v.tokenId;

        super._transfer(from, to, tokenId);

        _transferValidations[transferId].valid = true;
    }

    /**
     * @dev Validate the approval using the approval ID.
     *
     */
    function _validateApproval(uint256 approvalId) internal virtual {
        ApprovalValidation memory v = approvalValidation(approvalId);
        require(!v.valid, "ERC6997: the approval is already validated");

        if(!v.approveAll) {
            require(v.owner == ownerOf(v.tokenId), "ERC6997: The token have a new owner");
            super._approve(v.approve, v.tokenId);
        }
        else {
            super._setApprovalForAll(v.owner, v.approve, true);
        }

        _approvalValidations[approvalId].valid = true;
    }

    /**
     * @dev Create a transfer petition of `tokenId` from `from` to `to`.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     *
     * Emits a {TransferValidate} event.
     */
    function _transfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override {
        require(ERC721.ownerOf(tokenId) == from, "ERC6997: transfer from incorrect owner");
        require(to != address(0), "ERC6997: transfer to the zero address");

        if(_msgSender() == from) {
            TransferValidation memory v;

            v.from = from;
            v.to = to;
            v.tokenId = tokenId;

            _transferValidations[_totalTransferValidations] = v;

            emit ValidateTransfer(from, to, tokenId, _totalTransferValidations);

            _totalTransferValidations++;
        } else {
            super._transfer(from, to, tokenId);
        }
    }

    /**
     * @dev Create an approval petition from `to` to operate on `tokenId`
     *
     * Emits an {ValidateApproval} event.
     */
    function _approve(address to, uint256 tokenId) internal override virtual {
        ApprovalValidation memory v;

        v.owner = ownerOf(tokenId);
        v.approve = to;
        v.tokenId = tokenId;

        _approvalValidations[_totalApprovalValidations] = v;

        emit ValidateApproval(v.owner, to, tokenId, false, _totalApprovalValidations);

        _totalApprovalValidations++;
    }

    /**
     * @dev If approved is true create an approval petition from `operator` to operate on
     * all of `owner` tokens, if not remove `operator` from operate on all of `owner` tokens
     *
     * Emits an {ValidateApproval} event.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal override virtual {
        require(owner != operator, "ERC6997: approve to caller");

        if(approved) {
            ApprovalValidation memory v;

            v.owner = owner;
            v.approve = operator;
            v.approveAll = true;

            _approvalValidations[_totalApprovalValidations] = v;

            emit ValidateApproval(v.owner, operator, 0, true, _totalApprovalValidations);

            _totalApprovalValidations++;
        }
        else {
            super._setApprovalForAll(owner, operator, approved);
        }
    }
}
```

## 安全考虑

如规范中所定义，更改 NFT 所有权或启用管理 NFT 的批准的操作 SHALL 创建一个待验证的 `TransferValidation` 或 `ApprovalValidation`，并且 SHALL NOT 转移 NFT 的所有权或启用批准。

在此前提下，负责验证 `TransferValidation` 或 `ApprovalValidation` 的操作必须受到所应用系统所需的最大安全性保护。

例如，一个有效的系统将是一个有验证者地址负责验证交易的系统。

再举一个例子，允许每个用户选择其验证者地址的系统也是正确的。

无论如何，安全性的重要性在于没有地址可以在未获得所选系统的许可的情况下验证 `TransferValidation` 或 `ApprovalValidation`。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。