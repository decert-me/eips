---
eip: 5005
title: Zodiac 模块化账户
description: 可组合的互操作可编程账户
author: Auryn Macmillan (@auryn-macmillan), Kei Kreutler (@keikreutler)
discussions-to: https://ethereum-magicians.org/t/eip-zodiac-a-composable-design-philosophy-for-daos/8963
status: Stagnant
type: Standards Track
category: ERC
created: 2022-04-14
requires: 165
---

## 摘要
本 EIP 标准化了可组合和互操作的可编程以太坊账户的接口。这些接口将合约账户（“化身”）与其身份验证和执行逻辑（“守卫”和“模块”）分离。化身实现 `IAvatar` 接口，守卫实现 `IGuard` 接口。模块可以采取任何形式。

## 动机
目前，大多数可编程账户（如 DAO 工具和框架）都是作为单体系统构建的，其中授权和执行逻辑耦合在一起，或者在同一合约内，或者在紧密集成的合约系统中。这无谓地抑制了这些工具的灵活性，并通过高转换成本鼓励平台锁定。

通过使用本 EIP 标准来分离关注点（解耦身份验证和执行逻辑），用户能够：

1. 启用灵活的基于模块的可编程账户控制
2. 在工具和框架之间轻松切换，而无需不必要的开销
3. 并行启用多种控制机制
4. 启用跨链/跨层治理
5. 随着项目和社区的成熟，逐步去中心化其治理

## 规范
本文档中的关键字 “MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL” 应按 RFC 2119 中的描述进行解释。

本 EIP 包含四个关键概念：

- **化身** 是可编程的以太坊账户。化身是持有余额、拥有系统、执行交易、被外部引用并最终代表您的 DAO 的地址。化身 MUST 实现 `IAvatar` 接口。
- **模块** 是由化身启用的合约，实施某些执行逻辑。
- **修饰符** 是位于模块和化身之间的合约，用于修改模块的行为。例如，它们可能会对模块尝试执行的所有函数强制执行延迟，或限制模块可以发起的交易范围。修饰符 MUST 实现 `IAvatar` 接口。
- **守卫** 是可以在模块或修饰符上启用的合约，并对这些模块或修饰符执行的每个交易实施前检查或后检查。这允许化身执行诸如限制模块或修饰符可以调用的地址和函数的范围，或确保某个状态永远不会被模块或修饰符更改。守卫 MUST 暴露 `IGuard` 接口。希望被守卫的模块、修饰符和化身 MUST 继承 `Guardable`，MUST 在触发目标执行之前调用 `checkTransaction()`，并且 MUST 在执行完成后调用 `checkAfterExecution()`。

```solidity
/// @title Avatar - A contract that manages modules that can execute transactions via this contract.

pragma solidity >=0.7.0 <0.9.0;

import "./Enum.sol";


interface IAvatar {
    event EnabledModule(address module);
    event DisabledModule(address module);
    event ExecutionFromModuleSuccess(address indexed module);
    event ExecutionFromModuleFailure(address indexed module);

    /// @dev Enables a module on the avatar.
    /// @notice Can only be called by the avatar.
    /// @notice Modules should be stored as a linked list.
    /// @notice Must emit EnabledModule(address module) if successful.
    /// @param module Module to be enabled.
    function enableModule(address module) external;

    /// @dev Disables a module on the avatar.
    /// @notice Can only be called by the avatar.
    /// @notice Must emit DisabledModule(address module) if successful.
    /// @param prevModule Address that pointed to the module to be removed in the linked list
    /// @param module Module to be removed.
    function disableModule(address prevModule, address module) external;

    /// @dev Allows a Module to execute a transaction.
    /// @notice Can only be called by an enabled module.
    /// @notice Must emit ExecutionFromModuleSuccess(address module) if successful.
    /// @notice Must emit ExecutionFromModuleFailure(address module) if unsuccessful.
    /// @param to Destination address of module transaction.
    /// @param value Ether value of module transaction.
    /// @param data Data payload of module transaction.
    /// @param operation Operation type of module transaction: 0 == call, 1 == delegate call.
    function execTransactionFromModule(
        address to,
        uint256 value,
        bytes memory data,
        Enum.Operation operation
    ) external returns (bool success);

    /// @dev Allows a Module to execute a transaction and return data
    /// @notice Can only be called by an enabled module.
    /// @notice Must emit ExecutionFromModuleSuccess(address module) if successful.
    /// @notice Must emit ExecutionFromModuleFailure(address module) if unsuccessful.
    /// @param to Destination address of module transaction.
    /// @param value Ether value of module transaction.
    /// @param data Data payload of module transaction.
    /// @param operation Operation type of module transaction: 0 == call, 1 == delegate call.
    function execTransactionFromModuleReturnData(
        address to,
        uint256 value,
        bytes memory data,
        Enum.Operation operation
    ) external returns (bool success, bytes memory returnData);

    /// @dev Returns if an module is enabled
    /// @return True if the module is enabled
    function isModuleEnabled(address module) external view returns (bool);

    /// @dev Returns array of modules.
    /// @param start Start of the page.
    /// @param pageSize Maximum number of modules that should be returned.
    /// @return array Array of modules.
    /// @return next Start of the next page.
    function getModulesPaginated(address start, uint256 pageSize)
        external
        view
        returns (address[] memory array, address next);
}
```

```solidity
pragma solidity >=0.7.0 <0.9.0;

import "./Enum.sol";

interface IGuard {
    function checkTransaction(
        address to,
        uint256 value,
        bytes memory data,
        Enum.Operation operation,
        uint256 safeTxGas,
        uint256 baseGas,
        uint256 gasPrice,
        address gasToken,
        address payable refundReceiver,
        bytes memory signatures,
        address msgSender
    ) external;

    function checkAfterExecution(bytes32 txHash, bool success) external;
}

```

```solidity
pragma solidity >=0.7.0 <0.9.0;

import "./Enum.sol";
import "./BaseGuard.sol";

/// @title Guardable - A contract that manages fallback calls made to this contract
contract Guardable {
    address public guard;

    event ChangedGuard(address guard);

    /// `guard_` does not implement IERC165.
    error NotIERC165Compliant(address guard_);

    /// @dev Set a guard that checks transactions before execution.
    /// @param _guard The address of the guard to be used or the 0 address to disable the guard.
    function setGuard(address _guard) external {
        if (_guard != address(0)) {
            if (!BaseGuard(_guard).supportsInterface(type(IGuard).interfaceId))
                revert NotIERC165Compliant(_guard);
        }
        guard = _guard;
        emit ChangedGuard(guard);
    }

    function getGuard() external view returns (address _guard) {
        return guard;
    }
}
```

```solidity
pragma solidity >=0.7.0 <0.9.0;

import "./Enum.sol";
import "./IERC165.sol";
import "./IGuard.sol";

abstract contract BaseGuard is IERC165 {
    function supportsInterface(bytes4 interfaceId)
        external
        pure
        override
        returns (bool)
    {
        return
            interfaceId == type(IGuard).interfaceId || // 0xe6d7a83a
            interfaceId == type(IERC165).interfaceId; // 0x01ffc9a7
    }

    /// @dev Module transactions only use the first four parameters: to, value, data, and operation.
    /// Module.sol hardcodes the remaining parameters as 0 since they are not used for module transactions.
    function checkTransaction(
        address to,
        uint256 value,
        bytes memory data,
        Enum.Operation operation,
        uint256 safeTxGas,
        uint256 baseGas,
        uint256 gasPrice,
        address gasToken,
        address payable refundReceiver,
        bytes memory signatures,
        address msgSender
    ) external virtual;

    function checkAfterExecution(bytes32 txHash, bool success) external virtual;
}
```

```solidity
pragma solidity >=0.7.0 <0.9.0;

/// @title Enum - Collection of enums

contract Enum {

    enum Operation {Call, DelegateCall}

}
```

## 理由
本标准中定义的接口旨在与当前使用的大多数流行可编程账户大致兼容，以最小化对现有工具的更改需求。

## 向后兼容性
本标准未引入向后兼容性问题。

## 安全考虑
模块开发者和用户应考虑以下几点：
1. **模块具有绝对控制权：** 模块对其启用的任何化身具有绝对控制权，因此任何模块实现都应被视为安全关键，用户应对启用新模块保持谨慎。仅启用您信任的、能够完全控制化身价值的模块。
2. **竞争条件：** 给定的化身可以启用任意数量的模块，每个模块对安全具有单方面控制。在这种情况下，可能会出现不同模块和/或其他控制机制之间的竞争条件。
3. **不要使您的化身失效：** 没有任何保护措施可以阻止您添加或移除模块。如果您移除所有让您控制化身的模块，化身将停止工作，所有资金将被锁定。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。