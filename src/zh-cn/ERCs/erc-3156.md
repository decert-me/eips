---
eip: 3156
title: 闪电贷款
author: Alberto Cuesta Cañada (@alcueca), Fiona Kobayashi (@fifikobayashi), fubuloubu (@fubuloubu), Austin Williams (@onewayfunction)
discussions-to: https://ethereum-magicians.org/t/erc-3156-flash-loans-review-discussion/5077
status: 最终
type: 标准跟踪
category: ERC
created: 2020-11-15
---

## 简单总结

该 ERC 提供了单一资产闪电贷款的标准接口和流程。

## 摘要

闪电贷款是一种智能合约交易，其中贷方智能合约将资产借给借方智能合约，条件是在交易结束之前归还资产，并支付可选费用。该 ERC 指定了贷方接受闪电贷款请求的接口，以及借方在贷方执行过程中暂时控制交易的接口。还指定了安全执行闪电贷款的流程。

## 动机

闪电贷款允许智能合约在没有抵押品的情况下借出一定数量的代币，条件是在同一交易内归还。

闪电贷款模式的早期采用者产生了不同的接口和不同的使用模式。预计这种多样化将加剧，随之而来的是与多样化闪电贷款模式集成所需的技术债务。

不同协议之间的高层次差异包括：
- 交易结束时的还款方式，有些从贷款接收者那里提取本金加费用，而其他则需要贷款接收者手动将本金和费用归还给贷方。
- 一些贷方提供使用与最初借入的代币不同的代币偿还贷款的能力，这可以减少闪电交易的整体复杂性和 gas 费用。
- 一些贷方提供一个单一的入口，无论是购买、出售、存入还是将它们作为闪电贷款串联在一起，而其他协议则提供离散的入口。
- 一些贷方允许闪电铸造任意数量的其本地代币而不收取费用，从而有效地允许受计算限制的闪电贷款，而不是资产所有权限制。

## 规范

闪电贷款功能通过回调模式集成两个智能合约。这些合约在本 EIP 中称为贷方（LENDER）和接收者（RECEIVER）。

### 贷方规范

`lender` 必须实现 IERC3156FlashLender 接口。
```
pragma solidity ^0.7.0 || ^0.8.0;
import "./IERC3156FlashBorrower.sol";


interface IERC3156FlashLender {

    /**
     * @dev The amount of currency available to be lent.
     * @param token The loan currency.
     * @return The amount of `token` that can be borrowed.
     */
    function maxFlashLoan(
        address token
    ) external view returns (uint256);

    /**
     * @dev The fee to be charged for a given loan.
     * @param token The loan currency.
     * @param amount The amount of tokens lent.
     * @return The amount of `token` to be charged for the loan, on top of the returned principal.
     */
    function flashFee(
        address token,
        uint256 amount
    ) external view returns (uint256);

    /**
     * @dev Initiate a flash loan.
     * @param receiver The receiver of the tokens in the loan, and the receiver of the callback.
     * @param token The loan currency.
     * @param amount The amount of tokens lent.
     * @param data Arbitrary data structure, intended to contain user-defined parameters.
     */
    function flashLoan(
        IERC3156FlashBorrower receiver,
        address token,
        uint256 amount,
        bytes calldata data
    ) external returns (bool);
}
```

`maxFlashLoan` 函数必须返回 `token` 的最大贷款金额。如果当前不支持 `token`，`maxFlashLoan` 必须返回 0，而不是回退。

`flashFee` 函数必须返回贷款 `amount` `token` 的费用。如果不支持该代币，`flashFee` 必须回退。

`flashLoan` 函数必须包含对 `IERC3156FlashBorrower` 合约中 `onFlashLoan` 函数的回调。

```
function flashLoan(
    IERC3156FlashBorrower receiver,
    address token,
    uint256 amount,
    bytes calldata data
) external returns (bool) {
  ...
  require(
      receiver.onFlashLoan(msg.sender, token, amount, fee, data) == keccak256("ERC3156FlashBorrower.onFlashLoan"),
      "IERC3156: Callback failed"
  );
  ...
}
```

`flashLoan` 函数必须在回调到接收者之前将 `amount` 的 `token` 转移给 `receiver`。

`flashLoan` 函数必须将 `msg.sender` 作为 `initiator` 传递给 `onFlashLoan`。

`flashLoan` 函数不得修改接收到的 `token`、`amount` 和 `data` 参数，并必须将它们传递给 `onFlashLoan`。

`flashLoan` 函数必须包含一个 `fee` 参数传递给 `onFlashLoan`，该费用是贷款的费用，加在本金之上，确保 `fee == flashFee(token, amount)`。

`lender` 必须验证 `onFlashLoan` 回调返回 "ERC3156FlashBorrower.onFlashLoan" 的 keccak256 哈希。

在回调之后，`flashLoan` 函数必须从 `receiver` 中提取 `amount + fee` 的 `token`，如果不成功则回退。

如果成功，`flashLoan` 必须返回 `true`。

### 接收者规范

闪电贷款的 `receiver` 必须实现 IERC3156FlashBorrower 接口：

```
pragma solidity ^0.7.0 || ^0.8.0;


interface IERC3156FlashBorrower {

    /**
     * @dev Receive a flash loan.
     * @param initiator The initiator of the loan.
     * @param token The loan currency.
     * @param amount The amount of tokens lent.
     * @param fee The additional amount of tokens to repay.
     * @param data Arbitrary data structure, intended to contain user-defined parameters.
     * @return The keccak256 hash of "ERC3156FlashBorrower.onFlashLoan"
     */
    function onFlashLoan(
        address initiator,
        address token,
        uint256 amount,
        uint256 fee,
        bytes calldata data
    ) external returns (bytes32);
}
```

为了确保交易不回退，`receiver` 必须在 `onFlashLoan` 结束之前批准 `amount + fee` 的 `token` 被 `msg.sender` 提取。

如果成功，`onFlashLoan` 必须返回 "ERC3156FlashBorrower.onFlashLoan" 的 keccak256 哈希。

## 理由

本 ERC 中描述的接口已被选择以覆盖已知的闪电贷款用例，同时允许安全和高效的 gas 实现。

`flashFee` 在不支持的代币上回退，因为返回数值将是不正确的。

`flashLoan` 被选择为函数名称，因为它足够描述性，不太可能与贷方中的其他函数冲突，并且包括了代币被贷方持有或铸造的两种用例。

`receiver` 被作为参数以允许对单独贷款发起者和接收者的实现灵活性。

现有的闪电贷方都提供来自同一合约的多种代币类型的闪电贷款。在 `flashLoan` 和 `onFlashLoan` 函数中提供 `token` 参数与观察到的功能密切匹配。

`bytes calldata data` 参数被包含以便调用者可以向 `receiver` 传递任意信息，而不影响 `flashLoan` 标准的实用性。

`onFlashLoan` 被选择为函数名称，因为它足够描述性，不太可能与 `receiver` 中的其他函数冲突，并且遵循 EIP-667 中使用的 `onAction` 命名模式。

在 `onFlashLoan` 函数中通常需要 `initiator`，贷方知道它是 `msg.sender`。一种替代实现是通过调用者在 `data` 参数中嵌入 `initiator`，这将需要接收者验证其准确性的额外机制，不建议使用。

在 `onFlashLoan` 函数中将需要 `amount`，这是贷方作为参数获取的。另一种替代实现是通过调用者在 `data` 参数中嵌入 `amount`，这将需要接收者验证其准确性的额外机制，不建议使用。

在 `flashLoan` 函数中通常会计算 `fee`，接收者必须了解以便还款。将 `fee` 作为参数传递而不是附加到 `data` 中是简单有效的。

`amount + fee` 从 `receiver` 中提取，以允许贷方实现其他依赖于使用 `transferFrom` 的功能，而不必在闪电贷款期间锁定它们。另一种实现是将还款转移到贷方，但需要贷方中的所有其他功能也基于使用 `transfer` 而不是 `transferFrom`。考虑到 "pull" 架构的复杂性较低且更为普遍，因此选择了 "pull"。

## 向后兼容性

未发现向后兼容性问题。

## 实现

### 闪电借款人参考实现

```
pragma solidity ^0.8.0;

import "./interfaces/IERC20.sol";
import "./interfaces/IERC3156FlashBorrower.sol";
import "./interfaces/IERC3156FlashLender.sol";


contract FlashBorrower is IERC3156FlashBorrower {
    enum Action {NORMAL, OTHER}

    IERC3156FlashLender lender;

    constructor (
        IERC3156FlashLender lender_
    ) {
        lender = lender_;
    }

    /// @dev ERC-3156 Flash loan callback
    function onFlashLoan(
        address initiator,
        address token,
        uint256 amount,
        uint256 fee,
        bytes calldata data
    ) external override returns(bytes32) {
        require(
            msg.sender == address(lender),
            "FlashBorrower: Untrusted lender"
        );
        require(
            initiator == address(this),
            "FlashBorrower: Untrusted loan initiator"
        );
        (Action action) = abi.decode(data, (Action));
        if (action == Action.NORMAL) {
            // do one thing
        } else if (action == Action.OTHER) {
            // do another
        }
        return keccak256("ERC3156FlashBorrower.onFlashLoan");
    }

    /// @dev Initiate a flash loan
    function flashBorrow(
        address token,
        uint256 amount
    ) public {
        bytes memory data = abi.encode(Action.NORMAL);
        uint256 _allowance = IERC20(token).allowance(address(this), address(lender));
        uint256 _fee = lender.flashFee(token, amount);
        uint256 _repayment = amount + _fee;
        IERC20(token).approve(address(lender), _allowance + _repayment);
        lender.flashLoan(this, token, amount, data);
    }
}
```

### 闪电铸造参考实现

```
pragma solidity ^0.8.0;

import "../ERC20.sol";
import "../interfaces/IERC20.sol";
import "../interfaces/IERC3156FlashBorrower.sol";
import "../interfaces/IERC3156FlashLender.sol";


/**
 * @author Alberto Cuesta Cañada
 * @dev Extension of {ERC20} that allows flash minting.
 */
contract FlashMinter is ERC20, IERC3156FlashLender {

    bytes32 public constant CALLBACK_SUCCESS = keccak256("ERC3156FlashBorrower.onFlashLoan");
    uint256 public fee; //  1 == 0.01 %.

    /**
     * @param fee_ The percentage of the loan `amount` that needs to be repaid, in addition to `amount`.
     */
    constructor (
        string memory name,
        string memory symbol,
        uint256 fee_
    ) ERC20(name, symbol) {
        fee = fee_;
    }

    /**
     * @dev The amount of currency available to be lent.
     * @param token The loan currency.
     * @return The amount of `token` that can be borrowed.
     */
    function maxFlashLoan(
        address token
    ) external view override returns (uint256) {
        return type(uint256).max - totalSupply();
    }

    /**
     * @dev The fee to be charged for a given loan.
     * @param token The loan currency. Must match the address of this contract.
     * @param amount The amount of tokens lent.
     * @return The amount of `token` to be charged for the loan, on top of the returned principal.
     */
    function flashFee(
        address token,
        uint256 amount
    ) external view override returns (uint256) {
        require(
            token == address(this),
            "FlashMinter: Unsupported currency"
        );
        return _flashFee(token, amount);
    }

    /**
     * @dev Loan `amount` tokens to `receiver`, and takes it back plus a `flashFee` after the ERC3156 callback.
     * @param receiver The contract receiving the tokens, needs to implement the `onFlashLoan(address user, uint256 amount, uint256 fee, bytes calldata)` interface.
     * @param token The loan currency. Must match the address of this contract.
     * @param amount The amount of tokens lent.
     * @param data A data parameter to be passed on to the `receiver` for any custom use.
     */
    function flashLoan(
        IERC3156FlashBorrower receiver,
        address token,
        uint256 amount,
        bytes calldata data
    ) external override returns (bool){
        require(
            token == address(this),
            "FlashMinter: Unsupported currency"
        );
        uint256 fee = _flashFee(token, amount);
        _mint(address(receiver), amount);
        require(
            receiver.onFlashLoan(msg.sender, token, amount, fee, data) == CALLBACK_SUCCESS,
            "FlashMinter: Callback failed"
        );
        uint256 _allowance = allowance(address(receiver), address(this));
        require(
            _allowance >= (amount + fee),
            "FlashMinter: Repay not approved"
        );
        _approve(address(receiver), address(this), _allowance - (amount + fee));
        _burn(address(receiver), amount + fee);
        return true;
    }

    /**
     * @dev The fee to be charged for a given loan. Internal function with no checks.
     * @param token The loan currency.
     * @param amount The amount of tokens lent.
     * @return The amount of `token` to be charged for the loan, on top of the returned principal.
     */
    function _flashFee(
        address token,
        uint256 amount
    ) internal view returns (uint256) {
        return amount * fee / 10000;
    }
}
```

### 闪电贷款参考实现

```
pragma solidity ^0.8.0;

import "../interfaces/IERC20.sol";
import "../interfaces/IERC3156FlashBorrower.sol";
import "../interfaces/IERC3156FlashLender.sol";


/**
 * @author Alberto Cuesta Cañada
 * @dev Extension of {ERC20} that allows flash lending.
 */
contract FlashLender is IERC3156FlashLender {

    bytes32 public constant CALLBACK_SUCCESS = keccak256("ERC3156FlashBorrower.onFlashLoan");
    mapping(address => bool) public supportedTokens;
    uint256 public fee; //  1 == 0.01 %.


    /**
     * @param supportedTokens_ Token contracts supported for flash lending.
     * @param fee_ The percentage of the loan `amount` that needs to be repaid, in addition to `amount`.
     */
    constructor(
        address[] memory supportedTokens_,
        uint256 fee_
    ) {
        for (uint256 i = 0; i < supportedTokens_.length; i++) {
            supportedTokens[supportedTokens_[i]] = true;
        }
        fee = fee_;
    }

    /**
     * @dev Loan `amount` tokens to `receiver`, and takes it back plus a `flashFee` after the callback.
     * @param receiver The contract receiving the tokens, needs to implement the `onFlashLoan(address user, uint256 amount, uint256 fee, bytes calldata)` interface.
     * @param token The loan currency.
     * @param amount The amount of tokens lent.
     * @param data A data parameter to be passed on to the `receiver` for any custom use.
     */
    function flashLoan(
        IERC3156FlashBorrower receiver,
        address token,
        uint256 amount,
        bytes calldata data
    ) external override returns(bool) {
        require(
            supportedTokens[token],
            "FlashLender: Unsupported currency"
        );
        uint256 fee = _flashFee(token, amount);
        require(
            IERC20(token).transfer(address(receiver), amount),
            "FlashLender: Transfer failed"
        );
        require(
            receiver.onFlashLoan(msg.sender, token, amount, fee, data) == CALLBACK_SUCCESS,
            "FlashLender: Callback failed"
        );
        require(
            IERC20(token).transferFrom(address(receiver), address(this), amount + fee),
            "FlashLender: Repay failed"
        );
        return true;
    }

    /**
     * @dev The fee to be charged for a given loan.
     * @param token The loan currency.
     * @param amount The amount of tokens lent.
     * @return The amount of `token` to be charged for the loan, on top of the returned principal.
     */
    function flashFee(
        address token,
        uint256 amount
    ) external view override returns (uint256) {
        require(
            supportedTokens[token],
            "FlashLender: Unsupported currency"
        );
        return _flashFee(token, amount);
    }

    /**
     * @dev The fee to be charged for a given loan. Internal function with no checks.
     * @param token The loan currency.
     * @param amount The amount of tokens lent.
     * @return The amount of `token` to be charged for the loan, on top of the returned principal.
     */
    function _flashFee(
        address token,
        uint256 amount
    ) internal view returns (uint256) {
        return amount * fee / 10000;
    }

    /**
     * @dev The amount of currency available to be lent.
     * @param token The loan currency.
     * @return The amount of `token` that can be borrowed.
     */
    function maxFlashLoan(
        address token
    ) external view override returns (uint256) {
        return supportedTokens[token] ? IERC20(token).balanceOf(address(this)) : 0;
    }
}

```

## 安全考虑


### 回调参数的验证

`onFlashLoan` 的参数预计反映闪电贷款的条件，但不能无条件信任。它们可以分为两组，需要在信任之前进行不同的检查。

0. 不能假设任何参数是真实的，而不进行某种验证。`initiator`、`token` 和 `amount` 指的是过去的交易，如果 `onFlashLoan` 的调用者决定撒谎，则可能不会发生。`fee` 可能是错误的或计算不正确。`data` 可能已被调用者操纵。
1. 为了信任 `initiator`、`token`、`amount` 和 `fee` 的值是真实的，合理的模式是验证 `onFlashLoan` 的调用者是否在经过验证的闪电贷方的白名单中。由于 `flashLoan` 的调用者通常也会接收 `onFlashLoan` 的回调，因此这将是微不足道的。在所有其他情况下，如果要信任 `onFlashLoan` 中的参数，则需要批准闪电贷方。
2. 为了信任 `data` 的值是真实的，除了第 1 点中的检查外，建议验证 `initiator` 是否属于可信地址组。信任 `lender` 和 `initiator` 足以信任 `data` 的内容是真实的。
### 闪电借贷安全考虑

#### 自动批准
最安全的方法是在执行 `flashLoan` 之前实现对 `amount+fee` 的批准。    

任何对给定 `lender` 保持批准的 `receiver` 需要在 `onFlashLoan` 中包含一个机制，以验证发起者是可信的。

任何在 `onFlashLoan` 中包含对 `lender` 进行 `amount + fee` 批准的 `receiver` 需要结合一个机制，以验证发起者是可信的。

如果一个没有回退函数的合约，或者一个外部拥有账户（EOA），批准了一个实现 ERC3156 的 `lender`，并且没有立即使用该批准，而 `lender` 又没有验证 `onFlashLoan` 的返回值，那么这个毫无防备的合约或 EOA 可能会被提取资金，直到其允许额度或余额限制。这将通过一个 `initiator` 调用受害者的 `flashLoan` 来执行。闪电贷款将被执行并偿还，加上任何费用，这些费用将由 `lender` 累积。因此，重要的是 `lender` 完全实现该规范，并在 `onFlashLoan` 没有返回 "ERC3156FlashBorrower.onFlashLoan" 的 keccak256 哈希时回退。

### 闪电铸造外部安全考虑

闪电铸造交易中涉及的代币数量通常会产生新的创新攻击向量。

#### 示例 1 - 利率攻击
如果存在一个提供稳定利率的借贷协议，但没有底限/上限利率限制，并且没有根据闪电引发的流动性变化重新平衡固定利率，那么它可能会受到以下场景的影响：

FreeLoanAttack.sol
1. 闪电铸造 1 拍 STAB
2. 存入 1 拍 STAB + 150 万美元的 ETH 抵押品
3. 你总存款的数量现在将稳定利率压低到 0.00001% 的稳定利率
4. 在 150 万美元 ETH 抵押品的基础上，以 0.00001% 的稳定利率借入 100 万 STAB
5. 提取并销毁 1 拍 STAB 以关闭原始闪电铸造
6. 你现在有一个几乎是永久免息的 100 万 STAB 贷款（每年利息 0.10 美元）

关键的启示是显而易见的，需要实施一个平坦的底限/上限利率限制，并根据短期流动性变化重新平衡利率。

#### 示例 2 - 算术溢出和下溢
如果闪电铸造提供者对交易中可闪电铸造的代币数量没有任何限制，那么任何人都可以闪电铸造 2^256-1 数量的代币。

接收闪电铸造的协议需要确保它们的合约能够处理这一点，或者通过使用嵌入溢出保护的编译器，或者通过设置显式检查。

### 闪电铸造内部安全考虑
    
闪电铸造与平台上特定业务功能的耦合可能会导致意想不到的后果。

#### 示例 - 财库抽水
假设一个闪电借贷其本地代币的智能合约。该智能合约在用户销毁本地代币时从第三方借款。这个模式将用于在智能合约中将多个用户的抵押债务聚合到第三方的单一账户中。闪电铸造可以用来使借贷方借到其限额，然后推动基础借贷方的利率，清算闪电借贷方：
1. 从 `lender` 闪电铸造大量 FOO。
2. 将 FOO 兑换为 BAR，导致 `lender` 从 `underwriter` 借款直到其借款限额。
3. 触发 `underwriter` 的债务利率上升，使 `lender` 变得不足抵押。
4. 为了获利清算 `lender`。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。