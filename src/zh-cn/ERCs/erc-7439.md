---
eip: 7439
title: 防止票务倒卖
description: 一个供客户通过授权的票务转售商转售其票务的接口。
author: LeadBest Consulting Group <service@getoken.io>, Sandy Sung (@sandy-sung-lb), Mars Peng <mars.peng@getoken.io>, Taien Wang <taien.wang@getoken.io>
discussions-to: https://ethereum-magicians.org/t/prevent-ticket-touting/15269
status: Final
type: Standards Track
category: ERC
created: 2023-07-28
requires: 165, 721
---
## 摘要

该标准是 [ERC-721](./eip-721.md) 的扩展，定义了标准函数，概述了票务代理或活动组织者采取预防措施以阻止观众在票务倒卖市场中被剥削，并允许客户通过授权的票务转售商转售其票务的范围。

## 动机

工业规模的票务倒卖一直是一个长期存在的问题，其相关的欺诈和犯罪问题导致了不幸事件和社会资源的浪费。这对各个职业阶段的艺术家以及相关企业造成了巨大的损害。尽管各国政府已开始立法限制倒卖者的行为，但效果有限。他们仍然在禁止转售的活动中出售门票，或在尚未拥有门票的情况下，通过投机性销售获得了可观的非法利润。我们咨询了许多意见，以提供一个以消费者为友好的转售接口，使买家能够以他们最初支付的价格或更低的价格转售或重新分配门票，这是有效地打击“二级票务”的方式，使票务代理能够利用。

典型的票务可能是“纸质票”或甚至是您电子邮件收件箱中的凭证，这使得伪造或流通变得容易。为了限制这些票务的可转让性，我们设计了一种机制，禁止所有方（包括票务所有者）转让票务，除非是被授权的特定账户。特定账户可以是票务代理、经理、推广者和授权的转售平台。因此，票务倒卖者无法随意转让票务。此外，为了增强功能性，我们为每张票实施了一个代币信息架构，允许只有授权账户（不包括所有者）修改这些记录。

该标准定义了一个框架，使票务代理能够将 [ERC-721](./eip-721.md) 代币用作活动门票，并限制代币的可转让性以防止票务倒卖。通过实施该标准，我们旨在保护客户免受诈骗和欺诈活动的侵害。

## 规范

文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“NOT RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 接口

此处引用的接口和结构如下：

* TokenInfo
    * `signature`: 建议适配器自定义使用用户的私钥或代理的私钥进行签名，以证明代币的有效性。
    * `status`: 表示代币当前状态。
    * `expireTime`: 建议设置为活动截止时间。
* TokenStatus
    * `Sold`: 当代币被出售时，必须更改为 `Sold`。在此状态下，代币是有效的。
    * `Resell`: 当代币处于二级市场时，必须更改为 Resell。在此状态下，代币是有效的。
    * `Void`: 当代币所有者参与非法交易时，代币状态必须设置为 Void，在此状态下，代币是无效的。
    * `Redeemed`: 当代币被使用时，建议将代币状态更改为 `Redeemed`。

```solidity
/// @title IERC7439 Prevent Ticket Touting Interface
interface IERC7439 /* is ERC721 */ {
    /// @dev TokenStatus represent the token current status, only specific role can change status
    enum TokenStatus {
        Sold,    // 0
        Resell,  // 1
        Void,    // 2
        Redeemed // 3
    }

    /// @param signature Data signed by user's private key or agent's private key
    /// @param tokenStatus Token status changing to
    /// @param expireTime Event due time
    struct TokenInfo {
        bytes signature;
        TokenStatus tokenStatus;
        uint256 expireTime;
    }

    /// @notice Used to notify listeners that the token with the specified ID has been changed status
    /// @param tokenId The token has been changed status
    /// @param tokenStatus Token status has been changed to
    /// @param signature Data signed by user's private key or agent's private key
    event TokenStatusChanged(
        uint256 indexed tokenId,
        TokenStatus indexed tokenStatus,
        bytes signature
    );

    /// @notice Used to mint token with token status
    /// @dev MUST emit the `TokenStatusChanged` event if the token status is changed.
    /// @param to The receiptent of token
    /// @param signature Data signed by user's private key or agent's private key
    function safeMint(address to, bytes memory signature) external;

    /// @notice Used to change token status and can only be invoked by a specific role
    /// @dev MUST emit the `TokenStatusChanged` event if the token status is changed.
    /// @param tokenId The token need to change status
    /// @param signature Data signed by user's private key or agent's private key
    /// @param tokenStatus Token status changing to
    /// @param newExpireTime New event due time
    function changeState(
        uint256 tokenId,
        bytes memory signature,
        TokenStatus tokenStatus,
        uint256 newExpireTime
    ) external;
}
```
当调用 `supportsInterface` 方法时，必须返回 `true`，当传入 `0x15fbb306`。

## 理由

在设计该提案时，我们考虑了以下问题：
1. 对于票务代理、表演者和观众来说，最重要的是什么？
   * 对于票务公司来说，售罄所有门票至关重要。有时，为了创造一个活跃的销售环境，票务公司甚至可能与倒卖者合作。这种做法对观众和表演者都是有害的。为了防止这种情况，必须有一个开放和透明的主要销售渠道，以及一个公平的二级销售机制。在 `safeMint` 函数中，这是一个公共函数，我们希望每个人都能以列出的价格透明地铸造门票。那时，`TokenInfo` 添加一个签名，只有买家账户或代理可以根据机制解决，以证明门票的有效性。并且代币 `status` 为 `Sold`。尽管如此，我们还必须考虑票务公司的压力。他们的目标是最大化每张有效门票的效用，即售罄每一张。在传统机制中，票务公司仅从初始销售中获益，这意味着他们无法享受二级销售的额外利润。因此，我们设计了一个可管理的二级销售流程。在 `_beforeTokenTransfer()` 函数中，您可以看到这是一个访问控制函数，只有在 `PARTNER_ROLE` 的 `mint` 或 `burn` 情况下才能转让票务。`PARTNER_ROLE` 可以是票务代理或合法的二级票务销售平台，这可能是国家监管或指定的票务代理平台。为了维持公平的票务市场，我们不能允许他们自行转让票务，因为我们无法区分买家是否为倒卖者。

   * 对于表演者或活动主办方，他们不希望在售票期间看到坏消息。顺利的售票过程或没有可能损害他们表演者声誉的消息是他们所希望的。除此之外，真正重要的是所有真正的粉丝前来。门票落入倒卖者手中或进入混乱的二级市场并不真正吸引真正的粉丝。我们相信表演者不会对这种情况感到满意。通过透明的机制，表演者或活动主办方可以通过代币铸造数量和 `TokenInfo`-`TokenStatus` 的交叉比较，随时控制真实的销售状态。
        ```
        enum TokenStatus {
            Sold,    // 0
            Resell,  // 1
            Void,    // 2
            Redeemed // 3
        }
        ```
   * 对于观众来说，他们唯一需要的就是获得有效的门票。在传统机制中，粉丝面临许多障碍。在热门音乐会上，试图抢购门票的粉丝可能会遇到一些对手，比如倒卖者和票务公司。这些倒卖者就像专业人士一样，组织有序并策略性地抢购门票。令人惊讶的是，票务公司可能实际上会与这些倒卖者合作。或者，他们可能会将一堆免费票或 VIP 门票留给自己。透明的机制对观众同样重要。

2. 如何建立一个健康的票务生态系统？
   * 清晰的票务规则是确保供需保持平衡的关键。

   * 开放的定价系统是确保消费者受到保护的必要条件。

   * 优秀的流动性。在初始市场中，用户可以自己铸造门票。如果需要，购买的门票也可以在透明和开放的二级市场中转让。未在初始销售中购买门票的观众也可以在合法的二级市场中自信地购买门票。`changeState` 函数旨在帮助票务保持良好的流动性。只有 `PARTNER_ROLE` 可以更改票务状态。一旦售出的门票需要在二级市场中出售，就需要请求二级市场帮助其更改为转售状态。更改状态的过程是对二级销售门票的官方验证。这是对二手买家的保护机制。

3. 如何设计一个顺畅的票务流程？
   * 购买/销售简单。观众可以将门票作为铸造 NFT 进行购买。这是一种众所周知的做法。
   
   * 退款简单。当发生极端情况并需要取消演出时，处理门票退款可以是一个简单的过程。
 
   * 兑换简单。在演出前，票务代理可以通过签名验证门票，以确认观众是否真实。`TokenStatus` 需要等于 `sold`，而 `expireTime` 可以区分观众是否到达正确的场次。验证通过后，票务代理可以将 `TokenStatus` 更改为 `Redeemed`。
   
   * 正常流程
        ![Alt text](../assets/eip-7439/normal.png)

   * 作废流程
        ![Alt text](../assets/eip-7439/void.png)

   * 转售流程
        ![Alt text](../assets/eip-7439/resell.png)

## 向后兼容性

该标准与 [ERC-721](./eip-721.md) 兼容。

## 测试用例

```javascript
const { expectRevert } = require("@openzeppelin/test-helpers");
const { expect } = require("chai");
const ERC7439 = artifacts.require("ERC7439");

contract("ERC7439", (accounts) => {
  const [deployer, partner, userA, userB] = accounts;
  const expireTime = 19999999;
  const tokenId = 0;
  const signature = "0x993dab3dd91f5c6dc28e17439be475478f5635c92a56e17e82349d3fb2f166196f466c0b4e0c146f285204f0dcb13e5ae67bc33f4b888ec32dfe0a063e8f3f781b"
  const zeroHash = "0x";

  beforeEach(async () => {
    this.erc7439 = await ERC7439.new({
      from: deployer,
    });
    await this.erc7439.mint(userA, signature, { from: deployer });
  });

  it("Should mint a token", async () => {
    const tokenInfo = await this.erc7439.tokenInfo(tokenId);

    expect(await this.erc7439.ownerOf(tokenId)).to.equal(userA);
    expect(tokenInfo.signature).equal(signature);
    expect(tokenInfo.status).equal("0"); // Sold
    expect(tokenInfo.expireTime).equal(expireTime);
  });

  it("should ordinary users cannot transfer successfully", async () => {
    expectRevert(await this.erc7439.transferFrom(userA, userB, tokenId, { from: userA }), "ERC7439: You cannot transfer this NFT!");
  });

  it("should partner can transfer successfully and chage the token info to resell status", async () => {
    const tokenStatus = 1; // Resell

    await this.erc7439.changeState(tokenId, zeroHash, tokenStatus, { from: partner });
    await this.erc7439.transferFrom(userA, partner, tokenId, { from: partner });

    expect(tokenInfo.tokenHash).equal(zeroHash);
    expect(tokenInfo.status).equal(tokenStatus); // Resell
    expect(await this.erc7439.ownerOf(tokenId)).to.equal(partner);
  });

  it("should partner can change the token status to void", async () => {
    const tokenStatus = 2; // Void

    await this.erc7439.changeState(tokenId, zeroHash, tokenStatus, { from: partner });

    expect(tokenInfo.tokenHash).equal(zeroHash);
    expect(tokenInfo.status).equal(tokenStatus); // Void
  });

  it("should partner can change the token status to redeemed", async () => {
    const tokenStatus = 3; // Redeemed

    await this.erc7439.changeState(tokenId, zeroHash, tokenStatus, { from: partner });

    expect(tokenInfo.tokenHash).equal(zeroHash);
    expect(tokenInfo.status).equal(tokenStatus); // Redeemed
  });

  it("should partner can resell the token and change status from resell to sold", async () => {    
    let tokenStatus = 1; // Resell
    await this.erc7439.changeState(tokenId, zeroHash, tokenStatus, { from: partner });
    await this.erc7439.transferFrom(userA, partner, tokenId, { from: partner });
    
    expect(tokenInfo.status).equal(tokenStatus); // Resell
    expect(tokenInfo.tokenHash).equal(zeroHash);

    tokenStatus = 0; // Sold
    const newSignature = "0x113hqb3ff45f5c6ec28e17439be475478f5635c92a56e17e82349d3fb2f166196f466c0b4e0c146f285204f0dcb13e5ae67bc33f4b888ec32dfe0a063w7h2f742f";
    await this.erc7439.changeState(tokenId, newSignature, tokenStatus, { from: partner });
    await this.erc7439.transferFrom(partner, userB, tokenId, { from: partner });

    expect(tokenInfo.status).equal(tokenStatus); // Sold
    expect(tokenInfo.tokenHash).equal(newSignature);
  });
});
```

## 参考实现

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity 0.8.19;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
// If you need additional metadata, you can import ERC721URIStorage
// import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/AccessControl.sol";
import "@openzeppelin/contracts/utils/Counters.sol";
import "./IERC7439.sol";

contract ERC7439 is ERC721, AccessControl, IERC7439 {
    using Counters for Counters.Counter;

    bytes32 public constant PARTNER_ROLE = keccak256("PARTNER_ROLE");
    Counters.Counter private _tokenIdCounter;

    uint256 public expireTime;

    mapping(uint256 => TokenInfo) public tokenInfo;

    constructor(uint256 _expireTime) ERC721("MyToken", "MTK") {
        _grantRole(DEFAULT_ADMIN_ROLE, msg.sender);
        _grantRole(PARTNER_ROLE, msg.sender);
        expireTime = _expireTime;
    }

    function safeMint(address to, bytes memory signature) public {
        uint256 tokenId = _tokenIdCounter.current();
        _tokenIdCounter.increment();
        _safeMint(to, tokenId);
        tokenInfo[tokenId] = TokenInfo(signature, TokenStatus.Sold, expireTime);
        emit TokenStatusChanged(tokenId, TokenStatus.Sold, signature);
    }

    function changeState(
        uint256 tokenId,
        bytes memory signature,
        TokenStatus tokenStatus,
        uint256 newExpireTime
    ) public onlyRole(PARTNER_ROLE) {
        tokenInfo[tokenId] = TokenInfo(signature, tokenStatus, newExpireTime);
        emit TokenStatusChanged(tokenId, tokenStatus, signature);
    }
    
    function _burn(uint256 tokenId) internal virtual override(ERC721) {
        super._burn(tokenId);

        if (_exists(tokenId)) {
            delete tokenInfo[tokenId];
            // If you import ERC721URIStorage
            // delete _tokenURIs[tokenId];
        }
    }

    function supportsInterface(
        bytes4 interfaceId
    ) public view virtual override(AccessControl, ERC721) returns (bool) {
        return
            interfaceId == type(IERC7439).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override(ERC721) {
        if (!hasRole(PARTNER_ROLE, _msgSender())) {
            require(
                from == address(0) || to == address(0),
                "ERC7439: You cannot transfer this NFT!"
            );
        }

        super._beforeTokenTransfer(from, to, tokenId);
    }
}
```

## 安全考虑

与该标准的实施直接相关的安全考虑没有。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。