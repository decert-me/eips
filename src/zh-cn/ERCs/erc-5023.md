---
eip: 5023
title: 可分享的非同质化代币
description: 一个用于创建可由多个所有者分享的价值持有代币的接口
author: Jarno Marttila (@yaruno), Martin Moravek (@mmartinmo)
discussions-to: https://ethereum-magicians.org/t/new-nft-concept-shareable-nfts/8681
status: Final
type: Standards Track
category: ERC
created: 2022-01-28
requires: 165
---

## 摘要

本 EIP 标准化了可分享的非同质化价值持有代币的接口。通过为新接收者铸造现有代币的副本来实现可分享性。分享及相关事件允许构建一个图，描述谁向哪个方分享了什么。

## 动机

NFT 标准如 [EIP-721](./eip-721.md) 和 [EIP-1155](./eip-1155.md) 已被开发以标准化稀缺的数字资源。然而，许多非同质化数字资源并不需要稀缺。

我们试图在生态系统中捕捉积极的外部性，采用新的激励机制，这些机制表现出反竞争逻辑，作为会计单位并作为分享的媒介。我们设想可分享的代币既可以作为激励，也可以作为通常在其本质上是数字的项目的表现，并随着分享而获得更多价值。

这些要求促使我们定义可分享的 NFT，更具体地说，是一种称为不可转让可分享 NFT 的可分享 NFT。这些可分享的 NFT 可以以数字商品的方式“分享”，几乎没有技术交易成本。我们利用它们来捕捉反竞争价值，以会计积极外部性在经济系统中。

典型的 NFT 标准如 EIP-721 和 EIP-1155 并未定义分享模式。相反，ERC 标准定义了典型竞争用例的接口，如代币铸造和代币交易，NFT 合约实现应满足这些接口。‘标准合约实现’可能扩展这些标准的功能，超出接口的定义。我们在实验中设计和开发的可分享代币旨在接口级别与代币标准兼容。然而，代币合约的实现可能包含扩展功能，以满足实验的要求，如“可分享性”的要求。反思标准代币定义，代币的可分享性可以被视为将现有代币重新铸造给另一方，同时保留其原始版本。

分享是一个有趣的概念，因为它可以以不同的方式被思考和感知。例如，当我们谈论分享时，我们可以将其视为数字复制，给出数字资源的副本，同时保留一个版本。分享也可以是分数的，或者分享可能是关于给予使用某个资源的权利。可分享性的概念及其上下文可以采取不同形式，可能会使用不同类型的实现来实例化可分享代币。因此，我们没有限制接口必须要求任何特定的代币类型。

可分享代币可以在合约实现级别上被设置为不可转让。这样做使它们成为可分享的不可转让代币。在参考实现中，我们从我们的用例中提炼出一个通用案例，定义了使用可分享 NFT 接口的可分享不可转让 NFT。

我们相信，广泛的受众应该从更高抽象层次的可分享性定义中受益，例如这个接口实现，定义了满足可分享性概念所需实现的最少功能。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

```solidity
///  Note: the ERC-165 identifier for this interface is 0xded6338b
interface IERC5023 is IERC165 {

  /// @dev This emits when a token is shared, reminted and given to another wallet that isn't function caller
  event Share(address indexed from, address indexed to, uint256 indexed tokenId, uint256 derivedFromtokenId);

  /// @dev Shares, remints an existing token, gives a newly minted token a fresh token id, keeps original token at function callers possession and transfers newly minted token to receiver which should be another address than function caller. 
  function share(address to, uint256 tokenIdToBeShared) external returns(uint256 newTokenId);

} 
```

当成功调用函数方法 share 并基于给定的代币 ID 铸造并转移新代币给接收者时，预计会发出 Share 事件。

## 理由

当前的 NFT 标准定义了可转让的非同质化代币，但未定义可分享的非同质化代币。为了能够创建可分享的 NFT，我们认为现有的 NFT 合约可以通过定义分享基本原则的接口进行扩展，即分享事件和分享的函数方法。关于代币的可转让性应由合约实现者处理。如果允许转让，可分享代币的行为类似于现有代币，除了在分享时，保留了代币的一个版本。如果禁用转让，可分享代币变为可分享的不可转让代币，可以铸造并给予或分享给其他人，但不能转让。

想象一下，Bob 和 Alice 在一个项目上合作。Bob 获得了一个独特的 NFT，表明他为该项目做出了努力，但 Bob 感到他的成就不仅仅是出于他自己的努力。Bob 想与 Alice 分享他的代币，以表明 Alice 也应当因在他们的项目中付出的努力而获得认可。Bob 通过调用合约上的 `Share` 方法来发起代币分享，指明他希望分享的代币以及分享给谁，传递地址和代币 ID 参数。为 Alice 铸造一个新代币，并发起 `Share` 事件，以记录是 Bob 将他的代币分享给了 Alice，记录分享了哪个代币 ID 到哪个地址。

随着时间的推移，可以从 Share 事件信息中形成树状结构。如果 Bob 分享给了 Alice，而 Alice 进一步分享给了 Charlie，Alice 也分享给了 David，那么从分享活动中形成了一个初步的树结构。这些分享事件数据可以在后续用于获取代币所代表的分享活动的更多信息。

```text
B -> A -> C 
      \
       >  D
```

这些树结构可以进一步聚合并压缩为网络表示，例如基于谁在一段时间内分享给谁的社交图。例如，如果 Bob 将代币分享给 Alice，而 Alice 将不同的代币分享给 Charlie，Bob 也将代币分享给 Charlie，那么通过分享活动在所有这些方之间形成连接。

```text
 B----A----C         
  \_______/
```

## 向后兼容性

该提案与 EIP-721 和 EIP-1155 向后兼容。

## 参考实现

以下参考实现演示了我们试点项目的一个通用用例。在这种情况下，可分享的不可转让代币代表了合约所有者决定以代币形式奖励的对社区的贡献。合约所有者可以铸造一个奖励代币并将其给予某人。接收者可以进一步将该代币分享给其他方，例如分享所获得的奖励给其他参与或影响其贡献的人。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

import "./IERC5023.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";
import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/utils/Context.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "@openzeppelin/contracts/utils/introspection/ERC165.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/IERC721Metadata.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract ShareableERC721 is ERC721URIStorage, Ownable, IERC5023 /* EIP165 */ {

  string baseURI;

  uint256 internal _currentIndex;
    
  constructor(string memory _name, string memory _symbol) ERC721(_name, _symbol) {}

  function mint(
        address account,
        uint256 tokenId
    ) external onlyOwner {
        _mint(account, tokenId);
  }

  function setTokenURI(
        uint256 tokenId, 
        string memory tokenURI
    ) external {
        _setTokenURI(tokenId, tokenURI);
  }

  function setBaseURI(string memory baseURI_) external {
        baseURI = baseURI_;
  }
    
  function _baseURI() internal view override returns (string memory) {
        return baseURI;
  }

  function share(address to, uint256 tokenIdToBeShared) external returns(uint256 newTokenId) {
      require(to != address(0), "ERC721: mint to the zero address");
      require(_exists(tokenIdToBeShared), "ShareableERC721: token to be shared must exist");
      
      require(msg.sender == ownerOf(tokenIdToBeShared), "Method caller must be the owner of token");

      string memory _tokenURI = tokenURI(tokenIdToBeShared);
      _mint(to, _currentIndex);
      _setTokenURI(_currentIndex, _tokenURI);

      emit Share(msg.sender, to, _currentIndex, tokenIdToBeShared);

      return _currentIndex;
  }

  function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        revert('In this reference implementation tokens are not transferrable');
    }

    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        revert('In this reference implementation tokens are not transferrable');
    }
}

```

## 安全考虑

参考实现不应直接用于生产环境。
与该标准的实现直接相关的其他安全考虑没有。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。