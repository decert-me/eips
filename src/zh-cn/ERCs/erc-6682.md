---
eip: 6682
title: NFT 闪电贷款
description: ERC-721 NFT 闪电贷款的最小接口
author: out.eth (@outdoteth)
discussions-to: https://ethereum-magicians.org/t/eip-6682-nft-flashloans/13294
status: Draft
type: Standards Track
category: ERC
created: 2023-03-12
requires: 20, 721, 3156
---

## 摘要

该标准是现有闪电贷款标准 ([ERC-3156](./eip-3156.md)) 的扩展，以支持 [ERC-721](./eip-721.md) NFT 闪电贷款。它提出了一种闪电贷款提供者将 NFT 借给合约的方法，条件是贷款在同一交易中连同一些费用一起偿还。

## 动机

当前的闪电贷款标准 [ERC-3156](./eip-3156.md) 仅支持 [ERC-20](./erc-20.md) 代币。ERC-721 代币与 ERC-20 代币有足够的不同，因此需要扩展现有标准以支持它们。

NFT 闪电贷款在任何检查 NFT 所有权的操作中都可能有用。例如，领取空投、领取质押奖励或进行游戏内操作，如领取耕作资源。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 合约接口

```solidity
pragma solidity ^0.8.19;

interface IERC6682 {
    /// @dev The address of the token used to pay flash loan fees.
    function flashFeeToken() external view returns (address);

    /// @dev Whether or not the NFT is available for a flash loan.
    /// @param token The address of the NFT contract.
    /// @param tokenId The ID of the NFT.
    function availableForFlashLoan(address token, uint256 tokenId) external view returns (bool);
}
```

`flashFeeToken` 函数 MUST 返回用于支付闪电贷款费用的代币地址。

如果用于支付闪电贷款费用的代币是 ETH，则 `flashFeeToken` MUST 返回 `address(0)`。

`availableForFlashLoan` 函数 MUST 返回 `token` 的 `tokenId` 是否可用于闪电贷款。如果 `tokenId` 当前不可用于闪电贷款，`availableForFlashLoan` MUST 返回 `false` 而不是回退。

实现者 `MUST` 还需实现 `IERC3156FlashLender`。

## 理由

上述修改是对现有闪电贷款标准的最简单扩展，以支持 NFT。

我们选择尽可能扩展现有的闪电贷款标准 ([ERC-3156](./eip-3156.md))，而不是创建一个全新的标准，因为闪电贷款标准已经被广泛采用，并且支持 NFT 只需少量更改。

在大多数情况下，费用支付的处理希望以与借出的 NFT 不同的货币支付，因为 NFT 本身并不总是可以分割的。考虑以下示例，闪电贷款提供者对每个闪电贷款的 NFT 收取 0.1 ETH 的费用；接口必须提供方法，允许借款人确定每个 NFT 的费用率以及费用应支付的货币。

## 向后兼容性

该 EIP 完全向后兼容 [ERC-3156](./eip-3156.md)，唯一的例外是 `maxFlashLoan` 方法。该方法在 NFT 的上下文中没有意义，因为 NFT 不是可替代的。然而，它是现有闪电贷款标准的一部分，因此无法在不破坏向后兼容性的情况下将其删除。建议任何实现此 EIP 而不打算支持 ERC-20 闪电贷款的合约始终从 `maxFlashLoan` 返回 `1`。`1` 反映了每次 `flashLoan` 调用只能闪电贷款一个 NFT。例如：

```solidity
function maxFlashLoan(address token) public pure override returns (uint256) {
    // if a contract also supports flash loans for ERC20 tokens then it can
    // return some value here instead of 1
    return 1;
}
```

## 参考实现

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.19;

import "../interfaces/IERC20.sol";
import "../interfaces/IERC721.sol";
import "../interfaces/IERC3156FlashBorrower.sol";
import "../interfaces/IERC3156FlashLender.sol";
import "../interfaces/IERC6682.sol";

contract ExampleFlashLender is IERC6682, IERC3156FlashLender {
    uint256 internal _feePerNFT;
    address internal _flashFeeToken;

    constructor(uint256 feePerNFT_, address flashFeeToken_) {
        _feePerNFT = feePerNFT_;
        _flashFeeToken = flashFeeToken_;
    }

    function flashFeeToken() public view returns (address) {
        return _flashFeeToken;
    }

    function availableForFlashLoan(address token, uint256 tokenId) public view returns (bool) {
        // return if the NFT is owned by this contract
        try IERC721(token).ownerOf(tokenId) returns (address result) {
            return result == address(this);
        } catch {
            return false;
        }
    }

    function flashFee(address token, uint256 tokenId) public view returns (uint256) {
        return _feePerNFT;
    }

    function flashLoan(IERC3156FlashBorrower receiver, address token, uint256 tokenId, bytes calldata data)
        public
        returns (bool)
    {
        // check that the NFT is available for a flash loan
        require(availableForFlashLoan(token, tokenId), "IERC6682: NFT not available for flash loan");

        // transfer the NFT to the borrower
        IERC721(token).safeTransferFrom(address(this), address(receiver), tokenId);

        // calculate the fee
        uint256 fee = flashFee(token, tokenId);

        // call the borrower
        bool success =
            receiver.onFlashLoan(msg.sender, token, tokenId, fee, data) == keccak256("ERC3156FlashBorrower.onFlashLoan");

        // check that flashloan was successful
        require(success, "IERC6682: Flash loan failed");
        
        // check that the NFT was returned by the borrower
        require(IERC721(token).ownerOf(tokenId) == address(this), "IERC6682: NFT not returned by borrower");

        // transfer the fee from the borrower
        IERC20(flashFeeToken()).transferFrom(msg.sender, address(this), fee);

        return success;
    }

    function maxFlashLoan(address token) public pure override returns (uint256) {
        // if a contract also supports flash loans for ERC20 tokens then it can
        // return some value here instead of 1
        return 1;
    }

    function onERC721Received(address, address, uint256, bytes memory) public returns (bytes4) {
        return this.onERC721Received.selector;
    }
}
```

## 安全考虑

`flashFeeToken` 方法可能返回一个恶意合约。打算调用 `flashFeeToken` 方法返回的地址的借款人应注意确保该合约不是恶意的。可以通过验证 `flashFeeToken` 返回的地址是否与用户输入匹配来做到这一点。

需要讨论。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。