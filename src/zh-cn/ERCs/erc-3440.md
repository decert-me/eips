---
eip: 3440
title: ERC-721 版本标准
author: Nathan Ginnever (@nginnever)
discussions-to: https://ethereum-magicians.org/t/eip-3340-nft-editions-standard-extension/6044
status: Stagnant
type: Standards Track
category: ERC
created: 2021-04-20
requires: 712, 721
---

## 简单总结

该标准通过允许在代表艺术作品的 NFT 上进行签名，解决了对 [ERC-721 规范](./eip-721.md) 的扩展。这通过为艺术家指定其作品的原版和签名限量版印刷品提供了改进的来源。

## 摘要

ERC-3440 是一个专门设计用于增强艺术作品 NFT 的 ERC-721 扩展。它通过提供一种能力来指定原版和限量版印刷品，扩展了原始的 ERC-721 规范，具有类似于内置的 [原始 721 扩展](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/extensions/ERC721Enumerable.sol) 的专用枚举扩展。该扩展的关键改进是允许艺术家指定其印刷品的限量性质，并提供一段代表其独特签名的数据，关联到特定的代币 ID，就像艺术家在其作品的印刷品上签名一样。

## 动机
目前，NFT 与数字艺术作品之间的链接仅在存储在 NFT 的共享 `tokenURI` 状态中的代币元数据中得到强制执行。虽然区块链提供了不可变的历史记录，追溯到 NFT 的起源，但艺术家通常不会像维护手写签名那样密切维护起源。

版本是原始艺术作品的印刷复制品。ERC-721 并不是专门为艺术作品（如数字艺术和音乐）设计的。ERC-721（NFT）最初是为了处理契约和其他合同而创建的。最终，ERC-721 发展为游戏代币，其中由服务器托管的元数据可能是足够的。该提案认为，我们可以在 NFT、数字艺术、所有者和艺术家之间创建更有形的链接。通过为艺术制定简明的标准，艺术家将更容易与以太坊区块链及其购买代币的粉丝保持联系。

NFT 的使用案例已经演变为数字艺术作品，并且需要以无信任的方式指定原始 NFT 和带有签名的印刷版。ERC-721 合同可能会或可能不会由艺术家部署，目前，了解某件作品是否被艺术家独特触碰的唯一方法是通过在假设通过存在于区块链外部服务器上的元数据连接的第三方应用程序中展示它。该提案有助于消除这种距离，为艺术家提供签名其作品的现成功能，并为第三方应用程序提供展示 NFT 独特性的标准。限量版的指定结合不可变的签名，创建了一个无信任的强制链接。该签名伴随有视图函数，允许应用程序轻松展示这些签名和限量版印刷品，作为独特性的证据，表明艺术家特意使用其密钥指定总供应量并签署每个 NFT。

## 规范
本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中的描述进行解释。

符合 ERC-721 的合同可以实现此 ERC 以提供一种标准方法，用于指定原版和带有艺术家签名的限量版印刷品。

ERC-3440 的实现必须指定哪个代币 ID 是原始 NFT（默认为 ID 0），哪个代币 ID 是唯一复制品。原始印刷品应为代币 ID 0，但可以分配给其他 ID。原始印刷品必须仅被指定一次。实现必须指定最大铸造版数，之后不得再打印/铸造新 ID。

艺术家可以使用签名功能来签署原版或限量版印刷品，但这是可选的。建议签名的标准消息仅为代币 ID 整数的哈希。

签名消息必须使用 [EIP-712](https://eips.ethereum.org/EIPS/eip-712) 标准。

符合 ERC-3440 的合同应实现以下抽象合同（称为 ERC3440.sol）：

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";

/**
 * @dev ERC721 token with editions extension.
 */
abstract contract ERC3440 is ERC721URIStorage {

    // eip-712
    struct EIP712Domain {
        string  name;
        string  version;
        uint256 chainId;
        address verifyingContract;
    }
    
    // Contents of message to be signed
    struct Signature {
        address verificationAddress; // ensure the artists signs only address(this) for each piece
        string artist;
        address wallet;
        string contents;
    }

    // type hashes
    bytes32 constant EIP712DOMAIN_TYPEHASH = keccak256(
        "EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"
    );

    bytes32 constant SIGNATURE_TYPEHASH = keccak256(
        "Signature(address verifyAddress,string artist,address wallet, string contents)"
    );

    bytes32 public DOMAIN_SEPARATOR;
    
    // Optional mapping for signatures
    mapping (uint256 => bytes) private _signatures;
    
    // A view to display the artist's address
    address public artist;

    // A view to display the total number of prints created
    uint public editionSupply = 0;
    
    // A view to display which ID is the original copy
    uint public originalId = 0;
    
    // A signed token event
    event Signed(address indexed from, uint256 indexed tokenId);

    /**
     * @dev Sets `artist` as the original artist.
     * @param `address _artist` the wallet of the signing artist (TODO consider multiple
     * signers and contract signers (non-EOA)
     */
    function _designateArtist(address _artist) internal virtual {
        require(artist == address(0), "ERC721Extensions: the artist has already been set");

        // If there is no special designation for the artist, set it.
        artist = _artist;
    }
    
    /**
     * @dev Sets `tokenId as the original print` as the tokenURI of `tokenId`.
     * @param `uint256 tokenId` the nft id of the original print
     */
    function _designateOriginal(uint256 _tokenId) internal virtual {
        require(msg.sender == artist, "ERC721Extensions: only the artist may designate originals");
        require(_exists(_tokenId), "ERC721Extensions: Original query for nonexistent token");
        require(originalId == 0, "ERC721Extensions: Original print has already been designated as a different Id");

        // If there is no special designation for the original, set it.
        originalId = _tokenId;
    }
    

    /**
     * @dev Sets total number printed editions of the original as the tokenURI of `tokenId`.
     * @param `uint256 _maxEditionSupply` max supply
     */
    function _setLimitedEditions(uint256 _maxEditionSupply) internal virtual {
        require(msg.sender == artist, "ERC721Extensions: only the artist may designate max supply");
        require(editionSupply == 0, "ERC721Extensions: Max number of prints has already been created");

        // If there is no max supply of prints, set it. Leaving supply at 0 indicates there are no prints of the original
        editionSupply = _maxEditionSupply;
    }

    /**
     * @dev Creates `tokenIds` representing the printed editions.
     * @param `string memory _tokenURI` the metadata attached to each nft
     */
    function _createEditions(string memory _tokenURI) internal virtual {
        require(msg.sender == artist, "ERC721Extensions: only the artist may create prints");
        require(editionSupply > 0, "ERC721Extensions: the edition supply is not set to more than 0");
        for(uint i=0; i < editionSupply; i++) {
            _mint(msg.sender, i);
            _setTokenURI(i, _tokenURI);
        }
    }

    /**
     * @dev internal hashing utility 
     * @param `Signature memory _message` the signature message struct to be signed
     * the address of this contract is enforced in the hashing
     */
    function _hash(Signature memory _message) internal view returns (bytes32) {
        return keccak256(abi.encodePacked(
            "\x19\x01",
            DOMAIN_SEPARATOR,
            keccak256(abi.encode(
                SIGNATURE_TYPEHASH,
                address(this),
                _message.artist,
                _message.wallet,
                _message.contents
            ))
        ));
    }

    /**
     * @dev Signs a `tokenId` representing a print.
     * @param `uint256 _tokenId` id of the NFT being signed
     * @param `Signature memory _message` the signed message
     * @param `bytes memory _signature` signature bytes created off-chain
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Signed} event.
     */
    function _signEdition(uint256 _tokenId, Signature memory _message, bytes memory _signature) internal virtual {
        require(msg.sender == artist, "ERC721Extensions: only the artist may sign their work");
        require(_signatures[_tokenId].length == 0, "ERC721Extensions: this token is already signed");
        bytes32 digest = hash(_message);
        address recovered = ECDSA.recover(digest, _signature);
        require(recovered == artist, "ERC721Extensions: artist signature mismatch");
        _signatures[_tokenId] = _signature;
        emit Signed(artist, _tokenId);
    }

    
    /**
     * @dev displays a signature from the artist.
     * @param `uint256 _tokenId` NFT id to verify isSigned
     * @returns `bytes` gets the signature stored on the token
     */
    function getSignature(uint256 _tokenId) external view virtual returns (bytes memory) {
        require(_signatures[_tokenId].length != 0, "ERC721Extensions: no signature exists for this Id");
        return _signatures[_tokenId];
    }
    
    /**
     * @dev returns `true` if the message is signed by the artist.
     * @param `Signature memory _message` the message signed by an artist and published elsewhere
     * @param `bytes memory _signature` the signature on the message
     * @param `uint _tokenId` id of the token to be verified as being signed
     * @returns `bool` true if signed by artist
     * The artist may broadcast signature out of band that will verify on the nft
     */
    function isSigned(Signature memory _message, bytes memory _signature, uint _tokenId) external view virtual returns (bool) {
        bytes32 messageHash = hash(_message);
        address _artist = ECDSA.recover(messageHash, _signature);
        return (_artist == artist && _equals(_signatures[_tokenId], _signature));
    }

    /**
    * @dev Utility function that checks if two `bytes memory` variables are equal. This is done using hashing,
    * which is much more gas efficient then comparing each byte individually.
    * Equality means that:
    *  - 'self.length == other.length'
    *  - For 'n' in '[0, self.length)', 'self[n] == other[n]'
    */
    function _equals(bytes memory _self, bytes memory _other) internal pure returns (bool equal) {
        if (_self.length != _other.length) {
            return false;
        }
        uint addr;
        uint addr2;
        uint len = _self.length;
        assembly {
            addr := add(_self, /*BYTES_HEADER_SIZE*/32)
            addr2 := add(_other, /*BYTES_HEADER_SIZE*/32)
        }
        assembly {
            equal := eq(keccak256(addr, len), keccak256(addr2, len))
        }
    }
}
```

## 理由

NFT 的一个主要作用是展示数字艺术的独特性。来源是艺术作品所需的特性，该标准将通过提供更好的验证独特性的方法来改善 NFT。艺术家通过明确签署代币采取这一步骤，为艺术家与其在区块链上的作品之间提供了更好的连接。艺术家现在可以保留其私钥，并在未来签署消息，表明同一签名出现在唯一的 NFT 上。

## 向后兼容性

该提案结合了已经可用的 721 扩展，并与 ERC-721 标准向后兼容。

## 测试用例
可以在 [这里](https://github.com/nginnever/NFT-editions) 找到包括测试的示例实现。

## 参考实现
```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./ERC3440.sol";

/**
 * @dev ERC721 token with editions extension.
 */
contract ArtToken is ERC3440 {

    /**
     * @dev Sets `address artist` as the original artist to the account deploying the NFT.
     */
     constructor (
        string memory _name, 
        string memory _symbol,
        uint _numberOfEditions,
        string memory tokenURI,
        uint _originalId
    ) ERC721(_name, _symbol) {
        _designateArtist(msg.sender);
        _setLimitedEditions(_numberOfEditions);
        _createEditions(tokenURI);
        _designateOriginal(_originalId);

        DOMAIN_SEPARATOR = keccak256(abi.encode(
            EIP712DOMAIN_TYPEHASH,
            keccak256(bytes("Artist's Editions")),
            keccak256(bytes("1")),
            1,
            address(this)
        ));
    }
    
    /**
     * @dev Signs a `tokenId` representing a print.
     */
    function sign(uint256 _tokenId, Signature memory _message, bytes memory _signature) public {
        _signEdition(_tokenId, _message, _signature);
    }
}

```

## 安全考虑
该扩展使艺术家能够指定原版，设置版数的最大供应量以及打印版，并使用 `tokenURI` 扩展提供艺术作品的链接。为了最小化艺术家在出售原始作品后更改此值的风险，此功能只能执行一次。确保这些功能只能执行一次提供了独特性和可验证性的一致性。因此，参考实现将在构造函数中处理这些功能。一个版本只能签署一次，并且在发布代币之前应确保该版本正确签署。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。