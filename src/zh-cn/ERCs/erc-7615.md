---
eip: 7615
title: 基于原子推送的数据馈送机制
description: 一种原子机制，允许发布者合约将数据推送到订阅者合约
author: Elaine Zhang (@lanyinzly) <lz8aj@virginia.edu>, Jerry <jerrymindflow@gmail.com>, Amandafanny <amandafanny200@gmail.com>, Shouhao Wong (@wangshouh) <wongshouhao@outlook.com>, Doris Che (@Cheyukj) <dorischeyy@gmail.com>, Henry Yuan (@onehumanbeing) <hy2878@nyu.edu>
discussions-to: https://ethereum-magicians.org/t/erc-7615-smart-contract-data-push-mechanism/18466
status: 草稿
type: 标准跟踪
category: ERC
created: 2024-02-03
---
## 摘要
本 ERC 提出了一个基于推送的数据发送机制，允许发布者合约在调用期间自动将某些数据推送到订阅者合约。具体实现依赖于两个接口：一个用于发布者合约推送数据，另一个用于订阅者合约接收数据。当调用发布者合约时，它会检查被调用的函数是否对应于订阅者地址。如果是，发布者合约将数据推送到订阅者合约。

## 动机
目前，许多守护程序依赖于链外数据或单独的数据收集过程来监控链上的事件。该提案旨在建立一个系统，使发布者合约能够原子性地推送数据，以通知订阅者合约有关更新。发布者与订阅者之间的直接链上交互使系统更加去信任和高效。

该提案将在多个应用中提供显著优势，例如促进 DeFi 的无限和无权限扩展，以及增强 DAO 治理等。

### 借贷协议

发布者合约的一个示例可以是一个预言机，它可以通过发起对订阅者协议的调用自动推送价格更新。作为订阅者的借贷协议可以根据接收到的价格自动清算借贷头寸。

### 自动支付

服务提供商可以使用智能合约作为发布者合约，因此当用户调用此合约时，它可以将信息推送到订阅者合约，例如，用户的钱包，如遵循 [ERC-6551](./eip-6551.md) 的 NFT 绑定账户或其他智能合约钱包。用户的智能合约钱包可以因此自动执行相应的支付操作。与传统的 `approve` 方式相比，该解决方案允许在实现中使用更复杂的逻辑，例如限额支付等。

### 无需转移资产的 PoS

对于某些质押场景，特别是 NFT 质押，PoS 合约可以设置为订阅者，而 NFT 合约可以设置为发布者。通过合约交互可以实现质押，使用户在不转移资产的情况下赚取质押奖励。

当发生 NFT 的 `transfer` 操作时，NFT 合约可以将此信息推送到 PoS 合约，后者可以执行解除质押或其他功能。

### DAO 投票

作为发布者的 DAO 治理合约可以在投票完成后自动触发推送机制，调用相关的订阅者合约以直接实施投票结果，例如向某个账户或池注入资金。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

### 概述

推送机制可以分为以下四个步骤：

1. 调用发布者合约。
2. 发布者合约从被调用的函数的 `selector` 查询订阅者列表。订阅者合约可以将选定的数据放入 `inbox`。
3. 发布者合约通过调用订阅者合约的 `exec` 函数推送 `selector` 和数据。
4. 订阅者合约根据推送的 `selector` 和数据执行，或者根据需要请求发布者合约的 inbox 函数的信息。

在第二步中，被调用的函数与相应订阅者之间的关系可以在发布者合约中进行配置。提出了两种配置方案：

1. 无条件推送：对配置的 `selector` 的任何调用都会触发推送
2. 有条件推送：只有对配置的 `selector` 的有条件调用才会根据配置触发推送。

允许为单个 `selector` 配置多个不同类型的订阅者合约。发布者合约将调用每个订阅者合约的 `exec` 函数以推送请求。

在从 `selector` 取消订阅合约时，发布者合约必须检查订阅者合约的 `isLocked` 函数是否返回 `true`。

发布者合约可以选择使用 `inbox` 机制来存储数据。

在第四步中，订阅者合约应在 `exec` 函数的实现中处理所有可能的 `selector` 请求和数据。在某些情况下，`exec` 可能会调用发布者合约的 `inbox` 函数以获取完整的推送数据。

![工作流程](../assets/eip-7615/ERC7615.svg)

### 合约接口

如上所述，有无条件推送和有条件推送两种类型的实现。要实现无条件推送，发布者合约应实现以下接口：

```
interface IPushForce {
    event ForceApprove(bytes4 indexed selector, address indexed target);
    event ForceCancel(bytes4 indexed selector, address indexed target);
    event RenounceForceApprove();
    event RenounceForceCancel();

    error MustRenounce();
    error ForceApproveRenounced();
    error ForceCancelRenounced();

    function isForceApproved(bytes4 selector, address target) external returns (bool);
    function forceApprove(bytes4 selector, address target) external;
    function forceCancel(bytes4 selector, address target) external;
    function isRenounceForceApprove() external returns (bool);
    function isRenounceForceCancel() external returns (bool);
    function renounceForceApprove(bytes memory) external;
    function renounceForceCancel(bytes memory) external;
}
```

`isForceApproved` 用于查询 `selector` 是否已无条件绑定到地址为 `target` 的订阅者合约。 
`forceApprove` 用于将 `selector` 绑定到订阅者合约 `target`。 `forceCancel` 用于取消 `selector` 和 `target` 之间的绑定关系，其中 `target` 的 `isLocked` 函数返回 `true` 是必需的。

`renounceForceApprove` 用于放弃 `forceApprove` 权限。调用 `renounceForceApprove` 函数后，无法再调用 `forceApprove`。类似地，`renounceForceCancel` 用于放弃 `forceCancel` 权限。调用 `renounceForceCancel` 函数后，无法再调用 `forceCancel`。

要实现有条件推送，发布者合约应实现以下接口：

```
interface IPushFree {
    event Approve(bytes4 indexed selector, address indexed target, bytes data);
    event Cancel(bytes4 indexed selector, address indexed target, bytes data);

    function inbox(bytes4 selector) external returns (bytes memory);
    function isApproved(bytes4 selector, address target, bytes calldata data) external returns (bool);
    function approve(bytes4 selector, address target, bytes calldata data) external;
    function cancel(bytes4 selector, address target, bytes calldata data) external;
}
```

`isApproved`、`approve` 和 `cancel` 的功能类似于 `IPushForce` 中的相应函数。然而，这里引入了一个额外的 `data` 参数，用于检查是否需要推送。 
此处的 `inbox` 用于存储数据，以防从订阅者合约调用。

发布者合约应实现 `_push(bytes4 selector, bytes calldata data)` 函数，该函数充当钩子。发布者合约中需要实现推送机制的任何函数都必须调用此内部函数。该函数必须根据 `selector` 和 `data` 查询无条件和有条件的订阅合约，然后调用相应的订阅者的 `exec` 函数。

订阅者需要实现以下接口：

```solidity
interface IExec {
    function isLocked(bytes4 selector, bytes calldata data) external returns (bool);
    function exec(bytes4 selector, bytes calldata data) external;
}
```

`exec` 用于接收来自发布者合约的请求并进一步执行。 
`isLocked` 用于检查订阅者合约是否可以根据 `selector` 和 `data` 取消订阅发布者合约的状态。当收到取消订阅请求时触发。
## 理由

### 无条件和有条件配置

当发送合约被调用时，可以触发推送，要求调用者支付产生的 gas 费用。
在某些情况下，无条件推送是必要的，例如将价格变动推送到借贷协议。而有条件推送将减少不必要的 gas 消耗。

### 在取消订阅前检查 `isLocked`

在调用 `forceCancel` 或 `cancel` 之前，发布者合约必须调用订阅者合约的 `isLocked` 函数，以避免单方面取消订阅。订阅者合约可能对发布者合约有重要的逻辑依赖，因此取消订阅可能会导致订阅者合约内出现严重问题。因此，订阅者合约应在充分考虑的情况下实现 `isLocked` 函数。

### `inbox` 机制

在某些场景中，发布者合约可能仅通过 `selector` 向订阅者合约推送必要的数据，而完整数据可能存储在 `inbox` 中。在接收到来自发布者合约的推送后，订阅者合约可以选择调用 `inbox`。
`inbox` 机制简化了推送信息，同时仍确保完整数据的可用性，从而减少 gas 消耗。

### 使用函数选择器作为参数

使用函数选择器来检索订阅者合约的地址允许更详细的配置。
对于订阅者合约，基于推送信息具有请求源的特定函数可以更准确地处理推送信息。

### 放弃安全性增强

`forceApprove` 和 `forceCancel` 权限可以通过各自的放弃函数放弃。当同时调用 `renounceForceApprove` 和 `renounceForceCancel` 时，注册的推送目标将无法更改，从而大大增强安全性。

## 参考实现

```
pragma solidity ^0.8.24;

import {EnumerableSet} from "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";
import {IPushFree, IPushForce} from "./interfaces/IPush.sol";
import {IExec} from "./interfaces/IExec.sol";

contract Foo is IPushFree, IPushForce {
    using EnumerableSet for EnumerableSet.AddressSet;

    bool public override isRenounceForceApprove;
    bool public override isRenounceForceCancel;

    mapping(bytes4 selector => mapping(uint256 tokenId => EnumerableSet.AddressSet targets)) private _registry;
    mapping(bytes4 selector => EnumerableSet.AddressSet targets) private _registryOfAll;
    // mapping(bytes4 => bytes) public inbox;

    modifier notLock(bytes4 selector, address target, bytes memory data) {
        require(!IExec(target).isLocked(selector, data), "Foo: lock");
        _;
    }

    function inbox(bytes4 selector) public view returns (bytes memory data) {
        uint256 loadData;
        assembly {
            loadData := tload(selector)
        }

        data = abi.encode(loadData);
    }

    function isApproved(bytes4 selector, address target, bytes calldata data) external view override returns (bool) {
        uint256 tokenId = abi.decode(data, (uint256));
        return _registry[selector][tokenId].contains(target);
    }

    function isForceApproved(bytes4 selector, address target) external view override returns (bool) {
        return _registryOfAll[selector].contains(target);
    }

    function approve(bytes4 selector, address target, bytes calldata data) external override {
        uint256 tokenId = abi.decode(data, (uint256));
        _registry[selector][tokenId].add(target);
    }

    function cancel(bytes4 selector, address target, bytes calldata data)
        external
        override
        notLock(selector, target, data)
    {
        uint256 tokenId = abi.decode(data, (uint256));
        _registry[selector][tokenId].remove(target);
    }

    function forceApprove(bytes4 selector, address target) external override {
        if (isRenounceForceApprove) revert ForceApproveRenounced();
        _registryOfAll[selector].add(target);
    }

    function forceCancel(bytes4 selector, address target) external override notLock(selector, target, "") {
        if (isRenounceForceCancel) revert ForceCancelRenounced();
        _registryOfAll[selector].remove(target);
    }

    function renounceForceApprove(bytes memory data) external override {
        (bool burn) = abi.decode(data, (bool));
        if (burn != true) {
            revert MustRenounce();
        }

        isRenounceForceApprove = true;
        emit RenounceForceApprove();
    }

    function renounceForceCancel(bytes memory data) external override {
        (bool burn) = abi.decode(data, (bool));
        if (burn != true) {
            revert MustRenounce();
        }

        isRenounceForceCancel = true;
        emit RenounceForceCancel();
    }

    function send(uint256 message) external {
        _push(this.send.selector, message);
    }

    function _push(bytes4 selector, uint256 message) internal {
        assembly {
            tstore(selector, message)
        }

        address[] memory targets = _registry[selector][message].values();
        for (uint256 i = 0; i < targets.length; i++) {
            IExec(targets[i]).exec(selector, abi.encode(message));
        }

        targets = _registryOfAll[selector].values();
        for (uint256 i = 0; i < targets.length; i++) {
            IExec(targets[i]).exec(selector, abi.encode(message));
        }
    }
}

contract Bar is IExec {
    event Log(bytes4 indexed selector, bytes data, bytes inboxData);

    function isLocked(bytes4, bytes calldata) external pure override returns (bool) {
        return true;
    }

    function exec(bytes4 selector, bytes calldata data) external {
        bytes memory inboxData = IPushFree(msg.sender).inbox(selector);

        emit Log(selector, data, inboxData);
    }
}
```

## 安全考虑

### `exec` 攻击

`exec` 函数是 `public` 的，因此容易受到恶意调用的攻击，任意推送信息可能被插入。`exec` 的实现应仔细考虑调用的任意性，并且在未验证的情况下不应直接使用通过 exec 函数传递的数据。

### 重入攻击

发布者合约对订阅者合约的 `exec` 函数的调用可能导致重入攻击。恶意的订阅合约可能在 `exec` 中构造对发布者合约的重入攻击。

### 任意目标批准

`forceApprove` 和 `approve` 的实现应具有合理的访问控制；否则，可能会对调用者造成不必要的 gas 损失。

检查 `exec` 函数的 gas 使用情况。

### `isLocked` 实现

订阅者合约应实现 `isLocked` 函数，以避免因取消订阅而带来的潜在损失。这对于实施该提案的借贷协议尤为重要。不当的取消订阅可能导致异常清算，造成重大损失。

同样，在订阅时，发布者合约应考虑 `isLocked` 是否正确实现，以防止不可撤销的订阅。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。