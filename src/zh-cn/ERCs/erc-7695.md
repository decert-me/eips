---
eip: 7695
title: ERC-721 的所有权委托和上下文
description: 引入 ERC-721 代币的上下文和所有权委托，扩展 dApp 和金融用例而无需转移所有权
author: Duc Tho Tran (@ducthotran2010)
discussions-to: https://ethereum-magicians.org/t/erc-7695-ownership-delegation-and-context-for-non-fungible-token/19716
status: Draft
type: Standards Track
category: ERC
created: 2024-04-02
requires: 165, 721
---

## 摘要

本标准是对 [ERC-721](./eip-721.md) 的扩展，旨在为各种上下文指定用户，并提供锁定功能，允许临时所有权委托而不改变原始所有者。

该 EIP 保留了所有者的权益和权利，同时通过添加所有权委托和上下文的概念，扩展了 NFT 在各种 dApp 中的实用性，这些概念定义了特定角色：控制者和用户，他们可以在定义的上下文中使用 NFT。

## 动机

对于标准的 [ERC-721](./eip-721.md) NFT，金融应用中有几种用例，包括：

- 抵押 NFT 以赚取奖励。
- 抵押 NFT 以产生收入。
- 授权用户进行不同目的的使用，如租赁和代币委托——在这种情况下，某人支付使用代币的费用，并支付另一方使用这些代币。

传统上，这些应用需要所有权转移以将 NFT 锁定在合约中。然而，其他去中心化应用（dApp）将代币所有权视为代币所有者在其奖励系统中有权获得利益的证明，例如空投或分级奖励。如果代币所有者的代币被锁定在合约中，他们将无法获得持有这些代币的利益，或者奖励系统必须支持尽可能多的合约以帮助这些所有者。

这是因为只有一个所有者角色表示所有权利，基于 [ERC-721](./eip-721.md) 的开发往往面临挑战。该提案旨在通过将用例上下文化以由控制者处理，并通过所有权委托机制在标准层面区分所有权利与其他角色，来解决这些挑战。通过标准化这些措施，dApp 开发者可以更轻松地在此标准之上构建基础设施和协议。

## 规范

本文档中的关键词“必须”、“必须不”、“要求”、“应”、“应不”、“应该”、“应该不”、“推荐”、“可以”和“可选”应按 RFC 2119 中的描述进行解释。

### 定义

本规范包含以下组件：

**代币上下文** 提供代币的指定用例。它作为代币与上下文之间的关联关系。在每个唯一的代币上下文中，存在一个被授权在该上下文中使用代币的用户。在指定的上下文中，有两个不同的角色：

- **控制者**：该角色拥有控制上下文的权力。
- **用户**：该角色表示在给定上下文中主要的代币用户。

**代币的所有权利** 被定义为能够：

- 将该代币转让给新所有者。
- 添加代币上下文：将该代币附加到一个或多个上下文中。
- 移除代币上下文：将该代币从一个或多个上下文中分离。

**所有权委托** 涉及通过将所有权委托给其他账户在特定时间段内区分所有者和所有权利。在此期间，所有者暂时放弃所有权利，直到委托到期。

### 角色

| 角色               | 说明 / 权限                                                                                                                                    | 每个代币数量 |
| ------------------- | ------------------------------------------------------------------------------------------------------------------------------------------------ | -------------- |
| 所有者               | • 默认拥有 **所有权利**<br>• 委托一个账户在一段时间内持有 **所有权利**                                              | $1$            |
| 所有权委托人 | • 在委托期间拥有 **所有权利**<br>• 在委托到期前放弃权利                                                            | $1$            |
| 所有权管理者   | • 持有 **所有权利**<br>• 如果尚未委托，则引用 **所有者**，否则引用 **所有权委托人** | $1$            |
| **上下文角色**   |                                                                                                                                                 | $n$            |
| 控制者          | • 转移控制者<br>• 设置上下文用户<br>• (解)锁代币转移                                                                             | $1$ 每个上下文    |
| 用户                | • 被授权在其上下文中使用代币                                                                                                                | $1$ 每个上下文    |

### 接口

**实现本标准的智能合约必须实现 `IERC7695` 接口中的所有函数。**

实现本标准的智能合约必须实现 [ERC-165](./eip-165.md) 的 `supportsInterface` 函数，并且如果通过 `interfaceID` 参数传递 `0x486b6fba`，则必须返回常量值 `true`。

```solidity
/// Note: the ERC-165 identifier for this interface is 0x486b6fba.
interface IERC7695 /* is IERC721, IERC165 */ {
  /// @dev This emits when a context is updated by any mechanism.
  event ContextUpdated(bytes32 indexed ctxHash, address indexed controller, uint64 detachingDuration);
  /// @dev This emits when a token is attached to a certain context by any mechanism.
  event ContextAttached(bytes32 indexed ctxHash, uint256 indexed tokenId);
  /// @dev This emits when a token is requested to detach from a certain context by any mechanism.
  event ContextDetachmentRequested(bytes32 indexed ctxHash, uint256 indexed tokenId);
  /// @dev This emits when a token is detached from a certain context by any mechanism.
  event ContextDetached(bytes32 indexed ctxHash, uint256 indexed tokenId);
  /// @dev This emits when a user is assigned to a certain context by any mechanism.
  event ContextUserAssigned(bytes32 indexed ctxHash, uint256 indexed tokenId, address indexed user);
  /// @dev This emits when a token is (un)locked in a certain context by any mechanism.
  event ContextLockUpdated(bytes32 indexed ctxHash, uint256 indexed tokenId, bool locked);
  /// @dev This emits when the ownership delegation is started by any mechanism.
  event OwnershipDelegationStarted(uint256 indexed tokenId, address indexed delegatee, uint64 until);
  /// @dev This emits when the ownership delegation is accepted by any mechanism.
  event OwnershipDelegationAccepted(uint256 indexed tokenId, address indexed delegatee, uint64 until);
  /// @dev This emits when the ownership delegation is stopped by any mechanism.
  event OwnershipDelegationStopped(uint256 indexed tokenId, address indexed delegatee);

  /// @notice Gets the longest duration the detaching can happen.
  function maxDetachingDuration() external view returns (uint64);

  /// @notice Gets controller address and detachment duration of a context.
  /// @dev MUST revert if the context is not existent.
  /// @param ctxHash            A hash of context to query the controller.
  /// @return controller        The address of the context controller.
  /// @return detachingDuration The duration must be waited for detachment in second(s).
  function getContext(bytes32 ctxHash) external view returns (address controller, uint64 detachingDuration);

  /// @notice Creates a new context.
  /// @dev MUST revert if the context is already existent.
  /// MUST revert if the controller address is zero address.
  /// MUST revert if the detaching duration is larger than max detaching duration.
  /// MUST emit the event {ContextUpdated} to reflect context created and controller set.
  /// @param controller        The address that controls the created context.
  /// @param detachingDuration The duration must be waited for detachment in second(s).
  /// @param ctxMsg            The message of new context to be used for hashing.
  /// @return ctxHash          Hash of the created context.
  function createContext(address controller, uint64 detachingDuration, bytes calldata ctxMsg)
    external
    returns (bytes32 ctxHash);

  /// @notice Updates an existing context.
  /// @dev MUST revert if method caller is not the current controller.
  /// MUST revert if the context is non-existent.
  /// MUST revert if the new controller address is zero address.
  /// MUST revert if the detaching duration is larger than max detaching duration.
  /// MUST emit the event {ContextUpdated} on success.
  /// @param ctxHash              Hash of the context to set.
  /// @param newController        The address of new controller.
  /// @param newDetachingDuration The new duration must be waited for detachment in second(s).
  function updateContext(bytes32 ctxHash, address newController, uint64 newDetachingDuration) external;

  /// @notice Queries if a token is attached to a certain context.
  /// @param ctxHash Hash of a context.
  /// @param tokenId The NFT to query.
  /// @return        True if the token is attached to the context, false if not.
  function isAttachedWithContext(bytes32 ctxHash, uint256 tokenId) external view returns (bool);

  /// @notice Attaches a token with a certain context.
  /// @dev See "attachContext rules" in "Token (Un)lock Rules".
  /// @param ctxHash Hash of a context.
  /// @param tokenId The NFT to be attached.
  /// @param data    Additional data with no specified format, MUST be sent unaltered in call to the {IERC7695ContextCallback} hook(s) on controller.
  function attachContext(bytes32 ctxHash, uint256 tokenId, bytes calldata data) external;

  /// @notice Requests to detach a token from a certain context.
  /// @dev See "requestDetachContext rules" in "Token (Un)lock Rules".
  /// @param ctxHash Hash of a context.
  /// @param tokenId The NFT to be detached.
  /// @param data    Additional data with no specified format, MUST be sent unaltered in call to the {IERC7695ContextCallback} hook(s) on controller.
  function requestDetachContext(bytes32 ctxHash, uint256 tokenId, bytes calldata data) external;

  /// @notice Executes context detachment.
  /// @dev See "execDetachContext rules" in "Token (Un)lock Rules".
  /// @param ctxHash Hash of a context.
  /// @param tokenId The NFT to be detached.
  /// @param data    Additional data with no specified format, MUST be sent unaltered in call to the {IERC7695ContextCallback} hook(s) on controller.
  function execDetachContext(bytes32 ctxHash, uint256 tokenId, bytes calldata data) external;

  /// @notice Finds the context user of a token.
  /// @param ctxHash Hash of a context.
  /// @param tokenId The NFT to be detached.
  /// @return user   Address of the context user.
  function getContextUser(bytes32 ctxHash, uint256 tokenId) external view returns (address user);

  /// @notice Updates the context user of a token.
  /// @dev MUST revert if the method caller is not context controller.
  /// MUST revert if the context is non-existent.
  /// MUST revert if the token is not attached to the context.
  /// MUST emit the event {ContextUserAssigned} on success.
  /// @param ctxHash Hash of a context.
  /// @param tokenId The NFT to be update.
  /// @param user    Address of the new user.
  function setContextUser(bytes32 ctxHash, uint256 tokenId, address user) external;

  /// @notice Queries if the lock a token is locked in a certain context.
  /// @param ctxHash Hash of a context.
  /// @param tokenId The NFT to be queried.
  /// @return        True if the token context is locked, false if not.
  function isTokenContextLocked(bytes32 ctxHash, uint256 tokenId) external view returns (bool);

  /// @notice (Un)locks a token in a certain context.
  /// @dev See "setContextLock rules" in "Token (Un)lock Rules".
  /// @param ctxHash Hash of a context.
  /// @param tokenId The NFT to be queried.
  /// @param lock    New status to be (un)locked.
  function setContextLock(bytes32 ctxHash, uint256 tokenId, bool lock) external;

  /// @notice Finds the ownership manager of a specified token.
  /// @param tokenId  The NFT to be queried.
  /// @return manager Address of delegatee.
  function getOwnershipManager(uint256 tokenId) external view returns(address manager);

  /// @notice Finds the ownership delegatee of a token.
  /// @dev MUST revert if there is no (or an expired) ownership delegation.
  /// @param tokenId    The NFT to be queried.
  /// @return delegatee Address of delegatee.
  /// @return until     The delegation expiry time.
  function getOwnershipDelegatee(uint256 tokenId) external view returns (address delegatee, uint64 until);

  /// @notice Finds the pending ownership delegatee of a token.
  /// @dev MUST revert if there is no (or an expired) pending ownership delegation.
  /// @param tokenId    The NFT to be queried.
  /// @return delegatee Address of pending delegatee.
  /// @return until     The delegation expiry time in the future.
  function pendingOwnershipDelegatee(uint256 tokenId) external view returns (address delegatee, uint64 until);

  /// @notice Starts ownership delegation and retains ownership until a specific timestamp.
  /// @dev Replaces the pending delegation if any.
  /// See "startDelegateOwnership rules" in "Ownership Delegation Rules".
  /// @param tokenId   The NFT to be delegated.
  /// @param delegatee Address of new delegatee.
  /// @param until     The delegation expiry time.
  function startDelegateOwnership(uint256 tokenId, address delegatee, uint64 until) external;

  /// @notice Accepts ownership delegation request.
  /// @dev See "acceptOwnershipDelegation rules" in "Ownership Delegation Rules".
  /// @param tokenId The NFT to be accepted.
  function acceptOwnershipDelegation(uint256 tokenId) external;

  /// @notice Stops the current ownership delegation.
  /// @dev See "stopOwnershipDelegation rules" in "Ownership Delegation Rules".
  /// @param tokenId The NFT to be stopped.
  function stopOwnershipDelegation(uint256 tokenId) external;
}
```

**可枚举扩展**

枚举扩展是本标准的可选项。这允许您的合约发布其完整的上下文列表并使其可发现。当调用 `supportsInterface` 函数时，如果通过 `interfaceID` 参数传递 `0xcebf44b7`，则必须返回常量值 `true`。

```solidity
/// Note: the ERC-165 identifier for this interface is 0xcebf44b7.
interface IERC7695Enumerable /* is IERC165 */ {
  /// @dev Returns a created context in this contract at `index`.
  /// An index must be a value between 0 and {getContextCount}, non-inclusive.
  /// Note: When using {getContext} and {getContextCount}, make sure you perform all queries on the same block.
  function getContext(uint256 index) external view returns(bytes32 ctxHash);

  /// @dev Returns the number of contexts created in the contract.
  function getContextCount() external view returns(uint256);

  /// @dev Returns a context attached to a token at `index`.
  /// An index must be a value between 0 and {getAttachedContextCount}, non-inclusive.
  /// Note: When using {getAttachedContext} and {getAttachedContextCount}, make sure you perform all queries on the same block.
  function getAttachedContext(uint256 tokenId, uint256 index) external view returns(bytes32 ctxHash);

  /// @dev Returns the number of contexts attached to the token.
  function getAttachedContextCount(uint256 tokenId) external view returns(uint256);
}
```

**控制者接口**

建议控制者是一个合约，并包括回调方法，以便在有任何附加或分离请求时允许回调。当调用 `supportsInterface` 函数时，如果通过 `interfaceID` 参数传递 `0xad0491f1`，则必须返回常量值 `true`。

```solidity
/// Note: the ERC-165 identifier for this interface is 0xad0491f1.
interface IERC7695ContextCallback /* is IERC165 */  {
  /// @dev This method is called once the token is attached by any mechanism.
  /// This function MAY throw to revert and reject the attachment.
  /// @param ctxHash  The hash of context invoked this call.
  /// @param tokenId  NFT identifier which is being attached.
  /// @param operator The address which called {attachContext} function.
  /// @param data     Additional data with no specified format.
  function onAttached(bytes32 ctxHash, uint256 tokenId, address operator, bytes calldata data) external;

  /// @dev This method is called once the token detachment is requested by any mechanism.
  /// @param ctxHash  The hash of context invoked this call.
  /// @param tokenId  NFT identifier which is being requested for detachment.
  /// @param operator The address which called {requestDetachContext} function.
  /// @param data     Additional data with no specified format.
  function onDetachRequested(bytes32 ctxHash, uint256 tokenId, address operator, bytes calldata data) external;

  /// @dev This method is called once a token context is detached by any mechanism.
  /// @param ctxHash  The hash of context invoked this call.
  /// @param tokenId  NFT identifier which is being detached.
  /// @param user     The address of the context user which is being detached.
  /// @param operator The address which called {execDetachContext} function.
  /// @param data     Additional data with no specified format.
  function onExecDetachContext(bytes32 ctxHash, uint256 tokenId, address user, address operator, bytes calldata data) external;
}
```

### 所有权委托规则

**startDelegateOwnership 规则**

- 必须回退，除非没有委托。
- 必须回退，除非方法调用者是所有者、所有者的授权操作员或该 NFT 的批准地址。
- 必须回退，除非到期时间在未来。
- 必须回退，如果委托人地址是所有者或零地址。
- 必须回退，如果代币不存在。
- 必须在成功时发出事件 `OwnershipDelegationStarted`。
- 在满足上述条件后，该函数必须替换任何待处理的委托。

**acceptOwnershipDelegation 规则**

- 必须回退，如果没有委托。
- 必须回退，除非方法调用者是委托人，或委托人的授权操作员。
- 必须回退，除非到期时间在未来。
- 必须在成功时发出事件 `OwnershipDelegationAccepted`。
- 在满足上述条件后，委托人必须被记录为所有权管理者，直到委托到期。

**stopDelegateOwnership 规则**

- 必须回退，除非委托已被接受。
- 必须回退，除非到期时间在未来。
- 必须回退，除非方法调用者是委托人，或委托人的授权操作员。
- 必须在成功时发出事件 `OwnershipDelegationStopped`。
- 在满足上述条件后，所有者必须被记录为所有权管理者。
### **代币（解）锁规则**

为了更明确地说明代币是如何（解）锁的，这些功能：

- 可以使用 `attachContext` 方法将代币附加到上下文
- 控制器必须调用 `setContextLock` 函数来（解）锁
- 所有权管理者必须调用 `requestDetachContext` 和 `execDetachContext` 函数，并且必须遵循 `IERC7695ContextCallback` 钩子函数

以下是场景和规则的列表。

**场景**

**_场景#1:_** 上下文控制器想要（解）锁一个未请求分离的代币。

- `setContextLock` 必须成功调用

**_场景#2:_** 上下文控制器想要（解）锁一个已请求分离的代币。

- `setContextLock` 必须被回退

**_场景#3:_** 所有权管理者想要（解锁并）分离一个锁定的代币，并且回调控制器实现了 `IERC7695ContextCallback`。

- 调用者必须：
  - 成功调用 `requestDetachContext` 函数
  - 至少等待上下文分离持续时间（请参见 `getContext` 函数中的变量 `detachingDuration`）
  - 成功调用 `execDetachContext` 函数
- `requestDetachContext` 必须调用 `onDetachRequested` 函数，尽管调用结果如何
- `execDetachContext` 必须调用 `onExecDetachContext` 函数，尽管调用结果如何

**_场景#4:_** 所有权管理者想要（解锁并）分离一个锁定的代币，并且回调控制器未实现 `IERC7695ContextCallback`。

- 调用者必须：
  - 成功调用 `requestDetachContext` 函数
  - 至少等待上下文分离持续时间（请参见 `getContext` 函数中的变量 `detachingDuration`）
  - 成功调用 `execDetachContext` 函数

**_场景#5:_** 所有权管理者想要分离一个解锁的代币，并且回调控制器实现了 `IERC7695ContextCallback`。

- 调用者必须成功调用 `requestDetachContext` 函数
- `requestDetachContext` 必须调用 `onExecDetachContext` 函数，尽管结果如何
- 不得调用 `execDetachContext`

**_场景#6:_** 所有权管理者想要分离一个解锁的代币，并且回调控制器未实现 `IERC7695ContextCallback`。

- 调用者必须成功调用 `requestDetachContext` 函数
- 不得调用 `execDetachContext`

**规则**

**attachContext 规则**

- 除非方法调用者是所有权管理者、授权的所有权管理者操作员或该 NFT 的批准地址（如果代币未被委托），否则必须回退。
- 如果上下文不存在，则必须回退。
- 如果代币已附加到上下文，则必须回退。
- 必须发出事件 `ContextAttached`。
- 在满足上述条件后，此函数必须检查控制器地址是否为智能合约（例如，代码大小 > 0）。如果是，则必须调用 `onAttached`，并且如果调用失败则必须回退。
  - 调用者提供的 `data` 参数必须以未更改的内容传递给 `onAttached` 钩子函数的 `data` 参数。

**setContextLock 规则**

- 如果上下文不存在，则必须回退。
- 如果代币未附加到上下文，则必须回退。
- 如果之前已发出分离请求，则必须回退。
- 如果方法调用者不是上下文控制器，则必须回退。
- 成功时必须发出事件 `ContextLockUpdated`。

**requestDetachContext 规则**

- 如果之前已发出分离请求，则必须回退。
- 除非方法调用者是上下文控制器、所有权管理者、授权的所有权管理者操作员或该 NFT 的批准地址（如果代币未被委托），否则必须回退。
- 如果调用者是上下文控制器或代币上下文未锁定，则必须发出事件 `ContextDetached`。在满足上述条件后，此函数必须检查控制器地址是否为智能合约（例如，代码大小 > 0）。如果是，则必须调用 `onExecDetachContext`，并且调用结果必须被跳过。
  - 调用者提供的 `data` 参数必须以未更改的内容传递给 `onExecDetachContext` 钩子函数的 `data` 参数。
- 如果代币上下文被锁定，则必须发出事件 `ContextDetachRequested`。在满足上述条件后，此函数必须检查控制器地址是否为智能合约（例如，代码大小 > 0）。如果是，则必须调用 `onDetachRequested`，并且调用结果必须被跳过。
  - 调用者提供的 `data` 参数必须以未更改的内容传递给 `onDetachRequested` 钩子函数的 `data` 参数。

**execDetachContext 规则**

- 除非方法调用者是所有权管理者、授权的所有权管理者操作员或该 NFT 的批准地址（如果代币未被委托），否则必须回退。
- 除非之前已发出分离请求并且指定的分离持续时间已过去（在请求分离时使用 `getContext` 函数中的变量 `detachingDuration`），否则必须回退。
- 必须发出事件 `ContextDetached`。
- 在满足上述条件后，此函数必须检查控制器地址是否为智能合约（例如，代码大小 > 0）。如果是，则必须调用 `onExecDetachContext`，并且调用结果必须被跳过。
  - 调用者提供的 `data` 参数必须以未更改的内容传递给 `onExecDetachContext` 钩子函数的 `data` 参数。

### 额外转移规则

除了在转移 NFT 时扩展自 [ERC-721](./eip-721.md) 的转移机制外，实施：

- 除非方法调用者是所有权管理者、授权的所有权管理者操作员或该 NFT 的批准地址（如果代币未被委托），否则必须回退。
- 必须撤销所有权委托（如果有）。
- 必须分离每个附加的上下文：
  - 如果代币被锁定，则必须回退，除非之前已发出分离请求并且指定的分离持续时间已过去（在请求分离时使用 `getContext` 函数中的变量 `detachingDuration`）。
  - 必须检查控制器地址是否为智能合约（例如，代码大小 > 0）。如果是，则必须调用 `onExecDetachContext` 函数（使用空的 `data` 参数 `""`），并且调用结果必须被跳过。

## 理由

在设计提案时，我们考虑了以下问题。

### 多个上下文用于多个用例

该提案围绕代币上下文展开，以允许为各种去中心化应用程序（dApps）创建不同的上下文。上下文控制器的角色是促进（租赁或委托）dApps，通过允许将使用权授予另一个用户而不修改 NFT 的所有者记录。此外，该提案为上下文提供了锁定功能，以确保在执行这些 dApps 时的信任性，特别是在质押情况下。

### 为所有者提供解锁机制

通过为所有者提供解锁机制，这种方法允许所有者独立解锁他们的代币，而无需依赖上下文控制器来启动该过程。这防止了在控制器失去控制的情况下，所有者无法解锁其代币的情况。

### 附加和分离回调

在 **代币（解）锁规则** 中，`onDetachRequested` 和 `onExecDetachContext` 函数的回调结果被跳过，因为我们故意移除了控制器停止分离的能力，确保代币分离独立于控制器的行为。
此外，为了保留拒绝接收附件的权限，如果对 `onAttach` 函数的调用失败，则操作会回滚。

### 所有权委托

此功能通过分离所有者和所有权提供了一种新方法。主要设计用于便于第三方的委托，它允许将另一个账户委托为所有权的管理者，与所有者不同。

与 `approve` 或 `setApprovalForAll` 方法不同，这些方法在保持所有权状态的同时授予其他账户权限。所有权委托不仅仅是授予权限；它涉及将所有者的权利转移给受托人，并在到期时自动回滚。这一机制防止了潜在的滥用，例如在所有者保留所有权利的情况下请求抵押和转移到其他账户。

**2 步委托** 过程旨在防止在分配受托人时出现错误，必须通过两个步骤完成：提供和确认。如果需要在预定到期之前取消委托，受托人可以调用 `stopOwnershipDelegation` 方法。

### 转移方法机制

作为与转移方法集成的一部分，我们扩展了其隐式行为以包括代币批准：

- **重置所有权委托：** 自动重置所有权委托。故意不发出 `OwnershipDelegationStopped` 事件。
- **分离所有上下文：** 同样，如果没有任何上下文被锁定，则与代币相关的所有上下文都会被分离。故意不发出 `ContextDetached` 事件。

这些修改旨在确保在代币转移过程中实现无信任和 gas 效率，为用户提供无缝体验。

## 向后兼容性

该提案与 [ERC-721](./eip-721.md) 向后兼容。

## 安全考虑

### 分离持续时间

在开发此代币标准以服务多个上下文时：

- 合约部署者应建立适当的分离延迟上限（通过 `maxDetachingDuration` 方法）。
- 上下文所有者应预见潜在的用例，并建立适当的期限，不得超过上限。

这一预防措施对于减轻所有者通过垃圾邮件列表和在短时间内将代币转移给另一个所有者而滥用系统的风险至关重要。

### 重复代币使用

在启动新上下文时，上下文控制者应跟踪 NFT 合约中的所有其他上下文，以防止重复使用。

例如，假设一个代币在特定游戏中被锁定用于租赁目的。如果该游戏引入另一个上下文（例如，支持该游戏中的委托），则可能导致在游戏中重复使用代币，尽管其意图是用于不同的上下文。

在这种情况下，可以考虑为租赁和委托目的共享一个上下文。或者，必须对新的委托上下文施加一些限制，以防止在游戏中重复使用该代币。

### 所有权委托缓冲时间

在构建依赖于所有权委托进行产品开发的系统时，在请求所有权委托时必须包含缓冲时间（至少 `maxDetachingDuration` 秒）。这一预防措施对于减轻潜在滥用的风险至关重要，特别是如果其中一个相关上下文在委托时间到期之前锁定代币。
例如，考虑一个基于此标准构建的抵押合同，其最大分离持续时间为 7 天，而所需的委托期限仅为 3 天。在这种情况下，如果没有足够的缓冲时间，所有者可能会利用该系统提取资金并调用相关上下文来锁定代币，从而阻止其解锁和转移。

### 验证回调调用者

为了增强合约之间交互的安全性和完整性，在实现 `IERC7695ContextCallback` 时，验证任何回调函数的调用者至关重要。此验证确保回调的 `msg.sender` 确实是预期的合约地址，通常是代币合约或指定的控制合约。这种检查对于防止恶意实体假装是合法合约而执行未经授权的操作至关重要。

### 推荐实践

**租赁**

这是租赁的典型用例，假设 A（所有者）拥有一个代币并希望将其代币列出出租，而 B（用户）希望租用该代币以在某个游戏中玩。

![租赁流程](../assets/eip-7695/rental.svg)

**抵押**

在构建抵押系统时，建议支持希望出租其代币的代币所有者，同时将其用于抵押借贷。这种方法增强了抵押系统的吸引力，创造了一个更具吸引力和多功能的金融生态系统，以满足多种不同的需求。

这是抵押的典型用例，假设 A（所有者）拥有一个代币并希望抵押，而 B（贷方）希望通过将资金借给 A 来赚取利息。

![抵押流程](../assets/eip-7695/mortgage.svg)

### 代币所有者的风险

**网络钓鱼攻击**

需要注意的是，所有者角色有能力将所有权委托给另一个账户，从而允许其授权从相应钱包中转移。因此，一些恶意行为者可能会通过调用 `startDelegateOwnership` 方法欺骗代币所有者将其委托为受托人。这一风险可以视为与 `approve` 或 `setApprovalForAll` 方法相同。

**所有权权利丧失**

在与合约系统（例如抵押）交互时，所有者必须将其所有权利委托给智能合约，因此必须：

- 确保委托的时间框架合理且不太遥远。如果合约要求的委托期限过长，请确保包含在满足特定条件时撤销所有权委托的条款。未能包含此类条款可能导致所有权利在委托到期之前丧失。
- 注意，如果合约所有者或其操作员受到攻击，代币所有权可能会被更改。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。