---
eip: 5725
title: 可转让的归属 NFT
description: 一种可转让的归属 NFT 接口，随着时间的推移释放基础代币。
author: Apeguru (@Apegurus), Marco De Vries <marco@paladinsec.co>, Mario <mario@paladinsec.co>, DeFiFoFum (@DeFiFoFum), Elliott Green (@elliott-green)
discussions-to: https://ethereum-magicians.org/t/eip-5725-transferable-vesting-nft/11099
status: Final
type: Standards Track
category: ERC
created: 2022-09-08
requires: 721
---

## 摘要

一种用于在归属释放曲线下归属代币的**非同质化代币**（NFT）标准（[ERC-20](./erc-20.md) 或其他）。

以下标准允许实现一个标准 API，用于基于 NFT 的合约，这些合约持有并表示任何基础代币（[ERC-20](./erc-20.md) 或其他）归属和锁定的属性，这些代币会发放给 NFT 持有者。该标准是对 [ERC-721](./eip-721.md) 代币的扩展，提供了创建归属 NFT、认领代币和读取归属曲线属性的基本功能。

## 动机

归属合约，包括时间锁合约，缺乏标准化和统一的接口，导致此类合约的多样化实现。将这些合约标准化为单一接口将允许围绕这些合约创建一个链上和链下工具的生态系统。此外，以非同质化资产形式的流动归属可以比传统的**未来代币简单协议**（SAFTs）或**外部拥有账户**（EOA）基础的归属有巨大的改进，因为它使得可转让性和附加元数据的能力成为可能，类似于传统 NFT 提供的现有功能。

这样的标准不仅将提供一个急需的[ERC-20](./erc-20.md) 代币锁定标准，还将使得为半流动 SAFTs 创建二级市场成为可能。

该标准还允许轻松实现多种不同的归属曲线。

这些曲线可以表示：

- 线性归属
- 悬崖归属
- 指数归属
- 自定义确定性归属

### 用例

1. 一个在设定时间内释放代币的框架，可用于构建多种 NFT 金融产品，如债券、国库券等。
2. 以标准化形式复制 SAFT 合约的半流动归属 NFT 资产。
   - SAFTs 通常是链下的，而今天的链上版本主要是基于地址的，这使得将归属份额分配给多个代表变得困难。标准化简化了这一复杂过程。
3. 为归属和代币时间锁合约的标准化提供路径。
   - 目前有许多此类合约存在于市场上，它们在接口和实现上大多不同。
4. 专门针对归属 NFT 的 NFT 市场。
   - 可以从一个通用标准为代币归属 NFT 创建全新的接口和分析工具。
5. 将归属 NFT 集成到像 Safe Wallet 这样的服务中。
   - 一个标准意味着像 Safe Wallet 这样的服务可以更轻松和统一地支持与这些类型合约的交互。
6. 实现标准化的筹款实施和一般筹款，以更透明的方式出售归属代币（例如 SAFTs）。
7. 允许工具、前端应用、聚合器等展示归属代币及其可用属性的更全面视图。
   - 目前，每个项目都需要编写自己的归属资产的归属时间表可视化。如果这一点标准化，第三方工具可以开发出来，聚合所有项目的归属 NFT，展示它们的时间表，并允许用户进行聚合的归属操作。
   - 这样的工具可以通过 [ERC-165](./eip-165.md) `supportsInterface(InterfaceID)` 检查轻松发现合规性。
8. 使得在所有归属标准中使用单一的包装实现变得更容易，定义多个接收者、定期租赁归属代币等。

## 规范

文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";

/**
 * @title Non-Fungible Vesting Token Standard.
 * @notice A non-fungible token standard used to vest ERC-20 tokens over a vesting release curve 
 *  scheduled using timestamps.
 * @dev Because this standard relies on timestamps for the vesting schedule, it's important to keep track of the 
 *  tokens claimed per Vesting NFT so that a user cannot withdraw more tokens than allotted for a specific Vesting NFT.
 * @custom:interface-id 0xbd3a202b
 */
interface IERC5725 is IERC721 {
    /**
     *  This event is emitted when the payout is claimed through the claim function.
     *  @param tokenId the NFT tokenId of the assets being claimed.
     *  @param recipient The address which is receiving the payout.
     *  @param claimAmount The amount of tokens being claimed.
     */
    event PayoutClaimed(uint256 indexed tokenId, address indexed recipient, uint256 claimAmount);

    /**
     *  This event is emitted when an `owner` sets an address to manage token claims for all tokens.
     *  @param owner The address setting a manager to manage all tokens.
     *  @param spender The address being permitted to manage all tokens.
     *  @param approved A boolean indicating whether the spender is approved to claim for all tokens.
     */
    event ClaimApprovalForAll(address indexed owner, address indexed spender, bool approved);

    /**
     *  This event is emitted when an `owner` sets an address to manage token claims for a `tokenId`.
     *  @param owner The `owner` of `tokenId`.
     *  @param spender The address being permitted to manage a tokenId.
     *  @param tokenId The unique identifier of the token being managed.
     *  @param approved A boolean indicating whether the spender is approved to claim for `tokenId`.
     */
    event ClaimApproval(address indexed owner, address indexed spender, uint256 indexed tokenId, bool approved);

    /**
     * @notice Claim the pending payout for the NFT.
     * @dev MUST grant the claimablePayout value at the time of claim being called to `msg.sender`. 
     *  MUST revert if not called by the token owner or approved users. 
     *  MUST emit PayoutClaimed. 
     *  SHOULD revert if there is nothing to claim.
     * @param tokenId The NFT token id.
     */
    function claim(uint256 tokenId) external;

    /**
     * @notice Number of tokens for the NFT which have been claimed at the current timestamp.
     * @param tokenId The NFT token id.
     * @return payout The total amount of payout tokens claimed for this NFT.
     */
    function claimedPayout(uint256 tokenId) external view returns (uint256 payout);

    /**
     * @notice Number of tokens for the NFT which can be claimed at the current timestamp.
     * @dev It is RECOMMENDED that this is calculated as the `vestedPayout()` subtracted from `payoutClaimed()`.
     * @param tokenId The NFT token id.
     * @return payout The amount of unlocked payout tokens for the NFT which have not yet been claimed.
     */
    function claimablePayout(uint256 tokenId) external view returns (uint256 payout);

    /**
     * @notice Total amount of tokens which have been vested at the current timestamp. 
     *  This number also includes vested tokens which have been claimed.
     * @dev It is RECOMMENDED that this function calls `vestedPayoutAtTime` 
     *  with `block.timestamp` as the `timestamp` parameter.
     * @param tokenId The NFT token id.
     * @return payout Total amount of tokens which have been vested at the current timestamp.
     */
    function vestedPayout(uint256 tokenId) external view returns (uint256 payout);

    /**
     * @notice Total amount of vested tokens at the provided timestamp. 
     *  This number also includes vested tokens which have been claimed.
     * @dev `timestamp` MAY be both in the future and in the past. 
     *  Zero MUST be returned if the timestamp is before the token was minted.
     * @param tokenId The NFT token id.
     * @param timestamp The timestamp to check on, can be both in the past and the future.
     * @return payout Total amount of tokens which have been vested at the provided timestamp.
     */
    function vestedPayoutAtTime(uint256 tokenId, uint256 timestamp) external view returns (uint256 payout);

    /**
     * @notice Number of tokens for an NFT which are currently vesting.
     * @dev The sum of vestedPayout and vestingPayout SHOULD always be the total payout.
     * @param tokenId The NFT token id.
     * @return payout The number of tokens for the NFT which are vesting until a future date.
     */
    function vestingPayout(uint256 tokenId) external view returns (uint256 payout);

    /**
     * @notice The start and end timestamps for the vesting of the provided NFT. 
     *  MUST return the timestamp where no further increase in vestedPayout occurs for `vestingEnd`.
     * @param tokenId The NFT token id.
     * @return vestingStart The beginning of the vesting as a unix timestamp.
     * @return vestingEnd The ending of the vesting as a unix timestamp.
     */
    function vestingPeriod(uint256 tokenId) external view returns (uint256 vestingStart, uint256 vestingEnd);

    /**
     * @notice Token which is used to pay out the vesting claims.
     * @param tokenId The NFT token id.
     * @return token The token which is used to pay out the vesting claims.
     */
    function payoutToken(uint256 tokenId) external view returns (address token);

    /**
     * @notice Sets a global `operator` with permission to manage all tokens owned by the current `msg.sender`.
     * @param operator The address to let manage all tokens.
     * @param approved A boolean indicating whether the spender is approved to claim for all tokens.
     */
    function setClaimApprovalForAll(address operator, bool approved) external;

    /**
     * @notice Sets a tokenId `operator` with permission to manage a single `tokenId` owned by the `msg.sender`.
     * @param operator The address to let manage a single `tokenId`.
     * @param tokenId the `tokenId` to be managed.
     * @param approved A boolean indicating whether the spender is approved to claim for all tokens.
     */
    function setClaimApproval(address operator, bool approved, uint256 tokenId) external;

    /**
     * @notice Returns true if `owner` has set `operator` to manage all `tokenId`s.
     * @param owner The owner allowing `operator` to manage all `tokenId`s.
     * @param operator The address who is given permission to spend tokens on behalf of the `owner`.
     */
    function isClaimApprovedForAll(address owner, address operator) external view returns (bool isClaimApproved);

    /**
     * @notice Returns the operating address for a `tokenId`. 
     *  If `tokenId` is not managed, then returns the zero address.
     * @param tokenId The NFT `tokenId` to query for a `tokenId` manager.
     */
    function getClaimApproved(uint256 tokenId) external view returns (address operator);
}

```

## 理由

### 术语

这些是围绕规范使用的基本术语，功能名称和定义基于这些术语。

- *归属*: 归属 NFT 在未来日期之前归属的代币。
- *已归属*: 归属 NFT 已归属的代币总量。
- *可认领*: 可以解锁的已归属代币数量。
- *已认领*: 从归属 NFT 中解锁的代币总量。
- *时间戳*: 用于归属的日期的 unix `timestamp`（秒）表示。

### 归属函数

**`vestingPayout` + `vestedPayout`**

`vestingPayout(uint256 tokenId)` 和 `vestedPayout(uint256 tokenId)` 加起来等于在归属时间表结束时可以认领的代币总数。这也等于 `vestedPayoutAtTime(uint256 tokenId, uint256 timestamp)`，其中 `timestamp` 为 `type(uint256).max`。

这样做的理由是确保 `vested` 代币和 `vesting` 代币始终保持同步。意图是创建的归属曲线在 `vestingPeriod` 内是确定性的。这为与这些 NFT 的集成创造了有用的机会。例如：可以遍历归属时间表，并可视化归属曲线，无论是在链上还是链下。

**`vestedPayout` 与 `claimedPayout` & `claimablePayout`**

```solidity
vestedPayout - claimedPayout - claimablePayout = lockedPayout
```

- `vestedPayout(uint256 tokenId)` 提供的已归属代币总量**包括 `claimedPayout(uint256 tokenId)`**。
- `claimedPayout(uint256 tokenId)` 提供在当前 `timestamp` 解锁的代币总量。
- `claimablePayout(uint256 tokenId)` 提供在当前 `timestamp` 可以解锁的代币数量。

提供三个函数的理由是支持多种功能：

1. `vestedPayout(uint256 tokenId)` 的返回值将始终与 `vestedPayoutAtTime(uint256 tokenId, uint256 timestamp)` 的返回值相匹配，`timestamp` 为 `block.timestamp`。
2. `claimablePayout(uint256 tokenId)` 可用于轻松查看当前的解锁代币数量，并通过在 `timestamp` 过去之前返回零来允许解锁悬崖。
3. `claimedPayout(uint256 tokenId)` 有助于查看从 NFT 中解锁的代币，并且对于计算已归属但未解锁的代币是必要的：`vestedPayout - claimedPayout - claimablePayout = lockedPayout`。这将取决于该标准的实现如何配置归属曲线。

`vestedPayoutAtTime(uint256 tokenId, uint256 timestamp)` 提供了在 `vestingPeriod(uint256 tokenId)` 中迭代并提供释放曲线可视化的功能。意图是创建的释放曲线使得 `vestedPayoutAtTime(uint256 tokenId, uint256 timestamp)` 是确定性的。

### 时间戳

通常在 Solidity 开发中，不建议将 `block.timestamp` 用作状态依赖变量，因为区块的时间戳可能会被矿工操纵。选择使用 `timestamp` 而不是 `block` 是为了使接口能够在多个**以太坊虚拟机**（EVM）兼容网络上工作，这些网络通常具有不同的区块时间。具有显著伪造时间戳的区块提案通常会被所有节点实现丢弃，这使得滥用的窗口微不足道。
`timestamp` 使跨链集成变得简单，但在内部，参考实现会跟踪每个 Vesting NFT 的代币支付，以确保根据归属条款分配的多余代币无法被索取。

### 范围限制

- **历史索赔**：虽然可以通过 `vestedPayoutAtTime(uint256 tokenId, uint256 timestamp)` 在链上确定历史归属时间表，但历史索赔需要通过历史交易数据进行计算。最有可能的是查询 `PayoutClaimed` 事件以构建历史图表。

### 扩展可能性

这些功能在标准中并不支持，但可以通过扩展标准来支持这些更高级的功能。

- **自定义归属曲线**：该标准旨在根据 NFT `tokenId` 和 **timestamp** 作为输入返回确定性的 `vesting` 值。这是有意为之，因为它提供了灵活性，使归属曲线在内部工作时不受限制，从而不限制那些打算构建复杂智能合约归属架构的项目。
- **NFT 租赁**：如果可以租赁归属 NFT，则可以创建更复杂的 DeFi 工具。

这样做是为了保持基础标准的简单性。这些功能可以并且可能会通过对该标准的扩展添加。

## 向后兼容性

- Vesting NFT 标准旨在与任何当前的 [ERC-721](./eip-721.md) 集成和市场完全向后兼容。
- Vesting NFT 标准还支持 [ERC-165](./eip-165.md) 接口检测，以检测 `EIP-721` 兼容性，以及 Vesting NFT 兼容性。

## 测试用例

参考归属 NFT 仓库包括用 Hardhat 编写的测试。

## 参考实现

该 EIP 的参考实现可以在 [ERC-5725 assets](../assets/eip-5725/README.md) 中找到。

## 安全考虑

**时间戳**

- 归属时间表基于时间戳。因此，跟踪已索取的代币数量并确保不分配超过特定 Vesting NFT 的代币是很重要的。
  - 例如，`vestedPayoutAtTime(tokenId, type(uint256).max)` 必须返回给定 `tokenId` 的总支付。

**批准**

- 当对 Vesting NFT 进行 [ERC-721](./eip-721.md) 批准时，操作员将有权将 Vesting NFT 转移到自己名下，然后索取归属代币。
- 当对 Vesting NFT 进行 ERC-5725 批准时，操作员将有权索取归属代币，但不能将 NFT 转移给所有者以外的人。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。