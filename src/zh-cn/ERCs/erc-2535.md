---
eip: 2535
title: 钻石，多面代理
description: 创建可以在部署后扩展的模块化智能合约系统。
author: Nick Mudge (@mudgen)
discussions-to: https://ethereum-magicians.org/t/discussion-for-eip2535-diamonds/10459/
status: Final
type: Standards Track
category: ERC
created: 2020-02-22
---

## 摘要

<img align="right" src="../assets/eip-2535/diamond.svg" width="230" height="230" alt="钻石合约结构">

本提案标准化了钻石，这是一种可以在部署后升级/扩展的模块化智能合约系统，并且几乎没有大小限制。更技术性地说，**钻石**是一个具有外部函数的合约，这些函数由称为**面**的合约提供。面是独立的合约，可以共享内部函数、库和状态变量。

## 动机

使用钻石有多种不同的原因。以下是其中一些：

1. **一个地址提供无限的合约功能。** 使用单一地址进行合约功能使得部署、测试和与其他智能合约、软件和用户界面的集成变得更容易。
1. **您的合约超过了 24KB 的最大合约大小。** 您可能有相关的功能，保持在单个合约或单个合约地址中是有意义的。钻石没有最大合约大小。
1. **钻石提供了一种组织合约代码和数据的方式。** 您可能想构建一个具有大量功能的合约系统。钻石提供了一种系统化的方法来隔离不同的功能，并根据需要以节省 gas 的方式将它们连接在一起并共享数据。
1. **钻石提供了一种升级功能的方式。** 可升级的钻石可以升级以添加/替换/移除功能。由于钻石没有最大合约大小，因此可以随时间添加的功能没有限制。钻石可以在不重新部署现有功能的情况下进行升级。钻石的部分可以添加/替换/移除，而其他部分保持不变。
1. **钻石可以是不可变的。** 可以部署一个不可变的钻石，或者在稍后将可升级的钻石变为不可变。
1. **钻石可以重用已部署的合约。** 不必将合约部署到区块链上，可以使用已经部署的链上合约来创建钻石。可以从现有的已部署合约创建自定义钻石。这使得创建链上智能合约平台和库成为可能。

该标准是对 [EIP-1538](./eip-1538.md) 的改进。该标准的相同动机适用于本标准。

已部署的面可以被任意数量的钻石使用。

下图显示了两个钻石使用相同的两个面。

- `FacetA` 被 `Diamond1` 使用
- `FacetA` 被 `Diamond2` 使用
- `FacetB` 被 `Diamond1` 使用
- `FacetB` 被 `Diamond2` 使用

<img src="../assets/eip-2535/facetreuse.png" alt="面重用">

### 可升级钻石与集中式私有数据库

为什么要使用可升级的钻石而不是集中式、私有、可变的数据库？

1. 去中心化自治组织（DAO）和其他治理系统可以用来升级钻石。
1. 与以太坊生态系统的广泛互动和集成。
1. 通过开放存储数据和经过验证的源代码，可以展示可证明的可信历史。
1. 通过开放性，可以在不当行为发生时及时发现并报告。
1. 独立的安全和领域专家可以审查合约的变更历史，并为其可信历史作证。
1. 可升级的钻石可以变得不可变且无信任。

### 一些钻石的好处

1. 提供所需功能的稳定合约地址。
1. 一个地址具有多个合约（面）的功能，这些合约彼此独立，但可以共享内部函数、库和状态变量。
1. 从单一地址发出事件可以简化事件处理。
1. 以原子方式（在同一交易中）添加、替换和移除多个外部函数的方式。
1. 细粒度的升级，因此您可以仅更改需要更改的钻石部分。
1. 更好地控制何时以及存在哪些功能。
1. 去中心化自治组织（DAO）、多签合约和其他治理系统可以用来升级钻石。
1. 显示添加、替换和移除的功能的事件。
1. 能够展示对钻石所做的所有更改。
1. 通过展示对钻石所做的所有更改来增加信任。
1. 查看钻石以查看其当前的面和功能。
1. 拥有一个不可变的、无信任的钻石。
1. 解决 24KB 最大合约大小限制。钻石可以是任意大小。
1. 可以在不同的面中实现独立的功能，并在钻石中一起使用。
1. 钻石可以从已经部署的现有链上合约创建。
1. 较大的合约必须通过删除错误消息和其他内容来减少其大小。通过实现钻石，您可以保留所需的完整功能。
1. 根据需要和希望，能够实现零、部分或完全的钻石不可变性。
1. 随着时间的推移，使用可升级的钻石开发和改进应用程序，然后在需要时使其不可变和无信任。
1. 逐步开发，让您的钻石随着应用程序的增长而增长。
1. 升级钻石以修复错误、添加功能和实施新标准。
1. 使用钻石和面组织您的代码。
1. 钻石可以很大（具有许多功能），但仍然是模块化的，因为它们通过面进行隔离。
1. 在单个交易中调用多个合约的合约架构可以通过将这些合约压缩为单个钻石并直接访问状态变量来节省 gas。
1. 通过将外部函数转换为内部函数来节省 gas。这是通过在面之间共享内部函数来完成的。
1. 通过为特定用例（如批量转账）创建针对 gas 优化的外部函数来节省 gas。
1. 钻石旨在用于工具和用户界面软件。

## 规范

### 术语

1. **钻石**是一个外观智能合约，它通过`delegatecall`调用其面以执行函数调用。钻石是有状态的。数据存储在钻石的合约存储中。
1. **面**是一个无状态的智能合约或具有外部函数的 Solidity 库。一个面被部署，并且其一个或多个函数被添加到一个或多个钻石中。一个面不在其自己的合约存储中存储数据，但可以定义状态并读取和写入一个或多个钻石的存储。术语面源自钻石行业。它是钻石的一个侧面或平面。
1. **放大面**是提供自省函数的面。在钻石行业中，放大镜是用于观察钻石的工具。
1. **不可变函数**是一个不能被替换或移除的外部函数（因为它直接在钻石中定义，或者因为钻石的逻辑不允许其被修改）。
1. **映射**在本 EIP 中是指两个事物之间的关联，并不指特定的实现。

术语**合约**被宽松地用来指代智能合约或已部署的 Solidity 库。
当这个 EIP 使用 **function** 而不指定内部或外部时，它指的是外部函数。

在这个 EIP 中，适用于外部函数的信息也适用于公共函数。

### 概述

一个钻石通过 `delegatecall` 从其侧面调用函数。

在钻石行业，钻石通过切割来创建和塑形，形成多个面。在这个标准中，钻石通过添加、替换或移除侧面的函数来进行切割。

### 实现接口的说明

由于钻石的特性，钻石可以通过两种方式实现接口：直接（`contract Contract is Interface`），或通过从一个或多个侧面添加函数。为了本提案的目的，当说一个钻石实现一个接口时，允许使用任一实现方法。

### 回退函数

当在钻石上调用外部函数时，会执行其回退函数。回退函数根据调用数据的前四个字节（称为函数选择器）确定要调用哪个侧面，并使用 `delegatecall` 从该侧面执行该函数。

钻石的回退函数和 `delegatecall` 使钻石能够像是由钻石本身实现的那样执行侧面的函数。`msg.sender` 和 `msg.value` 的值不会改变，只有钻石的存储被读取和写入。

以下是一个钻石回退函数可能实现的示例：

```solidity
// Find facet for function that is called and execute the
// function if a facet is found and return any value.
fallback() external payable {
  // get facet from function selector
  address facet = selectorTofacet[msg.sig];
  require(facet != address(0));
  // Execute external function from facet using delegatecall and return any value.
  assembly {
    // copy function selector and any arguments
    calldatacopy(0, 0, calldatasize())
    // execute function call using the facet
    let result := delegatecall(gas(), facet, 0, calldatasize(), 0, 0)
    // get any return value
    returndatacopy(0, 0, returndatasize())
    // return any return value or error back to the caller
    switch result
      case 0 {revert(0, returndatasize())}
      default {return (0, returndatasize())}
  }
}
```

该图显示了钻石的结构：

<img src="../assets/eip-2535/DiamondDiagram.png" alt="Mapping facets and storage">

### 存储

需要一个状态变量或存储布局组织模式，因为 Solidity 的内置存储布局系统不支持代理合约或钻石。存储的具体布局在这个 EIP 中没有定义，但可能会在后续提案中定义。与钻石兼容的存储布局模式的示例包括 [Diamond Storage](../assets/eip-2535/storage-examples/DiamondStorage.sol) 和 [AppStorage](../assets/eip-2535/storage-examples/AppStorage.sol)。

侧面可以通过在相同存储位置使用相同的结构体来共享状态变量。侧面可以通过继承相同的合约或使用相同的库来共享内部函数和库。通过这些方式，侧面是独立的单元，但可以共享状态和功能。

下图显示了各个侧面及其共享的数据。

注意，所有数据都存储在钻石的存储中，但不同的侧面对数据的访问不同。

在此图中

- 只有 `FacetA` 可以访问 `DataA`
- 只有 `FacetB` 可以访问 `DataB`
- 只有钻石自己的代码可以访问 `DataD`。
- `FacetA` 和 `FacetB` 共享对 `DataAB` 的访问。
- 钻石自己的代码、`FacetA` 和 `FacetB` 共享对 `DataABD` 的访问。

<img src="../assets/eip-2535/diamondstorage1.png" alt="Mapping code, data, and facets">

### Solidity 库作为侧面

智能合约或已部署的 Solidity 库可以是钻石的侧面。

只有具有一个或多个外部函数的 Solidity 库才能部署到区块链并成为侧面。

仅包含内部函数的 Solidity 库不能被部署，也不能成为侧面。仅包含内部函数的 Solidity 库在使用它们的侧面和合约的字节码中包含。仅包含内部函数的 Solidity 库对于在侧面之间共享内部函数是有用的。

Solidity 库侧面具有一些与其作为侧面使用相匹配的属性：
* 它们不能被删除。
* 它们是无状态的。它们没有合约存储。
* 它们的语法防止在 Diamond Storage 之外声明状态变量。

### 添加/替换/移除函数

#### `IDiamond` 接口

所有钻石必须实现 `IDiamond` 接口。

在钻石的部署过程中，任何不可变函数和添加到钻石的任何外部函数必须在 `DiamondCut` 事件中发出。

**每当添加、替换或移除外部函数时，必须发出 `DiamondCut` 事件。** 这适用于所有升级、所有函数更改，在任何时候，无论是通过 `diamondCut` 还是其他方式。

```solidity
interface IDiamond {
    enum FacetCutAction {Add, Replace, Remove}
    // Add=0, Replace=1, Remove=2

    struct FacetCut {
        address facetAddress;
        FacetCutAction action;
        bytes4[] functionSelectors;
    }

    event DiamondCut(FacetCut[] _diamondCut, address _init, bytes _calldata);
}
```

`DiamondCut` 事件记录了对钻石的所有函数更改。

#### `IDiamondCut` 接口

一个钻石内部包含函数选择器到侧面地址的映射。通过修改此映射来添加/替换/移除函数。

如果在部署后允许修改其函数选择器映射，钻石应实现 `IDiamondCut` 接口。

`diamondCut` 函数在单个交易中更新来自任意数量侧面的任意数量的函数。将所有更改在单个交易中执行可以防止在多次交易中进行升级时可能发生的数据损坏。

`diamondCut` 的指定是为了互操作性。钻石工具、软件和用户界面应期望并使用标准的 `diamondCut` 函数。

```solidity
interface IDiamondCut is IDiamond {
    /// @notice Add/replace/remove any number of functions and optionally execute
    ///         a function with delegatecall
    /// @param _diamondCut Contains the facet addresses and function selectors
    /// @param _init The address of the contract or facet to execute _calldata
    /// @param _calldata A function call, including function selector and arguments
    ///                  _calldata is executed with delegatecall on _init
    function diamondCut(
        FacetCut[] calldata _diamondCut,
        address _init,
        bytes calldata _calldata
    ) external;
}
```

`_diamondCut` 参数是一个 `FacetCut` 结构体数组。

每个 `FacetCut` 结构体包含一个侧面地址和在钻石中更新的函数选择器数组。

对于每个 `FacetCut` 结构体：

 * 如果 `action` 是 `Add`，则将每个 `functionSelectors` 项的函数选择器映射更新为 `facetAddress`。如果任何 `functionSelectors` 有映射的侧面，则回退。
 * 如果 `action` 是 `Replace`，则将每个 `functionSelectors` 项的函数选择器映射更新为 `facetAddress`。如果任何 `functionSelectors` 的值等于 `facetAddress` 或选择器未设置，则回退。
 * 如果 `action` 是 `Remove`，则移除每个 `functionSelectors` 项的函数选择器映射。如果任何 `functionSelectors` 之前未设置，则回退。

任何尝试替换或移除不可变函数的操作必须回退。

在添加/替换/移除函数时保持意图明确有助于捕捉和防止升级错误。

##### 执行 `_calldata`

在添加/替换/移除函数后，使用 `delegatecall` 在 `_init` 上执行 `_calldata` 参数。此执行用于初始化数据或设置或移除在添加、替换和/或移除函数后所需或不再需要的任何内容。

如果 `_init` 值为 `address(0)`，则跳过 `_calldata` 执行。在这种情况下，`_calldata` 可以包含 0 字节或自定义信息。

### 检查侧面和函数

> 放大镜是一种用于观察钻石的小型放大镜。

钻石必须通过实现 `IDiamondLoupe` 接口来支持检查侧面和函数。

#### `IDiamondLoupe` 接口

```solidity
// A loupe is a small magnifying glass used to look at diamonds.
// These functions look at diamonds
interface IDiamondLoupe {
    struct Facet {
        address facetAddress;
        bytes4[] functionSelectors;
    }

    /// @notice Gets all facet addresses and their four byte function selectors.
    /// @return facets_ Facet
    function facets() external view returns (Facet[] memory facets_);

    /// @notice Gets all the function selectors supported by a specific facet.
    /// @param _facet The facet address.
    /// @return facetFunctionSelectors_
    function facetFunctionSelectors(address _facet) external view returns (bytes4[] memory facetFunctionSelectors_);

    /// @notice Get all the facet addresses used by a diamond.
    /// @return facetAddresses_
    function facetAddresses() external view returns (address[] memory facetAddresses_);

    /// @notice Gets the facet that supports the given selector.
    /// @dev If facet is not found return address(0).
    /// @param _functionSelector The function selector.
    /// @return facetAddress_ The facet address.
    function facetAddress(bytes4 _functionSelector) external view returns (address facetAddress_);
}
```

请参见 [参考实现](#reference-implementation) 以了解如何实现。

放大镜函数可以在用户界面软件中使用。用户界面调用这些函数以提供有关钻石的信息并进行可视化。

放大镜函数可以用于部署功能、升级功能、测试和其他软件。

### 实现要点

钻石必须实现以下内容：

1. 钻石包含一个回退函数和零个或多个在其中定义的不可变函数。
1. 钻石将函数选择器与侧面关联。
1. 当在钻石上调用函数时，如果它是直接在钻石中定义的“不可变函数”，则立即执行。否则，执行钻石的回退函数。回退函数找到与该函数关联的侧面并使用 `delegatecall` 执行该函数。如果该函数没有侧面，则可以选择执行默认函数。如果该函数没有侧面、没有默认函数且没有其他处理机制，则执行回退。
1. 每次添加、替换或移除函数时，都会发出 `DiamondCut` 事件以记录。
1. 钻石实现 DiamondLoupe 接口。
1. 所有不可变函数必须在 `DiamondCut` 事件中作为新添加的函数发出。如果存在，放大镜函数必须返回有关不可变函数的信息。不可变函数的侧面地址是钻石的地址。任何尝试删除或替换不可变函数的操作必须回退。
一个钻石可以实现以下内容：

1. [EIP-165](./eip-165.md) 的 `supportsInterface`。如果一个钻石具有 `diamondCut` 函数，则用于它的接口 ID 是 `IDiamondCut.diamondCut.selector`。用于钻石 loupe 接口的接口 ID 是 `IDiamondLoupe.facets.selector ^ IDiamondLoupe.facetFunctionSelectors.selector ^ IDiamondLoupe.facetAddresses.selector ^ IDiamondLoupe.facetAddress.selector`。

钻石地址是用户交互的地址。钻石地址不会改变。只有通过使用 `diamondCut` 函数或其他函数，面（facet）地址可以改变。

## 原理

### 使用函数选择器

用户界面软件可以用来从钻石中检索函数选择器和面地址，以显示钻石具有哪些函数。

该标准旨在使钻石与用户界面软件良好兼容。具有合约 ABI 的函数选择器提供了足够的信息，以便对用户界面软件有用。

### gas 考虑

委托函数调用确实会有一些 gas 开销。这可以通过几种方式来缓解：

1. 由于钻石没有最大大小限制，可以添加 gas 优化函数以满足用例。例如，有人可以使用钻石来实现 [EIP-721](./eip-721.md) 标准，并实现批量转移函数以减少 gas（并使批量转移更方便）。
2. 一些合约架构要求在一个交易中调用多个合约。通过将这些合约浓缩为一个单一的钻石并直接访问合约存储，可以实现 gas 节省。
3. 面可以包含较少的外部函数，从而降低 gas 成本。因为在具有许多函数的合约中调用函数的 gas 成本高于在具有少量函数的合约中调用函数的成本。
4. Solidity 优化器可以设置为高设置，导致生成更多字节码，但在执行时面将使用更少的 gas。

### 函数版本

软件或用户可以通过获取函数的面地址来验证调用的是哪个版本的函数。这可以通过调用 `IDiamondLoupe` 接口中的 `facetAddress` 函数来完成。该函数将函数选择器作为参数，并返回实现该函数的面地址。

### 默认函数

Solidity 提供了 `fallback` 函数，以便在调用合约中不存在的函数时执行特定功能。通过实现和使用默认函数，可以在钻石中选择性地实现相同的行为，默认函数是在调用钻石中不存在的函数时执行的函数。

默认函数可以通过多种方式实现，该标准并未指定必须如何实现。

### Loupe 函数与 `DiamondCut` 事件

要找出常规合约具有哪些函数，只需查看其经过验证的源代码。

钻石的经过验证的源代码不包括其具有的函数，因此需要不同的机制。

钻石有四个标准函数，称为 loupe 函数，用于显示钻石具有哪些函数。

loupe 函数可以用于许多事情，包括：
1. 显示钻石使用的所有函数。
2. 查询服务如 Etherscan 或文件以检索并显示钻石使用的所有源代码。
3. 查询服务如 Etherscan 或文件以检索钻石的 ABI 信息。
4. 测试或验证在钻石上添加/替换/移除函数的交易是否成功。
5. 在调用函数之前找出钻石具有哪些函数。
6. 被工具和编程库用于部署和升级钻石。
7. 被用户界面用于显示有关钻石的信息。
8. 被用户界面用于使用户能够调用钻石上的函数。

钻石支持另一种透明度形式，即钻石上所有升级的历史记录。这是通过 `DiamondCut` 事件来完成的，该事件用于记录在钻石上添加、替换或移除的所有函数。

### 在面之间共享函数

在某些情况下，可能需要调用在不同面中定义的函数。以下是实现此目的的方法：

1. 将一个面中的内部函数代码复制到另一个面。
2. 将公共内部函数放入一个由多个面继承的合约中。
3. 将公共内部函数放入 Solidity 库中，并在面中使用该库。
4. 调用在另一个面中定义的外部函数的一种类型安全的方法是这样做：`MyOtherFacet(address(this)).myFunction(arg1, arg2)`
5. 调用在另一个面中定义的外部函数的一种更节省 gas 的方法是使用 delegatecall。以下是这样做的示例：
```solidity
DiamondStorage storage ds = diamondStorage();
bytes4 functionSelector = bytes4(keccak256("myFunction(uint256)"));
// get facet address of function
address facet = ds.selectorToFacet[functionSelector];
bytes memory myFunctionCall = abi.encodeWithSelector(functionSelector, 4);
(bool success, bytes memory result) = address(facet).delegatecall(myFunctionCall);
```
6. 你可以创建外部函数的内部函数版本，而不是调用在另一个面中定义的外部函数。将内部版本的函数添加到需要使用它的面中。

### 面可以是可重用和可组合的

已部署的面可以被任意数量的钻石使用。

不同组合的面可以与不同的钻石一起使用。

可以创建和部署一组面，这些面在不同的钻石中重复使用。

能够将相同的已部署面用于多个钻石可以降低部署成本。

可以以使其可用/可组合/与其他面兼容的方式实现面。也可以以使其不可用/不可组合/与其他面不兼容的方式实现面。

函数签名是函数的名称及其参数类型。示例函数签名：`myfunction(uint256)`。一个限制是，两个具有相同函数签名的外部函数不能同时添加到同一个钻石中，因为钻石或任何合约不能有两个具有相同函数签名的外部函数。

面中的所有函数不必都添加到钻石中。面中的某些函数可以添加到钻石中，而面中的其他函数则不添加到钻石中。

## 向后兼容性

该标准使可升级的钻石与未来的标准和功能兼容，因为可以添加新函数并替换或移除现有函数。

## 参考实现

完整参考实现的所有 Solidity 代码已放入一个文件中：[Diamond.sol](../assets/eip-2535/reference/Diamond.sol)

相同的参考实现已组织成多个文件和目录，并且还包括部署脚本和测试。将其作为 zip 文件下载：[`EIP2535-Diamonds-Reference-Implementation.zip`](../assets/eip-2535/reference/EIP2535-Diamonds-Reference-Implementation.zip)

## 安全考虑

### 所有权和身份验证

> **注意：** 钻石所有权/身份验证的设计和实现**不**是该标准的一部分。该标准和参考实现中给出的示例仅仅是**示例**，说明它可以如何实现。

可以使用此提案创建许多不同的身份验证或所有权方案。身份验证方案可以非常简单或复杂，细粒度或粗粒度。该提案没有以任何方式限制它。例如，所有权/身份验证可以简单到只有一个账户地址有权添加/替换/移除函数。或者一个去中心化自治组织可以有权仅添加/替换/移除某些函数。
共识功能可以实现，例如一个审批功能，多个不同的人调用以批准更改，然后通过 `diamondCut` 函数执行。这些只是示例。

鼓励开发标准和实现对钻石的所有权、控制和认证。

### 使用 `diamondCut` 的任意执行

`diamondCut` 函数允许通过访问钻石的存储（通过 `delegatecall`）进行任意执行。对该函数的访问必须谨慎限制。

### 不要自毁
在一个面中使用 `selfdestruct` 是强烈不建议的。错误使用可能会删除一个钻石或一个面。

### 函数选择器冲突

函数选择器冲突发生在两个不同的函数签名哈希到相同的四字节哈希时。这会导致意外替换钻石中现有的函数，而原本的意图是添加一个新函数。由于适当实现的 `diamondCut` 函数可以防止添加已经存在的函数选择器，因此这种情况是不可能发生的。

### 透明性

每当添加、替换或移除一个或多个函数时，钻石都会发出事件。所有源代码都可以被验证。这使得人们和软件能够监控合约的变化。如果任何不良行为的函数被添加到钻石中，那么它是可以被看到的。

安全和领域专家可以审查钻石的变更历史，以检测任何不当行为的历史。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。