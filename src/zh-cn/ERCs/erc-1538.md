---
eip: 1538
title: 透明合约标准
author: Nick Mudge <nick@perfectabstractions.com>
discussions-to: https://github.com/ethereum/EIPs/issues/1538
status: 撤回
type: 标准跟踪
category: ERC
created: 2018-10-31
---

被 [EIP-2535 Diamond Standard](./eip-2535.md) 替代。

## 简单总结
该标准提供了一种合约架构，使可升级合约灵活、无限制且透明。

透明合约公开记录了对其所做的所有更改的完整历史。

对透明合约的所有更改都以标准格式报告。

## 摘要
透明合约是一种代理合约设计模式，提供以下功能：

1. 一种原子性地添加、替换和移除合约多个函数的方法（同时进行）。
1. 标准事件显示从合约中添加、替换和移除的函数，以及更改的原因。
2. 一种标准方式查询合约，以发现和检索其公开的所有函数的信息。
3. 解决了 24KB 最大合约大小限制，使透明合约的最大合约大小在实际中是无限的。该标准使得对合约大小的担忧成为过去。
4. 使可升级合约在未来如有需要可以变得不可变。

## 动机
以太坊合约的一个基本好处是其代码是不可变的，从而通过无信任获得信任。如果合约无法更改，人们就不必信任他人。

然而，无法更改的无信任合约的一个根本问题是它们无法被更改。

#### 缺陷

缺陷和安全漏洞无意中被写入不可变合约中，导致其失效。

#### 改进

不可变的、无信任的合约无法改进，导致随着时间的推移合约质量逐渐下降。

合约标准不断演变，新的标准不断出现。人们、团体和组织随着时间的推移了解人们想要什么、什么更好以及下一步应该构建什么。无法改进的合约不仅阻碍了创建它们的作者，也阻碍了所有使用它们的人。

#### 可升级合约与集中式私有数据库
为什么要使用可升级合约而不是集中式、私有、可变的数据库？
以下是一些原因：
1. 由于存储数据和验证代码的开放性，可以展示可证明的可信历史。
2. 由于开放性，坏行为可以在发生时被发现和报告。
3. 独立的安全和领域专家可以审查合约的变更历史，并为其可信历史作证。
4. 可升级合约可以在未来变得不可变和无信任。
5. 可升级合约可以有部分不可升级，因此是部分不可变和无信任的。

#### 不可变性

在某些情况下，不可变的、无信任的合约是合适的。这种情况发生在合约只需要短时间使用，或者事先知道永远没有理由去更改或改进它。

### 中间地带

透明合约提供了不可变的无信任合约（无法改进）与可升级合约（无法信任）之间的中间地带。

### 目的

1. 创建可升级合约，通过展示可证明的可信历史来获得信任。
2. 记录合约的发展，使其发展和变更是可证明的公开且可理解的。
3. 创建可升级合约，如果需要，可以在未来变得不可变。
4. 创建不受最大大小限制的合约。

### 好处与用例
该标准适用于以下受益的用例：
1. 原子性地添加、替换或移除合约多个函数的能力（同时进行）。
2. 每次添加、替换或移除函数时，都会通过事件进行记录。
3. 通过展示对合约所做的所有更改来建立信任。
4. 无限的合约大小。
5. 查询合约当前支持的函数的信息的能力。
6. 一个合约地址提供所有所需功能，且不需要被另一个合约地址替换。
7. 合约可以在一段时间内可升级，然后变得不可变。
8. 通过“不可更改的函数”向合约添加无信任的保证。

### 新的软件可能性

该标准使得可以编写一种合约版本控制软件。

可以编写软件和用户界面来过滤合约地址的 `FunctionUpdate` 和 `CommitMessage` 事件。这样的软件可以展示任何实现该标准的合约的完整变更历史。

用户界面和软件还可以使用该标准来协助或自动化合约的更改。

## 规范

> **注意：**
Solidity 的 `delegatecall` 操作码使合约能够从另一个合约执行函数，但它的执行方式就像该函数来自调用合约一样。基本上，`delegatecall` 使合约能够“借用”另一个合约的函数。使用 `delegatecall` 执行的函数会影响调用合约的存储变量，而不是定义函数的合约。

### 一般总结

透明合约将函数调用委托或转发给其他合约，使用 `delegatecode`。

透明合约具有一个 `updateContract` 函数，允许添加、替换或移除多个函数。

每当添加、替换或移除函数时，都会发出事件，以便以标准方式跟踪合约的所有更改。

透明合约是实现并遵循以下设计要点的合约。

### 术语

1. 在本标准中，**委托合约**是一个透明合约的回退函数使用 `delegatecall` 转发函数调用的合约。
2. 在本标准中，**不可更改的函数**是直接在透明合约中定义的函数，因此无法被替换或移除。

### 设计要点

如果合约实现以下设计要点，则它是透明合约：

1. 透明合约是一个包含回退函数、构造函数和零个或多个直接在其中定义的不可更改函数的合约。
2. 透明合约的构造函数将 `updateContract` 函数与实现 ERC1538 接口的合约关联。`updateContract` 函数可以是直接在透明合约中定义的“不可更改的函数”，也可以在委托合约中定义。其他函数也可以在构造函数中与合约关联。
3. 透明合约部署后，通过调用 `updateContract` 函数添加、替换和移除函数。
4. `updateContract` 函数将函数与实现这些函数的合约关联，并发出 `CommitMessage` 和 `FunctionUpdate` 事件以记录函数更改。
5. 每当添加、替换或移除函数时，都会发出 `FunctionUpdate` 事件。每次调用 `updateContract` 函数时，都会发出一次 `CommitMessage` 事件，并在发出任何 `FunctionUpdate` 事件后发出。
6. `updateContract` 函数可以在其 `_functionSignatures` 参数中接受多个函数签名的列表，因此可以同时添加/替换/移除多个函数。
7. 当在透明合约上调用函数时，如果是“不可更改的函数”，则立即执行。否则，执行回退函数。回退函数找到与该函数关联的委托合约，并使用 `delegatecall` 执行该函数。如果该函数没有委托合约，则执行将回退。
8. 透明合约的源代码及其使用的所有委托合约都是公开可见和经过验证的。
透明合约地址是用户交互的地址。透明合约地址永远不会改变。只有通过使用 `updateContracts` 函数，委托地址才可以改变。

通常需要某种身份验证来添加/替换/移除透明合约中的函数，**然而身份验证或所有权的方案不属于此标准**。

### 示例

以下是透明合约实现的示例。请注意，下面的示例仅为**示例**。当合约实现并遵循上述设计要点时，它就是透明合约。

```solidity
pragma solidity ^0.5.7;

contract ExampleTransparentContract {
  // owner of the contract
  address internal contractOwner;
  event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

  // maps functions to the delegate contracts that execute the functions
  // funcId => delegate contract
  mapping(bytes4 => address) internal delegates;

  // maps each function signature to its position in the funcSignatures array.
  // signature => index+1
  mapping(bytes => uint256) internal funcSignatureToIndex;
    
  event CommitMessage(string message);
  event FunctionUpdate(bytes4 indexed functionId, address indexed oldDelegate, address indexed newDelegate, string functionSignature);
  
  // this is an example of an "unchangeable function".
  // return the delegate contract address for the supplied function signature
  function delegateAddress(string calldata _functionSignature) external view returns(address) {
    require(funcSignatureToIndex[bytes(_functionSignature)] != 0, "Function signature not found.");
    return delegates[bytes4(keccak256(bytes(_functionSignature)))];
  }
  
  // add a function using the updateContract function
  // this is an internal helper function
  function addFunction(address _erc1538Delegate, address contractAddress, string memory _functionSignatures, string memory _commitMessage) internal {    
    // 0x03A9BCCF == bytes4(keccak256("updateContract(address,string,string)"))
    bytes memory funcdata = abi.encodeWithSelector(0x03A9BCCF, contractAddress, _functionSignatures, _commitMessage);
    bool success;
    assembly {
      success := delegatecall(gas, _erc1538Delegate, add(funcdata, 0x20), mload(funcdata), funcdata, 0)
    }
    require(success, "Adding a function failed");   
  }

  constructor(address _erc1538Delegate) public {
    contractOwner = msg.sender;
    emit OwnershipTransferred(address(0), msg.sender);

    // adding ERC1538 updateContract function
    bytes memory signature = "updateContract(address,string,string)";
    bytes4 funcId = bytes4(keccak256(signature));
    delegates[funcId] = _erc1538Delegate;
    emit FunctionUpdate(funcId, address(0), _erc1538Delegate, string(signature));
    emit CommitMessage("Added ERC1538 updateContract function at contract creation");
	
    // associate "unchangeable functions" with this transparent contract address
    // prevents function selector clashes with delegate contract functions
    // uses the updateContract function
    string memory functions = "delegateAddress(string)";
    addFunction(_erc1538Delegate, address(this), functions, "Associating unchangeable functions");
	
    // adding ERC1538Query interface functions
    functions = "functionByIndex(uint256)functionExists(string)delegateAddresses()delegateFunctionSignatures(address)functionById(bytes4)functionBySignature(string)functionSignatures()totalFunctions()";    
    // "0x01234567891011121314" is an example address of an ERC1538Query delegate contract
    addFunction(_erc1538Delegate, 0x01234567891011121314, functions, "Adding ERC1538Query functions");
    
    // additional functions could be added at this point
  }

  // Making the fallback function payable makes it work for delegate contract functions 
  // that are payable and not payable.
  function() external payable {
    // Delegate every function call to a delegate contract
    address delegate = delegates[msg.sig];
    require(delegate != address(0), "Function does not exist.");
    assembly {
      let ptr := mload(0x40)
      calldatacopy(ptr, 0, calldatasize)
      let result := delegatecall(gas, delegate, ptr, calldatasize, 0, 0)
      let size := returndatasize
      returndatacopy(ptr, 0, size)
      switch result
      case 0 {revert(ptr, size)}
      default {return (ptr, size)}
    }
  }
}
```
如上例所示，每个函数调用都委托给一个委托合约，除非该函数直接在透明合约中定义（使其成为不可更改的函数）。

构造函数将 `updateContract` 函数添加到透明合约中，然后用于向透明合约添加其他函数。

每次向透明合约添加函数时，都会发出事件 `CommitMessage` 和 `FunctionUpdate`，以记录添加或替换了哪些函数以及原因。

实现 `updateContract` 函数的委托合约实现以下接口：
### ERC1538 接口

```solidity
pragma solidity ^0.5.7;

/// @title ERC1538 Transparent Contract Standard
/// @dev Required interface
///  Note: the ERC-165 identifier for this interface is 0x61455567
interface ERC1538 {
  /// @dev This emits when one or a set of functions are updated in a transparent contract.
  ///  The message string should give a short description of the change and why
  ///  the change was made.
  event CommitMessage(string message);
  
  /// @dev This emits for each function that is updated in a transparent contract.
  ///  functionId is the bytes4 of the keccak256 of the function signature.
  ///  oldDelegate is the delegate contract address of the old delegate contract if
  ///  the function is being replaced or removed.
  ///  oldDelegate is the zero value address(0) if a function is being added for the
  ///  first time.
  ///  newDelegate is the delegate contract address of the new delegate contract if 
  ///  the function is being added for the first time or if the function is being 
  ///  replaced.
  ///  newDelegate is the zero value address(0) if the function is being removed.
  event FunctionUpdate(
    bytes4 indexed functionId, 
    address indexed oldDelegate, 
    address indexed newDelegate, 
    string functionSignature
  );

  /// @notice Updates functions in a transparent contract.
  /// @dev If the value of _delegate is zero then the functions specified 
  ///  in _functionSignatures are removed.
  ///  If the value of _delegate is a delegate contract address then the functions 
  ///  specified in _functionSignatures will be delegated to that address.
  /// @param _delegate The address of a delegate contract to delegate to or zero
  ///        to remove functions.      
  /// @param _functionSignatures A list of function signatures listed one after the other
  /// @param _commitMessage A short description of the change and why it is made
  ///        This message is passed to the CommitMessage event.          
  function updateContract(address _delegate, string calldata _functionSignatures, string calldata _commitMessage) external;  
}
```
### 函数签名字符串格式

`_functionSignatures` 参数的文本格式只是一个函数签名的字符串。例如：`"myFirstFunction()mySecondFunction(string)"` 这种格式易于解析且简洁。

以下是调用 `updateContract` 函数的示例，该函数将 ERC721 标准函数添加到透明合约中：
```javascript
functionSignatures = "approve(address,uint256)balanceOf(address)getApproved(uint256)isApprovedForAll(address,address)ownerOf(uint256)safeTransferFrom(address,address,uint256)safeTransferFrom(address,address,uint256,bytes)setApprovalForAll(address,bool)transferFrom(address,address,uint256)"
tx = await transparentContract.updateContract(erc721Delegate.address, functionSignatures, "Adding ERC721 functions");
```

### 移除函数

通过将 `address(0)` 作为第一个参数传递给 `updateContract` 函数来移除函数。传递的函数列表将被移除。

### 源代码验证

透明合约的源代码和委托合约的源代码应通过第三方来源（如 etherscan.io）以可证明的方式进行验证。
<!--
透明合约必须通过委托合约实现 [ERC-165 标准接口检测标准](./eip-165.md)，方法是使用 `updateContract` 函数添加 `supportsInterface` 函数。ERC1538 标准的 interfaceID 为 `0x61455567`。
-->

### 函数选择器冲突
函数选择器冲突发生在向合约添加一个函数时，该函数的哈希与现有函数的四字节哈希相同。这种情况不太可能发生，但应在 `updateContract` 函数的实现中加以防止。请参见 [ERC1538 的参考实现](https://github.com/mudgen/transparent-contracts-erc1538)，以查看如何防止函数冲突的示例。

### ERC1538Query

可选地，透明合约的函数签名可以存储在透明合约中的数组中，并查询以获取透明合约支持的函数及其委托合约地址。

以下是一个可选接口，用于查询透明合约的函数信息：

```solidity
pragma solidity ^0.5.7;

interface ERC1538Query {
    
  /// @notice Gets the total number of functions the transparent contract has.
  /// @return The number of functions the transparent contract has,
  ///  not including the fallback function.
  function totalFunctions() external view returns(uint256);
	
  /// @notice Gets information about a specific function
  /// @dev Throws if `_index` >= `totalFunctions()`
  /// @param _index The index position of a function signature that is stored in an array
  /// @return The function signature, the function selector and the delegate contract address
  function functionByIndex(uint256 _index) 
    external 
    view 
    returns(
      string memory functionSignature, 
      bytes4 functionId, 
      address delegate
    );
	
  /// @notice Checks to see if a function exists
  /// @param The function signature to check
  /// @return True if the function exists, false otherwise
  function functionExists(string calldata _functionSignature) external view returns(bool);
	
  /// @notice Gets all the function signatures of functions supported by the transparent contract
  /// @return A string containing a list of function signatures
  function functionSignatures() external view returns(string memory);
	
  /// @notice Gets all the function signatures supported by a specific delegate contract
  /// @param _delegate The delegate contract address
  /// @return A string containing a list of function signatures
  function delegateFunctionSignatures(address _delegate) external view returns(string memory);
	
  /// @notice Gets the delegate contract address that supports the given function signature
  /// @param The function signature
  /// @return The delegate contract address
  function delegateAddress(string calldata _functionSignature) external view returns(address);
	
  /// @notice Gets information about a function
  /// @dev Throws if no function is found
  /// @param _functionId The id of the function to get information about
  /// @return The function signature and the contract address
  function functionById(bytes4 _functionId) 
    external 
    view 
    returns(
      string memory signature, 
      address delegate
    );
	
  /// @notice Get all the delegate contract addresses used by the transparent contract
  /// @return An array of all delegate contract addresses
  function delegateAddresses() external view returns(address[] memory);
}
```

请参见 [ERC1538 的参考实现](https://github.com/mudgen/transparent-contracts-erc1538) 以了解如何实现。

从 `delegateFunctionSignatures` 和 `functionSignatures` 函数返回的函数签名列表的文本格式只是一个函数签名的字符串。以下是这样的字符串示例：`"approve(address,uint256)balanceOf(address)getApproved(uint256)isApprovedForAll(address,address)ownerOf(uint256)safeTransferFrom(address,address,uint256)safeTransferFrom(address,address,uint256,bytes)setApprovalForAll(address,bool)transferFrom(address,address,uint256)"`

### 如何部署透明合约
1. 创建并部署一个实现 ERC1538 接口的合约到区块链。如果区块链上已经部署了这样的合约，可以跳过此步骤。
2. 创建你的透明合约，并如上所述添加一个回退函数。你的透明合约还需要一个构造函数来添加 `updateContract` 函数。
3. 将你的透明合约部署到区块链。如果构造函数需要，传入 ERC1538 委托合约的地址。

请参见 [参考实现](https://github.com/mudgen/transparent-contracts-erc1538) 以获取这些合约的示例。

### 依赖于其他委托合约的委托合约的包装合约
在某些情况下，一些委托合约可能需要调用其他委托合约中的外部/公共函数。解决此问题的一种方便方法是创建一个包含所需函数的空实现的合约，并在调用其他委托合约函数的委托合约中导入并扩展此合约。这使得委托合约能够编译，而无需提供已经在其他委托合约中给出的函数的实现。这是一种节省 gas、防止达到最大合约大小限制和防止代码重复的方法。此策略由 @amiromayer 提出。[请参见他的评论以获取更多信息。](https://github.com/ethereum/EIPs/issues/1538#issuecomment-451985155) 解决此问题的另一种方法是使用汇编调用其他委托合约提供的函数。

### 去中心化权威
可以扩展此标准以添加共识功能，例如多个不同人员调用的批准函数，以在通过 `updateContract` 函数提交更改之前批准更改。只有在更改完全获得批准时，才会生效。只有在更改生效时，才应发出 `CommitMessage` 和 `FunctionUpdate` 事件。

## 安全
> 本标准将 **所有者** 视为一个或多个有权添加/替换/移除可升级合约函数的个人。

### 一般

可升级合约的所有者有能力更改、添加或从合约的数据存储中移除数据。合约的所有者还可以代表任何地址在合约中执行任意代码。所有者可以通过向合约添加一个函数来执行任意代码。对于可升级合约而言，这通常是一个问题，并不特定于透明合约。

>**注意：** 合约所有权的设计和实现**不**属于此标准。此标准及参考实现中给出的示例仅为**示例**，说明可以如何实现。

### 不可更改的函数

“不可更改的函数”是指在透明合约中定义的函数，而不是在委托合约中定义的函数。透明合约的所有者无法替换这些函数。不可更改函数的使用是有限的，因为在某些情况下，如果它们读取或写入透明合约的存储数据，仍然可能被操控。从透明合约的存储中读取的数据可能已被合约的所有者更改。写入透明合约存储的数据可以被合约的所有者撤销或更改。
在某些情况下，不可更改的函数为透明合约提供了无信任的保证。

### 透明性

实现此标准的合约在每次添加、替换或删除函数时都会发出事件。这使得人们和软件能够监控合约的变化。如果合约中添加了任何不当行为的函数，则可以看到。为了遵守此标准，透明合约及其委托合约的所有源代码必须公开可用并经过验证。

安全和领域专家可以审查任何透明合约的变更历史，以检测任何不当行为的历史。

## 理由

### 使用函数签名字符串而不是 bytes4[] 数组的函数选择器

`updateContract` 函数接受一个 `string` 类型的函数签名列表作为参数，而不是 `bytes4[]` 数组的函数选择器，原因有三：

1. 传入函数签名使得 `updateContract` 的实现能够防止选择器冲突。
2. 此标准的一个主要部分是通过使人们更容易看到随时间变化的内容和原因，从而使可升级合约更加透明。当添加、替换或删除函数时，其函数签名会包含在发出的 FunctionUpdate 事件中。这使得编写软件来过滤合约事件以显示随时间添加/删除和更改的函数变得相对简单，而无需访问合约的源代码或 ABI。如果只提供四字节函数选择器，则无法实现这一点。
3. 通过查看透明合约的源代码，无法看到它支持的所有函数。这就是 ERC1538Query 接口存在的原因，以便人们和软件可以查找、检查或显示透明合约当前支持的所有函数。使用函数签名是为了使 ERC1538Query 函数能够显示它们。

### gas 考虑

委托函数调用确实会有一些 gas 开销。这通过两种方式得以缓解：
1. 委托合约可以很小，从而降低 gas 成本。因为在具有许多函数的合约中调用函数的 gas 成本高于在具有少量函数的合约中调用函数的成本。
2. 由于透明合约没有最大大小限制，因此可以为用例添加优化 gas 的函数。例如，有人可以使用透明合约来实现 ERC721 标准，并从 [ERC1412 标准](https://github.com/ethereum/EIPs/issues/1412) 实现批量转移函数，以帮助减少 gas（并使批量转移更方便）。

### 存储

该标准未指定透明合约如何存储或组织数据。但这里有一些建议：

**继承存储**

1. 透明合约的存储变量由透明合约源代码中定义的存储变量和已添加的委托合约的源代码中的存储变量组成。

2. 委托合约可以使用透明合约中存在的任何存储变量，只要它在其中定义了所有存在的存储变量，并且按顺序定义，包括正在使用的存储变量。

3. 委托合约可以创建新的存储变量，只要它按顺序定义了透明合约中存在的所有存储变量。

以下是实现继承存储的简单方法：

1. 创建一个存储合约，包含您的透明合约和委托合约将使用的存储变量。
2. 使您的委托合约继承存储合约。
3. 如果您想添加一个新的委托合约以添加新的存储变量，则创建一个新的存储合约，添加新的存储变量并继承旧的存储合约。使用您的新存储合约与您的新委托合约。
4. 对于每个新的委托合约，重复步骤 2 或 3。

**非结构化存储**

使用汇编在特定存储位置存储和读取数据。这种方法的一个优点是，如果之前使用的存储位置未被委托合约使用，则不必在委托合约中定义或提及它们。

**永恒存储**

可以使用基于数据类型的通用 API 存储数据。[有关更多信息，请参见 ERC930。](https://github.com/ethereum/EIPs/issues/930)

### 变为不可变

可以使透明合约变为不可变。这是通过调用 `updateContract` 函数来移除 `updateContract` 函数来实现的。移除后，将不再可能添加、替换和删除函数。

### 函数版本

软件或用户可以通过获取函数的委托合约地址来验证调用的函数版本。这可以通过调用 ERC1538Query 接口中的 `delegateAddress` 函数来完成（如果已实现）。该函数接受一个函数签名作为参数，并返回实现该函数的委托合约地址。

### 最佳实践、工具和更多信息

> 需要开发和发布有关透明合约的更多信息、工具、教程和最佳实践。

以下是有关透明合约及其使用的不断增长的文章列表。如果您有关于透明合约的文章想要分享，请在此问题中提交评论以将其添加。

[ERC1538: 未来保障智能合约和代币](https://coinjournal.net/erc1538-future-proofing-smart-contacts-and-tokens/)

[ERC1538 改进“透明合约”标准](https://www.crypto-economy.net/en/ethereum-eth-erc1538-transparent-contract-standard/)

### 灵感

该标准的灵感来自 ZeppelinOS 实现的 [使用虚表的可升级性](https://github.com/zeppelinos/labs/tree/master/upgradeability_with_vtable)。

该标准还受到 [Mokens 合约](https://etherscan.io/address/0xc1eab49cf9d2e23e43bcf23b36b2be14fc2f8838#code) 的设计和实现的启发，来自 [Mokens 项目](https://github.com/Mokens/MIPs/blob/master/MIPS/mip-2-Goals-and-Objectives.md)。Mokens 合约已被 [升级以实现此标准](https://etherscan.io/address/0x0ac5637fe62ec14fd9e237a81a9679d4adef701f#code)。

## 向后兼容性

该标准使合约与未来标准和功能兼容，因为可以添加新函数并替换或删除现有函数。

该标准为合约提供了未来保障。

## 实现

此标准的参考实现见于 [transparent-contracts-erc1538](https://github.com/mudgen/transparent-contracts-erc1538) 仓库。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。