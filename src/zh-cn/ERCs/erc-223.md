---
eip: 223
title: 带有交易处理模型的代币
description: 带有交易处理模型的代币，设计为与本地货币（以太币）行为相同
author: Dexaran (@Dexaran) <dexaran@ethereumclassic.org>
discussions-to: https://ethereum-magicians.org/t/erc-223-token-standard/12894
status: Final
type: Standards Track
category: ERC
created: 2017-05-03
---

## 摘要

以下描述了一个可替代代币的接口和逻辑，支持 `tokenReceived` 回调，以在代币接收时通知合约接收者。这使得代币的行为与以太币相同。

## 动机

该代币引入了一种合约通信模型，可用于规范与此类代币交互的合约行为。具体而言，该提案：

1. 通知接收合约即将到来的代币转移，而不是 [ERC-20](./eip-20.md)，在该标准中，代币转移的接收者不会收到通知。
2. 在向合约存入代币时更具 gas 效率。
3. 允许对金融转移进行 `_data` 记录。

## 规范

打算接收这些代币的合约必须实现 `tokenReceived`。

未实现以下所述 `tokenReceived` 的合约接收代币转移时必须回退。

### 代币合约

#### 代币方法

##### `totalSupply`

```solidity
function totalSupply() view returns (uint256)
```

返回代币的总供应量。该方法的功能与 ERC-20 相同。

##### `name`

```solidity
function name() view returns (string memory)
```

返回代币的名称。该方法的功能与 ERC-20 相同。

可选 - 此方法可用于提高可用性，但接口和其他合约不得期望这些值存在。

##### `symbol`

```solidity
function symbol() view returns (string memory)
```

返回代币的符号。该方法的功能与 ERC-20 相同。

可选 - 此方法可用于提高可用性，但接口和其他合约不得期望这些值存在。

##### `decimals`

```solidity
function decimals() view returns (uint8)
```

返回代币的小数位数。该方法的功能与 ERC-20 相同。

可选 - 此方法可用于提高可用性，但接口和其他合约不得期望这些值存在。

##### `balanceOf`

```solidity
function balanceOf(address _owner) view returns (uint256)
```

返回另一个地址为 `_owner` 的账户余额。该方法的功能与 ERC-20 相同。

##### `transfer(address, uint)`

```solidity
function transfer(address _to, uint _value) returns (bool)
```

此函数必须转移代币，如果 `_to` 是合约，则必须调用 `_to` 的 `tokenReceived(address, uint256, bytes calldata)` 函数。如果 `_to`（接收合约）未实现 `tokenReceived` 函数，则交易必须失败，代币转移必须回退。
如果 `_to` 是外部拥有地址，则交易必须在不执行 `_to` 中的 `tokenReceived` 的情况下发送。
 `_data` 可以附加到此代币交易，但需要更多的 gas。`_data` 可以为空。

`_to` 的 `tokenReceived` 函数必须在所有其他操作之后调用，以避免重入攻击。

注意：如果 `transfer` 函数是 `payable` 并且以太币被存入，则存入的以太币金额必须与代币一起交付给 `_to` 地址。如果以太币与代币一起以这种方式发送，则必须首先交付以太币，然后更新代币余额，然后如果 `_to` 是合约，则必须调用 `tokenReceived` 函数。

##### `transfer(address, uint, bytes)`

```solidity
function transfer(address _to, uint _value, bytes calldata _data) returns (bool)
```

此函数必须转移代币并在 `_to` 中调用 `tokenReceived (address, uint256, bytes)` 函数，如果 `_to` 是合约。如果 `_to`（接收合约）未实现 `tokenReceived` 函数，则交易必须失败，代币转移不得发生。
如果 `_to` 是外部拥有地址（通过代码大小为零来确定），则交易必须在不执行 `_to` 中的 `tokenReceived` 的情况下发送。
 `_data` 可以附加到此代币交易，但需要更多的 gas。`_data` 可以为空。

注意：检查 `_to` 是合约还是地址的一种可能方法是组装 `_to` 的代码。如果 `_to` 中没有代码，则这是一个外部拥有地址，否则它是一个合约。如果 `transfer` 函数是 `payable` 并且以太币被存入，则存入的以太币金额必须与代币一起交付给 `_to` 地址。

`_to` 的 `tokenReceived` 函数必须在所有其他操作之后调用，以避免重入攻击。

#### 事件

##### `Transfer`

```solidity
event Transfer(address indexed _from, address indexed _to, uint256 _value, bytes _data)
```

当代币被转移时触发。与 ERC-20 的 `Transfer` 事件兼容且类似。

### [ERC-223](./eip-223.md) 代币接收器

#### 接收器方法

```solidity
function tokenReceived(address _from, uint _value, bytes calldata _data) returns (bytes4)
```

处理代币转移的函数，当代币持有者发送代币时从代币合约调用。`_from` 是代币发送者的地址，`_value` 是即将到来的代币数量，`_data` 是附加数据，类似于以太交易的 `msg.data`。它的工作原理类似于以太交易的回退函数，并且不返回任何内容。

注意：在 `tokenReceived` 函数中，`msg.sender` 将是代币合约。过滤发送了哪些代币（通过代币合约地址）可能很重要。代币发送者（发起代币交易的人）将在 `tokenReceived` 函数中为 `_from`。`tokenReceived` 函数在处理完即将到来的代币转移后必须返回 `0x8943ec02`。`tokenReceived` 函数调用可以通过接收合约的回退函数处理（在这种情况下，它可能不会返回魔法值 0x8943ec02）。

重要：此函数必须命名为 `tokenReceived`，并接受参数 `address`、`uint256`、`bytes` 以匹配函数签名 `0x8943ec02`。此函数可以由 EOA 手动调用。

## 理由

该标准通过强制 `transfer` 在目标地址执行处理函数引入了一种通信模型。这是一个重要的安全考虑，因为接收者必须明确实现代币处理函数。在接收者未实现此类函数的情况下，转移必须回退。

该标准坚持推送交易模型，其中资产的转移在发送方发起并在接收方处理。因此，ERC-223 转移在处理存入合约时更具 gas 效率，因为 ERC-223 代币可以通过一次交易存入，而 ERC-20 代币至少需要两次调用（一次用于 `approve`，第二次将调用 `transferFrom`）。

- [ERC-20](./eip-20.md) 存入：`approve` ~46 gas，`transferFrom` ~75K gas

- ERC-223 存入：`transfer` 和在接收方处理 ~54K gas

该标准引入了通过允许在接收方处理任何交易并拒绝不正确或不当转移来纠正用户错误的能力。这些代币利用一种转移方法处理与合约和外部拥有地址的两种交互，这可以简化用户体验并避免可能的用户错误。
一个常用的 [ERC-20](./eip-20.md) 标准的缺点是 ERC-223 旨在解决的，即 [ERC-20](./eip-20.md) 实现了两种代币转移的方法：（1）`transfer` 函数和（2）`approve + transferFrom` 模式。[ERC-20](./eip-20.md) 标准的转移函数不会通知接收者，因此如果任何代币通过 `transfer` 函数发送到合约，接收者将无法识别此转移，代币可能会卡在接收者地址中，无法恢复。[ERC-20](./eip-20.md) 标准将确定转移方法的责任放在用户身上，如果选择了错误的方法，用户可能会失去转移的代币。ERC-223 自动确定转移方法，防止用户因选择错误的方法而失去代币。

ERC-223 旨在简化与旨在处理代币的合约的交互。ERC-223 采用类似于普通以太的“存款”模式。对合约的 ERC-223 存款是对 `transfer` 函数的简单调用。这是一个交易，而不是 `approve + transferFrom` 存款的两步过程。

该标准允许使用 `bytes calldata _data` 参数将有效载荷附加到交易中，这可以在目标地址中编码第二个函数调用，类似于以太交易中的 `msg.data`，或者在必要时允许在链上进行公共日志记录，以便进行金融交易。

## 向后兼容性

该代币的接口与 ERC-20 类似，大多数函数的作用与 ERC-20 中的对应函数相同。
`transfer(address, uint256, bytes calldata)` 函数与 ERC-20 接口不向后兼容。

ERC-20 代币可以通过 `transfer` 函数发送到非合约地址。ERC-20 代币可以通过 `approve` + `transferFrom` 模式存入合约地址。通过 `transfer` 函数将 ERC-20 代币存入合约地址将始终导致接收合约无法识别代币存款。

以下是处理 ERC-20 代币存款的合约代码示例。以下合约可以接受 `tokenA` 存款。无法防止非 tokenA 的存款进入此合约。如果通过 `transfer` 函数存入 tokenA，则会导致存款人损失代币，因为用户在 tokenA 合约中的余额将减少，但 `ERC20Receiver` 中的 `deposits` 变量的值不会增加，即存款不会被记入。截至 2023 年 5 月 9 日，**在以太坊主网上已经有价值 2.01 亿美元的 50 种被检查的 ERC-20 代币以这种方式丢失**。

```solidity
contract ERC20Receiver
{
    address tokenA;
    mapping (address => uint256) deposits;
    function deposit(uint _value, address _token) public
    {
        require(_token == tokenA);
        IERC20(_token).transferFrom(msg.sender, address(this), _value);
        deposits[msg.sender] += _value;
    }
}
```

ERC-223 代币必须以与 `transfer` 函数相同的方式发送到非合约地址或合约地址。

以下是处理 ERC-223 代币存款的合约代码示例。以下合约可以过滤代币，仅接受 `tokenA`。其他 ERC-223 代币将被拒绝。

```solidity
contract ERC223Receiver
{
    address tokenA;
    mapping (address => uint256) deposits;
    function tokenReceived(address _from, uint _value, bytes memory _data) public returns (bytes4)
    {
        require(msg.sender == tokenA);
        deposits[_from] += _value;
        return 0x8943ec02;
    }
}
```

## 安全考虑

该代币采用类似于普通以太的模型。因此，必须考虑重放问题。

### 参考实现

```solidity
pragma solidity ^0.8.19;

library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * This test is non-exhaustive, and there may be false-negatives: during the
     * execution of a contract's constructor, its address will be reported as
     * not containing a contract.
     *
     * > It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies in extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }
}

abstract contract IERC223Recipient {
/**
 * @dev Standard ERC-223 receiving function that will handle incoming token transfers.
 *
 * @param _from  Token sender address.
 * @param _value Amount of tokens.
 * @param _data  Transaction metadata.
 */
    function tokenReceived(address _from, uint _value, bytes memory _data) public virtual returns (bytes4);
}

/**
 * @title Reference implementation of the ERC223 standard token.
 */
contract ERC223Token {

     /**
     * @dev Event that is fired on successful transfer.
     */
    event Transfer(address indexed from, address indexed to, uint value, bytes data);

    string  private _name;
    string  private _symbol;
    uint8   private _decimals;
    uint256 private _totalSupply;
    
    mapping(address => uint256) private balances; // List of user balances.

    /**
     * @dev Sets the values for {name} and {symbol}, initializes {decimals} with
     * a default value of 18.
     *
     * To select a different value for {decimals}, use {_setupDecimals}.
     *
     * All three of these values are immutable: they can only be set once during
     * construction.
     */
     
    constructor(string memory new_name, string memory new_symbol, uint8 new_decimals)
    {
        _name     = new_name;
        _symbol   = new_symbol;
        _decimals = new_decimals;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view returns (string memory)
    {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view returns (string memory)
    {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5,05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC223} uses, unless {_setupDecimals} is
     * called.
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC223-balanceOf} and {IERC223-transfer}.
     */
    function decimals() public view returns (uint8)
    {
        return _decimals;
    }

    /**
     * @dev See {IERC223-totalSupply}.
     */
    function totalSupply() public view returns (uint256)
    {
        return _totalSupply;
    }

    /**
     * @dev See {IERC223-standard}.
     */
    function standard() public view returns (string memory)
    {
        return "223";
    }

    
    /**
     * @dev Returns balance of the `_owner`.
     *
     * @param _owner   The address whose balance will be returned.
     * @return balance Balance of the `_owner`.
     */
    function balanceOf(address _owner) public view returns (uint256)
    {
        return balances[_owner];
    }
    
    /**
     * @dev Transfer the specified amount of tokens to the specified address.
     *      Invokes the `tokenFallback` function if the recipient is a contract.
     *      The token transfer fails if the recipient is a contract
     *      but does not implement the `tokenFallback` function
     *      or the fallback function to receive funds.
     *
     * @param _to    Receiver address.
     * @param _value Amount of tokens that will be transferred.
     * @param _data  Transaction metadata.
     */
    function transfer(address _to, uint _value, bytes calldata _data) public returns (bool success)
    {
        // Standard function transfer similar to ERC20 transfer with no _data .
        // Added due to backwards compatibility reasons .
        balances[msg.sender] = balances[msg.sender] - _value;
        balances[_to] = balances[_to] + _value;
        if(Address.isContract(_to)) {
            IERC223Recipient(_to).tokenReceived(msg.sender, _value, _data);
        }
        emit Transfer(msg.sender, _to, _value, _data);
        return true;
    }
    
    /**
     * @dev Transfer the specified amount of tokens to the specified address.
     *      This function works the same with the previous one
     *      but doesn't contain `_data` param.
     *      Added due to backwards compatibility reasons.
     *
     * @param _to    Receiver address.
     * @param _value Amount of tokens that will be transferred.
     */
    function transfer(address _to, uint _value) public returns (bool success)
    {
        bytes memory _empty = hex"00000000";
        balances[msg.sender] = balances[msg.sender] - _value;
        balances[_to] = balances[_to] + _value;
        if(Address.isContract(_to)) {
            IERC223Recipient(_to).tokenReceived(msg.sender, _value, _empty);
        }
        emit Transfer(msg.sender, _to, _value, _empty);
        return true;
    }
}
```

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。