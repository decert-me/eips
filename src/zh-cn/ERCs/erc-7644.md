---
eip: 7644
title: ERC-721 名称注册扩展
description: 为 ERC-721 中的每个代币扩展一个时间限制的唯一名称，利用名称注册进行注册和管理。
author: 陈辽源 (@chenly)
discussions-to: https://ethereum-magicians.org/t/erc-7644-erc-721-name-registry-extension/19022
status: 草案
type: 标准跟踪
category: ERC
created: 2024-03-01
requires: 721
---

## 摘要

此扩展定义了一个接口，为 [ERC-721](./eip-721.md) 代币添加命名机制。它允许每个代币拥有一个具有设定到期日期的唯一名称，确保在当前 NFT 合同内的唯一性。该接口包括分配、更新和查询名称及其相关代币的功能，确保名称在到期之前保持唯一。负责设置名称的实体取决于使用此扩展时的具体用例场景。

## 动机

随着去中心化域名注册方法与 NFT 的结合发展，我们看到将这一范式扩展到用户名领域的机会。通过将代币 ID 与用户名关联，我们增强了去中心化生态系统中实体的直观识别。

这种整合服务于多个目的：

- **直观性：** 数字代币 ID 缺乏直观识别。通过引入用户名，代币 ID 变得更具代表性，提高了可用性。
  
- **用户名经济探索：** 注册机制为探索用户名经济开辟了途径，提供身份验证和社交互动等好处。
  
- **与 NFT 的协同作用：** 用户名与 NFT 的融合解锁了协同增长，启用认证社交互动和个性化数字资产等新应用。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

此扩展的实现者 **MUST** 具备以下所有功能：

```solidity
pragma solidity ^0.8.0;

/**
 * @title INameRegistry
 * @dev Interface for the NameRegistry smart contract.
 * This interface allows interaction with a NameRegistry, 
 * enabling the registration, management, and lookup of names 
 * with associated expiry dates tied to specific tokens.
 */
interface IERC7644 /* is IERC721 */ {

    /**
     * @dev Emitted when the name of a token is changed.
     * @param tokenId The token ID whose name is changed.
     * @param oldName The previous name of the token.
     * @param newName The new name assigned to the token.
     * @param expiryDate The expiry date of the new name registration.
     */
    event NameChanged(uint256 indexed tokenId, bytes32 oldName, bytes32 newName, uint256 expiryDate);

    /**
     * @dev Returns the name of the specified token, if the name has not expired.
     * @param tokenId The token ID to query for its name.
     * @return The name of the token, or an empty bytes32 if no name is set or it has expired.
     */
    function nameOf(uint256 tokenId) external view returns (bytes32);

    /**
     * @dev Returns the token ID associated with a given name, if the name registration has not expired.
     * @param _name The name to query for its associated token ID.
     * @return The token ID associated with the name, or zero if no token is found or the name has expired.
     */
    function tokenIdOf(bytes32 _name) external view returns (uint256);

    /**
     * @dev Allows a token owner to set or update the name of their token, subject to a duration for the name's validity.
     * @param tokenId The token ID whose name is to be set or updated.
     * @param _name The new name to assign to the token.
     * @param duration The duration in seconds for which the name is valid, starting from the time of calling this function.
     * Note: The name must be unique and not currently in use by an active (non-expired) registration.
     */
    function setName(uint256 tokenId, bytes32 _name, uint256 duration) external;

    /**
     * @dev Returns the tokenId and expiryDate for a given name, if the name registration has not expired.
     * @param _name The name to query for its associated token ID and expiry date.
     * @return tokenId The token ID associated with the name.
     * @return expiryDate The expiry date of the name registration.
     */
    function nameInfo(bytes32 _name) external view returns (uint256 tokenId, uint256 expiryDate);
	
}
```

## 理由

#### 名称到期

通过为用户名实施到期周期，我们引入了几个优势。此机制确保了一个动态环境，在该环境中，未使用或过时的用户名可以被释放，促进健康的生态系统。它鼓励用户名的周转，防止长期囤积，促进积极参与。用户被激励管理他们的用户名组合，续订有价值的名称，同时放弃不相关的名称。最终，这促进了公平和效率，确保命名资源得到有效利用，并根据不断变化的需求进行更新。

#### 名称唯一性

强制执行唯一用户名对于维护清晰和直观的识别系统至关重要。它防止混淆，并使去中心化生态系统中的无缝互动成为可能。唯一的用户名增强了可发现性，并促进了交易和社交互动中的信任。这一要求强调了在去中心化环境中清晰度的重要性，在这些环境中，精确识别对于建立信任和促进高效互动至关重要。

#### 名称注册系统

引入用户名注册系统可以防止滥用行为，并促进对命名资源的公平访问。预留和续订机制防止对理想用户名的垄断，同时使合法用户能够确保感兴趣的名称。预留确保了公平的机会来认领所需的用户名，防止囤积和投机活动。续订机制鼓励积极参与和对命名生态系统的投资。这些功能共同创造了一个平衡和包容的环境，促进了用户社区的活力。

## 向后兼容性

该标准完全兼容 [ERC-721](./eip-721.md)。

## 参考实现

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";

contract ERC7644 is ERC721 {
    event NameChanged(uint256 indexed tokenId, bytes32 oldName, bytes32 newName, uint256 expiryDate);

    struct NameRegistration {
        uint256 tokenId;
        uint256 expiryDate;
    }

    mapping(uint256 => bytes32) private _tokenNames;
    mapping(bytes32 => NameRegistration) private _nameRegistrations;
    mapping(uint256 => uint256) private _lastSetNameTime;

    uint256 public constant MAX_DURATION = 10 * 365 days;
    uint256 public constant MIN_SET_NAME_INTERVAL = 1 days;

    constructor() ERC721("Asd Token", "ASDT") {}

    function nameOf(uint256 tokenId) public view returns (bytes32) {
        if(_tokenNames[tokenId] != bytes32(0) && _nameRegistrations[_tokenNames[tokenId]].expiryDate > block.timestamp)
        {
            return _tokenNames[tokenId];
        }else{
            return bytes32(0);
        }
    }

    function tokenIdOf(bytes32 _name) public view returns (uint256) {
        require(_nameRegistrations[_name].expiryDate > block.timestamp, "NameRegistry: Name expired");
        if(_nameRegistrations[_name].tokenId > 0)
        {
            return _nameRegistrations[_name].tokenId;
        }else{
            return uint256(0);
        }
    }

    function setName(uint256 tokenId, bytes32 _name, uint256 duration) public {
        require(ownerOf(tokenId) == msg.sender, "NameRegistry: Caller is not the token owner");
        require(duration <= MAX_DURATION, "NameRegistry: Duration exceeds maximum limit");
        require(block.timestamp - _lastSetNameTime[tokenId] >= MIN_SET_NAME_INTERVAL, "NameRegistry: Minimum interval not met");
        require(tokenIdOf(_name) == uint256(0) || tokenIdOf(_name) == tokenId, "NameRegistry: Name already in use and not expired");

        bytes32 oldName = _tokenNames[tokenId];
        uint256 expiryDate = block.timestamp + duration;
        _setTokenName(tokenId, _name, expiryDate);

        emit NameChanged(tokenId, oldName, _name, expiryDate);

        _lastSetNameTime[tokenId] = block.timestamp;
    }

    function nameInfo(bytes32 _name) public view returns (uint256, uint256) {
        require(_nameRegistrations[_name].tokenId > 0 && _nameRegistrations[_name].expiryDate > block.timestamp, "NameRegistry: Name expired or does not exist");
        NameRegistration memory registration = _nameRegistrations[_name];
        return (registration.tokenId, registration.expiryDate);
    }

    function _setTokenName(uint256 tokenId, bytes32 _name, uint256 expiryDate) internal {
        _tokenNames[tokenId] = _name;
        _nameRegistrations[_name] = NameRegistration(tokenId, expiryDate);
    }
}
```

## 安全考虑

#### 减轻滥用行为和资源囤积

该设计包括防止滥用行为和资源囤积的机制。设定名称的最小间隔和名称到期的最大持续时间，以遏制垃圾邮件和恶意攻击，限制快速连续的名称注册，并鼓励对命名资源的公平和高效使用。这些措施减轻了潜在的安全风险，确保名称不能被无限期垄断，并促进所有用户的可持续和公平环境。

#### 用户名限制

为了促进索引和 gas 效率，用户名应遵循 3 到 32 个字符的长度限制。此范围防止注册过长的名称，这在 gas 成本上可能很高且难以管理。将字符限制在 [a-zA-Z0-9] 范围内增强了可读性，并通过限制特殊字符的使用来防止命名系统的滥用，这些特殊字符可能会使域名解析或用户识别变得复杂。实施这些限制不仅促进了生态系统内的高可用性，还防止了垃圾注册的泛滥，确保注册处对真实用户保持可访问和功能性。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。