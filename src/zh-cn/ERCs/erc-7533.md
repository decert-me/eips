---
eip: 7533
title: 公共跨链端口
description: 帮助桥接连接所有 EVM 链
author: George (@JXRow), Zisu (@lazy1523)
discussions-to: https://ethereum-magicians.org/t/connect-all-l2s/15534
status: 草案
type: 标准跟踪
category: ERC
created: 2023-10-11
---


## 摘要

公共跨链端口（PCP）的目标是安全高效地连接各种 EVM 链。它用从多个链拉取消息的方法替代了向多个链推送消息的方法，显著减少了跨链桥的数量和 gas 成本，随着更多跨链桥项目基于 PCP 构建，整体安全性也随之提高。


## 动机

目前，L2 和 L1 之间有官方的跨链桥，但 L2 之间没有。如果有 10 条 L2 链需要相互跨链，则需要 10 x 9 = 90 条跨链桥。然而，如果使用拉取机制将其他 9 条链的消息合并为一个同步到其自身链的交易，则只需要 10 条跨链桥。这显著减少了所需的跨链桥数量，并最小化了 gas 成本。

这种实现，借助多个跨链桥项目的参与，将大大增强安全性。目前，跨链桥的冗余建设相当可观，这并没有提高安全性。通过使用标准化的 `SendPort` 合约，如果相同的跨链消息被多个冗余桥传输，目标链的 `IReceivePort` 上的验证应该产生相同的结果。这个结果由多个跨链桥项目确认，提供的安全性远高于依赖单一确认。该 EIP 的目的是鼓励更多跨链桥项目参与，将冗余建设转变为增强的安全性。

为了吸引跨链桥项目参与，除了减少桥的数量和 gas 成本外，在 `SendPort` 中使用 Hash MerkleTree 数据结构确保添加跨链消息不会增加桥的开销。只需一个小型根即可用于跨链桥的传输，进一步节省 gas。


### 用例

该 EIP 将跨链生态系统分为 3 层，并在基础层定义了 `SendPort` 合约和 `IReceivePort` 接口。其他层的实现留给生态系统项目参与者。

![](../assets/eip-7533/0.png)

在跨链消息的基础上，应用程序可以将桥作为服务使用，例如代币跨链。

跨链消息桥可以与代币跨链功能结合使用，如参考实现中的代码示例所示。或者，它们也可以分开。以 NFT 跨链应用为例，它可以重用代币的消息桥，甚至利用多个消息桥。重用多个桥进行消息验证可以显著增强安全性，而不会增加跨链和验证服务的额外成本。


## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

跨链的本质是通知目标链源链上发生的事件。这个过程可以分为 3 个步骤。以下图示说明了整体原理：

![](../assets/eip-7533/1.png)

### 1. 添加跨链消息

在此 EIP 下，每条链上部署一个 `SendPort` 合约。它负责收集该链上的跨链消息并进行打包。`SendPort` 作为一个公共、无权限、无管理员、自动化的系统运行。跨链桥运营商从 `SendPort` 中检索跨链消息并将其传输到目标链，以完成跨链消息传递过程。

`SendPort` 合约可以为多个桥服务，负责收集在该链上发生的事件（即跨链消息）并将其打包成 MerkleTree。例如，假设一个桥合约接收到用户的 USDT 存款。它可以将该事件的哈希和目标链的 ID 发送到 `SendPort` 合约。`SendPort` 将此信息与发送者地址的哈希（即桥合约的地址）一起作为叶子添加到数组中。在收集到一定数量的叶子（例如 1 分钟）后，`SendPort` 会自动将它们打包成 MerkleTree，并开始下一个收集阶段。`SendPort` 的角色仅专注于事件收集和打包。它自主运行，无需管理。因此，无需在每条链上重复部署 `SendPort`，**推荐** 一条链一个 `SendPort`。

`SendPort.addMsgHash()` 函数可以由不同的跨链桥项目或任何其他合约调用。该函数不需要权限，这意味着可能会发送不正确或欺诈的消息。为了防止欺诈，`SendPort` 在打包过程中包含发送者的地址。这表明 `sender` 意图将信息 `msgHash` 发送到 `toChainId` 链。当在目标链上解码此信息时，可以帮助防止欺诈活动。

### 2. 拉取根 & 设置根

在打包新的 MerkleTree 完成后，包裹承运人（通常是跨链桥项目）从多个链中拉取根并将其存储在每条链的 `IReceivePort` 合约中。

一个根包含来自一个源链到多个目标链的消息。对于包裹承运人来说，根 **MAY** 可能不包含相关消息或 **MAY** 可能不包括针对特定目标链的消息。因此，包裹承运人可以根据其相关性决定是否将根传输到特定目标链。

因此，`IReceivePort` 合约并不是唯一的，而是由包裹承运人根据 `IReceivePort` 接口实现。随着多个包裹承运人的出现，将会有多个 `IReceivePort` 合约。

### 3. 验证跨链消息

`IReceivePort` 合约存储每条链的根，允许在提供完整消息时验证消息的真实性。重要的是要注意，根本身不能用来解密消息；它只能用于验证其真实性。完整消息可以从源链的 `SendPort` 合约中检索。

由于根来自同一个 `SendPort`，不同 `IReceivePort` 合约中的根 **SHOULD** 是相同的。换句话说，如果一条消息是可信的，它 **SHOULD** 能够在不同的 `IReceivePort` 合约中被验证为可信。这显著增强了安全性。这类似于多重签名的原则，如果大多数 `IReceivePort` 合约验证一条消息为真实，则它很可能是真的。相反，任何将消息验证为假的 `IReceivePort` 合约可能表明潜在的黑客攻击或相应跨链桥的故障。这种去中心化的参与模型确保系统的安全性不受单点故障的影响。它将冗余建设转变为安全性的提升。
关于数据完整性：

`SendPort` 保留所有根和连续的索引号，不进行删除或修改。每个跨链桥的 `IReceivePort` 合约 **应** 也遵循这种方法。

### `ISendPort` 接口

```solidity
pragma solidity ^0.8.0;

interface ISendPort {
    event MsgHashAdded(uint indexed packageIndex, address sender, bytes32 msgHash, uint toChainId, bytes32 leaf);

    event Packed(uint indexed packageIndex, uint indexed packTime, bytes32 root);

    struct Package {
        uint packageIndex;
        bytes32 root;
        bytes32[] leaves;
        uint createTime;
        uint packTime;
    }

    function addMsgHash(bytes32 msgHash, uint toChainId) external;

    function pack() external;

    function getPackage(uint packageIndex) external view returns (Package memory);

    function getPendingPackage() external view returns (Package memory);
}
```

让：

- `Package`：在一定时间内收集跨链消息并将其打包成一个单一的包。
  - `packageIndex`：包的索引，从 0 开始。
  - `root`：由 MerkleTree 从 `leaves` 生成的根，表示打包的包。
  - `leaves`：每个叶子代表一条跨链消息，它是从 `msgHash`、`sender` 和 `toChainId` 计算得出的哈希。
    - `msgHash`：消息的哈希，从外部合约传入。
    - `sender`：外部合约的地址，无需显式传入。
    - `toChainId`：目标链的链 ID，从外部合约传入。
  - `createTime`：包开始收集消息的时间戳。它也是上一个包打包时的时间戳。
  - `packTime`：包被打包的时间戳。打包后，不能再添加更多的叶子。
- `addMsgHash()`：外部合约将跨链消息的哈希发送到 SendPort。
- `pack()`：手动触发打包过程。通常，当最后一个提交者提交他的消息时，会自动触发。如果等待最后一个提交者的时间过长，可以手动触发打包过程。
- `getPackage()`：检索 SendPort 中的每个包，包括已打包和待处理的包。
- `getPendingPackage()`：检索 SendPort 中的待处理包。

### `IReceivePort` 接口

```solidity
pragma solidity ^0.8.0;

interface IReceivePort {
    event PackageReceived(uint indexed fromChainId, uint indexed packageIndex, bytes32 root);

    struct Package {
        uint fromChainId;
        uint packageIndex;
        bytes32 root;
    }

    function receivePackages(Package[] calldata packages) external;

    function getRoot(uint fromChainId, uint packageIndex) external view returns (bytes32);

    function verify(
        uint fromChainId,
        uint packageIndex,
        bytes32[] memory proof,
        bytes32 msgHash,
        address sender
    ) external view returns (bool);
}
```

让：

- `Package`：在一定时间内收集跨链消息并将其捆绑成一个单一的包。
  - `fromChainId`：包的来源链。
  - `packageIndex`：包的索引，从 0 开始。
  - `root`：由 MerkleTree 从 `leaves` 生成的根，表示打包的包。
- `receivePackages()`：从不同源链的 SendPort 接收多个根。
- `getRoot()`：从特定链检索特定根。
- `verify()`：验证源链上的消息是否由发送者发送。

## 理由

传统方法涉及使用推送方法，如下图所示：

![](../assets/eip-7533/2.png)

如果有 6 条链，每条链需要推送到其他 5 条链，这将导致需要 30 个跨链桥，如下图所示：

![](../assets/eip-7533/3.png)

当 N 条链需要相互进行跨链通信时，所需的跨链桥数量计算为：num = N * (N - 1)。

使用拉取方法可以将来自 5 条链的跨链消息批量处理为 1 个交易，显著减少所需的跨链桥数量，如下图所示：

![](../assets/eip-7533/4.png)

如果每条链从其他 5 条链拉取消息到自己的链上，只需要 6 个跨链桥。对于需要跨链通信的 N 条链，所需的跨链桥数量为：num = N。

因此，拉取方法可以大大减少跨链桥的数量。

MerkleTree 数据结构有效地压缩了跨链消息的大小。无论跨链消息的数量如何，它们都可以压缩为一个根，表示为一个 byte32 值。包承载者只需运输根，从而导致低 gas 成本。

## 向后兼容性

此 EIP 不改变共识层，因此对以太坊整体没有向后兼容性问题。

此 EIP 不改变其他 ERC 标准，因此对以太坊应用程序没有向后兼容性问题。

## 参考实现

以下是一个跨链桥的示例合约：

### `SendPort.sol`

```solidity
pragma solidity ^0.8.0;

import "./ISendPort.sol";

contract SendPort is ISendPort {
    uint public constant PACK_INTERVAL = 6000;
    uint public constant MAX_PACKAGE_MESSAGES = 100;

    uint public pendingIndex = 0;

    mapping(uint => Package) public packages;

    constructor() {
        packages[0] = Package(0, bytes32(0), new bytes32[](0), block.timestamp, 0);
    }

    function addMsgHash(bytes32 msgHash, uint toChainId) public {
        bytes32 leaf = keccak256(
            abi.encodePacked(msgHash, msg.sender, toChainId)
        );
        Package storage pendingPackage = packages[pendingIndex];
        pendingPackage.leaves.push(leaf);

        emit MsgHashAdded(pendingPackage.packageIndex, msg.sender, msgHash, toChainId, leaf);

        if (pendingPackage.leaves.length >= MAX_PACKAGE_MESSAGES) {
            console.log("MAX_PACKAGE_MESSAGES", pendingPackage.leaves.length);
            _pack();
            return;
        }

        // console.log("block.timestamp", block.timestamp);
        if (pendingPackage.createTime + PACK_INTERVAL <= block.timestamp) {
            console.log("PACK_INTERVAL", pendingPackage.createTime, block.timestamp);
            _pack();
        }
    }

    function pack() public {
        require(packages[pendingIndex].createTime + PACK_INTERVAL <= block.timestamp, "SendPort::pack: pack interval too short");

       _pack();
    }

    function getPackage(uint packageIndex) public view returns (Package memory) {
        return packages[packageIndex];
    }

    function getPendingPackage() public view returns (Package memory) {
        return packages[pendingIndex];
    }

    function _pack() internal {
        Package storage pendingPackage = packages[pendingIndex];
        bytes32[] memory _leaves = pendingPackage.leaves;
        while (_leaves.length > 1) {
            _leaves = _computeLeaves(_leaves);
        }
        pendingPackage.root = _leaves[0];
        pendingPackage.packTime = block.timestamp;

        emit Packed(pendingPackage.packageIndex, pendingPackage.packTime, pendingPackage.root);

        pendingIndex = pendingPackage.packageIndex + 1;
        packages[pendingIndex] = Package(pendingIndex, bytes32(0), new bytes32[](0), pendingPackage.packTime, 0);
    }

    function _computeLeaves(bytes32[] memory _leaves) pure internal returns (bytes32[] memory _nextLeaves) {
        if (_leaves.length % 2 == 0) {
            _nextLeaves = new bytes32[](_leaves.length / 2);
            bytes32 computedHash;
            for (uint i = 0; i + 1 < _leaves.length; i += 2) {
                computedHash = _hashPair(_leaves[i], _leaves[i + 1]);
                _nextLeaves[i / 2] = computedHash;
            }

        } else {
            bytes32 lastLeaf = _leaves[_leaves.length - 1];
            _nextLeaves = new bytes32[]((_leaves.length / 2 + 1));
            bytes32 computedHash;
            for (uint i = 0; i + 1 < _leaves.length; i += 2) {
                computedHash = _hashPair(_leaves[i], _leaves[i + 1]);
                _nextLeaves[i / 2] = computedHash;
            }
            _nextLeaves[_nextLeaves.length - 1] = lastLeaf;
        }
    }

    function _hashPair(bytes32 a, bytes32 b) private pure returns (bytes32) {
        return a < b ? _efficientHash(a, b) : _efficientHash(b, a);
    }

    function _efficientHash(bytes32 a, bytes32 b) private pure returns (bytes32 value) {
        /// @solidity memory-safe-assembly
        assembly {
            mstore(0x00, a)
            mstore(0x20, b)
            value := keccak256(0x00, 0x40)
        }
    }
}
```

外部特性：

- `PACK_INTERVAL`：两次连续打包操作之间的最小时间间隔。如果超过此间隔，可以启动新的打包操作。
- `MAX_PACKAGE_MESSAGES`：一旦收集到 `MAX_PACKAGE_MESSAGES` 消息，将立即触发打包操作。这优先于 `PACK_INTERVAL` 设置。

### `ReceivePort.sol`

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "./IReceivePort.sol";

abstract contract ReceivePort is IReceivePort, Ownable {

    //fromChainId => packageIndex => root
    mapping(uint => mapping(uint => bytes32)) public roots;

    constructor() {}

    function receivePackages(Package[] calldata packages) public onlyOwner {
        for (uint i = 0; i < packages.length; i++) {
            Package calldata p = packages[i];
            require(roots[p.fromChainId][p.packageIndex] == bytes32(0), "ReceivePort::receivePackages: package already exist");
            roots[p.fromChainId][p.packageIndex] = p.root;

            emit PackageReceived(p.fromChainId, p.packageIndex, p.root);
        }
    }

    function getRoot(uint fromChainId, uint packageIndex) public view returns (bytes32) {
        return roots[fromChainId][packageIndex];
    }

    function verify(
        uint fromChainId,
        uint packageIndex,
        bytes32[] memory proof,
        bytes32 msgHash,
        address sender
    ) public view returns (bool) {
        bytes32 leaf = keccak256(
            abi.encodePacked(msgHash, sender, block.chainid)
        );
        return _processProof(proof, leaf) == roots[fromChainId][packageIndex];
    }

    function _processProof(bytes32[] memory proof, bytes32 leaf) internal pure returns (bytes32) {
        bytes32 computedHash = leaf;
        for (uint256 i = 0; i < proof.length; i++) {
            computedHash = _hashPair(computedHash, proof[i]);
        }
        return computedHash;
    }

    function _hashPair(bytes32 a, bytes32 b) private pure returns (bytes32) {
        return a < b ? _efficientHash(a, b) : _efficientHash(b, a);
    }

    function _efficientHash(bytes32 a, bytes32 b) private pure returns (bytes32 value) {
        /// @solidity memory-safe-assembly
        assembly {
            mstore(0x00, a)
            mstore(0x20, b)
            value := keccak256(0x00, 0x40)
        }
    }
}
```

### `BridgeExample.sol`

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "./ISendPort.sol";
import "./ReceivePort.sol";

contract BridgeExample is ReceivePort {
    using SafeERC20 for IERC20;

    ISendPort public sendPort;

    mapping(bytes32 => bool) public usedMsgHashes;

    mapping(uint => address) public trustBridges;

    mapping(address => address) public crossPairs;

    constructor(address sendPortAddr) {
        sendPort = ISendPort(sendPortAddr);
    }

    function setTrustBridge(uint chainId, address bridge) public onlyOwner {
        trustBridges[chainId] = bridge;
    }

    function setCrossPair(address fromTokenAddr, address toTokenAddr) public onlyOwner {
        crossPairs[fromTokenAddr] = toTokenAddr;
    }

    function getLeaves(uint packageIndex, uint start, uint num) view public returns(bytes32[] memory) {
        ISendPort.Package memory p = sendPort.getPackage(packageIndex);
        bytes32[] memory result = new bytes32[](num);
        for (uint i = 0; i < p.leaves.length && i < num; i++) {
            result[i] = p.leaves[i + start];
        }
        return result;
    }

    function transferTo(
        uint toChainId,
        address fromTokenAddr,
        uint amount,
        address receiver
    ) public {
        bytes32 msgHash = keccak256(
            abi.encodePacked(toChainId, fromTokenAddr, amount, receiver)
        );
        sendPort.addMsgHash(msgHash, toChainId);

        IERC20(fromTokenAddr).safeTransferFrom(msg.sender, address(this), amount);
    }

    function transferFrom(
        uint fromChainId,
        uint packageIndex,
        bytes32[] memory proof,
        address fromTokenAddr,
        uint amount,
        address receiver
    ) public {
        bytes32 msgHash = keccak256(
            abi.encodePacked(block.chainid, fromTokenAddr, amount, receiver)
        );

        require(!usedMsgHashes[msgHash], "transferFrom: Used msgHash");

        require(
            verify(
                fromChainId,
                packageIndex,
                proof,
                msgHash,
                trustBridges[fromChainId]
            ),
            "transferFrom: verify failed"
        );

        usedMsgHashes[msgHash] = true;

        address toTokenAddr = crossPairs[fromTokenAddr];
        require(toTokenAddr != address(0), "transferFrom: fromTokenAddr is not crossPair");
        IERC20(toTokenAddr).safeTransfer(receiver, amount);
    }
}
```

## 安全考虑

关于跨链桥之间的竞争和双重支付：

`SendPort` 负责一项任务：打包要跨链传输的消息。消息的传输和验证由每个跨链桥项目独立实现。目标是确保不同跨链桥在源链上获得的跨链消息是一致的。因此，跨链桥之间无需竞争运输或验证根的权利。每个桥独立运行。如果某个跨链桥的实现存在漏洞，它对自身构成风险，但不会影响其他跨链桥。

**建议**：

1. 不要让 `IReceivePort.receivePackages()` 被任何人调用。
2. 在进行验证时，存储已验证的 `msgHash`，以避免后续验证中的双重支付。
3. 不要信任 MerkleTree 中的所有发送者。

关于跨链消息的伪造：

由于 `SendPort` 是一个没有使用限制的公共合约，任何人都可以向其发送任意跨链消息。`SendPort` 在打包过程中包括 `msg.sender`。如果黑客试图伪造跨链消息，黑客的地址将与伪造的消息一起包含在打包中。在验证过程中，可以识别出黑客的地址。这就是为什么建议不要信任 MerkleTree 中的所有发送者。

关于消息的顺序：

虽然 `SendPort` 按时间对接收到的跨链消息进行排序，但在验证过程中并不能保证顺序。例如，如果用户先进行 10 ETH 的跨链转账，然后进行 20 USDT 的转账，在目标链上，他可能先提取 20 USDT，然后再提取 10 ETH，反之亦然。具体顺序取决于 `IReceivePort` 的实现。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。