---
eip: 4987
title: 持有代币接口
description: 查询持有代币的所有权和余额的接口
author: Devin Conley (@devinaconley)
discussions-to: https://ethereum-magicians.org/t/eip-4987-held-token-standard-nfts-defi/7117
status: Stagnant
type: Standards Track
category: ERC
created: 2021-09-21
requires: 20, 165, 721, 1155
---

## 摘要

提议的标准定义了一个轻量级接口，用于公开持有代币的功能性所有权和余额。持有代币是由合约拥有的代币。该标准可以由持有 [EIP-20](./erc-20.md)、[EIP-721](./eip-721.md) 或 [EIP-1155](./eip-1155.md) 代币的智能合约实现，旨在被依赖于所有权和余额验证的链上和链下系统使用。

## 动机

随着加密领域（DeFi、NFT 等）的不同领域趋同和可组合性提高，代币的实际所有者（可能是合约）和功能性所有者（可能是用户）之间的区别将变得更加常见。目前，这导致了需要代币存款的机制与依赖这些代币进行所有权或余额验证的系统之间的冲突。

该提案旨在通过提供一个标准接口，使代币持有者能够公开所有权和余额信息，从而解决这一冲突。这将允许用户参与这些 DeFi 机制，而无需放弃现有的代币实用性。总体而言，这将大大提高系统之间的互操作性，惠及用户和协议开发者。

该 ERC 标准的示例实现者包括

- 质押或农场合约
- 借贷池
- 时间锁或归属金库
- 分割 NFT 合约
- 智能合约钱包

该 ERC 标准的示例消费者包括

- 治理系统
- 游戏
- PFP 验证
- 艺术画廊或展示
- 基于代币的会员程序

## 规范

实现 `ERC20` 持有代币标准的智能合约必须实现 `IERC20Holder` 接口中的所有函数。

实现 `ERC20` 持有代币标准的智能合约还必须实现 `ERC165`，并在传入接口 ID `0x74c89d54` 时返回 true。

```solidity
/**
 * @notice the ERC20 holder standard provides a common interface to query
 * token balance information
 */
interface IERC20Holder is IERC165 {
  /**
   * @notice emitted when the token is transferred to the contract
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenAmount held token amount
   */
  event Hold(
    address indexed owner,
    address indexed tokenAddress,
    uint256 tokenAmount
  );

  /**
   * @notice emitted when the token is released back to the user
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenAmount held token amount
   */
  event Release(
    address indexed owner,
    address indexed tokenAddress,
    uint256 tokenAmount
  );

  /**
   * @notice get the held balance of the token owner
   * @dev should throw for invalid queries and return zero for no balance
   * @param tokenAddress held token address
   * @param owner functional token owner
   * @return held token balance
   */
  function heldBalanceOf(address tokenAddress, address owner)
    external
    view
    returns (uint256);
}

```

实现 `ERC721` 持有代币标准的智能合约必须实现 `IERC721Holder` 接口中的所有函数。

实现 `ERC721` 持有代币标准的智能合约还必须实现 `ERC165`，并在传入接口 ID `0x16b900ff` 时返回 true。

```solidity
/**
 * @notice the ERC721 holder standard provides a common interface to query
 * token ownership and balance information
 */
interface IERC721Holder is IERC165 {
  /**
   * @notice emitted when the token is transferred to the contract
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenId held token ID
   */
  event Hold(
    address indexed owner,
    address indexed tokenAddress,
    uint256 indexed tokenId
  );

  /**
   * @notice emitted when the token is released back to the user
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenId held token ID
   */
  event Release(
    address indexed owner,
    address indexed tokenAddress,
    uint256 indexed tokenId
  );

  /**
   * @notice get the functional owner of a held token
   * @dev should throw for invalid queries and return zero for a token ID that is not held
   * @param tokenAddress held token address
   * @param tokenId held token ID
   * @return functional token owner
   */
  function heldOwnerOf(address tokenAddress, uint256 tokenId)
    external
    view
    returns (address);

  /**
   * @notice get the held balance of the token owner
   * @dev should throw for invalid queries and return zero for no balance
   * @param tokenAddress held token address
   * @param owner functional token owner
   * @return held token balance
   */
  function heldBalanceOf(address tokenAddress, address owner)
    external
    view
    returns (uint256);
}
```

实现 `ERC1155` 持有代币标准的智能合约必须实现 `IERC1155Holder` 接口中的所有函数。

实现 `ERC1155` 持有代币标准的智能合约还必须实现 `ERC165`，并在传入接口 ID `0xced24c37` 时返回 true。

```solidity
/**
 * @notice the ERC1155 holder standard provides a common interface to query
 * token balance information
 */
interface IERC1155Holder is IERC165 {
  /**
   * @notice emitted when the token is transferred to the contract
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenId held token ID
   * @param tokenAmount held token amount
   */
  event Hold(
    address indexed owner,
    address indexed tokenAddress,
    uint256 indexed tokenId,
    uint256 tokenAmount
  );

  /**
   * @notice emitted when the token is released back to the user
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenId held token ID
   * @param tokenAmount held token amount
   */
  event Release(
    address indexed owner,
    address indexed tokenAddress,
    uint256 indexed tokenId,
    uint256 tokenAmount
  );

  /**
   * @notice get the held balance of the token owner
   * @dev should throw for invalid queries and return zero for no balance
   * @param tokenAddress held token address
   * @param owner functional token owner
   * @param tokenId held token ID
   * @return held token balance
   */
  function heldBalanceOf(
    address tokenAddress,
    address owner,
    uint256 tokenId
  ) external view returns (uint256);
}
```

## 理由

该接口旨在极其轻量且与任何现有代币合约兼容。任何代币持有合约可能已经存储了所有相关信息，因此该标准仅仅是添加一个公共接口以公开这些数据。

代币地址参数的包含是为了支持可以同时持有多个代币合约的合约。虽然某些合约可能只持有单个代币地址，但这对于任一场景来说更为通用。

为每种代币类型（EIP-20、EIP-721、EIP-1155）提出了单独的接口，因为支持持有这些不同代币的任何合约逻辑可能是独立的。在单个合约确实持有多种代币类型的情况下，它可以简单地实现每个适当的持有代币接口。

## 向后兼容性

重要的是，提议的规范与所有现有的 EIP-20、EIP-721 和 EIP-1155 代币合约完全兼容。

代币持有合约需要更新以实现该轻量级接口。

该标准的消费者需要更新以在任何相关的所有权逻辑中遵循该接口。

## 参考实现

完整的示例实现包括 [接口](../assets/eip-4987/IERC721Holder.sol)、一个金库 [代币持有者](../assets/eip-4987/Vault.sol) 和一个 [消费者](../assets/eip-4987/Consumer.sol)，可以在 `assets/eip-4987/` 找到。

值得注意的是，`IERC721Holder` 接口的消费者可以使用以下逻辑对任何特定代币 ID 的所有者进行链式查找。

```solidity
  /**
   * @notice get the functional owner of a token
   * @param tokenId token id of interest
   */
  function getOwner(uint256 tokenId) external view returns (address) {
    // get raw owner
    address owner = token.ownerOf(tokenId);

    // if owner is not contract, return
    if (!owner.isContract()) {
      return owner;
    }

    // check for token holder interface support
    try IERC165(owner).supportsInterface(0x16b900ff) returns (bool ret) {
      if (!ret) return owner;
    } catch {
      return owner;
    }

    // check for held owner
    try IERC721Holder(owner).heldOwnerOf(address(token), tokenId) returns (address user) {
      if (user != address(0)) return user;
    } catch {}

    return owner;
  }
```

## 安全考虑

该标准的消费者在使用来自未知合约的所有权信息时应保持谨慎。恶意行为者可能会实现该接口，但报告无效或恶意信息，目的是操纵治理系统、游戏、会员程序等。

消费者还应验证持有合约的整体代币余额和所有权，以作为合理性检查。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。