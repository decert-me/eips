---
eip: 6353
title: 慈善代币
description: EIP-20 代币的扩展，可以部分捐赠给慈善项目
author: Aubay <blockchain-team@aubay.com>, BOCA Jeabby (@bjeabby1507), EL MERSHATI Laith (@lth-elm), KEMP Elia (@eliakemp)
discussions-to: https://ethereum-magicians.org/t/erc20-charity-token/12617
status: 停滞
type: 标准跟踪
category: ERC
created: 2022-05-13
requires: 20
---

## 摘要

对 [EIP-20](./erc-20.md) 的扩展，可以自动将每次转账的额外百分比发送给第三方，并提供检索此信息的接口。这可以让代币持有者在每次转账时向慈善机构捐款。这也可以用于实现自动储蓄计划。

## 动机

有一些慈善组织在链上有地址，并且有代币持有者希望进行自动捐赠。拥有一种标准化的方式来收集和管理这些捐赠有助于用户和用户界面开发者。用户可以通过他们的代币产生影响，并为实现可持续的区块链发展做出贡献。项目可以轻松检索慈善捐赠地址和给定 [EIP-20](./erc-20.md) 代币的费率，代币持有者可以比较代币合约所有者允许的最低捐赠费率。该标准提供了使代币持有者轻松捐赠的功能。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

合约的所有者 **MAY** 在审核后，将慈善地址注册到 `whitelistedRate` 中，并全局设置默认捐赠费率。要注册地址，费率 **MUST** 不能为空。

代币持有者 **MAY** 从 `_defaultAddress` 中选择并指定一个默认的慈善地址，该地址 **SHOULD** 与空地址不同，以便激活捐赠。

捐赠是基于百分比的费率模型，但计算可以以不同方式进行。应用程序和个人可以通过调用 `charityInfo()` 来实现该标准，该函数指定给定地址的分配费率。

该标准提供了使代币持有者轻松捐赠的功能。激活时的捐赠直接在重写的 `transfer`、`transferFrom` 和 `approve` 函数中完成。

当调用 `transfer`、`transferFrom` 时，发送者的余额减少初始金额，并扣除捐赠金额。初始转账金额转移到接收者的余额，额外的捐赠金额转移到第三方（慈善机构）。这两个转账同时进行，并发出两个 `Transfer` 事件。
此外，如果账户的余额不足以覆盖转账和捐赠，则整个转账将回滚。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.4;

///
/// @dev Required interface of an ERC20 Charity compliant contract.
///
interface IERC20charity is IERC165 {
    /// The EIP-165 identifier for this interface is 0x557512b6

    
    /**
     * @dev Emitted when `toAdd` charity address is added to `whitelistedRate`.
     */
    event AddedToWhitelist (address toAdd);

    /**
     * @dev Emitted when `toRemove` charity address is deleted from `whitelistedRate`.
     */
    event RemovedFromWhitelist (address toRemove);

    /**
     * @dev Emitted when `_defaultAddress` charity address is modified and set to `whitelistedAddr`.
     */
    event DonnationAddressChanged (address whitelistedAddr);

    /**
     * @dev Emitted when `_defaultAddress` charity address is modified and set to `whitelistedAddr` 
    * and _donation is set to `rate`.
     */
    event DonnationAddressAndRateChanged (address whitelistedAddr,uint256 rate);

    /**
     * @dev Emitted when `whitelistedRate` for `whitelistedAddr` is modified and set to `rate`.
     */
    event ModifiedCharityRate(address whitelistedAddr,uint256 rate);
    
    /**
    *@notice Called with the charity address to determine if the contract whitelisted the address
    *and if it is the rate assigned.
    *@param addr - the Charity address queried for donnation information.
    *@return whitelisted - true if the contract whitelisted the address to receive donnation
    *@return defaultRate - the rate defined by the contract owner by default , the minimum rate allowed different from 0
    */
    function charityInfo(
        address addr
    ) external view returns (
        bool whitelisted,
        uint256 defaultRate
    );

    /**
    *@notice Add address to whitelist and set rate to the default rate.
    * @dev Requirements:
     *
     * - `toAdd` cannot be the zero address.
     *
     * @param toAdd The address to whitelist.
     */
    function addToWhitelist(address toAdd) external;

    /**
    *@notice Remove the address from the whitelist and set rate to the default rate.
    * @dev Requirements:
     *
     * - `toRemove` cannot be the zero address.
     *
     * @param toRemove The address to remove from whitelist.
     */
    function deleteFromWhitelist(address toRemove) external;

    /**
    *@notice Get all registered charity addresses.
     */
    function getAllWhitelistedAddresses() external ;

    /**
    *@notice Display for a user the rate of the default charity address that will receive donation.
     */
    function getRate() external view returns (uint256);

    /**
    *@notice Set personlised rate for charity address in {whitelistedRate}.
    * @dev Requirements:
     *
     * - `whitelistedAddr` cannot be the zero address.
     * - `rate` cannot be inferior to the default rate.
     *
     * @param whitelistedAddr The address to set as default.
     * @param rate The personalised rate for donation.
     */
    function setSpecificRate(address whitelistedAddr , uint256 rate) external;

    /**
    *@notice Set for a user a default charity address that will receive donation. 
    * The default rate specified in {whitelistedRate} will be applied.
    * @dev Requirements:
     *
     * - `whitelistedAddr` cannot be the zero address.
     *
     * @param whitelistedAddr The address to set as default.
     */
    function setSpecificDefaultAddress(address whitelistedAddr) external;

    /**
    *@notice Set for a user a default charity address that will receive donation. 
    * The rate is specified by the user.
    * @dev Requirements:
     *
     * - `whitelistedAddr` cannot be the zero address.
     * - `rate` cannot be less than to the default rate 
     * or to the rate specified by the owner of this contract in {whitelistedRate}.
     *
     * @param whitelistedAddr The address to set as default.
     * @param rate The personalised rate for donation.
     */
    function setSpecificDefaultAddressAndRate(address whitelistedAddr , uint256 rate) external;

    /**
    *@notice Display for a user the default charity address that will receive donation. 
    * The default rate specified in {whitelistedRate} will be applied.
     */
    function specificDefaultAddress() external view returns (
        address defaultAddress
    );

    /**
    *@notice Delete The Default Address and so deactivate donnations .
     */
    function deleteDefaultAddress() external;
}

```

### 函数

#### **addToWhitelist**

将地址添加到白名单并将费率设置为默认费率。

| 参数 | 描述 |
| ---------|-------------|
| toAdd | 要添加到白名单的地址。 |

#### **deleteFromWhitelist**

从白名单中移除地址并将费率设置为默认费率。

| 参数 | 描述 |
| ---------|-------------|
| toRemove | 要从白名单中移除的地址。 |

#### **getAllWhitelistedAddresses**

获取所有注册的慈善地址。

#### **getRate**

为用户显示将接收捐赠的默认慈善地址的费率。

#### **setSpecificRate**

为 {whitelistedRate} 中的慈善地址设置个性化费率。

| 参数 | 描述 |
| ---------|-------------|
| whitelistedAddr | 要设置为默认的地址。 |
| rate  | 捐赠的个性化费率。 |

#### **setSpecificDefaultAddress**

为用户设置一个将接收捐赠的默认慈善地址。将应用 {whitelistedRate} 中指定的默认费率。

| 参数 | 描述 |
| ---------|-------------|
| whitelistedAddr | 要设置为默认的地址。 |

#### **setSpecificDefaultAddressAndRate**

为用户设置一个将接收捐赠的默认慈善地址。费率由用户指定。

| 参数 | 描述 |
| ---------|-------------|
| whitelistedAddr | 要设置为默认的地址。 |
| rate  | 捐赠的个性化费率。 |

#### **specificDefaultAddress**

为用户显示将接收捐赠的默认慈善地址。将应用 {whitelistedRate} 中指定的默认费率。

#### **deleteDefaultAddress**

删除默认地址，从而停用捐赠。

#### **charityInfo**

调用慈善地址以确定合约是否将该地址列入白名单，如果是，则返回分配的费率。

| 参数 | 描述 |
| ---------|-------------|
| addr | 查询捐赠信息的慈善地址。 |

## 理由

该 EIP 选择通过使用数组来列入慈善地址的白名单，并通过映射 `whitelistedRate` 跟踪“活动”状态，以允许多个收件人选择并保持透明。捐赠地址也可以是由合约所有者选择的单个地址，并定期修改。

如果发送者余额不足，即代币的总金额（初始转账 + 捐赠）不足，则转账将回滚。捐赠在 `transfer` 函数中进行，以简化使用并不添加额外的函数，但实现可以以不同方式进行，例如允许在激活捐赠时转账而不包括捐赠金额。代币实现者还可以选择将捐赠存储在合约中或另一个合约中，并添加提取或可索取的功能，以便慈善机构可以自行索取分配的代币金额，额外的转账将由慈善机构触发，而不是代币持有者。

此外，捐赠金额在此处计算为转账代币金额的百分比，以允许不同的场景，但代币实现者可以选择采用其他方法，例如向上取整交易值。

## 向后兼容性

该实现是对 [EIP-20](./erc-20.md) 功能的扩展，引入了新功能，同时保留了 [EIP-20](./erc-20.md) 标准的核心接口和功能。确实存在一个小的向后兼容性问题，如果账户余额不足，转账可能会失败。

## 测试用例

测试可以在 [`charity.js`](../assets/eip-6353/test/charity.js) 中找到。

## 参考实现

该标准的参考实现可以在 [`contracts/`](../assets/eip-6353/contracts/ERC20Charity.sol) 文件夹中找到。

## 安全考虑

与 EIP-20 相比，没有额外的安全考虑。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。