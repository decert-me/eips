---
eip: 7561
title: 简单 NFT，简化的 ERC-721
description: 设计用于合约钱包，移除了 ERC-721 中的 safeTransferFrom、approve、setApprovalForAll、getApproved、isApprovedForAll 函数
author: Xiang (@wenzhenxiang), Ben77 (@ben2077), Mingshi S. (@newnewsms)
discussions-to: https://ethereum-magicians.org/t/erc-7561-simple-nft/16695
status: 草案
type: 标准跟踪
category: ERC
created: 2023-10-29
requires: 721
---

## 摘要

该 ERC 是基于用户合约钱包（包括账户抽象）设计的新 NFT 资产，并与 [ERC-721](./eip-721.md) 向前兼容。为了保持 NFT 资产的简单性，该 ERC 移除了 ERC-721 的 `approve`、`setApprovalForAll`、`getApproved`、`isApprovedForAll` 和 `safeTransferFrom` 函数。

## 动机

[ERC-721](./eip-721.md) 定义了基于以太坊的标准 NFT，可以进行交易和转移，但 ERC-721 的本质是基于外部拥有账户（EOA）钱包设计。EOA 钱包没有状态和代码存储，而智能合约钱包则不同。

几乎所有与 NFT 相关的 ERC 都是添加功能，但我们的观点正好相反。我们认为 NFT 合约应该更简单，更多的功能由智能合约钱包来处理。

我们的提案是基于智能合约钱包设计一个更简单的 NFT 资产。

它旨在实现以下目标：

1. 保持 NFT 合约简单，仅负责 `transferFrom` 函数。
2. `approve`、`getApproved`、`setApprovalForAll` 和 `isApprovedForAll` 函数不由 NFT 合约管理。相反，这些权限由用户级别管理，为用户提供更大的灵活性和控制权。这一变化不仅增强了用户的自主性，还减轻了与 ERC-721 合约实现这些功能相关的某些风险。
3. 移除 `safeTransferFrom` 函数。调用对方的 NFT 资产的更好方式是访问对方自己的合约，而不是直接访问 NFT 资产合约。
4. 与 ERC-721 的向前兼容意味着所有 NFT 都可以与该提案兼容。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

合规合约 MUST 实现以下接口：

```solidity
pragma solidity ^0.8.20;

/**
 * @title ERC7561 Simple NFT interface 
 * @dev See https://ercs.ethereum.org/ERCS/erc-7561
 */
interface IERC7561 {
    /**
     * @notice Used to notify transfer NFT.
     * @param from Address of the from
     * @param to Address of the receive
     * @param tokenId The transaction token id 
     */
    event Transfer(
        address indexed from,
        address indexed to,
        uint256 indexed tokenId
    );

    /**
     * @notice  Count all NFTs assigned to an owner
     * @param owner Address of the owner
     * @return The number of NFTs owned by `owner`, possibly zero
     */
    function balanceOf(address owner) 
        external
        view
        returns (uint256);

    /**
     * @notice Find the owner of an NFT
     * @param tokenId The identifier for an NFT
     * @return The address of the owner of the NFT
     */
    function ownerOf(uint256 tokenId) 
        external  
        view
        returns (address);
	  

    /**
     * @notice Transfer ownership of an NFT
     * @param from Address of the from
     * @param to Address of the to
     * @param tokenId The NFT to transfer
     */
    function transferFrom(address from, address to, uint256 tokenId) external;

}
```

## 理由

该提案通过移除 `approve`、`setApprovalForAll`、`getApproved`、`isApprovedForAll` 和 `safeTransferFrom` 函数来简化 NFT 标准。这一简化旨在增强安全性、减少复杂性并提高效率，使标准更适合智能合约钱包环境，同时保持基本功能。

## 向后兼容性

如开头所述，该 ERC 与 [ERC-721](./eip-721.md) 向前兼容，ERC-721 与该 ERC 向后兼容。

## 参考实现

**与 [ERC-721](./eip-721.md) 向前兼容**

```solidity
pragma solidity ^0.8.20;

import "./IERC7561.sol";
import "../../math/SafeMath.sol";

/**
 * @title Standard ERC7561 NFT
 * @dev Note: the ERC-165 identifier for this interface is 0xc1b31357
 * @dev Implementation of the basic standard NFT.
 */
contract ERC7561 is IERC7561 {

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    mapping(uint256 tokenId => address) private _owners;

    mapping(address owner => uint256) private _balances;

    uint256 private _totalSupply;

    function totalSupply() external view returns (uint256) {
        return _totalSupply;
    }

    function balanceOf(address owner) public view  returns (uint256) {
        require (owner != address(0));
        
        return _balances[owner];
    }

    function ownerOf(uint256 tokenId) public view  returns (address) {
        return _requireOwned(tokenId);
    }


    function transferFrom(address from, address to, uint256 tokenId) public  {

        require(from == msg.sender);

        require (to != address(0) );

        address previousOwner = _update(to, tokenId);

        require(previousOwner == from);
    }


    function _ownerOf(uint256 tokenId) internal view virtual returns (address) {
        return _owners[tokenId];
    }

    function _requireOwned(uint256 tokenId) internal view returns (address) {
        address owner = _ownerOf(tokenId);
        require(owner != address(0));
            
        return owner;
    }

    function _update(address to, uint256 tokenId) internal virtual returns (address) {
        address from = _ownerOf(tokenId);

        
        // Execute the update
        if (from != address(0)) {         

            unchecked {
                _balances[from] -= 1;
            }
        }

        if (to != address(0)) {
            unchecked {
                _balances[to] += 1;
            }
        }

        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);

        return from;
    }

}
```

## 安全考虑

需要注意的是，该 ERC 与 [ERC-721](./eip-721.md) 不向后兼容，因此与现有 dapp 会存在不兼容性。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。