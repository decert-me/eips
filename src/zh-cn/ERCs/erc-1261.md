---
eip: 1261
title: 会员验证代币 (MVT)
author: Chaitanya Potti (@chaitanyapotti), Partha Bhattacharya (@pb25193)
type: 标准跟踪
category: ERC
status: 停滞
created: 2018-07-14
requires: 165, 173
discussions-to: https://github.com/ethereum/EIPs/issues/1261
---

## 简要总结

会员验证代币 (MVT) 的标准接口。

## 摘要

以下标准允许在智能合约（称为实体）中实现会员验证代币的标准 API。该标准提供基本功能，以跟踪个人在某些链上“组织”中的会员资格。这允许多种用例，如自动合规以及多种形式的治理和会员结构。

我们考虑了将 MVT 分配给个人的用例，这些代币是不可转让的，并且可以由所有者撤销。MVT 可以代表认可的证明、会员资格的证明、投票权的证明以及区块链上其他抽象概念的证明。以下是一些这些用例的示例，并且可以提出其他多个用例：

- 投票：投票本质上应该是一项有权限的活动。到目前为止，链上投票系统只能通过币余额进行投票。这现在可以改变，并采取各种形式。
- 护照签发、社会福利分配、旅行许可证签发、驾驶执照签发都是可以抽象为会员的应用，即个人属于某个小组，被某些权威机构认可为拥有某些权利，而无需任何个人特定信息（如福利权、自由流动、操作车辆的授权、移民）。
- 投资者许可：使合规监管成为一个简单的链上过程。证券的代币化，流向仅限于合格地址，追踪和认证链上地址以满足反洗钱目的。
- 软件许可：软件公司如游戏开发者可以使用该协议授权某些硬件单元（控制台）下载和使用特定软件（游戏）。

一般来说，个人在日常生活中可以拥有不同的会员资格。该协议允许创建将所有内容集中在一个地方的软件。他们的身份可以立即得到验证。想象一个世界，你不需要携带装满身份证件的钱包（护照、健身房会员、社会安全号码、公司 ID 等），组织可以轻松跟踪所有成员。组织可以轻松识别并禁止虚假身份。

属性是 ERC-1261 的一个重要部分，有助于存储有关其成员的可识别信息。投票可以利用属性来计算选民基础。
例如：用户应属于美国实体，而不属于华盛顿州属性，以便参与投票。

将存在一个映射表，将属性头映射到所有可能属性的数组。这是为了将实体细分为独占且穷尽的子组。例如，
头：血型字母
数组：[o, a, b, ab]
头：血型符号
数组：[+, -]

不是独占穷尽的示例：
头：视频订阅
数组：[Netflix, HBO, Amazon]
因为一个人不必恰好拥有其中一个元素。他或她可能没有或拥有多个。

## 动机

标准接口允许任何用户、应用程序与以太坊上的任何 MVT 进行交互。我们提供简单的 ERC-1261 智能合约。下文讨论了其他应用程序。

该标准的灵感来自于区块链上的投票是通过代币余额权重进行的这一事实。尽管区块链提供了巨大的治理潜力，但这对灵活治理系统的形成造成了极大的损害。我们的想法是创建一个权限系统，允许组织在区块链上对人进行审查，然后在治理方面获得巨大的灵活性。

我们还审查了其他会员 EIP，包括 EIP-725/735 声明注册。#735 声明和 #1261 MVT 之间的一个显著区别是信息所有权。在 #735 中，声明持有者拥有关于他们自己的任何声明。问题在于，一旦声明被发出，声明发行者就无法撤销或更改声明。虽然 #735 确实指定了 removeClaim 方法，但恶意实现可以简单地忽略该方法调用，因为他们拥有该声明。

想象一下，背景调查公司 SafeEmploy™ 对 Timmy 发出了一项声明。该声明指出 Timmy 从未被定罪。Timmy 做了一些错误的决定，现在该声明不再真实。SafeEmploy™ 执行 removeClaim，但 Timmy 的 #735 合约只是忽略它，因为 Timmy 想继续工作（并且很聪明）。#1261 MVT 没有这个问题。徽章/声明的所有权完全由发出徽章的合约决定，而不是接收它们的合约。发行者可以自由地根据需要删除或更改这些徽章。

**信任 lessness 与可用性之间的权衡：**
要真正理解该协议的价值，重要的是要理解我们所处的权衡。MVT 合约允许创建者撤销代币，并实质上没收相关成员的会员资格。对某些人来说，这可能看起来是一个不可接受的缺陷，然而这是一种设计选择，而不是缺陷。
这个选择似乎将大量信任放在管理实体合约的个人（实体所有者）身上。如果实体所有者的利益与成员的利益相冲突，所有者可能会增加虚假地址（以主导共识）或驱逐成员（以审查不利决策）。乍一看，这似乎是一个重大缺陷，因为区块链空间在大多数情况下习惯于绝对消除权威。即使官方对 dapp 的定义也要求没有任何管理应用程序提供的服务的方。然而，如果可以确保实体所有者的利益与成员的利益一致，那么对实体所有者的信任并不是错位的。
对这种系统的另一个批评是，区块链中介的标准边缘——“如果你不知道该贿赂谁，就无法贿赂系统”——不再成立。可以贿赂实体所有者使其屈服，并让他们审查或伪造投票。对此论点有几种回应。首先，所有活动，例如增加成员和移除成员，都可以在区块链上追踪，这些活动的痕迹无法被删除。构建分析工具以检测恶意活动（突然增加 100 个虚假成员投票朝某个方向/突然移除投票朝某个方向的多个成员）并不困难。其次，实体所有者的权力仅限于增加和移除成员。这意味着他们无法篡改任何投票。他们只能更改计数系统以包括虚假投票者或移除真实投票者。任何理智的审计员都可以识别恶意/受害地址，并创建一个开源审计工具以找出正确的结果。在这种攻击中，最大的输家将是实体所有者，他们有声誉可失。

最后，必须理解我们为何在这种权衡中逐步远离信任 lessness。答案是可用性。引入权限系统扩展了可以通过区块链提供的产品和服务的范围，同时利用区块链的其他方面（便宜、不变、无繁文缛节、安全）。考虑使用 ERC-1300 标准的驾驶执照发放机构的例子。这是一项根本无法在完全信任 lessness 的环境中部署的服务。引入权限系统扩展了区块链上服务的范围，以涵盖这一特定服务。当然，他们有权在没有理由的情况下撤销某人的执照。但他们会这样做吗？如果该机构行为不当，谁会损失最大？该机构本身。现在考虑替代方案，执照（不一定是驾驶执照，而是说股东证书等）的发放方式，所消耗的时间，完全缺乏透明度。可以说，如果提供这些服务的传统系统真的想在执行这些服务时进行腐败和裙带关系，那么现有系统使其变得更容易。此外，它们不透明，这意味着根本无法检测到它们是否恶意行事。
总之，我们非常高兴与社区分享我们的提案，并在这一领域开放建议。
## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按照 RFC 2119 中的描述进行解释。

**每个符合 ERC-1261 的合约必须实现 `ERC1261`、`ERC173` 和 `ERC165` 接口**（受以下“注意事项”限制）：

```solidity
/// @title ERC-1261 MVT Standard
/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1261.md
///  The constructor should define the attribute set for this MVT.
///  Note: the ERC-165 identifier for this interface is 0x1d8362cf.
interface IERC1261 {/* is ERC173, ERC165 */
    /// @dev This emits when a token is assigned to a member.
    event Assigned(address indexed _to, uint[] attributeIndexes);

    /// @dev This emits when a membership is revoked.
    event Revoked(address indexed _to);

    /// @dev This emits when a user forfeits his membership
    event Forfeited(address indexed _to);

    /// @dev This emits when a membership request is accepted
    event ApprovedMembership(address indexed _to, uint[] attributeIndexes);

    /// @dev This emits when a membership is requested by an user
    event RequestedMembership(address indexed _to);

    /// @dev This emits when data of a member is modified.
    ///  Doesn't emit when a new membership is created and data is assigned.
    event ModifiedAttributes(address indexed _to, uint attributeIndex, uint attributeValueIndex);

    /// @notice Adds a new attribute (key, value) pair to the set of pre-existing attributes.
    /// @dev Adds a new attribute at the end of the array of attributes and maps it to `values`.
    ///  Contract can set a max number of attributes and throw if limit is reached.
    /// @param _name Name of the attribute which is to be added.
    /// @param values List of values of the specified attribute.
    function addAttributeSet(bytes32 _name, bytes32[] calldata values) external;

    /// @notice Modifies the attribute value of a specific attribute for a given `_to` address.
    /// @dev Use appropriate checks for whether a user/admin can modify the data.
    ///  Best practice is to use onlyOwner modifier from ERC173.
    /// @param _to The address whose attribute is being modified.
    /// @param _attributeIndex The index of attribute which is being modified.
    /// @param _modifiedValueIndex The index of the new value which is being assigned to the user attribute.
    function modifyAttributeByIndex(address _to, uint _attributeIndex, uint _modifiedValueIndex) external;

    /// @notice Requests membership from any address.
    /// @dev Throws if the `msg.sender` already has the token.
    ///  The individual `msg.sender` can request for a membership if some existing criteria are satisfied.
    ///  When a membership is requested, this function emits the RequestedMembership event.
    ///  dev can store the membership request and use `approveRequest` to assign membership later
    ///  dev can also oraclize the request to assign membership later
    /// @param _attributeIndexes the attribute data associated with the member.
    ///  This is an array which contains indexes of attributes.
    function requestMembership(uint[] calldata _attributeIndexes) external payable;

    /// @notice User can forfeit his membership.
    /// @dev Throws if the `msg.sender` already doesn't have the token.
    ///  The individual `msg.sender` can revoke his/her membership.
    ///  When the token is revoked, this function emits the Revoked event.
    function forfeitMembership() external payable;

    /// @notice Owner approves membership from any address.
    /// @dev Throws if the `_user` doesn't have a pending request.
    ///  Throws if the `msg.sender` is not an owner.
    ///  Approves the pending request
    ///  Make oraclize callback call this function
    ///  When the token is assigned, this function emits the `ApprovedMembership` and `Assigned` events.
    /// @param _user the user whose membership request will be approved.
    function approveRequest(address _user) external;

    /// @notice Owner discards membership from any address.
    /// @dev Throws if the `_user` doesn't have a pending request.
    ///  Throws if the `msg.sender` is not an owner.
    ///  Discards the pending request
    ///  Make oraclize callback call this function if criteria are not satisfied
    /// @param _user the user whose membership request will be discarded.
    function discardRequest(address _user) external;

    /// @notice Assigns membership of an MVT from owner address to another address.
    /// @dev Throws if the member already has the token.
    ///  Throws if `_to` is the zero address.
    ///  Throws if the `msg.sender` is not an owner.
    ///  The entity assigns the membership to each individual.
    ///  When the token is assigned, this function emits the Assigned event.
    /// @param _to The address to which the token is assigned.
    /// @param _attributeIndexes The attribute data associated with the member.
    ///  This is an array which contains indexes of attributes.
    function assignTo(address _to, uint[] calldata _attributeIndexes) external;

    /// @notice Only Owner can revoke the membership.
    /// @dev This removes the membership of the user.
    ///  Throws if the `_from` is not an owner of the token.
    ///  Throws if the `msg.sender` is not an owner.
    ///  Throws if `_from` is the zero address.
    ///  When transaction is complete, this function emits the Revoked event.
    /// @param _from The current owner of the MVT.
    function revokeFrom(address _from) external;

    /// @notice Queries whether a member is a current member of the organization.
    /// @dev MVT's assigned to the zero address are considered invalid, and this
    ///  function throws for queries about the zero address.
    /// @param _to An address for whom to query the membership.
    /// @return Whether the member owns the token.
    function isCurrentMember(address _to) external view returns (bool);

     /// @notice Gets the value collection of an attribute.
    /// @dev Returns the values of attributes as a bytes32 array.
    /// @param _name Name of the attribute whose values are to be fetched
    /// @return The values of attributes.
    function getAttributeExhaustiveCollection(bytes32 _name) external view returns (bytes32[] memory);

    /// @notice Returns the list of all past and present members.
    /// @dev Use this function along with isCurrentMember to find wasMemberOf() in Js.
    ///  It can be calculated as present in getAllMembers() and !isCurrentMember().
    /// @return List of addresses who have owned the token and currently own the token.
    function getAllMembers() external view returns (address[]);

    /// @notice Returns the count of all current members.
    /// @dev Use this function in polls as denominator to get percentage of members voted.
    /// @return Count of current Members.
    function getCurrentMemberCount() external view returns (uint);

    /// @notice Returns the list of all attribute names.
    /// @dev Returns the names of attributes as a bytes32 array.
    ///  AttributeNames are stored in a bytes32 Array.
    ///  Possible values for each attributeName are stored in a mapping(attributeName => attributeValues).
    ///  AttributeName is bytes32 and attributeValues is bytes32[].
    ///  Attributes of a particular user are stored in bytes32[].
    ///  Which has a single attributeValue for each attributeName in an array.
    ///  Use web3.toAscii(data[0]).replace(/\u0000/g, "") to convert to string in JS.
    /// @return The names of attributes.
    function getAttributeNames() external view returns (bytes32[] memory);

    /// @notice Returns the attributes of `_to` address.
    /// @dev Throws if `_to` is the zero address.
    ///  Use web3.toAscii(data[0]).replace(/\u0000/g, "") to convert to string in JS.
    /// @param _to The address whose current attributes are to be returned.
    /// @return The attributes associated with `_to` address.
    function getAttributes(address _to) external view returns (bytes32[]);

    /// @notice Returns the `attribute` stored against `_to` address.
    /// @dev Finds the index of the `attribute`.
    ///  Throws if the attribute is not present in the predefined attributes.
    ///  Returns the attributeValue for the specified `attribute`.
    /// @param _to The address whose attribute is requested.
    /// @param _attributeIndex The attribute Index which is required.
    /// @return The attribute value at the specified name.
    function getAttributeByIndex(address _to, uint _attributeIndex) external view returns (bytes32);
}

interface ERC173 /* is ERC165 */ {
    /// @dev This emits when ownership of a contract changes.
    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /// @notice Get the address of the owner
    /// @return The address of the owner.
    function owner() external view;

    /// @notice Set the address of the new owner of the contract
    /// @param _newOwner The address of the new owner of the contract
    function transferOwnership(address _newOwner) external;
}

interface ERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///  uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

**元数据扩展** 对于 ERC-1261 智能合约是可选的（见下文“注意事项”）。这允许您的智能合约被查询其名称以及关于您的 MV 代币所代表的组织的详细信息。

```solidity
/// @title ERC-1261 MVT Standard, optional metadata extension
/// @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1261.md
interface ERC1261Metadata /* is ERC1261 */ {
    /// @notice A descriptive name for a collection of MVTs in this contract
    function name() external view returns (string _name);

    /// @notice An abbreviated name for MVTs in this contract
    function symbol() external view returns (string _symbol);
}
```

这是上述提到的“ERC1261 元数据 JSON 架构”。

```json
{
  "title": "Organization Metadata",
  "type": "object",
  "properties": {
    "name": {
      "type": "string",
      "description": "Identifies the organization to which this MVT represents"
    },
    "description": {
      "type": "string",
      "description": "Describes the organization to which this MVT represents"
    }
  }
}
```

### 注意事项

0.4.24 版本的 Solidity 接口语法不足以记录 ERC-1261 标准。符合 ERC-1261 的合约还必须遵守以下规定：

- Solidity 问题 #3412：上述接口为每个函数提供了明确的可变性保证。可变性保证从弱到强依次为：`payable`、隐式非支付、`view` 和 `pure`。您的实现必须满足此接口中的可变性保证，您可以满足更强的保证。例如，此接口中的 `payable` 函数可以在您的合约中实现为非支付（未指定状态可变性）。我们预计后续的 Solidity 版本将允许您的更严格的合约从此接口继承，但对于 0.4.24 版本的解决方法是您可以编辑此接口以在从您的合约继承之前添加更严格的可变性。
- Solidity 问题 #3419：实现 `ERC1261Metadata` 的合约也必须实现 `ERC1261`。
- Solidity 问题 #2330：如果本规范中显示某个函数为 `external`，则如果合约使用 `public` 可见性，则该合约将符合规范。作为 0.4.24 版本的解决方法，您可以在从您的合约继承之前编辑此接口以切换为 `public`。
- Solidity 问题 #3494、#3544：使用 `this.*.selector` 在 Solidity 中被标记为警告，未来版本的 Solidity 将不再将其标记为错误。

*如果更新版本的 Solidity 允许在代码中表达注意事项，则此 EIP 可能会被更新并移除注意事项，这将等同于原始规范。*

## 理由

以太坊智能合约有许多潜在用途依赖于跟踪会员资格。现有或计划中的 MVT 系统的示例包括 Vault（一个 DAICO 平台）和 Stream（一个安全代币框架）。未来的用途包括直接民主的实施、游戏内会员资格和徽章、许可证和旅行文件的发行、电子投票机的审计、软件许可等。

**MVT 词汇选择：**

由于代币是不可转让和可撤销的，因此它们的功能类似于会员卡。因此称为会员验证代币。

**转移机制**

MVT 不能被转移。这是一个设计选择，也是区分此协议的特性之一。
任何成员始终可以要求发行者从现有地址撤销代币并分配给新地址。
可以将 MVT 的集合视为识别用户，您不能将用户拆分为多个部分而仍然是同一个用户，但您可以将用户转移到新的私钥。

**分配和撤销机制**

分配和撤销函数的文档仅指定交易必须抛出异常的条件。您的实现也可以在其他情况下抛出异常。这允许实现达到有趣的结果：

- **在满足条件后不允许额外的会员资格** — GitHub 上提供示例合约
- **将某些地址列入黑名单，禁止其接收 MV 代币** — GitHub 上提供示例合约
- **在达到某个时间后不允许额外的会员资格** — GitHub 上提供示例合约
- **向交易用户收取费用** — 在调用 `assign` 和 `revoke` 时要求支付，以便可以进行外部来源的条件检查

**ERC-173 接口**

我们选择标准所有权接口（ERC-173）来管理 ERC-1261 合约的所有权。

未来的 EIP/Zeppelin 可能会创建一个多所有权实现。我们强烈支持这样的 EIP，它将允许您的 ERC-1261 实现实现 `ERC1261Metadata` 或通过委托给单独的合约实现其他接口。

**ERC-165 接口**

我们选择标准接口检测（ERC-165）来公开 ERC-1261 智能合约支持的接口。

未来的 EIP 可能会创建一个合约接口的全局注册表。我们强烈支持这样的 EIP，它将允许您的 ERC-1261 实现实现 `ERC1261Metadata` 或通过委托给单独的合约实现其他接口。

**Gas 和复杂性**（关于枚举扩展）

本规范考虑了管理少量和*任意大量* MVT 的实现。如果您的应用能够增长，则应避免在代码中使用 for/while 循环。这表明您的合约可能无法扩展，且 gas 成本将随着时间的推移而不断上升。

**隐私**

个人信息：该协议不会将任何个人信息放入区块链，因此在这方面没有隐私泄露。
会员隐私：该协议通过设计使得哪些地址是/不是会员是公开的。如果不公开该信息，将无法独立审计治理活动或跟踪管理员（实体所有者）活动。

**元数据选择**（元数据扩展）

我们在元数据扩展中要求 `name` 和 `symbol` 函数。我们审查的每个代币 EIP 和草案（ERC-20、ERC-223、ERC-677、ERC-777、ERC-827）都包含这些函数。

我们提醒实现作者，如果您反对使用此机制，空字符串是对 `name` 和 `symbol` 的有效响应。我们还提醒大家，任何智能合约都可以使用与*您的*合约相同的名称和符号。客户端如何确定哪些 ERC-1261 智能合约是知名（规范）的超出本标准的范围。

提供了一种机制将 MVT 与 URI 关联。我们预计许多实现将利用这一点为每个 MVT 系统提供元数据。URI 可以是可变的（即，可能会不时更改）。我们考虑了一个 MVT 代表某个地方的会员资格，在这种情况下，关于组织的元数据可以自然地发生变化。

元数据作为字符串值返回。目前这仅可通过 `web3` 调用，而不能通过其他合约调用。这是可以接受的，因为我们没有考虑到区块链上的应用程序查询此类信息的用例。

*考虑的替代方案：将每个资产的所有元数据放在区块链上（成本太高），使用 URL 模板查询元数据部分（URL 模板不适用于所有 URL 方案，尤其是 P2P URL），multiaddr 网络地址（尚不成熟）*

**社区共识**

在这个过程中我们非常包容，邀请任何有问题或贡献的人参与我们的讨论。然而，本标准仅旨在支持此处列出的已识别用例。

## 向后兼容性

我们采用了 ERC-20 规范中的 `name` 和 `symbol` 语义。

截至 2018 年 7 月的示例 MVT 实现：
- 会员验证代币(https://github.com/chaitanyapotti/MembershipVerificationToken)

## 测试用例

会员验证代币 ERC-1261 代币包括使用 Truffle 编写的测试用例。

## 实现

会员验证代币 ERC1261 -- 参考实现

- MIT 许可，因此您可以自由地将其用于您的项目
- 包括测试用例
- 也可以作为 npm 包使用 - npm i membershipverificationtoken

## 参考

**标准**

1. ERC-20 代币标准. ./erc-20.md
1. ERC-165 标准接口检测. ./eip-165.md
1. ERC-725/735 声明注册. ./eip-725.md
1. ERC-173 所有权标准. ./eip-173.md
1. JSON Schema. https://json-schema.org/
1. Multiaddr. https://github.com/multiformats/multiaddr
1. RFC 2119 用于指示要求级别的 RFC 中使用的关键词. https://www.ietf.org/rfc/rfc2119.txt

**问题**

1. 原始 ERC-1261 问题. https://github.com/ethereum/eips/issues/1261
1. Solidity 问题 \#2330 -- 接口函数是外部的. https://github.com/ethereum/solidity/issues/2330
1. Solidity 问题 \#3412 -- 实现接口：允许更严格的可变性. https://github.com/ethereum/solidity/issues/3412
1. Solidity 问题 \#3419 -- 接口不能继承. https://github.com/ethereum/solidity/issues/3419

**讨论**

1. Gitter #EIPs (首次实时讨论的公告). https://gitter.im/ethereum/EIPs?at=5b5a1733d2f0934551d37642
1. ERC-1261 (首次实时讨论的公告). https://github.com/ethereum/eips/issues/1261

**MVT 实现和其他项目**

1. 会员验证代币 ERC-1261 代币. https://github.com/chaitanyapotti/MembershipVerificationToken

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。