---
eip: 4361
title: 使用以太坊登录
description: 以太坊账户的链外认证以建立会话。
author: Wayne Chang (@wyc), Gregory Rocco (@obstropolos), Brantly Millegan (@brantlymillegan), Nick Johnson (@Arachnid), Oliver Terbu (@awoie)
discussions-to: https://ethereum-magicians.org/t/eip-4361-sign-in-with-ethereum/7263
status: 审核中
type: 标准跟踪
category: ERC
created: 2021-10-11
requires: 55, 137, 155, 191, 1271, 1328
---

## 摘要

使用以太坊登录描述了以太坊账户如何通过签署由范围、会话细节和安全机制（例如，随机数）参数化的标准消息格式与链外服务进行认证。本规范的目标是提供一个自我保管的替代方案，以取代集中式身份提供者，提高以太坊基础认证的链外服务之间的互操作性，并为钱包供应商提供一致的机器可读消息格式，以改善用户体验和同意管理。

## 动机

在今天登录流行的非区块链服务时，用户通常会使用身份提供者（IdP），这些提供者是对用户标识符拥有最终控制权的集中实体，例如大型互联网公司和电子邮件提供商。这些方之间的激励往往不一致。使用以太坊登录为希望对自己的数字身份承担更多控制和责任的用户提供了一种新的自我保管选项。

目前，许多服务支持通过消息签名来认证以太坊账户的工作流程，例如建立一个基于 cookie 的网络会话，以管理有关认证地址的特权元数据。这是标准化登录工作流程并提高现有服务之间互操作性的机会，同时为钱包供应商提供可靠的方法，以识别签名请求为使用以太坊登录请求，从而改善用户体验。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“NOT RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 和 RFC 8174 中的描述进行解释。

### 概述

使用以太坊登录（SIWE）工作流程如下：

1. 依赖方生成一个 SIWE 消息，并在 SIWE 消息前加上 `\x19Ethereum Signed Message:\n<length of message>`，如 [ERC-191](./eip-191.md) 所定义。
2. 钱包向用户展示一个结构化的明文消息或等效界面，以使用 [ERC-191](./eip-191.md) 签名数据格式签署 SIWE 消息。
3. 签名随后呈现给依赖方，依赖方检查签名的有效性和 SIWE 消息内容。
4. 依赖方可能进一步获取与以太坊地址相关的数据，例如来自以太坊区块链的数据（例如，ENS、账户余额、[ERC-20](./eip-20.md)/[ERC-721](./eip-721.md)/[ERC-1155](./eip-1155.md) 资产所有权），或其他可能或不需要权限的数据源。

### 消息格式

#### ABNF 消息格式

SIWE 消息必须符合以下增强巴科斯-诺尔范式（ABNF，RFC 5234）表达式（注意 `%s` 表示字符串术语的大小写敏感性，按 RFC 7405）。

```abnf
sign-in-with-ethereum =
    [ scheme "://" ] domain %s" wants you to sign in with your Ethereum account:" LF
    address LF
    LF
    [ statement LF ]
    LF
    %s"URI: " uri LF
    %s"Version: " version LF
    %s"Chain ID: " chain-id LF
    %s"Nonce: " nonce LF
    %s"Issued At: " issued-at
    [ LF %s"Expiration Time: " expiration-time ]
    [ LF %s"Not Before: " not-before ]
    [ LF %s"Request ID: " request-id ]
    [ LF %s"Resources:"
    resources ]

scheme = ALPHA *( ALPHA / DIGIT / "+" / "-" / "." )
    ; See RFC 3986 for the fully contextualized
    ; definition of "scheme".

domain = authority
    ; From RFC 3986:
    ;     authority     = [ userinfo "@" ] host [ ":" port ]
    ; See RFC 3986 for the fully contextualized
    ; definition of "authority".

address = "0x" 40*40HEXDIG
    ; Must also conform to capitalization
    ; checksum encoding specified in EIP-55
    ; where applicable (EOAs).

statement = *( reserved / unreserved / " " )
    ; See RFC 3986 for the definition
    ; of "reserved" and "unreserved".
    ; The purpose is to exclude LF (line break).

uri = URI
    ; See RFC 3986 for the definition of "URI".

version = "1"

chain-id = 1*DIGIT
    ; See EIP-155 for valid CHAIN_IDs.

nonce = 8*( ALPHA / DIGIT )
    ; See RFC 5234 for the definition
    ; of "ALPHA" and "DIGIT".

issued-at = date-time
expiration-time = date-time
not-before = date-time
    ; See RFC 3339 (ISO 8601) for the
    ; definition of "date-time".

request-id = *pchar
    ; See RFC 3986 for the definition of "pchar".

resources = *( LF resource )

resource = "- " URI
```

#### 消息字段

本规范定义了以下 SIWE 消息字段，可以通过遵循 [ABNF 消息格式](#abnf-message-format) 中的规则从 SIWE 消息中解析：

- `scheme` 可选。请求来源的 URI 方案。其值必须是 RFC 3986 URI 方案。
- `domain` 必需。请求签名的域。其值必须是 RFC 3986 权威。权威包括一个可选端口。如果未指定端口，则假定提供的 `scheme` 的默认端口（例如，HTTPS 的 443）。如果未指定 `scheme`，则默认假定为 HTTPS。
- `address` 必需。执行签名的以太坊地址。其值应符合 [ERC-55](./eip-55.md) 中规定的混合大小写校验和地址编码。
- `statement` 可选。用户将签署的可读 ASCII 断言，必须不包含 `'\n'`（字节 `0x0a`）。
- `uri` 必需。指向签名主题的 RFC 3986 URI（如 _声明的主题_）。
- `version` 必需。SIWE 消息的当前版本，对于本规范，必须为 `1`。
- `chain-id` 必需。会话绑定的 [EIP-155](./eip-155.md) 链 ID，以及合约账户必须解析的网络。
- `nonce` 必需。通常由依赖方选择的随机字符串，用于防止重放攻击，至少 8 个字母数字字符。
- `issued-at` 必需。消息生成的时间，通常为当前时间。其值必须是 ISO 8601 日期时间字符串。
- `expiration-time` 可选。签名认证消息不再有效的时间。其值必须是 ISO 8601 日期时间字符串。
- `not-before` 可选。签名认证消息何时变为有效的时间。其值必须是 ISO 8601 日期时间字符串。
- `request-id` 可选。一个系统特定的标识符，可用于唯一引用登录请求。
- `resources` 可选。用户希望在依赖方认证过程中解析的信息或信息引用的列表。每个资源必须是由 `"\n- "` 分隔的 RFC 3986 URI，其中 `\n` 是字节 `0x0a`。

#### 非正式消息模板

下面提供了一个类似 Bash 的非正式模板，以便于阅读和理解，并且它不反映字段的可选性。字段描述在以下部分提供。完整的 ABNF 描述在 [ABNF 消息格式](#abnf-message-format) 中提供。

```
${scheme}:// ${domain} wants you to sign in with your Ethereum account:
${address}

${statement}

URI: ${uri}
Version: ${version}
Chain ID: ${chain-id}
Nonce: ${nonce}
Issued At: ${issued-at}
Expiration Time: ${expiration-time}
Not Before: ${not-before}
Request ID: ${request-id}
Resources:
- ${resources[0]}
- ${resources[1]}
...
- ${resources[n]}
```

#### 示例

以下是一个隐式方案的 SIWE 消息示例：

```
example.com wants you to sign in with your Ethereum account:
0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2

I accept the ExampleOrg Terms of Service: https://example.com/tos

URI: https://example.com/login
Version: 1
Chain ID: 1
Nonce: 32891756
Issued At: 2021-09-30T16:25:24Z
Resources:
- ipfs://bafybeiemxf5abjwjbikoz4mc3a3dla6ual3jsgpdr4cjr3oz3evfyavhwq/
- https://example.com/my-web2-claim.json
```

以下是一个具有隐式方案和显式端口的 SIWE 消息示例：

```
example.com:3388 wants you to sign in with your Ethereum account:
0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2

I accept the ExampleOrg Terms of Service: https://example.com/tos

URI: https://example.com/login
Version: 1
Chain ID: 1
Nonce: 32891756
Issued At: 2021-09-30T16:25:24Z
Resources:
- ipfs://bafybeiemxf5abjwjbikoz4mc3a3dla6ual3jsgpdr4cjr3oz3evfyavhwq/
- https://example.com/my-web2-claim.json
```

以下是一个具有显式方案的 SIWE 消息示例：

```
https://example.com wants you to sign in with your Ethereum account:
0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2

I accept the ExampleOrg Terms of Service: https://example.com/tos

URI: https://example.com/login
Version: 1
Chain ID: 1
Nonce: 32891756
Issued At: 2021-09-30T16:25:24Z
Resources:
- ipfs://bafybeiemxf5abjwjbikoz4mc3a3dla6ual3jsgpdr4cjr3oz3evfyavhwq/
- https://example.com/my-web2-claim.json
```

### 使用以太坊账户签名和验证消息

- 对于外部拥有账户（EOA），必须使用 [ERC-191](./eip-191.md) 中指定的验证方法。

- 对于合约账户，
    - 应使用 [ERC-1271](./eip-1271.md) 中指定的验证方法，如果不使用，则实现者必须清楚地定义验证方法，以确保钱包和依赖方的安全性和互操作性。
    - 在执行 [ERC-1271](./eip-1271.md) 签名验证时，执行验证的合约必须从指定的 `chain-id` 解析。
    - 实现者应考虑到 [ERC-1271](./eip-1271.md) 实现不要求是纯函数，并且可能根据区块链状态对相同输入返回不同结果。这可能会影响安全模型和会话验证规则。例如，启用 [ERC-1271](./eip-1271.md) 签名的服务可以依赖于 Webhook 接收状态变化的通知。当它收到通知时，它会使任何匹配的会话失效。

### 解析以太坊名称服务（ENS）数据
- 依赖方或钱包可以额外执行 ENS 数据的解析，因为这可以通过显示与 `address` 相关的人性化信息来改善用户体验。可解析的 ENS 数据包括：
    - [主要 ENS 名称](./eip-181.md)。
    - ENS 头像。
    - ENS 文档中指定的任何其他可解析资源。
- 如果执行 ENS 数据的解析，实施者应采取预防措施以保护用户隐私和同意，因为他们的 `address` 可能在解析过程中被转发给第三方服务。

### 依赖方实施者步骤

#### 指定请求来源

- SIWE 消息中的 `domain` 和（如果存在）`scheme` 必须与发起签名请求的来源相对应。例如，如果签名请求是在嵌入在父浏览器窗口中的跨源 iframe 内发出的，则 `domain`（以及（如果存在）`scheme`）必须与 iframe 的来源匹配，而不是父窗口的来源。这对于防止 iframe 在安全原因上错误地声称其祖先窗口的来源至关重要。此行为由符合规范的钱包强制执行。

#### 验证签名消息

- SIWE 消息必须检查是否符合前面章节中定义的 ABNF 消息格式，在解析后检查预期值（例如，`expiration-time`、`nonce`、`request-uri` 等），并且其签名必须按照 [使用以太坊账户签名和验证消息](#signing-and-verifying-messages-with-ethereum-accounts) 中的定义进行检查。

#### 创建会话

- 会话必须绑定到 `address`，而不是可以更改的进一步解析资源。

#### 解释和解析资源

- 实施者应确保列出的 `resources` 中的 URI 在明文形式下是人性化的。
- SIWE 消息中列出的 `resources` 的解释超出了本规范的范围。

### 钱包实施者步骤

#### 验证消息格式

- 完整的 SIWE 消息必须检查是否符合 [ABNF 消息格式](#abnf-message-format) 中定义的 ABNF。
- 如果在 [ERC-191](./eip-191.md) 消息签名请求中出现子字符串 `"wants you to sign in with your Ethereum account"`，钱包实施者应警告用户，除非消息完全符合 [ABNF 消息格式](#abnf-message-format) 中定义的格式。

#### 验证请求来源

- 钱包实施者必须通过验证请求的来源与 SIWE 消息中的 `scheme` 和 `domain` 字段来防止钓鱼攻击。例如，当处理以 `"example.com wants you to sign in..."` 开头的 SIWE 消息时，钱包检查请求是否确实来自 `https://example.com`。
- 来源应从受信任的数据源读取，例如浏览器窗口或通过 WalletConnect ([ERC-1328](./eip-1328.md)) 会话，以便与签名消息内容进行比较。
- 如果来源指向 localhost，钱包实施者可以发出警告，而不是拒绝验证。

以下是钱包遵循本规范中定义的请求来源验证要求的推荐算法。

该算法接受以下输入变量：

- SIWE 消息中的字段。
- 签名请求的 `origin` - 在浏览器钱包实现的情况下，请求通过提供者进行登录的页面的来源。
- `allowedSchemes` - 钱包允许的方案列表。
- `defaultScheme` - 当未提供方案时假定的方案。浏览器中的钱包实施者应使用 `https`。
- 开发者模式指示 - 决定某些风险是否应作为警告而不是拒绝的设置。可以手动配置或从 `origin` 为 localhost 派生。

算法描述如下：

- 如果未提供 `scheme`，则将 `defaultScheme` 赋值为 `scheme`。
- 如果 `scheme` 不在 `allowedSchemes` 中，则该 `scheme` 不被期望，钱包必须拒绝请求。浏览器中的钱包实施者应将 `allowedSchemes` 列表限制为仅 `'https'`，除非激活了开发者模式。
- 如果 `scheme` 与 `origin` 的方案不匹配，钱包应拒绝请求。如果激活了开发者模式，钱包实施者可以发出警告，而不是拒绝请求。在这种情况下，钱包继续处理请求。
- 如果 `domain` 和 `origin` 的 `host` 部分不匹配，钱包必须拒绝请求，除非钱包处于开发者模式。在开发者模式下，钱包可以发出警告并继续处理请求。
- 如果 `domain` 和 `origin` 的子域不匹配，钱包应拒绝请求，除非钱包处于开发者模式。在开发者模式下，钱包可以发出警告并继续处理请求。
- 让 `port` 为 `domain` 的端口组件，如果 `domain` 中不包含端口，则将 `port` 赋值为 `scheme` 指定的默认端口。
- 如果 `port` 不为空，则如果 `port` 与 `origin` 的端口不匹配，钱包应发出警告。
- 如果 `port` 为空，则如果 `origin` 包含特定端口，钱包可以发出警告。（注意 'https' 的默认端口为 443，因此这仅适用于 `allowedSchemes` 包含不寻常的方案）
- 返回请求来源验证完成。

#### 创建以太坊登录接口

- 钱包实施者必须默认向用户显示 SIWE 消息请求中的以下字段，并在签名之前显示，如果它们存在：`scheme`、`domain`、`address`、`statement` 和 `resources`。其他存在的字段也必须在签名之前通过默认或扩展接口提供给用户。
- 钱包实施者在向用户显示明文 SIWE 消息时，应要求用户在签名之前滚动到文本区域的底部。
- 钱包实施者可以通过将 ABNF 术语解析为数据元素来构建自定义 SIWE 用户界面，以便在界面中使用。上述显示规则仍适用于自定义界面。

#### 支持国际化 (i18n)

- 在成功将消息解析为 ABNF 术语后，可以在用户体验层面进行翻译。

## 理由

### 要求

编写一份关于以太坊登录应如何工作的规范。该规范应简单，并大致遵循现有实践。避免功能膨胀，特别是包括那些希望通过进入规范来获得采用的较少使用的项目。核心规范应去中心化、开放、非专有，并具有长期可行性。它不应依赖于集中式服务器，除了用户登录的应用程序已经运行的服务器。基本规范应包括：用于身份验证的以太坊账户、用于用户名的 ENS 名称（通过反向解析）以及来自 ENS 名称文本记录的额外个人资料信息（例如头像、社交媒体账号等）。

其他功能要求：

1. 用户在签名之前必须看到一个人类可理解的界面，基本上不包含机器目标的伪影，例如 JSON 块、十六进制代码（以太坊地址除外）和 baseXX 编码字符串。
2. 应用服务器必须能够完全实现其端的可用支持，而不强迫钱包进行更改。
3. 对于已经使用以太坊账户签名进行身份验证的应用程序和钱包，必须有简单明了的升级路径。
4. 必须有足够的设施和指南来有效缓解中间人（MITM）攻击、重放攻击和恶意签名请求。
### 设计目标

1. 以人为本
2. 简单易实现
3. 安全
4. 机器可读
5. 可扩展

### 技术决策

- 为什么选择 [ERC-191](./eip-191.md)（签名数据标准）而不是 [EIP-712](./eip-712.md)（以太坊类型结构化数据哈希和签名）
    - [ERC-191](./eip-191.md) 已在钱包用户体验中得到广泛支持，而 [EIP-712](./eip-712.md) 对于友好的用户显示支持尚待完善。 **(1, 2, 3, 4)**
    - [ERC-191](./eip-191.md) 使用预设前缀进行签名，简单易实现，而 [EIP-712](./eip-712.md) 实现更复杂，需要进一步实现定制的 Solidity 风格类型系统、基于 RLP 的编码格式和自定义的 keccak 哈希方案。 **(2)**
    - [ERC-191](./eip-191.md) 生成更易于人类阅读的消息，而 [EIP-712](./eip-712.md) 创建的签名输出供机器使用，大多数钱包未以对人类友好的方式显示待签名的有效负载。 **(1)**![](../assets/eip-4361/signing.png)

    - [EIP-712](./eip-712.md) 具有链上表示和链上可验证性的优势，例如在元交易中的使用，但此特性与规范的范围无关。 **(2)**
- 为什么不使用 JWT？钱包不支持 JWT。keccak 哈希函数未被 IANA 指定为 JOSE 算法。 **(2, 3)**
- 为什么不使用 YAML 或带有例外的 YAML？YAML 相较于 ABNF 更为宽松，后者可以清晰地表达字符集限制、所需顺序和严格的空白。 **(2, 3)**

### 超出范围

以下问题超出了本版本规范的定义范围：

- 不基于以太坊地址的额外身份验证。
- 对服务器资源的授权。
- 将 `resources` 字段中的 URI 解释为声明或其他资源。
- 确保域绑定的具体机制。
- 生成随机数和评估其适当性的具体机制。
- 在没有 TLS 连接的情况下使用的协议。

### 向前兼容的考虑

以下项目被考虑用于未来的支持，可能通过本规范的迭代或使用本规范作为依赖的新工作项。

- 可能支持去中心化标识符和可验证凭证。
- 可能支持跨链。
- 可能支持 SIOPv2。
- 可能未来支持 [EIP-712](./eip-712.md)。
- 版本解释规则，例如，签名时次要修订版本大于理解的版本，但不大于主要版本。

## 向后兼容性

- 大多数钱包实现已经支持 [ERC-191](./eip-191.md)，因此将其作为基础模式并增加额外功能。
- 从现有类似登录工作流的实现中收集了需求，包括允许用户接受服务条款的声明、用于重放保护的随机数，以及在消息中包含以太坊地址本身。

## 参考实现

参考实现可在 [这里](../assets/eip-4361/example.js) 获取。

## 安全考虑

### 标识符重用

- 为了实现完美隐私，理想情况下每次数字交互都应使用新的不相关标识符（例如，以太坊地址），选择性地披露所需的信息，而不多于此。
- 这一问题对某些用户群体的相关性较低，这些用户可能是本规范的早期采用者，例如那些故意将以太坊地址和/或 ENS 名称与其公共形象关联的用户。这些用户通常更倾向于重用标识符，以在多个服务中保持单一的相关身份。
- 随着主流采用，这一考虑将变得越来越重要。有几种方法可以朝着这一模型迈进，例如使用 HD 钱包、签名委托和零知识证明。然而，这些方法超出了本规范的范围，更适合后续规范。

### 密钥管理

- 通过以太坊登录，用户可以通过其密钥控制。这是额外的责任，主流用户可能不习惯接受，尤其是个人的密钥管理是一个难题。例如，没有像集中身份提供者常见的“忘记密码”按钮。
- 本规范的早期采用者可能已经熟练掌握密钥管理，因此这一考虑在主流采用时变得更加相关。
- 某些钱包可以使用智能合约和多重签名提供与密钥使用和密钥恢复相关的增强用户体验，这些可以通过 [ERC-1271](./eip-1271.md) 签名来支持。

### 钱包和依赖方的联合安全

- 钱包和依赖方都必须实现本规范，以提高最终用户的安全性。具体而言，钱包必须确认 SIWE 消息是针对正确的请求来源，或提供用户手动确认的方式（例如，在通过二维码或深度链接连接之前，指示用户在 TLS 保护的网站中视觉确认正确的域），否则用户将面临钓鱼攻击。

### 最小化钱包和服务器交互

- 在某些钱包登录工作流的实现中，服务器首先将 SIWE 消息的参数发送到钱包。其他实现则完全在客户端（例如，去中心化应用）生成 SIWE 消息以进行签名。后者在没有初始服务器交互的情况下应优先考虑，当通过最小化钱包-服务器交互带来用户隐私优势时。通常，后端服务器首先生成一个 `nonce` 以防止重放攻击，并在签名后进行验证。下一节关于防止重放攻击中建议了隐私保护的替代方案。
- 在钱包向用户呈现 SIWE 消息签名请求之前，钱包可以咨询服务器以获取待签名消息的适当内容，例如可接受的 `nonce` 或请求的 `resources` 集合。在与服务器通信时，钱包应采取预防措施，尽可能减少用户信息的泄露。
- 在签名之前，钱包可以咨询用户的偏好，例如从多个 `address` 中选择一个，或从多个中选择一个首选的 ENS 名称。

### 防止重放攻击

- 每次会话初始化时应选择一个 `nonce`，并具有足够的熵以防止重放攻击，即中间人攻击，攻击者能够捕获用户的签名并重新发送以为自己建立新的会话。
- 实现者可以考虑使用隐私保护且广泛可用的 `nonce` 值，例如从最近的以太坊区块哈希或最近的 Unix 时间戳派生的值。

### 防止钓鱼攻击

- 为了防止钓鱼攻击，钱包必须实施请求来源验证，如 [验证请求来源](#verifying-the-request-origin) 中所述。

### 通道安全

- 对于基于 Web 的应用程序，所有通信应使用 HTTPS，以防止对消息签名的中间人攻击。
- 当使用 HTTPS 以外的协议时，所有通信应使用适当的技术进行保护，以维护机密性、数据完整性和发送者/接收者的真实性。

### 会话失效

在与会话相关的状态变化中，实施者应检查多个情况。
- 如果 [ERC-1271](./eip-1271.md) 实现或相关数据更改了签名计算，服务器应适当地使会话失效。
- 如果 `resources` 中指定的任何资源发生变化，服务器应适当地使会话失效。然而，`resources` 的解释超出了本规范的范围。

### ABNF 术语的最大长度

- 虽然本规范不包含关于最大字符串长度的规范性要求，但实现者应选择最大长度，以在防止拒绝服务攻击、支持任意用例和用户可读性之间取得平衡。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。