---
eip: 7744
title: 代码索引
description: 全局字节码库，使开发者、审计员和研究人员能够高效地查找、分析和重用字节码。
author: Tim Pechersky (@peersky) <t@peersky.xyz>
discussions-to: https://ethereum-magicians.org/t/erc-7744-code-index/20569
status: 草案
type: 标准跟踪
category: ERC
created: 2024-07-16
---

## 摘要

本 EIP 定义了一个标准接口，用于通过字节码哈希在以太坊上索引智能合约。这使得无信任的发现和验证合约代码成为可能，促进了字节码签名、白名单和去中心化分发机制等用例。

## 动机

现有的合约发现依赖于地址，这些地址是非确定性的，并且可以通过代理进行混淆。通过字节码哈希进行索引提供了一种确定性和防篡改的方式来识别和验证合约代码，从而增强了以太坊生态系统的安全性和信任。

考虑一个希望证明合约代码完整性的安全审计员。通过引用字节码哈希，审计员可以将审计重点放在字节码本身，而无需评估部署参数或存储内容。这种方法在不审计整个合约状态的情况下验证了合约代码库的完整性。

此外，字节码引用允许在部署前对合约进行白名单处理，使开发者能够在不披露代码本身的情况下获得其代码库的预批准，甚至可以预先设置基础设施，以便在链上添加某些确定功能时改变其行为。

对于依赖于广泛代码重用的开发者，字节码引用可以防止通过代理进行地址引用时可能发生的恶意更改。这建立了延伸到最终用户应用程序的长期信任链。

对于去中心化应用（dApp）开发者，代码索引可以通过允许他们引用现有代码库而不是重新部署它们来节省 gas 成本，从而优化资源使用。这对于依赖于广泛重用相同代码库作为自身依赖项的 dApp 来说是非常有用的。

### 为什么这个注册表需要成为 ERC

代码索引对于无信任和安全的智能合约开发至关重要。通过标准化按字节码索引合约的接口，开发者可以轻松地将此功能集成到他们的智能合约中，从而增强以太坊生态系统的安全性和可信度。

其简单性和通用性使其适合广泛的应用。能够全球引用相同的代码库使其成为标准化的理想候选者。

最终，这一特性应纳入 EIP 标准，因为它是无信任和安全智能合约开发的基本构建块。这个标准是朝着这个目标迈出的一步。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 的描述进行解释。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity =0.8.20;

interface ICodeIndex {
    event Indexed(address indexed container, bytes32 indexed codeHash);
    error alreadyExists(bytes32 id, address source);

    function register(address container) external;

    function get(bytes32 id) external view returns (address);
}

/**
 * @title Byte Code Indexer Contract
 * @notice You can use this contract to index contracts by their bytecode.
 * @dev This allows to query contracts by their bytecode instead of addresses.
 * @author Tim Pechersky (@Peersky)
 */
contract CodeIndex is ICodeIndex {
    mapping(bytes32 => address) private index;

    /**
     * @notice Registers a contract in the index by its bytecode hash
     * @param container The contract to register
     * @dev `msg.codeHash` will be used
     * @dev It will revert if the contract is already indexed
     */
    function register(address container) external {
        address etalon = index[container.codehash];
        if (etalon != address(0)) {
            revert alreadyExists(container.codehash, etalon);
        }
        index[container.codehash] = container;
        emit Indexed(container, container.codehash);
    }

    /**
     * @notice Returns the contract address by its bytecode hash
     * @dev returns zero if the contract is not indexed
     * @param id The bytecode hash
     * @return The contract address
     */
    function get(bytes32 id) external view returns (address) {
        return index[id];
    }
}

```

### 部署方法

`CodeIndex` 合约部署在：`0xc0D31d398c5ee86C5f8a23FA253ee8a586dA03Ce`，使用 `CREATE2` 通过位于 `0x4e59b44847b379578588920ca78fbf26c0b4956c` 的确定性部署者，盐值为 `0x220a70730c743a005cfd55180805d2c0d5b8c7695c5496100dcffa91c02befce`，通过寻求具有有意义名称 "Code ID (`c0D31d`)" 的虚荣地址获得。

## 理由

**字节码优于地址**：字节码是确定性的，可以在链上验证，而地址是模糊和可变的。

**重新索引时的回退**：哈希碰撞攻击的概率虽小，但并非零。禁止更新字节码的索引位置可以解决这个问题。

**简单接口**：该接口是最小化的，专注于最大化组合性和易于实现。

**库实现**：将其作为库实现会限制其影响，使代码重用变得更加困难，并缺乏单一的官方真实来源。通过将其确立为 ERC，我们确保了标准化和广泛采用，推动生态系统向前发展。

## 参考实现

代码索引的参考实现可以在资产文件夹中找到。您可以找到代码索引的 [接口](../assets/eip-7744/ICodeIndex.sol) 和 [实现](../assets/eip-7744/CodeIndex.sol)。

## 安全考虑

**恶意代码**：索引不保证索引合约的安全性或功能。用户必须谨慎行事，并在与索引合约交互之前进行自己的尽职调查。

**注册合约的存储内容**：索引仅引用合约的字节码，而不引用存储内容。这意味着合约状态未被索引，并可能随时间变化。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。