---
eip: 3135
title: 独占可索取代币
author: Zhenyu Sun (@Ungigdu)
discussions-to: https://github.com/ethereum/EIPs/issues/3132
status: 停滞
type: 标准跟踪
category: ERC
created: 2020-08-10
requires: 20
---

## 简单总结

该标准定义了一种只能由代币发行者通过付款者的签名进行索取的代币。

## 摘要

该 EIP 定义了一组对默认代币标准（如 ERC-20）的扩展，允许在线/离线服务提供商通过签署和验证关于代币链下消费的消息，与任意数量的用户建立微支付通道。使用此机制将减少参与者与区块链的交互，从而节省 gas 并提高性能。

## 动机

该 EIP 有两个主要目的，一个是减少与区块链的交互，另一个是将以太坊与现实世界的支付问题联系起来。

许多小企业希望基于区块链构建支付系统，但发现这很困难。基本上有两种方式：

1. 直接用代币支付。许多钱包可以接收和转移代币，但以太坊上的交易需要支付 gas 并且需要时间确认。
2. 用户在支付智能合约上锁定代币，服务提供商使用用户签署的支付消息释放代币，建立微支付通道。其优点是与区块链的交互减少，签名/验证过程在链下进行。但与支付合约的交互需要服务提供商构建 DApp，这需要许多小企业没有的资源。即使他们设法构建了 DApp，它们也都是不同的，未标准化。此外，用户需要有一个带 DApp 浏览器的钱包，并且必须学习如何使用它。

该 EIP 有助于标准化微支付系统的交互，并使钱包在未来能够构建通用 UI。

## 规范

```solidity

/// @return Image url of this token or descriptive resources
function iconUrl() external view returns (string memory);

/// @return Issuer of this token. Only issuer can execute claim function
function issuer() external view returns (address);

/**
 *  @notice   Remove consumption from payer's deposite
 *  @dev      Check if msg.sender == issuer
 *  @param    from          Payer's address
 *  @param    consumption   How many token is consumed in this epoch, specified
 *  @param    epoch         Epoch increased by 1 after claim or withdraw, at the beginning of each epoch, consumption goes back to 0
 *  @param    signature     Signature of payment message signed by payer
*/
function claim(address from, uint256 consumption, uint256 epoch, bytes calldata signature) external;

function transferIssuer(address newIssuer) external;

/// @notice   Move amount from payer's token balance to deposite balance to ensure payment is sufficient
function deposit(uint256 amount) external;

/**
 *  @notice   Give remaining deposite balance back to "to" account, act as "refund" function
 *  @dev      In prepayment module, withdraw is executed from issuer account
 *            In lock-release module, withdraw is executed from user account
 *  @param    to            the account receiving remaining deposite
 *  @param    amount        how many token is returned
*/
function withdraw(address to, uint256 amount) external;

function depositBalanceOf(address user) external view returns(uint256 depositBalance, uint256 epoch);

event Deposit(
    address indexed from,
    uint256 amount
);

event Withdraw(
    address indexed to,
    uint256 amount
);
    
event TransferIssuer(
    address indexed oldIssuer,
    address indexed newIssuer
);

event Claim(
    address indexed from,
    address indexed to,
    uint256 epoch,
    uint256 consumption
);

```

### 签名

生成 ECDSA 签名的伪代码：
```
sign(keccak256(abi_encode(
    "\x19Ethereum Signed Message:\n32", 
        keccak256(abi_encode(
            token_address,
            payer_address,
            token_issuer,
            token_consumption,        //calculated by user client
            epoch
        ))
    ))
,private_key)

```

### 验证过程

验证过程包含对签名和代币消费的检查

由验证服务器运行的伪代码如下：

```

serving_loop:

    for {
        /**
         * unpaied_consumption is calculated by provider
         * signed_consumption is claimable amount
         * tolerance allows payer "owes" provider to a certain degree
        */
        //getSignedConsumption returns amount that are already claimable 
        if(unpaied_consumption <  signed_consumption + tolerance){
            informUser("user need charge", unpaied_consumption)
            interruptService() 
        }else{
            isServing() || recoverService()
        }
    }

verification_loop:

    for {
        message = incomingMessage()
        if(recover_signer(message, signature) != payer_address){
            informUser("check signature failed", hash(message))
            continue
        }

        /**
        * optional: when using echo server to sync messages between verification servers
        * more info about this in Security Considerations section
        */
        if(query(message) != message){
            informUser("message outdate", hash(message))
            continue   
        }

        if(epoch != message.epoch || message.consumption > getDepositBalance()){
            informUser("invalid message", epoch, unpaied_consumption)
            continue
        }
       
        signed_consumption = message.consumption
        save(message)
    }
    
claim_process:

    if(claim()){
        unpaied_consumption -= signed_consumption
        signed_consumption = 0
        epoch+=1
    }

```
### 关于提现

提现功能根据商业模型略有不同

1. 预付款模型

在预付款商业模型中，例如使用代币作为一般商店的充值卡，用户提前支付（加密）货币以索取可索取代币作为充值卡（带有奖金或折扣）。结账时，客户用更新的消费（旧消费 + 本次消费）签署一条消息给商店，商店在链下验证此消息。购物过程循环进行，直到客户想要退卡并取回钱。因为商店已经持有所有货币，提现功能应该由代币发行者（商店）执行，以在索取后返回剩余的存款余额。预付款模型可以很容易地构建到具有二维码扫描功能的钱包中。

2. 锁定-释放模型

如果我们运行一个接受代币作为支付的付费端到端加密电子邮件服务，我们可以使用锁定-释放模型。与预付款不同，我们对发送给 N 个收件人的电子邮件收取 X * N 代币。在这种“按使用付费”的场景中，服务的计数在客户端和服务器端同时进行。客户端不应信任服务器给出的收费金额，以防服务器出现故障或恶意。当客户端决定不信任服务器时，它停止签署消息，但部分代币被扣押在存款余额中。为了解决这个问题，提现功能应该由付款者账户执行，并限制例如一个月内未更改的时间段。

## 理由

该 EIP 针对 ERC-20 代币，因为其广泛采用。然而，该扩展旨在与其他代币标准兼容。

我们选择在代币合约中实现这些功能而不是单独的记录合约的原因如下：
- 代币转移比与 DApp 交互更方便和更通用
- 代币更标准化，具有更好的 UI 支持
- 代币等同于服务，使代币经济更加繁荣
- 移除批准过程

## 向后兼容性

该 EIP 完全向后兼容，因为其实现扩展了 [ERC-20](./erc-20.md) 的功能。

## 实现

```solidity

mapping (address => StampBalance) private _depositBalance;
    
struct StampBalance{
    uint256 balance;
    uint256 epoch;
}
    
function deposit(uint256 value) override external{
    require(value <= _balances[msg.sender]);
    _balances[msg.sender] = _balances[msg.sender].sub(value);
    _depositBalance[msg.sender].balance = _depositBalance[msg.sender].balance.add(value);
    emit Deposit(msg.sender, value);
}

function withdraw(address to, uint256 value) override onlyIssuer external{
    require(value <= _depositBalance[to].balance);
    _depositBalance[to].balance = _depositBalance[to].balance.sub(value);
    _depositBalance[to].epoch += 1;
    _balances[to] = _balances[to].add(value);
    emit Withdraw(to, value);
}
    
function depositBalanceOf(address user) override public view returns(uint256 depositBalance, uint256 epoch){
    return (_depositBalance[user].balance, _depositBalance[user].epoch);
}

// prepayment model
function claim(address from, uint credit, uint epoch, bytes memory signature) override onlyIssuer external{
    require(credit > 0);
    require(_depositBalance[from].epoch + 1 == epoch);
    require(_depositBalance[from].balance >= credit);
    bytes32 message = keccak256(abi.encode(this, from, _issuer, credit, epoch));
    bytes32 msgHash = prefixed(message);
    require(recoverSigner(msgHash, signature) == from);
    _depositBalance[from].balance = _depositBalance[from].balance.sub(credit);
    _balances[_issuer] = _balances[_issuer].add(credit);
    _depositBalance[from].epoch += 1;
    emit Claim(from, msg.sender, credit, epoch);
}

function prefixed(bytes32 hash) internal pure returns (bytes32) {
    return keccak256(abi.encode("\x19Ethereum Signed Message:\n32", hash));
}

function recoverSigner(bytes32 message, bytes memory sig) internal pure  returns (address) {
    (uint8 v, bytes32 r, bytes32 s) = splitSignature(sig);
    return ecrecover(message, v, r, s);
}

function splitSignature(bytes memory sig) internal pure returns (uint8 v, bytes32 r, bytes32 s) {
    require(sig.length == 65);
    assembly {
        r := mload(add(sig, 32))
        s := mload(add(sig, 64))
        v := byte(0, mload(add(sig, 96)))
    }
    return (v, r, s);
}

```

## 安全考虑

通过将索取功能限制为发行者，链层上没有竞争条件。然而，当发行者使用多个验证者并且付款者同时签署多个支付消息时，可能会出现双重支付问题。这些消息中的一些可能有机会被检查为有效，尽管只有消费最大的消息可以被索取。这个问题可以通过引入一个回声服务器来解决，该服务器接受来自验证者的消息，按顺序返回消费最大和时间戳最大的消息。如果验证者收到的答案与他发送的消息不同，它会将回声服务器的消息更新为它收到的最后一条消息，并更新关于该付款者的本地存储状态。然后，验证者再次向付款者请求新消息。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。