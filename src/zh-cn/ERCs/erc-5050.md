---
eip: 5050
title: 具有模块化环境的互动 NFT
description: 用于 NFT 之间及其上的交互的动作消息和发现协议
author: Alexi (@alexi)
discussions-to: https://ethereum-magicians.org/t/eip-5050-nft-interaction-standard/9922
status: Stagnant
type: Standards Track
category: ERC
created: 2021-04-18
requires: 165, 173, 721, 1155, 1820, 4906
---

## 摘要

本标准定义了一种广泛适用的动作消息协议，用于在代币之间传输用户发起的动作。通过可选的状态控制合约（即环境）实现模块化状态性，这些合约管理共享状态，并提供动作过程的仲裁和结算。

## 动机

代币化项目标准如 [EIP-721](./eip-721.md) 和 [EIP-1155](./eip-1155.md) 作为以太坊计算环境的对象。越来越多的项目希望在 NFT 中构建互动性和 *“数字物理”*，特别是在游戏和去中心化身份的背景下。标准的动作消息协议将允许这一物理层以与其操作对象相同的开放、以太坊原生方式进行开发。

所述的消息协议定义了如何在代币和（可选的）共享状态环境之间发起和传输动作。它与一个通用接口配对，用于定义功能，允许链外服务聚合和查询支持的合约以获取功能和互操作性；创建一个可发现的、可读的人类网络的互动代币合约。实现此标准的合约不仅可以被此类服务自动发现，它们的 *交互政策* 也可以被发现。这使得客户端能够轻松发现兼容的发送者和接收者，以及允许的动作。

聚合器还可以解析动作事件日志，以推导出新动作类型、趋势/热门/新互动合约，用户可能与之交互的代币和状态合约对，以及其他发现工具以促进交互。

### 好处

1. 使互动代币合约 **可被应用发现和使用**
2. 为游戏和其他应用创建去中心化的“数字物理”层
3. 为开发者提供一个简单的解决方案，具有可行的有效性保证，以制作动态 NFT 和其他代币
4. 允许通用的动作桥接在链之间传输动作（使 L1 资产上的动作能够保存到 L2，L1 资产与 L2 资产交互，以及 L2 动作在 L1 上“汇总”/最终确定）。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 中描述的方式进行解释。

实现此 EIP 标准的智能合约必须实现 [EIP-165](./eip-165.md) 的 supportsInterface 函数，并且如果通过 `interfaceID` 参数传递 `IERC5050Sender` 接口 ID `0xc8c6c9f3` 和/或 `IERC5050Receiver` 接口 ID `0x1a3f02f4`，则必须返回常量值 `true`（具体取决于合约实现了哪些接口）。

```solidity
pragma solidity ^0.8.0;

/// @param _address The address of the interactive object
/// @param tokenId The token that is interacting (optional)
struct Object {
    address _address;
    uint256 _tokenId;
}

/// @param selector The bytes4(keccack256()) encoding of the action string
/// @param user The address of the sender
/// @param from The initiating object
/// @param to The receiving object
/// @param state The state controller contract
/// @param data Additional data with no specified format
struct Action {
    bytes4 selector;
    address user;
    Object from;
    Object to;
    address state;
    bytes data;
}

/// @title EIP-5050 Interactive NFTs with Modular Environments
interface IERC5050Sender {
    /// @notice Send an action to the target address
    /// @dev The action's `fromContract` is automatically set to `address(this)`,
    /// and the `from` parameter is set to `msg.sender`.
    /// @param action The action to send
    function sendAction(Action memory action) external payable;

    /// @notice Check if an action is valid based on its hash and nonce
    /// @dev When an action passes through all three possible contracts
    /// (`fromContract`, `to`, and `state`) the `state` contract validates the
    /// action with the initiating `fromContract` using a nonced action hash.
    /// This hash is calculated and saved to storage on the `fromContract` before
    /// action handling is initiated. The `state` contract calculates the hash
    /// and verifies it and nonce with the `fromContract`.
    /// @param _hash The hash to validate
    /// @param _nonce The nonce to validate
    function isValid(bytes32 _hash, uint256 _nonce) external returns (bool);

    /// @notice Retrieve list of actions that can be sent.
    /// @dev Intended for use by off-chain applications to query compatible contracts,
    /// and to advertise functionality in human-readable form.
    function sendableActions() external view returns (string[] memory);

    /// @notice Change or reaffirm the approved address for an action
    /// @dev The zero address indicates there is no approved address.
    ///  Throws unless `msg.sender` is the `_account`, or an authorized
    ///  operator of the `_account`.
    /// @param _account The account of the account-action pair to approve
    /// @param _action The action of the account-action pair to approve
    /// @param _approved The new approved account-action controller
    function approveForAction(
        address _account,
        bytes4 _action,
        address _approved
    ) external returns (bool);

    /// @notice Enable or disable approval for a third party ("operator") to conduct
    ///  all actions on behalf of `msg.sender`
    /// @dev Emits the ApprovalForAll event. The contract MUST allow
    ///  an unbounded number of operators per owner.
    /// @param _operator Address to add to the set of authorized operators
    /// @param _approved True if the operator is approved, false to revoke approval
    function setApprovalForAllActions(address _operator, bool _approved)
        external;

    /// @notice Get the approved address for an account-action pair
    /// @dev Throws if `_tokenId` is not a valid NFT.
    /// @param _account The account of the account-action to find the approved address for
    /// @param _action The action of the account-action to find the approved address for
    /// @return The approved address for this account-action, or the zero address if
    ///  there is none
    function getApprovedForAction(address _account, bytes4 _action)
        external
        view
        returns (address);

    /// @notice Query if an address is an authorized operator for another address
    /// @param _account The address on whose behalf actions are performed
    /// @param _operator The address that acts on behalf of the account
    /// @return True if `_operator` is an approved operator for `_account`, false otherwise
    function isApprovedForAllActions(address _account, address _operator)
        external
        view
        returns (bool);

    /// @dev This emits when an action is sent (`sendAction()`)
    event SendAction(
        bytes4 indexed name,
        address _from,
        address indexed _fromContract,
        uint256 _tokenId,
        address indexed _to,
        uint256 _toTokenId,
        address _state,
        bytes _data
    );

    /// @dev This emits when the approved address for an account-action pair
    ///  is changed or reaffirmed. The zero address indicates there is no
    ///  approved address.
    event ApprovalForAction(
        address indexed _account,
        bytes4 indexed _action,
        address indexed _approved
    );

    /// @dev This emits when an operator is enabled or disabled for an account.
    ///  The operator can conduct all actions on behalf of the account.
    event ApprovalForAllActions(
        address indexed _account,
        address indexed _operator,
        bool _approved
    );
}

interface IERC5050Receiver {
    /// @notice Handle an action
    /// @dev Both the `to` contract and `state` contract are called via
    /// `onActionReceived()`.
    /// @param action The action to handle
    function onActionReceived(Action calldata action, uint256 _nonce)
        external
        payable;

    /// @notice Retrieve list of actions that can be received.
    /// @dev Intended for use by off-chain applications to query compatible contracts,
    /// and to advertise functionality in human-readable form.
    function receivableActions() external view returns (string[] memory);

    /// @dev This emits when a valid action is received.
    event ActionReceived(
        bytes4 indexed name,
        address _from,
        address indexed _fromContract,
        uint256 _tokenId,
        address indexed _to,
        uint256 _toTokenId,
        address _state,
        bytes _data
    );
}
```

### 动作命名

动作应使用点分隔符进行命名空间划分（例如，`"spells.cast"` 指定命名空间为 `"spells"` 的 `"cast"` 动作），并使用箭头分隔符进行序列指定（例如，`"settle>build"` 表示 `"settle"` 必须在 `"build"` 之前接收）。

### 状态合约的工作原理

动作并不要求使用状态合约。动作可以从一个代币合约（`Object`）传输到另一个，或从用户传输到单个代币合约。在这些情况下，发送和接收合约各自控制自己的状态。

状态合约允许任意发送者和接收者共享用户指定的状态环境。每个 `Object` 可以定义自己的动作处理，这可能包括在动作期间从状态合约读取，但动作必须由状态合约最终确定。这意味着状态合约作为真实依据。

预期的工作流程是状态合约定义有状态的游戏环境，通常具有供其他合约使用的自定义 `IState` 接口。`Objects` 向状态合约注册以初始化其状态。然后，用户使用特定的状态合约提交动作，以在游戏中实现某些效果。

状态合约的模块化允许创建多个相同或相似的“游戏环境”副本，并由客户端进行替换。此模块化可以通过多种方式使用：

- 聚合服务可以分析动作事件，以确定给定发送者/接收者的可能状态合约
- 发送者/接收者合约可以要求特定的状态合约
- 发送者/接收者合约可以允许任何状态合约，但设置一个默认值。这对于根据状态改变其渲染的 NFT 非常重要。此默认值也可以由代币持有者配置。
- 状态合约可以是另一个链上状态合约的桥接，允许 L1 验证、L2 存储使用模式（使用层 1 资产验证动作，在存储更便宜的 L2 保存）。

#### 示例

状态合约 `FightGame` 定义了一个格斗游戏环境。代币持有者调用 `FightGame.register(contract, tokenId)` 随机初始化他们的属性（力量/生命值等）。持有合约 `Fighters` 的注册代币 A 的账户调用 `Fighters.sendAction(AttackAction)`，指定来自 `Fighters` 的代币 A 作为发送者，来自 `Pacifists` 合约的代币 B 作为接收者，以及 `FightGame` 作为状态合约。

该动作被传递给代币 B，代币 B 可以以任何方式处理该动作，然后将动作传递给 `FightGame` 状态合约。状态合约可以通过 `Fighters` 合约验证存储的动作哈希，以验证该动作的真实性，然后在更新属性时对代币 B 造成伤害。

代币 A 和 B 可以根据 `FightGame` 状态合约中的属性或根据其自身存储的数据（在响应发送/接收动作时更新）更新其元数据。

### 扩展

#### 互动

某些合约可能具有自定义用户界面，以促进交互。

```solidity
pragma solidity ^0.8.0;

/// @title EIP-5050 Interactive NFTs with Modular Environments
interface IERC5050Interactive {
    function interfaceURI(bytes4 _action) external view returns (string);
}
```

#### 动作代理

动作代理可用于支持与不可升级合约的向后兼容性，并可能用于跨链动作桥接。

它们可以使用修改版的 [EIP-1820](./eip-1820.md#erc-1820-registry-smart-contract) 实现，该版本允许 [EIP-173](./eip-173.md) 合约所有者调用 `setManager()`。

#### 可控

使用此标准的用户可能希望允许受信任的合约控制动作过程，以提供安全保证，并支持动作桥接。控制者逐步执行动作链，依次调用每个合约。

支持控制者的合约应忽略与动作验证相关的 require/revert 语句，并且不得将动作传递给链中的下一个合约。

```solidity
pragma solidity ^0.8.0;

/// @title EIP-5050 Action Controller
interface IControllable {
    
    /// @notice Enable or disable approval for a third party ("controller") to force
    ///  handling of a given action without performing EIP-5050 validity checks.
    /// @dev Emits the ControllerApproval event. The contract MUST allow
    ///  an unbounded number of controllers per action.
    /// @param _controller Address to add to the set of authorized controllers
    /// @param _action Selector of the action for which the controller is approved / disapproved
    /// @param _approved True if the controller is approved, false to revoke approval
    function setControllerApproval(address _controller, bytes4 _action, bool _approved)
        external;

    /// @notice Enable or disable approval for a third party ("controller") to force
    ///  action handling without performing EIP-5050 validity checks. 
    /// @dev Emits the ControllerApproval event. The contract MUST allow
    ///  an unbounded number of controllers per action.
    /// @param _controller Address to add to the set of authorized controllers
    /// @param _approved True if the controller is approved, false to revoke approval
    function setControllerApprovalForAll(address _controller, bool _approved)
        external;

    /// @notice Query if an address is an authorized controller for a given action.
    /// @param _controller The trusted third party address that can force action handling
    /// @param _action The action selector to query against
    /// @return True if `_controller` is an approved operator for `_account`, false otherwise
    function isApprovedController(address _controller, bytes4 _action)
        external
        view
        returns (bool);
    
    /// @dev This emits when a controller is enabled or disabled for the given
    ///  action. The controller can force `action` handling on the emitting contract, 
    ///  bypassing the standard EIP-5050 validity checks.
    event ControllerApproval(
        address indexed _controller,
        bytes4 indexed _action,
        bool _approved
    );
    
    /// @dev This emits when a controller is enabled or disabled for all actions.
    ///  Disabling all action approval for a controller does not override explicit action
    ///  action approvals. Controller's approved for all actions can force action handling 
    ///  on the emitting contract for any action.
    event ControllerApprovalForAll(
        address indexed _controller,
        bool _approved
    );
}
```

#### 元数据更新

互动 NFT 可能会根据某些动作更新其元数据，开发者可能希望实现 [EIP-4906](./eip-4906.md) 事件发射器。

## 理由

此互动代币标准的关键特性是：1）创建一种定义、宣传和进行对象交互的通用方式，2）以最低的 gas 开销启用可选的、经中介的状态性，并提供 *有用* 的有效性保证，3）易于开发者实现，4）易于最终用户使用。
### 动作名称与选择器

动作使用人类可读的字符串进行广告，并通过函数选择器（`bytes4(keccack256(action_key))`）进行处理。人类可读的字符串使最终用户能够轻松理解功能，而函数选择器则允许对任意长度的动作键进行高效的比较操作。该方案还允许简单的命名空间和序列规范。

离线服务可以在与实现此 EIP 的合约交互或解析 `SendAction` 和 `ActionReceived` 事件日志时，轻松将字符串转换为 `bytes4` 选择器编码。

### 验证

通过动作数据的哈希对发起合约进行验证，几乎得到了所有受访者的满意，并且是探索的最具 gas 效率的验证解决方案。我们认识到，这种解决方案不允许接收合约和状态合约在不使用 `tx.origin` 的情况下验证发起的 `user` 账户，而 `tx.origin` 易受钓鱼攻击。

我们考虑使用签名消息来验证用户发起，但这种方法有两个主要缺点：

1. **用户体验** 用户需要执行两个步骤来提交每个动作（签名消息和发送交易）
2. **Gas** 执行签名验证的计算成本高

最重要的是，受访开发者之间的共识是，严格的用户验证并不是必要的，因为关注点仅在于恶意发起合约会钓鱼用户以提交带有恶意合约资产的动作。**该协议将发起合约的代币视为主要推动者，而不是用户。** 任何人都可以向比尔·盖茨发推文。任何代币都可以向另一个代币发送动作。哪些动作被接受，以及如何处理，留给合约自行决定。高价值动作可以通过状态合约进行声誉门控，或通过允许/不允许列表进行访问门控。 [`Controllable`](#controllable) 合约也可以通过受信任的控制器作为动作链的替代方案使用。

*考虑的替代方案：将动作作为签名消息传输，将动作保存到发起合约的可重用存储槽中*

### 状态合约

将状态逻辑移动到专用的参数化合约中，使状态成为动作原语，并防止状态管理在合约中被模糊化。具体而言，它允许用户决定在哪个“环境”中提交动作，并允许发起合约和接收合约共享状态数据，而无需它们进行通信。

状态合约接口的具体细节超出了本标准的范围，旨在为独特的交互环境量身定制。

### Gas 和复杂性（关于动作链）

每个合约中的动作处理可以是任意复杂的，并且无法消除某些合约交互耗尽 gas 的可能性。然而，开发者应尽一切努力在其动作处理方法中最小化 gas 使用，并避免使用 for 循环。

*考虑的替代方案：多请求动作链，从一个合约推送到下一个合约。*

## 向后兼容性

不可升级的、已部署的代币合约将不兼容此标准，除非使用代理注册扩展。

## 参考实现

参考实现包含在 `../assets/eip-5050` 中，提供一个简单的无状态示例 [`ExampleToken2Token.sol`](../assets/eip-5050/ExampleToken2Token.sol)，以及一个有状态示例 [`ExampleStateContract.sol`](../assets/eip-5050/ExampleStateContract.sol)

## 安全考虑

该协议的核心安全考虑是动作验证。动作从一个合约传递到另一个合约，这意味着接收合约无法原生验证发起合约的调用者是否与 `action.from` 地址匹配。该协议的一个重要贡献是提供了一种替代使用签名消息的方法，后者要求用户为每个提交的动作执行两个操作。

正如在 [验证](#验证) 中讨论的那样，这是可行的，因为发起合约/代币被视为主要推动者，而不是用户。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。