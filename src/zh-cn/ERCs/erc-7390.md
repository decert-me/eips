---
eip: 7390
title: ERC-20 代币的普通期权
description: 创建、管理和执行简单的时间限制买入/卖出（普通）期权的接口。
author: Ewan Humbert (@Xeway) <xeway@protonmail.com>, Lassi Maksimainen (@mlalma) <lassi.maksimainen@gmail.com>
discussions-to: https://ethereum-magicians.org/t/erc-7390-vanilla-option-standard/15206
status: Draft
type: Standards Track
category: ERC
created: 2022-09-02
requires: 20, 1155
---

## 摘要

该标准定义了一组全面的函数和事件，以促进普通期权的无缝交互（创建、管理、行使等）。

普通期权赋予在指定时间内以设定价格买入或卖出资产的权利，而无义务。

该标准并不代表在到期日后将无用的简单期权。相反，它可以存储所需的任意数量的发行。每个发行都有一个 ID，可以独立于其他发行进行购买、行使、取消等。\
每个发行都是有抵押的，这意味着在买方可以购买期权之前，卖方必须向合约提供抵押。若买方在行使窗口内未行使，卖方可以取回抵押。\
买方可以选择仅购买发行的一部分（这意味着可以有多个买方），并将相应地收到代表发行部分的代币（[ERC-1155](./eip-1155.md)）。从现在起，我们将这些代币称为 *赎回代币*。这些代币可以在用户之间交换，并用于行使期权。通过这种机制，买方可以选择仅行使他所购买的一部分。\
此外，卖方可以选择取消发行，如果尚未购买任何期权。他也有权在任何时候更新溢价价格。这不会影响已经购买的期权。\
基础代币、行权代币和溢价代币均为 [ERC-20](./eip-20.md) 代币。

在以下内容中，复数术语 option**s** 有时会被使用。这可以指买方购买并可以行使的赎回代币的数量。

## 动机

期权是广泛使用的金融工具，对投资者和交易者具有真正的实用性。它提供了多种风险管理工具和投机机会。\
在去中心化金融中，许多期权销售平台应运而生，但这些协议各自实现了自己的期权定义。这导致了不兼容性，这很可惜，因为期权应该像可替代/不可替代代币一样具有互操作性。\
通过引入普通期权合约的标准接口，我们旨在促进一个更具包容性和互操作性的衍生品生态系统。该标准将增强用户体验，促进去中心化期权平台的发展，使用户能够在不同应用之间无缝交易期权。此外，该标准旨在代表普通期权，这是最常见的期权类型。该标准可以作为更复杂期权（如奇异期权）的基础。

## 规范

该提案的实现必须同时实现 ERC-1155，以便能够仅购买发行的一部分。

### 接口

```solidity
interface IERC7390 {
    enum Side {
        Call,
        Put
    }

    struct VanillaOptionData {
        Side side;
        address underlyingToken;
        uint256 amount;
        address strikeToken;
        uint256 strike;
        address premiumToken;
        uint256 premium;
        uint256 exerciseWindowStart;
        uint256 exerciseWindowEnd;
        address[] allowed;
    }

    struct OptionIssuance {
        VanillaOptionData data;
        address writer;
        uint256 exercisedAmount;
        uint256 soldAmount;
    }

    error Forbidden();
    error TransferFailed();
    error TimeForbidden();
    error AmountForbidden();
    error InsufficientBalance();

    event Created(uint256 indexed id);
    event Bought(uint256 indexed id, uint256 amount, address indexed buyer);
    event Exercised(uint256 indexed id, uint256 amount);
    event Expired(uint256 indexed id);
    event Canceled(uint256 indexed id);
    event PremiumUpdated(uint256 indexed id, uint256 amount);
    event AllowedUpdated(uint256 indexed id, address[] allowed);

    function create(VanillaOptionData calldata optionData) external returns (uint256);

    function buy(uint256 id, uint256 amount) external;

    function exercise(uint256 id, uint256 amount) external;

    function retrieveExpiredTokens(uint256 id, address receiver) external;

    function cancel(uint256 id, address receiver) external;

    function updatePremium(uint256 id, uint256 amount) external;

    function updateAllowed(uint256 id, address[] memory allowed) external;

    function issuance(uint256 id) external view returns (OptionIssuance memory);
}
```

### 状态变量描述

在创建时，用户必须提供填充的 `VanillaOptionData` 结构实例，该结构包含初始化期权发行所需的所有关键信息。

#### `side`

**类型: `enum`**

期权的方向。可以取值为 `Call` 或 `Put`。`Call` 期权赋予期权买方行使任何获得的期权代币以在给定的 `strike` 价格下使用期权卖方的 `strikeToken` 购买 `underlying` 代币的权利。类似地，`Put` 期权赋予期权买方以 `strike` 价格将 `underlying` 代币卖给期权卖方的权利。

#### `underlyingToken`

**类型: `address` ([ERC-20](./eip-20.md) 合约)**

基础代币。

#### `amount`

**类型: `uint256`**

可以行使的基础代币的最大数量。

> 注意代币的小数位数！

#### `strikeToken`

**类型: `address` (ERC-20 合约)**

用于作为参考以确定行权价格的代币。

#### `strike`

**类型: `uint256`**

行权价格。期权买方可能只能行使发行的一部分，且支付的行权价格必须由合约进行调整以反映这一点。

请注意，`strike` 旨在表示单个 `underlyingToken` 的 `strikeToken` 价格。

> 注意代币的小数位数！

#### `premiumToken`

**类型: `address` (ERC-20 合约)**

溢价代币。

#### `premium`

**类型: `uint256`**

溢价价格是期权买方必须支付给期权卖方的价格，以补偿卖方因发行期权而承担的风险。期权溢价根据各种因素变化，最重要的因素是基础代币的波动性、行权价格和剩余的行使时间。

**请注意，溢价价格是为行使总 `amount` 的发行而设定的。买方可能只能购买期权代币的一部分，且支付的溢价价格必须由合约进行调整以反映这一点。**

> 注意代币的小数位数！

#### `exerciseWindowStart`

**类型: `uint256`**\
**格式: *自 Unix 纪元以来的秒数时间戳***

期权行使窗口开始时间。当当前时间大于或等于 `exerciseWindowStart` 且小于或等于 `exerciseWindowEnd` 时，期权的拥有者可以行使它们。

#### `exerciseWindowEnd`

**类型: `uint256`**\
**格式: *自 Unix 纪元以来的秒数时间戳***

期权行使窗口结束时间。当当前时间大于或等于 `exerciseWindowStart` 且小于或等于 `exerciseWindowEnd` 时，期权的拥有者可以行使它们。当当前时间大于 `exerciseWindowEnd` 时，买方无法行使，卖方可以取回剩余的基础（看涨）或行权（看跌）代币。

#### `allowed`

**类型: `address[]`**

被允许购买发行的地址。如果数组为空，则所有地址都被允许购买发行。

`VanillaOptionData` 存储在 `OptionIssuance` 结构中，该结构用于存储期权发行数据。它包含其他信息。

#### `writer`

**类型: `address`**

卖方的地址，意味着创建期权的地址。

#### `exercisedAmount`

**类型: `uint256`**

已行使的基础代币数量。

#### `soldAmount`

**类型: `uint256`**

为该发行购买的基础代币数量。

#### `transferredExerciseCost`

**类型: `uint256`**

已转移给期权发行的卖方（看涨）或买方（看跌）的 `strikeToken` 代币数量。\
这是一个实用变量，用于不必每次都计算转移的总行使成本。它在 `exercisedAmount` 更新时同时更新。计算公式为 `(amount * selectedIssuance.data.strike) / (10**underlyingToken.decimals())`。

#### `exerciseCost`

**类型: `uint256`**

行使成本。它代表卖方必须存入合约的抵押（看跌），或如果所有买方决定行使，卖方可以收到的 `strikeToken` 代币数量（看涨）。\
这是一个实用变量，用于不必每次都计算行使成本。我们在期权创建时计算它。计算公式为 `(strike * amount) / (10 ** underlyingToken.decimals())`。
### 函数描述

#### `constructor`

此标准不需要构造函数，但合约必须实现 ERC-1155 接口。因此，合约必须调用 ERC-1155 构造函数。

#### `create`

```solidity
function create(VanillaOptionData calldata optionData) external returns (uint256);
```

期权写入者使用 `create()` 创建新的期权代币并定义期权参数。作为参数，期权写入者需要填写 `VanillaOptionData` 数据结构实例并将其传递给该方法。作为创建期权代币的一部分，该函数将抵押品从期权写入者转移到合约。

强烈建议在调用 `create()` 时，期权发行完全抵押，以防止增加对手方风险。为了创建看涨（看跌）期权发行，写入者需要允许将 `underlyingToken`（`strikeToken`）的 `amount`（`strike`）代币转移到期权合约，然后再调用 `create()`。

请注意，此标准未定义期权写入者在期权合约允许不足抵押的情况下“重新补充”抵押品的功能。合约需要相应地调整其 API 和实现。

如果 `underlyingToken` 或 `strikeToken` 是零地址，必须回退。\
如果 `premium` 不为 0 且 `premiumToken` 是零地址，必须回退。\
如果 `amount` 或 `strike` 为 0，必须回退。\
如果 `exerciseWindowStart` 小于当前时间或 `exerciseWindowEnd` 小于 `exerciseWindowStart`，必须回退。

*如果期权发行成功，则返回一个引用创建的期权发行的 id 值。*\
*如果期权发行成功，则触发 `Created` 事件。*

#### `buy`

```solidity
function buy(uint256 id, uint256 amount) external;
```

允许买方从具有定义 `id` 的期权发行中购买 `amount` 的期权代币。

买方必须允许代币合约将指定的 `premiumToken` 中的（总额的一部分）`premium` 转移给期权写入者。在调用该函数时，保费将直接转移给写入者。

如果 `allowed` 数组不为空，买方的地址必须包含在此列表中。\
如果 `amount` 为 0 或大于可供购买的剩余期权，必须回退。\
如果当前时间大于 `exerciseWindowEnd`，必须回退。

*如果购买成功，则铸造 `amount` 赎回代币到买方地址。*\
*如果购买成功，则触发 `Bought` 事件。*

#### `exercise`

```solidity
function exercise(uint256 id, uint256 amount) external;
```

允许买方从具有定义 `id` 的期权发行中行使 `amount` 的期权代币。

- 如果期权是看涨期权，买方以指定的行使价格支付给写入者，并获得指定的基础代币。
- 如果期权是看跌期权，买方将基础代币转移给写入者，并以指定的行使价格获得支付。

买方必须在调用 `exercise()` 之前允许支出 `strikeToken` 或 `underlyingToken`。

行使必须仅在 `exerciseWindowStart` <= 当前时间 <= `exerciseWindowEnd` 时进行。\
如果 `amount` 为 0 或买方没有必要的赎回代币以行使期权，必须回退。

*如果行使成功，则从买方地址销毁 `amount` 赎回代币。*\
*如果期权行使成功，则触发 `Exercised` 事件。*

#### `retrieveExpiredTokens`

```solidity
function retrieveExpiredTokens(uint256 id, address receiver) external;
```

允许写入者检索未行使的抵押代币。这些代币将转移给 `receiver`。\
如果期权是看涨期权，`receiver` 将检索基础代币。如果期权是看跌期权，`receiver` 将检索行使代币。

如果调用该函数的地址不是期权发行的写入者，必须回退。\
如果 `exerciseWindowEnd` 大于或等于当前时间，必须回退。\
如果等于零地址，必须将 `receiver` 设置为调用者的地址。

*将未行使的抵押品转移到写入者的地址。*\
*如果检索成功，可以从合约中删除期权发行。*\
*如果检索成功，则触发 `Expired` 事件。*

#### `cancel`

```solidity
function cancel(uint256 id, address receiver) external;
```

允许写入者取消期权并检索用作抵押的代币。这些代币将转移给 `receiver`。\
如果期权是看涨期权，`receiver` 将检索基础代币。如果期权是看跌期权，`receiver` 将检索行使代币。

如果调用该函数的地址不是期权发行的写入者，必须回退。\
如果至少有一个期权的份额已被购买，必须回退。\
如果等于零地址，必须将 `receiver` 设置为调用者的地址。

*将未行使的抵押品转移到写入者的地址。*\
*如果取消成功，可以从合约中删除期权发行。*\
*如果取消成功，则触发 `Canceled` 事件。*

#### `updatePremium`

```solidity
function updatePremium(uint256 id, uint256 amount) external;
```

允许写入者更新买方在购买期权时需要提供的保费。

**请注意，`amount` 将是整个基础金额，而不仅仅是可能仍可供购买的期权。**

如果调用该函数的地址不是期权发行的写入者，必须回退。\
如果当前时间大于 `exerciseWindowEnd`，必须回退。

*如果函数调用成功处理，则触发 `PremiumUpdated` 事件。*

#### `updateAllowed`

```solidity
function updateAllowed(uint256 id, address[] memory allowed) external;
```

允许写入者更新可以购买期权发行的允许地址列表。\
如果买方已经购买了期权，而他的地址不在新列表中，他仍然可以行使他购买的期权。

如果调用该函数的地址不是期权发行的写入者，必须回退。\
如果当前时间大于 `exerciseWindowEnd`，必须回退。

*如果函数调用成功处理，则触发 `AllowedUpdated` 事件。*

#### `issuance`

```solidity
function issuance(uint256 id) external view returns (OptionIssuance memory);
```

返回具有给定 `id` 的期权发行的所有关键信息。

### 事件

#### `Created`

```solidity
event Created(uint256 id);
```

当写入者成功提供期权发行数据（并将抵押品锁定到合约中）时触发。给定的 `id` 标识特定的期权发行。

#### `Bought`

```solidity
event Bought(uint256 indexed id, uint256 amount, address indexed buyer);
```

当期权被购买时触发。提供有关期权发行 `id`、`buyer` 地址和购买的 `amount` 的信息。

#### `Exercised`

```solidity
event Exercised(uint256 indexed id, uint256 amount);
```

当期权从具有给定 `id` 和给定 `amount` 的期权发行中被行使时触发。

#### `Expired`

```solidity
event Expired(uint256 indexed id);
```

当具有 `id` 的期权发行的写入者检索未行使的抵押品时触发。

#### `Canceled`

```solidity
event Canceled(uint256 indexed id);
```

当具有给定 `id` 的期权发行被写入者取消时触发。

#### `PremiumUpdated`

```solidity
event PremiumUpdated(uint256 indexed id, uint256 amount);
```

当写入者将保费更新为 `amount` 以用于具有给定 `id` 的期权发行时触发。请注意，更新的保费是针对总发行的。

#### `AllowedUpdated`

```solidity
event AllowedUpdated(uint256 indexed id, address[] allowed);
```

当写入者更新具有给定 `id` 的期权发行的允许地址列表时触发。

### 错误

#### `Forbidden`

当调用者不被允许执行某些操作时回退（通用）。

#### `TransferFailed`

当代币转移失败时回退。
#### `TimeForbidden`

当执行的当前时间无效时，回滚。

#### `AmountForbidden`

当金额无效时，回滚。

#### `InsufficientBalance`

当调用者的余额不足以执行该操作时，回滚。

### 具体示例

#### 看涨期权

假设 Bob 卖出一个 **看涨** 期权。\
他给予任何人权利在 **2023 年 7 月 14 日** 到 **2023 年 7 月 16 日（午夜）** 之间以 **25 TokenB** 的价格购买 **8 TokenA**。\
对于这样的合同，他希望获得 **10 TokenC** 的溢价。

在创建期权之前，Bob 必须将抵押品转移到合同中。这个抵押品对应于他在期权完全行使时需要支付的代币（`amount`）。对于这个期权，他必须提供 8 TokenA 作为抵押。他通过在 TokenA 的合约上调用 `approve(address spender, uint256 amount)` 函数来实现，并将合约地址（`spender`）和 `amount` 设置为：**8 \* 10^(TokenA 的小数位数)**。然后 Bob 可以在合约上执行 `create()` 来发行期权，提供以下参数：

- `side`: **Call**
- `underlyingToken`: **TokenA 的地址**
- `amount`: **8 \* 10^(TokenA 的小数位数)**
- `strikeToken`: **TokenB 的地址**
- `strike`: **25 \* 10^(TokenB 的小数位数)**
- `premiumToken`: **TokenC 的地址**
- `premium`: **10 \* 10^(TokenC 的小数位数)**
- `exerciseWindowStart`: **1689292800** *(2023-07-14 时间戳)*
- `exerciseWindowEnd`: **1689465600** *(2023-07-16 时间戳)*
- `allowed`: `[]` (对任何人开放)

该发行的 ID 为 88。

Alice 想要购买 **4** TokenA。她首先必须支付溢价（与其份额成比例），通过调用 `approve(address spender, uint256 amount)` 在 TokenC 的合约上允许支出她的 10 TokenC，并将合约地址（`spender`）和 `amount` 设置为：**4\*10^(TokenA 的小数位数) \* 10\*10^(TokenC 的小数位数) / 8\*10^(TokenA 的小数位数)**（amountToBuy \* `premium` / `amount`）。然后她可以在合约上执行 `buy(88, 4 * 10^(TokenA 的小数位数))`，并将收到 4\*10^(TokenA 的小数位数) 的赎回代币。

John 想要购买 **2** TokenA。他做了同样的事情，收到了 **2\*10^(TokensA 的小数位数)** 的赎回代币。

我们在 7 月 15 日，Alice 想要行使她的期权，因为 1 TokenA 的交易价格为 50 TokenB！她需要允许合约从她的账户转移 **4\*10^(TokenA 的小数位数) \* 25\*10^(TokenB 的小数位数) / 10^(TokenA 的小数位数)**（amountToExercise \* `strike` / 10^(`TokenA` 的小数位数)）的 TokenB，以便能够行使。当她在合约上调用 `exercise(88, 4 * 10^(TokenA 的小数位数))` 时，它将把 4 TokenA 转移给 Alice，并将 4\*25 TokenB 转移给 Bob。

John 决定将他的行使权转让给他的朋友 Jimmy。他通过将 **2\*10^(TokensA 的小数位数)** 的赎回代币转移到 Jimmy 的地址来实现。\
Jimmy 决定只用这个期权购买 **1** TokenA。因此，他将给 Bob（通过合约） **1\*10^(TokenA 的小数位数) \* 25\*10^(TokenB 的小数位数) / 10^(TokenA 的小数位数)**。

#### 看跌期权

假设 Bob 卖出一个 **看跌** 期权。\
他给予任何人权利在 **2023 年 7 月 14 日** 到 **2023 年 7 月 16 日（午夜）** 之间以 **25 TokenB** 的价格向他出售 **8 TokenA**。\
对于这样的合同，他希望获得 **10 TokenC** 的溢价。

在创建期权之前，Bob 必须将抵押品转移到合同中。这个抵押品对应于他在期权完全行使时需要支付的代币（`exerciseCost`）。对于这个期权，他必须提供 200 TokenB（8 \* 25）作为抵押。他通过在 TokenB 的合约上调用 `approve(address spender, uint256 amount)` 函数来实现，并将合约地址（`spender`）和 `amount` 设置为：**25\*10^(Token B 的小数位数) \* 8\*10^(TokenB 的小数位数) / 10^(TokenA 的小数位数)**（`strike` \* `amount` / 10^(`underlyingToken` 的小数位数)）。然后 Bob 可以在合约上执行 `create()` 来发行期权，提供以下参数：

- `side`: **Put**
- `underlyingToken`: **TokenA 的地址**
- `amount`: **8 \* 10^(TokenA 的小数位数)**
- `strikeToken`: **TokenB 的地址**
- `strike`: **25 \* 10^(TokenB 的小数位数)**
- `premiumToken`: **TokenC 的地址**
- `premium`: **10 \* 10^(TokenC 的小数位数)**
- `exerciseWindowStart`: **1689292800** *(2023-07-14 时间戳)*
- `exerciseWindowEnd`: **1689465600** *(2023-07-16 时间戳)*
- `allowed`: `[]` (对任何人开放)

该发行的 ID 为 88。

Alice 想要能够出售 **4** TokenA。她首先必须支付溢价（与其份额成比例），通过调用 `approve(address spender, uint256 amount)` 在 TokenC 的合约上允许支出她的 10 TokenC，并将合约地址（`spender`）和 `amount` 设置为：**4\*10^(TokenA 的小数位数) \* 10\*10^(TokenC 的小数位数) / 8\*10^(TokenA 的小数位数)**（amountToSell \* `premium` / `amount`）。然后她可以在合约上执行 `buy(88, 4 * 10^(TokenA 的小数位数))`，并将收到 4\*10^(TokenA 的小数位数) 的赎回代币。

John 想要出售 **2** TokenA。他做了同样的事情，收到了 **2\*10^(TokensA 的小数位数)** 的赎回代币。

我们在 7 月 15 日，Alice 想要行使她的期权，因为 1 TokenA 的交易价格仅为 10 TokenB！她需要允许合约从她的账户转移 **4 \* 10^(TokenA 的小数位数)** TokenA，以便能够行使。当她在合约上调用 `exercise(88, 4 * 10^(TokenA 的小数位数))` 时，它将把 4\*25 TokenB 转移给 Alice，并将 4 TokenA 转移给 Bob。

John 决定将他的行使权转让给他的朋友 Jimmy。他通过将 **2\*10^(TokensA 的小数位数)** 的赎回代币转移到 Jimmy 的地址来实现。\
Jimmy 决定只用这个期权出售 **1** TokenA。因此，他将给 Bob（通过合约） **1\*10^(TokenA 的小数位数)**。

#### 取回抵押品

假设 Alice 从未行使她的期权，因为对她来说不够有利可图。要取回她的抵押品，Bob 必须等待当前时间大于 `exerciseWindowEnd`。在这些示例中，这个特性设置为 2 天，因此他可以通过简单地调用 `retrieveExpiredTokens()` 从 7 月 16 日开始取回他的抵押品。

## 理由

该合约的概念是无 oracle 的，因为我们假设理性的买家只有在对他有利时才会行使他的期权。

溢价由期权写入者决定。写入者可以自由选择如何计算溢价，例如使用 *Black-Scholes 模型* 或其他方法。写入者可以随时更新溢价，以便根据基础资产的价格、波动性、到期时间和其他因素的变化进行调整。离线计算溢价在 gas 成本方面更为有利。

该 ERC 旨在表示普通期权。然而，复杂期权可以在此 ERC 之上构建。\
该合约可以存储尽可能多的发行，而不是表示在到期日后无用的单一期权。每个发行都有一个 ID，可以独立于其他发行进行购买、行使、取消等。这在 gas 成本方面是一种更好的方法。

它的设计使得期权可以是欧洲式或美国式，通过引入 `exerciseWindowStart` 和 `exerciseWindowEnd` 数据点。买家只能在 `exerciseWindowStart` 和 `exerciseWindowEnd` 之间行使。
- 如果期权发行者认为该期权是欧洲期权，他可以将 `exerciseWindowStart` 设置为到期日，并将 `exerciseWindowEnd` 设置为到期日 + 一段确定的时间范围，以便买方有时间行使期权。
- 如果期权发行者认为该期权是美国期权，他可以将 `exerciseWindowStart` 设置为当前时间，买方将能够立即行使期权。

该合约本质上支持单个期权发行的多个买方。这是通过使用 ERC-1155 代币来表示期权实现的。当买方购买期权发行的一部分时，他会收到代表该部分期权发行的 ERC-1155 代币。这些代币可以在用户之间交换，并用于行使期权。通过这种机制，买方可以决定只行使他购买的部分。

该合约实现了 `allowed` 数组，可以用来限制可以购买期权发行的地址。这在两个用户在链下达成期权协议并希望在链上创建时非常有用。这可以防止在合约创建和第二个用户购买之间，链上用户已经购买了合约的风险。

该 ERC 旨在处理 ERC-20 代币。然而，该标准可以作为处理其他类型代币（如 [ERC-721](./eip-721.md) 代币）的良好基础。一些属性和函数签名（例如提供 ID 而不是数量）需要更改，但总体思路将保持不变。

## 安全考虑

合约包含 `exerciseWindowStart` 和 `exerciseWindowEnd` 数据点。这些定义了买方行使期权的确定时间范围。当当前时间大于 `exerciseWindowEnd` 时，买方将无法行使期权，发行者将能够收回任何剩余的抵押品。

为了防止在期权发行者认为发行是欧洲期权的情况下出现明显的套利情况，我们强烈建议期权发行者在行使窗口开启时调用 `updatePremium` 来大幅提高溢价价格。这将确保机器人无法购买任何剩余的期权并立即行使以获取快速利润。当然，该标准可以自定义，用户可能会发现使用可用工具自动更新溢价比手动更新更方便（特别是当溢价基于特定动态指标，如 *Black-Scholes 模型* 时）。如果期权发行被认为是美国期权，则当然不需要这种调整。

该标准实现了 `updatePremium` 函数，允许发行者在任何时候更新溢价价格。该函数可能会导致买方的安全问题：买方可以购买期权，而发行者可以通过将溢价价格更新为非常高的值来抢先执行买方的交易。为了防止这种情况，我们建议买方仅允许合约支出约定的溢价金额，而不是更多。

该合约支持单个期权发行的多个买方，这意味着可以购买期权发行的部分。生态系统并不真正支持非整数，因此部分有时会导致舍入错误。这可能导致意外结果，特别是在 `buy` 函数中：如果设置了溢价，买方只需支付与他想购买的期权数量成比例的部分。如果该部分不是整数，这将被截断并因此向下舍入。这意味着发行者将收到低于预期的溢价。我们认为这个风险相当小，因为大多数代币有很高的小数位数，但重要的是要意识到这一点。一些买方可能会通过反复购买小部分来利用这一点，从而支付低于预期的溢价。然而，考虑到 gas 成本，这可能不会盈利。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。