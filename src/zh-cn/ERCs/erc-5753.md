---
eip: 5753
title: EIP-721 的可锁定扩展
description: 禁用代币转移（锁定）和重新启用它们（解锁）的接口。
author: Filipp Makarov (@filmakarov)
discussions-to: https://ethereum-magicians.org/t/lockable-nfts-extension/8800
status: Stagnant
type: Standards Track
category: ERC
created: 2022-10-05
requires: 165, 721
---

## 摘要

该标准是 [EIP-721](./eip-721.md) 的扩展。它引入了可锁定的 NFT。被锁定的资产可以以任何方式使用，除了出售和/或转移。所有者或操作员可以锁定代币。当代币被锁定时，解锁者地址（一个 EOA 或合约）被设置。只有解锁者能够 `unlock` 代币。

## 动机

通过 NFT，数字对象变成了可验证拥有、易于交易且不可变地存储在区块链上的数字商品。因此，不断改善非同质化代币的用户体验非常重要，而不仅仅是从某个同质化代币继承。

在 DeFi 中，有一种用户体验模式是将代币锁定在服务智能合约中。例如，如果你想借一些 $DAI，你必须提供一些 $ETH 作为贷款的抵押。在贷款期间，这些 $ETH 会被锁定在借贷服务合约中。这种模式适用于 $ETH 和其他同质化代币。

然而，对于 NFT 来说，情况应该有所不同，因为 NFT 有许多用例需要 NFT 在作为贷款抵押时仍然留在持有者的钱包中。你可能希望继续将你的 NFT 用作 Twitter 上的验证 PFP，或通过 collab.land 授权 Discord 服务器。你可能希望在 P2E 游戏中使用你的 NFT。即使在借贷期间，你也应该能够做到这一点，就像你可以在抵押贷款的情况下继续住在自己的房子里一样。

以下用例适用于可锁定的 NFT：

- **NFT 抵押贷款** 使用你的 NFT 作为贷款的抵押，而不需要将其锁定在借贷协议合约中。将其锁定在你的钱包中，并继续享受 NFT 的所有实用性。
- **无抵押 NFT 租赁** 借用 NFT 收取费用，无需巨额抵押。你可以使用 NFT，但不能转移它，因此借出者是安全的。借用服务合约会在借用期结束时自动将 NFT 转回给借出者。
- **初级销售** 以部分价格铸造 NFT，并在对收藏品的发展感到满意时支付其余部分。
- **二级销售** 分期购买和出售你的 NFT。买家获得锁定的 NFT，并立即开始使用它。同时，他/她在所有分期付款完成之前无法出售 NFT。如果未收到全额付款，NFT 将连同费用一起返回给卖方。
- **安全性** 安全便捷地使用你的独家蓝筹 NFT。使用 NFT 的最便捷方式是与 MetaMask 一起使用。然而，MetaMask 易受各种漏洞和攻击的影响。通过 `Lockable` 扩展，你可以锁定你的 NFT，并将你的安全冷钱包声明为解锁者。因此，即使黑客获得了你 MetaMask 的访问权限，他们也无法在没有冷钱包访问权限的情况下转移你的 NFT。这就是 `Lockable` NFT 安全的原因。
- **元宇宙准备** 在大型元宇宙活动中，锁定 NFT 门票可能会很有用。这将防止已经使用 NFT 登录的用户出售或转移它。因此，我们避免了一个门票的双重使用。
- **非托管质押** 社区如 CyberKongz、Moonbirds 等提出了不同的非托管质押方法。本实现中建议的方法假设代币只能在一个地方质押，而不能同时在多个地方质押（就像你不能同时在两个银行账户存款一样）。此外，它不需要任何额外的代码，仅通过锁定功能即可实现。
另一种相同概念的方法是使用锁定来提供 HODL 的证明。你可以锁定你的 NFT 以防止出售，作为对社区的忠诚表现，并开始获得奖励。这是对最初由 The Hashmasks 和他们的 $NCT 代币引入的奖励机制的更好版本。
- **安全便捷的共同拥有和共同使用** 安全共同拥有和共同使用的扩展。例如，你想和朋友一起购买一个昂贵的 NFT 资产，但使用多签名不方便，因此你可以安全地在钱包之间轮换和使用它。NFT 将存储在共同拥有者之一的钱包中，他可以以任何方式使用它（除了转移），而无需多重批准。转移将需要多重批准。

## 规范

本文档中的关键字 “MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL” 应按 RFC 2119 中的描述进行解释。

符合 EIP-721 的合约可以实现此 EIP，以提供在当前所有者地址上锁定和解锁代币的标准方法。
如果代币被锁定，`getLocked` 函数必须返回一个能够解锁代币的地址。
对于未锁定的代币，`getLocked` 函数必须返回 `address(0)`。
用户可以通过调用 `lock(address(1), tokenId)` 永久锁定代币。

当代币被锁定时，所有 [EIP-721](./eip-721.md) 转移函数必须回退，除非交易是由解锁者发起的。
当代币被锁定时，[EIP-721](./eip-721.md) 的 `approve` 方法必须回退。
当代币被锁定时，[EIP-721](./eip-721.md) 的 `getApproved` 方法应该返回该代币的 `unlocker` 地址，以便解锁者能够转移该代币。
当代币被锁定时，`lock` 方法必须回退，即使它是用相同的 `unlocker` 作为参数调用的。
当锁定的代币被解锁者转移时，代币必须在转移后解锁。

市场应该调用 EIP-721 可锁定代币合约的 `getLocked` 方法，以了解指定 tokenId 的代币是否被锁定。被锁定的代币不应可用于上市。被锁定的代币不能被出售。因此，市场应该隐藏已被锁定的代币的上市，因为此类订单无法完成。

### 合约接口

```solidity
pragma solidity >=0.8.0;

/// @dev Interface for the Lockable extension

interface ILockable {

    /**
     * @dev Emitted when `id` token is locked, and `unlocker` is stated as unlocking wallet.
     */
    event Lock (address indexed unlocker, uint256 indexed id);

    /**
     * @dev Emitted when `id` token is unlocked.
     */
    event Unlock (uint256 indexed id);

    /**
     * @dev Locks the `id` token and gives the `unlocker` address permission to unlock.
     */
    function lock(address unlocker, uint256 id) external;

    /**
     * @dev Unlocks the `id` token.
     */
    function unlock(uint256 id) external;

    /**
     * @dev Returns the wallet, that is stated as unlocking wallet for the `tokenId` token.
     * If address(0) returned, that means token is not locked. Any other result means token is locked.
     */
    function getLocked(uint256 tokenId) external view returns (address);

}
```

当使用 `0x72b68110` 调用时，`supportsInterface` 方法必须返回 `true`。

## 理由

这种方法提出了一种旨在尽可能简化的解决方案。它仅允许锁定项目（声明谁将能够解锁它）并在需要时解锁，如果用户有权限这样做。

同时，它是一种通用实现。它允许许多扩展性以及动机部分提到的任何潜在用例（或所有用例）。

当需要授予代币的临时和/或可赎回权利（租赁、分期购买）时，此 EIP 涉及将代币真实转移到临时用户的钱包，而不仅仅是分配角色。
这一选择是为了提高与所有现有 NFT 生态系统工具和 dApp 的兼容性，例如 Collab.land。否则，这将要求所有此类 dApp 实现额外的接口和逻辑。
命名和引用实现的函数和存储实体模仿了 [EIP-721] 的批准流程，以便于理解。

## 向后兼容性

该标准与当前的 [EIP-721](./eip-721.md) 标准兼容。

## 参考实现

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity >=0.8.0;

import '../ILockable.sol';
import '@openzeppelin/contracts/token/ERC721/ERC721.sol';

/// @title Lockable Extension for ERC721

abstract contract ERC721Lockable is ERC721, ILockable {

    /*///////////////////////////////////////////////////////////////
                            LOCKABLE EXTENSION STORAGE                        
    //////////////////////////////////////////////////////////////*/

    mapping(uint256 => address) internal unlockers;

    /*///////////////////////////////////////////////////////////////
                              LOCKABLE LOGIC
    //////////////////////////////////////////////////////////////*/

    /**
     * @dev Public function to lock the token. Verifies if the msg.sender is the owner
     *      or approved party.
     */

    function lock(address unlocker, uint256 id) public virtual {
        address tokenOwner = ownerOf(id);
        require(msg.sender == tokenOwner || isApprovedForAll(tokenOwner, msg.sender)
        , "NOT_AUTHORIZED");
        require(unlockers[id] == address(0), "ALREADY_LOCKED"); 
        unlockers[id] = unlocker;
        _approve(unlocker, id);
    }

    /**
     * @dev Public function to unlock the token. Only the unlocker (stated at the time of locking) can unlock
     */
    function unlock(uint256 id) public virtual {
        require(msg.sender == unlockers[id], "NOT_UNLOCKER");
        unlockers[id] = address(0);
    }

    /**
     * @dev Returns the unlocker for the tokenId
     *      address(0) means token is not locked
     *      reverts if token does not exist
     */
    function getLocked(uint256 tokenId) public virtual view returns (address) {
        require(_exists(tokenId), "Lockable: locking query for nonexistent token");
        return unlockers[tokenId];
    }

    /**
     * @dev Locks the token
     */
    function _lock(address unlocker, uint256 id) internal virtual {
        unlockers[id] = unlocker;
    }

    /**
     * @dev Unlocks the token
     */
    function _unlock(uint256 id) internal virtual {
        unlockers[id] = address(0);
    }

    /*///////////////////////////////////////////////////////////////
                              OVERRIDES
    //////////////////////////////////////////////////////////////*/

    function approve(address to, uint256 tokenId) public virtual override {
        require (getLocked(tokenId) == address(0), "Can not approve locked token");
        super.approve(to, tokenId);
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override {
        // if it is a Transfer or Burn
        if (from != address(0)) { 
            // token should not be locked or msg.sender should be unlocker to do that
            require(getLocked(tokenId) == address(0) || msg.sender == getLocked(tokenId), "LOCKED");
        }
    }

    function _afterTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override {
        // if it is a Transfer or Burn, we always deal with one token, that is startTokenId
        if (from != address(0)) { 
            // clear locks
            delete unlockers[tokenId];
        }
    }

    /**
     * @dev Optional override, if to clear approvals while the tken is locked
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        if (getLocked(tokenId) != address(0)) {
            return address(0);
        }
        return super.getApproved(tokenId);
    }

    /*///////////////////////////////////////////////////////////////
                              ERC165 LOGIC
    //////////////////////////////////////////////////////////////*/

    function supportsInterface(bytes4 interfaceId)
        public
        view
        virtual
        override
        returns (bool)
    {
        return
            interfaceId == type(IERC721Lockable).interfaceId ||
            super.supportsInterface(interfaceId);
    }

}
```

## 安全考虑

与管理 [EIP-721](./eip-721.md) 代币的合约直接相关的安全考虑不存在。

### 针对可锁定代币的合约的考虑

- 确保每个声明为 `unlocker` 的合约在所有情况下都可以实际解锁代币。
- 有一些用例涉及将代币转移给临时所有者，然后再锁定它。例如，NFT 租赁。管理此类服务的智能合约应始终使用 `transferFrom` 而不是 `safeTransferFrom` 以避免重入攻击。
- 关于可锁定代币没有 MEV 考虑，因为只有授权方被允许锁定和解锁。 

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。