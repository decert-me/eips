---
eip: 6860
title: Web3 URL 到 EVM 调用消息翻译
description: 将 HTTP 风格的 Web3 URL 翻译为 EVM 调用消息
author: Qi Zhou (@qizhou), Chao Pi (@pichaoqkc), Sam Wilson (@SamWilsn), Nicolas Deschildre (@nand2)
discussions-to: https://ethereum-magicians.org/t/eip-4804-web3-url-to-evm-call-message-translation/8300
status: Draft
type: Standards Track
category: ERC
created: 2023-09-29
requires: 137
---

## 摘要

该标准将一个 [RFC 3986](https://www.rfc-editor.org/rfc/rfc3986) URI，如 `web3://uniswap.eth/` 翻译为 EVM 消息，例如：

```
EVMMessage {
   To: 0xaabbccddee.... // where uniswap.eth's address registered at ENS
   Calldata: 0x
   ...
}
```

⚠️ 本提案对 [ERC-4804](./eip-4804.md) 进行了小幅修正、澄清和修改。

## 动机

目前，从 Web3 读取数据通常依赖于 Web2 代理到 Web3 区块链的翻译。翻译主要由 dApp 网站/节点服务提供商/Etherscan 等代理完成，这些代理不在用户的控制之下。此标准旨在为 Web2 用户提供一种简单的方法，以直接访问 Web3 的内容，特别是链上 Web 内容，如 SVG/HTML。此外，该标准还支持与其他已兼容 URI 的标准的互操作性，如 SVG/HTML。

## 规范

本规范仅定义只读（即 Solidity 的 `view` 函数）语义。状态修改函数可能作为未来的扩展进行定义。

本规范使用 [RFC 2234](https://www.rfc-editor.org/rfc/rfc2234) 的增强巴科斯-诺尔形式（ABNF）符号。完整的 URI 语法列在附录 A 中。

Web3 URL 是以下形式的 ASCII 字符串：

```
web3URL         = schema "://" [ userinfo "@" ] contractName [ ":" chainid ] pathQuery [ "#" fragment ]
schema          = "w3" / "web3"
userinfo        = address
```

**userinfo** 表示哪个用户正在调用 EVM，即 EVM 调用消息中的 "From" 字段。如果未指定，协议将使用 0x0 作为发送者地址。

```
contractName    = address 
                / domainName
address         = "0x" 20( HEXDIG HEXDIG )
domainName      = *( unreserved / pct-encoded / sub-delims ) ; As in RFC 3986
```

**contractName** 表示要调用的合约，即 EVM 调用消息中的 "To" 字段。如果 **contractName** 是一个地址，则将用于 "To" 字段。否则，**contractName** 是来自域名服务的域名，必须解析为一个地址以用于 "To" 字段。

从域名服务解析域名到地址的方式在 [ERC-6821](./eip-6821.md) 中为以太坊名称服务进行了说明，其他名称服务将在后续 ERC 中讨论。

```
chainid         = %x31-39 *DIGIT
```

**chainid** 表示要解析 **contractName** 并调用消息的链。如果未指定，协议将使用所使用的名称服务提供者的主链，例如，以太坊的 1。如果未使用名称服务提供者，则默认的 chainid 为 1。

```
pathQuery       = mPathQuery ; path+query for manual mode
                / aPathQuery ; path+query for auto mode
```

**pathQuery** 由路径和可选查询组成，其结构在 "手动" 或 "自动" 解析模式下会有所不同。

```
fragment        = *VCHAR
```

**fragment**，如同 HTTP URL，是一串字符，用于引用资源，并不会传输到智能合约。

```
web3UrlRef      = web3URL 
                / relativeWeb3URL
relativeWeb3URL = relPathQuery
relPathQuery    = relMPathQuery ; Relative URL path+query for manual mode
                / relAPathQuery ; Relative URL path+query for auto mode
```

支持相对 URL，但支持程度取决于解析模式。


### 解析模式

一旦确定了 "To" 地址和 chainid，协议将通过调用 "To" 地址的 `resolveMode` 方法来检查合约的解析模式。`resolveMode` 的 Solidity 签名为：

```solidity
function resolveMode() external returns (bytes32);
```

协议当前支持两种解析模式：自动和手动。

- 如果 `resolveMode` 返回值为 `0x6d616e75616c0000000000000000000000000000000000000000000000000000`，即字节 32 中的 "manual"，则将使用手动模式
- 如果 `resolveMode` 返回值为：
    - `0x6175746f00000000000000000000000000000000000000000000000000000000`，即字节 32 中的 "auto"，或
    - `0x0000000000000000000000000000000000000000000000000000000000000000`，或
    - 调用 `resolveMode` 抛出错误（方法未实现或方法抛出错误）
- 否则，协议将以 "unsupported resolve mode" 的错误失败请求。

#### 手动模式

```
mPathQuery      = mPath [ "?" mQuery ]

mPath           = mPathAbempty ; begins with "/" or is empty
mPathAbempty    = [ *( "/" segment ) "/" segment [ "." fileExtension ] ]
segment         = *pchar ; as in RFC 3986
fileExtension   = 1*( ALPHA / DIGIT )

mQuery = *( pchar / "/" / "?" ) ; as in RFC 3986
```

手动模式将直接使用原始 **mPathQuery** 作为消息的 calldata（不会进行百分比编码解码）。如果 **mPathQuery** 为空，发送的 calldata 将为 ``/``（0x2f）。

返回的消息数据将被视为 ABI 编码的字节，解码后的字节将返回给前端。

返回给前端的 MIME 类型默认为 ``text/html``，但如果存在 **fileExtension**，则会被覆盖。在这种情况下，MIME 类型将根据文件名扩展名推断。

```
relMPathQuery   = relMPath [ "?" mQuery ]
relMPath        = mPathAbsolute ; begins with "/" but not "//"
                / mPathNoscheme ; begins with a non-colon segment
                / mPathEmpty    ; zero characters

mPathAbsolute   = "/" [ segmentNz *( "/" segment ) ] [ "." fileExtension ]
mPathNoscheme   = segmentNzNc *( "/" segment ) [ "." fileExtension ]
mPathEmpty      = 0<pchar>

segmentNz       = 1*pchar ; as in RFC 3986
segmentNzNc     = 1*( unreserved / pct-encoded / sub-delims / "@" )
                ; as in RFC 3986: non-zero-length segment without any colon ":"
```

手动模式相对 URL 的支持类似于 HTTP URL：允许相对于当前合约的 URL，既可以使用绝对路径，也可以使用相对路径。

#### 自动模式

```
aPathQuery      = aPath [ "?" aQuery ]
aPath           = [ "/" [ method *( "/" argument ) ] ]
```

在自动模式下，如果 **aPath** 为空或为 "/"，则协议将以空 calldata 调用目标合约。否则，EVM 消息的 calldata 将使用标准 Solidity 合约 ABI。

```
method          = ( ALPHA / "$" / "_" ) *( ALPHA / DIGIT / "$" / "_" )
```

**method** 是要调用的函数方法的字符串

```
argument        = boolArg
                / uintArg
                / intArg
                / addressArg
                / bytesArg
                / stringArg
boolArg         = [ "bool!" ] ( "true" / "false" )
uintArg         = [ "uint" [ intSizes ] "!" ] 1*DIGIT
intArg          = "int" [ intSizes ] "!" 1*DIGIT
intSizes        = "8" / "16" / "24" / "32" / "40" / "48" / "56" / "64" / "72" / "80" / "88" / "96" / "104" / "112" / "120" / "128" / "136" / "144" / "152" / "160" / "168" / "176" / "184" / "192" / "200" / "208" / "216" / "224" / "232" / "240" / "248" / "256"
addressArg      = [ "address!" ] ( address / domainName )
bytesArg        = [ "bytes!" ] bytes
                / "bytes1!0x" 1( HEXDIG HEXDIG )
                / "bytes2!0x" 2( HEXDIG HEXDIG )
                ...
                / "bytes32!0x" 32( HEXDIG HEXDIG )
stringArg       = "string!" *pchar [ "." fileExtension ]
```

**argument** 是方法的参数，采用类型无关的语法 ``[ type "!" ] value``。如果指定了 **type**，则值将被转换为相应的类型。协议当前支持以下基本类型：bool、int、uint、int&lt;X&gt;、uint&lt;X&gt;（X 范围从 8 到 256，步长为 8）、address、bytes&lt;X&gt;（X 范围从 1 到 32）、bytes 和 string。如果未指定 **type**，则将使用以下规则按顺序自动检测类型：

  1. **type**="uint256"，如果 **value** 是数字；或
  2. **type**="bytes32"，如果 **value** 以 0x+32 字节数据的十六进制形式表示；或
  3. **type**="address"，如果 **value** 以 0x+20 字节数据的十六进制形式表示；或
  4. **type**="bytes"，如果 **value** 以 0x 开头，后面跟着任意数量的字节，除了 20 或 32；或
  5. **type**="bool"，如果 **value** 是 ``true`` 或 ``false``；或
  6. 否则 **type**="address"，并将参数解析为域名。如果无法解析域名，将返回不支持的名称服务提供者错误。

```
aQuery          = attribute *( "&" attribute )
attribute       = attrName "=" attrValue
attrName        = "returns"
                / "returnTypes"
attrValue       = [ "(" [ retTypes ] ")" ]
retTypes        = retType *( "," retType )
retType         = retRawType *( "[" [ %x31-39 *DIGIT ] "]" )
retRawType      = "(" retTypes ")"
                / retBaseType
retBaseType      = "bool" / "uint" [ intSizes ] / "int" [ intSize ] / "address" / "bytes" [ bytesSizes ] / "string"
bytesSizes      = %x31-39              ; 1-9
                / ( "1" / "2" ) DIGIT  ; 10-29
                / "31" / "32"          ; 31-32
```

**aQuery** 中的 "returns" 属性指示返回数据的格式。它遵循以太坊 ABI 函数签名的参数部分的语法（``uint`` 和 ``int`` 别名被授权）。

- 如果 "returns" 属性值未定义或为空，则返回的消息数据将被视为 ABI 编码的字节，解码后的字节将返回给前端。返回给前端的 MIME 类型默认为未定义，但如果最后一个参数是字符串类型并且具有 **fileExtension**，则 MIME 类型将根据文件名扩展名推断。（请注意，**fileExtension** 不排除在传递给智能合约的字符串参数中）
- 如果 "returns" 属性值等于 "()"，则返回的消息数据的原始字节将作为 "0x" 前缀的十六进制字符串以数组形式返回： ``["0xXXXXX"]``
- 否则，返回的消息数据将根据 **returns** 值中指定的数据类型进行 ABI 解码，并以 JSON 格式编码。数据的编码将遵循以太坊 JSON-RPC 格式：
  - 未格式化的数据（字节、地址）将编码为十六进制，以 "0x" 为前缀，每个字节两个十六进制数字
  - 数量（整数）将编码为十六进制，以 "0x" 为前缀，采用最紧凑的表示（轻微例外：零应表示为 "0x0"）
  - 布尔值和字符串将是原生 JSON 布尔值和字符串
如果存在多个“returns”属性，将应用最后一个“returns”属性的值。请注意，“returnTypes”是“returns”的别名，但不建议使用，主要是为了与 [ERC-4804](./eip-4804.md) 向后兼容。

```
relAPathQuery   = aPath [ "?" aQuery ]
```

对自动模式相对 URL 的支持有限：允许相对于当前合约的 URL，这些 URL 将引用自身（空），``/``路径或完整的方法及其参数。

### 示例

#### 示例 1a

```
web3://w3url.eth/
```

其中 **w3url.eth** 的合约处于手动模式。

协议将从链 ID 1（主网）中的 ENS 查找 **w3url.eth** 的地址。然后协议将调用该地址，"Calldata" = `keccak("resolveMode()")[0:4]` = "0xDD473FAE"，返回 ABI 类型为 "(bytes32)" 的 "manual"。在确定合约的手动模式后，协议将调用该地址，"To" = **contractAddress** 和 "Calldata" = "0x2F"。返回的数据将被视为 ABI 类型为 "(bytes)"，解码后的字节将返回给前端，信息为 MIME 类型是 ``text/html``.

#### 示例 1b

```
web3://w3url.eth/
```

其中 **w3url.eth** 的合约处于自动模式。

协议将从链 ID 1（主网）中的 ENS 查找 **w3url.eth** 的地址。然后协议将调用该地址，"Calldata" = `keccak("resolveMode()")[0:4]` = "0xDD473FAE"，返回 ""，即合约处于自动模式。在确定合约的自动模式后，协议将调用该地址，"To" = **contractAddress** 和 "Calldata" = ""。返回的数据将被视为 ABI 类型为 "(bytes)"，解码后的字节将返回给前端，信息为 MIME 类型未定义。

#### 示例 2

```
web3://cyberbrokers-meta.eth/renderBroker/9999
```

其中 **cyberbrokers-meta.eth** 的合约处于自动模式。

协议将从链 ID 1（主网）中的 ENS 查找 **cyberbrokers-meta.eth** 的地址。然后协议将调用该地址，"Calldata" = `keccak("resolveMode()")[0:4]` = "0xDD473FAE"，返回 ""，即合约处于自动模式。在确定合约的自动模式后，协议将调用该地址，"To" = **contractAddress** 和 "Calldata" = "0x" + `keccak("renderBroker(uint256)")[0:4] + abi.encode(uint256(9999))`。返回的数据将被视为 ABI 类型为 "(bytes)"，解码后的字节将返回给前端，信息为 MIME 类型未定义。

#### 示例 3

```
web3://vitalikblog.eth:5/
```

其中 **vitalikblog.eth:5** 的合约处于手动模式。

协议将从链 ID 5（Goerli）中的 ENS 查找 **vitalikblog.eth** 的地址。然后在确定合约处于手动模式后，协议将调用该地址，"To" = **contractAddress** 和 "Calldata" = "0x2F"，链 ID = 5。返回的数据将被视为 ABI 类型为 "(bytes)"，解码后的字节将返回给前端，信息为 MIME 类型是 ``text/html``.

#### 示例 4

```
web3://0xe4ba0e245436b737468c206ab5c8f4950597ab7f:42170/
```

其中合约 "0xe4ba0e245436b737468c206ab5c8f4950597ab7f:42170" 处于手动模式。

在确定合约处于手动模式后，协议将调用该地址，"To" = "0xe4ba0e245436b737468c206ab5c8f4950597ab7f" 和 "Calldata" = "0x2F"，链 ID = 42170（Arbitrum Nova）。返回的数据将被视为 ABI 类型为 "(bytes)"，解码后的字节将返回给前端，信息为 MIME 类型是 ``text/html``.

#### 示例 5

```
web3://0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48/balanceOf/vitalik.eth?returns=(uint256)
```

其中合约 "0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48" 处于自动模式。

协议将从链 ID 1（主网）中的 ENS 查找 **vitalik.eth** 的地址，然后调用合约的 "balanceOf(address)" 方法，使用 **vitalik.eth** 的地址。合约调用返回的数据将被视为 ABI 类型为 "(uint256)"，解码后的数据将以 JSON 格式返回给前端，如 `[ "0x9184e72a000" ]`，信息为 MIME 类型是 ``application/json``.

#### 示例 6

```
web3://0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48/balanceOf/vitalik.eth?returns=()
```

其中合约 "0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48" 处于自动模式。

协议将从链 ID 1（主网）中的 ENS 查找 **vitalik.eth** 的地址，然后调用该地址的 "balanceOf(address)" 方法。合约调用返回的数据将被视为原始字节，并将以 JSON 格式编码，如 `["0x000000000000000000000000000000000000000000000000000009184e72a000"]`，并返回给前端，信息为 MIME 类型是 ``application/json``.

### 附录 A：Web3 URL 的完整 ABNF

```
web3URL         = schema "://" [ userinfo "@" ] contractName [ ":" chainid ] pathQuery [ "#" fragment ]
schema          = "w3" / "web3"
userinfo        = address
contractName    = address 
                / domainName
chainid         = %x31-39 *DIGIT

pathQuery       = mPathQuery ; path+query for manual mode
                / aPathQuery ; path+query for auto mode
fragment        = *VCHAR

web3UrlRef      = web3URL 
                / relativeWeb3URL
relativeWeb3URL = relPathQuery
relPathQuery    = relMPathQuery ; Relative URL path+query for manual mode
                / relAPathQuery ; Relative URL path+query for auto mode

mPathQuery      = mPath [ "?" mQuery ]
mPath           = mPathAbempty ; begins with "/" or is empty

relMPathQuery   = relMPath [ "?" mQuery ]
relMPath        = mPathAbsolute ; begins with "/" but not "//"
                / mPathNoscheme ; begins with a non-colon segment
                / mPathEmpty    ; zero characters

mPathAbempty    = [ *( "/" segment ) "/" segment [ "." fileExtension ] ]
mPathAbsolute   = "/" [ segmentNz *( "/" segment ) ] [ "." fileExtension ]
mPathNoscheme   = segmentNzNc *( "/" segment ) [ "." fileExtension ]
mPathEmpty      = 0<pchar>

segment         = *pchar ; as in RFC 3986
segmentNz       = 1*pchar ; as in RFC 3986
segmentNzNc     = 1*( unreserved / pct-encoded / sub-delims / "@" )
                ; as in RFC 3986: non-zero-length segment without any colon ":"

mQuery          = *( pchar / "/" / "?" ) ; as in RFC 3986

aPathQuery      = aPath [ "?" aQuery ]
aPath           = [ "/" [ method *( "/" argument ) ] ]
relAPathQuery   = aPath [ "?" aQuery ]
method          = ( ALPHA / "$" / "_" ) *( ALPHA / DIGIT / "$" / "_" )
argument        = boolArg
                / uintArg
                / intArg
                / addressArg
                / bytesArg
                / stringArg
boolArg         = [ "bool!" ] ( "true" / "false" )
uintArg         = [ "uint" [ intSizes ] "!" ] 1*DIGIT
intArg          = "int" [ intSizes ] "!" 1*DIGIT
intSizes        = "8" / "16" / "24" / "32" / "40" / "48" / "56" / "64" / "72" / "80" / "88" / "96" / "104" / "112" / "120" / "128" / "136" / "144" / "152" / "160" / "168" / "176" / "184" / "192" / "200" / "208" / "216" / "224" / "232" / "240" / "248" / "256"
addressArg      = [ "address!" ] ( address / domainName )
bytesArg        = [ "bytes!" ] bytes
                / "bytes1!0x" 1( HEXDIG HEXDIG )
                / "bytes2!0x" 2( HEXDIG HEXDIG )
                / "bytes3!0x" 3( HEXDIG HEXDIG )
                / "bytes4!0x" 4( HEXDIG HEXDIG )
                / "bytes5!0x" 5( HEXDIG HEXDIG )
                / "bytes6!0x" 6( HEXDIG HEXDIG )
                / "bytes7!0x" 7( HEXDIG HEXDIG )
                / "bytes8!0x" 8( HEXDIG HEXDIG )
                / "bytes9!0x" 9( HEXDIG HEXDIG )
                / "bytes10!0x" 10( HEXDIG HEXDIG )
                / "bytes11!0x" 11( HEXDIG HEXDIG )
                / "bytes12!0x" 12( HEXDIG HEXDIG )
                / "bytes13!0x" 13( HEXDIG HEXDIG )
                / "bytes14!0x" 14( HEXDIG HEXDIG )
                / "bytes15!0x" 15( HEXDIG HEXDIG )
                / "bytes16!0x" 16( HEXDIG HEXDIG )
                / "bytes17!0x" 17( HEXDIG HEXDIG )
                / "bytes18!0x" 18( HEXDIG HEXDIG )
                / "bytes19!0x" 19( HEXDIG HEXDIG )
                / "bytes20!0x" 20( HEXDIG HEXDIG )
                / "bytes21!0x" 21( HEXDIG HEXDIG )
                / "bytes22!0x" 22( HEXDIG HEXDIG )
                / "bytes23!0x" 23( HEXDIG HEXDIG )
                / "bytes24!0x" 24( HEXDIG HEXDIG )
                / "bytes25!0x" 25( HEXDIG HEXDIG )
                / "bytes26!0x" 26( HEXDIG HEXDIG )
                / "bytes27!0x" 27( HEXDIG HEXDIG )
                / "bytes28!0x" 28( HEXDIG HEXDIG )
                / "bytes29!0x" 29( HEXDIG HEXDIG )
                / "bytes30!0x" 30( HEXDIG HEXDIG )
                / "bytes31!0x" 31( HEXDIG HEXDIG )
                / "bytes32!0x" 32( HEXDIG HEXDIG )
stringArg       = "string!" *pchar [ "." fileExtension ]

aQuery          = attribute *( "&" attribute )
attribute       = attrName "=" attrValue
attrName        = "returns"
                / "returnTypes"
attrValue       = [ "(" [ retTypes ] ")" ]
retTypes        = retType *( "," retType )
retType         = retRawType *( "[" [ %x31-39 *DIGIT ] "]" )
retRawType      = "(" retTypes ")"
                / retBaseType
retBaseType      = "bool" / "uint" [ intSizes ] / "int" [ intSize ] / "address" / "bytes" [ bytesSizes ] / "string"
bytesSizes      = %x31-39              ; 1-9
                / ( "1" / "2" ) DIGIT  ; 10-29
                / "31" / "32"          ; 31-32

domainName      = *( unreserved / pct-encoded / sub-delims ) ; As in RFC 3986

fileExtension   = 1*( ALPHA / DIGIT )

address         = "0x" 20( HEXDIG HEXDIG )
bytes           = "0x" *( HEXDIG HEXDIG )

pchar           = unreserved / pct-encoded / sub-delims / ":" / "@" ; As in RFC 3986

pct-encoded     = "%" HEXDIG HEXDIG ; As in RFC 3986

unreserved      = ALPHA / DIGIT / "-" / "." / "_" / "~" ; As in RFC 3986
sub-delims    = "!" / "$" / "&" / "'" / "(" / ")"
                / "*" / "+" / "," / ";" / "=" ; As in RFC 3986

```

### 附录 B：与 [ERC-4804](./eip-4804.md) 的变化

#### 更正

- 手动模式： [ERC-4804](./eip-4804.md) 规定路径 [ "?" 查询 ] 没有解释。该 ERC 表示实际上对路径有解释，以确定 MIME 类型。
- 自动模式：如果 **query** 中没有 **returns** 属性， [ERC-4804](./eip-4804.md) 规定返回的数据被视为 ABI 编码的 bytes32。该 ERC 表示实际上返回的数据被视为 ABI 编码的 bytes。

#### 澄清

- 正式规范：该 ERC 添加了 URL 格式的 ABNF 定义。
- 解析模式：该 ERC 指出了解析模式如何确定的更多细节。
- 手动模式：该 ERC 指出如何处理 URI 百分比编码、返回数据以及如何确定 MIME 类型。
- 自动模式：该 ERC 更详细地说明了参数值的编码，以及 **returns** 值的格式和处理。
- 示例：该 ERC 为示例添加了更多细节。

#### 修改

- 协议名称： [ERC-4804](./eip-4804.md) 提到 ``ethereum-web3://`` 和 ``eth-web3://``，这些已被删除。
- 自动模式：支持的类型： [ERC-4804](./eip-4804.md) 仅支持 uint256、bytes32、address、bytes 和 string。该 ERC 添加了更多类型。
- 自动模式：当指定 **returns** 属性时返回整数的编码： [ERC-4804](./eip-4804.md) 在示例 5 中建议将整数编码为字符串。该 ERC 指出遵循以太坊 JSON RPC 规范，将整数编码为以 "0x" 为前缀的十六进制字符串。

## 理由

该提案的目的是为以太坊添加一个去中心化的展示层。通过该层，我们能够使用人类可读的 URL 在链上呈现任何 Web 内容（包括 HTML/CSS/JPG/PNG/SVG 等），从而使 EVM 可以作为去中心化的后端。该标准的设计基于以下原则：

- **人类可读**。Web3 URL 应该易于被人类识别，类似于 Web2 URL（`http://`）。因此，我们支持来自名称服务的名称以替代地址，以提高可读性。此外，我们使用人类可读的方法 + 参数，而不是使用十六进制的 calldata，以提高可读性。

- **与 HTTP-URL 标准最大兼容**。Web3 URL 应该与 HTTP-URL 标准兼容，包括相对路径、查询、片段、百分比编码等，以便现有的 HTTP-URL（例如，浏览器）可以轻松扩展到 Web3 URL，修改最小。这也意味着现有的 Web2 用户可以轻松迁移到 Web3，几乎不需要额外了解该标准。
- **简单**。我们使用“最大似然”原则自动检测参数的类型，如地址、bytes32 和 uint256，而不是提供显式类型。这可以大大减少 URL 的长度，同时避免混淆。此外，显式类型也被支持，以便在必要时消除混淆。

- **灵活**。该合约能够覆盖编码规则，从而使合约能够精确控制理解用户想要定位的实际 Web 资源。

## 安全考虑

未发现安全考虑。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。