---
eip: 6170
title: 跨链消息接口
description: 用于与消息协议交互的通用智能合约接口。
author: Sujith Somraaj (@sujithsomraaj)
discussions-to: https://ethereum-magicians.org/t/cross-chain-messaging-standard/12197
status: 草案
type: 标准跟踪
category: ERC
created: 2022-12-19
---

## 摘要

本 EIP 引入了一个通用接口，用于跨链任意消息桥（AMB）发送和接收跨链消息（状态）。

## 动机

目前，跨链任意消息桥缺乏标准化，导致复杂的竞争实现：Layerzero、Hyperlane、Axelar、Wormhole、Matic State Tunnel 等。无论是链本地（或）独立消息桥，问题依然存在。为任意消息桥添加一个通用的标准化接口提供了以下好处：

- **开发简便性：** 通用标准接口将帮助开发者轻松构建可扩展的跨链应用。

- **可扩展性提升：** 跨链应用可以高效地使用多个消息桥。

- **安全性提升：** 针对特定参数的安全性。目前，每个消息桥都有其不同的安全变量。例如，在 Layerzero 中，nonce 用于防止重放攻击，而 Hyperlane 使用默克尔根哈希。

- **鲁棒性提升：** 涉及链外组件的消息桥不具备抗审查能力，且容易出现停机。因此，基于它们构建的应用别无选择，只能迁移其整个状态（对于大型复杂应用来说几乎不可能）。

## 规范

本文档中的关键词 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按照 RFC 2119 中的描述进行解释。

每个合规的跨链任意消息桥必须实现以下接口。

``` solidity
// SPDX-License-Identifier: Apache-3.0

pragma solidity >=0.8.0;

/// @title Cross-Chain Messaging interface
/// @dev Allows seamless interchain messaging.
/// @author Sujith Somraaj
/// Note: Bytes are used throughout the implementation to support non-evm chains.

interface IEIP6170 {
    /// @dev This emits when a cross-chain message is sent.
    /// Note: MessageSent MUST trigger when a message is sent, including zero bytes transfers.
    event MessageSent(
        bytes to,
        bytes toChainId,
        bytes message,
        bytes extraData
    );

    /// @dev This emits when a cross-chain message is received.
    /// MessageReceived MUST trigger on any successful call to receiveMessage(bytes chainId, bytes sender, bytes message) function.
    event MessageReceived(bytes from, bytes fromChainId, bytes message);

    /// @dev Sends a message to a receiving address on a different blockchain.
    /// @param chainId_ is the unique identifier of receiving blockchain.
    /// @param receiver_ is the address of the receiver.
    /// @param message_ is the arbitrary message to be delivered.
    /// @param data_ is a bridge-specific encoded data for off-chain relayer infrastructure.
    /// @return the status of the process on the sending chain.
    /// Note: this function is designed to support both evm and non-evm chains
    /// Note: proposing chain-ids be the bytes encoding their native token name string. For eg., abi.encode("ETH"), abi.encode("SOL") imagining they cannot override.
    function sendMessage(
        bytes memory chainId_,
        bytes memory receiver_,
        bytes memory message_,
        bytes memory data_
    ) external payable returns (bool);

    /// @dev Receives a message from a sender on a different blockchain.
    /// @param chainId_ is the unique identifier of the sending blockchain.
    /// @param sender_ is the address of the sender.
    /// @param message_ is the arbitrary message sent by the sender.
    /// @param data_ is an additional parameter to be used for security purposes. E.g, can send nonce in layerzero.
    /// @return the status of message processing/storage.
    /// Note: sender validation (or) message validation should happen before processing the message.
    function receiveMessage(
        bytes memory chainId_,
        bytes memory sender_,
        bytes memory message_,
        bytes memory data_
    ) external payable returns (bool);
}
```

## 理由

跨链任意消息接口将优化区块链之间的互操作性层，提供一个功能齐全但简约的接口。轻量级的方法还为任意消息桥提供了创新的自由，使其在中继层面展示其技术实力。

该 EIP 将使区块链更易用和可扩展。它为构建跨链应用打开了可能性，利用任何两个区块链，而不仅限于以太坊和兼容的 L2。为了更好地理解，一个易于沟通的机制将允许开发者在以太坊和 Solana 之间构建跨链应用，利用它们各自的独特优势。

该接口还旨在降低应用/协议的单点故障（SPOF）风险，因为它们可以通过更新其 AMB 地址继续运行。

## 安全考虑

完全无权限的消息传递可能对协议构成安全威胁。建议所有集成者在集成之前审查消息隧道的实现。

在没有发送者身份验证的情况下，任何人都可以向接收智能合约写入任意消息。

本 EIP 仅关注消息应如何以特定标准发送和接收。但集成者可以在接收函数中利用 `data_` 参数实现任何身份验证（或）消息隧道特定操作。

## 版权

通过 [CC0](../LICENSE.md) 放弃版权及相关权利。