---
eip: 1616
title: 属性注册标准
author: 0age (@0age), Santiago Palladino (@spalladino), Leo Arias (@elopio), Alejo Salles (@fiiiu), Stephane Gosselin (@thegostep)
discussions-to: https://github.com/ethereum/EIPs/issues/1616
status: 停滞
type: 标准跟踪
category: ERC
created: 2018-11-23
requires: 165
---


## 简单总结
EIP-1616 提供了一个基本接口，用于查询分配给以太坊账户的属性元数据的注册表。

## 摘要
本 EIP 包含以下核心思想：
1. 不再直接依赖于索赔发起人的声誉来评估特定索赔的真实性，而是可以将信任提升到注册表策展人的层面。我们称之为“**属性注册表**”的这个注册表允许在实现中减少复杂性，因为需要验证属性的一方现在可以与一个受信任的索赔聚合器合作，而不必依赖于单个索赔提供者。
2. 索赔被抽象为标准“属性”，表示分配给账户的元数据，索赔与发起方解耦。属性作为扁平的 `uint256 -> uint256` 键值对在每个账户上注册，重要属性是 **每种属性类型在每个地址上只有一个规范值**。这个属性允许属性注册表的组合性和高级属性形成。
3. 有一种通用方法来确定注册表提供的属性键或 ID 集。该标准未指定属性及其值的管理要求或建议，也未说明可能与属性相关的其他元数据。可能会在单独的 EIP 中提出一组标准的属性名称和元数据模式。

属性注册表的潜在高级用途包括：
* 编码将多个属性组合成单个 uint256 键的复杂布尔表达式，然后由注册表逻辑解析和评估。
* 使用与属性相关的值查询额外的链上或链下元数据。
* 通过调用其他属性注册表或其他合约来解析属性值，委托权限而不改变注册表的接口。

## 动机
本 EIP 的动机在于需要合约和外部账户能够从单一受信任来源验证有关特定地址的信息 **而不必关心信息获取的具体细节**，并尽可能简单地做到这一点。它还受到促进属性注册表之间广泛 **跨兼容性和组合性** 的愿望的驱动，这一属性通过接口的简单性以及所提议标准提供的唯一性保证得到了增强。

现有的 EIP 用于将元数据分配给账户，包括 EIP-735 和 EIP-780，这两者都允许在同一地址上针对任何给定索赔主题发出多个索赔。这迫使元数据的验证者评估每个索赔的真实性，考虑每个索赔发起人的相对声誉。它还规定了添加和删除索赔的方法，这可能并不适合所有用例。

本 EIP 提出了一个轻量级的抽象层，用于标准账户元数据注册接口。该抽象层可以位于 EIP-735 和 EIP-780 等索赔注册表之上，或其他注册表，作为属性注册表策展人选择的受信任数据源。

## 规范
属性注册表接口包含四个函数，概述如下：
```solidity
/**
 * @title EIP-1616 Attribute Registry Standard interface. EIP-165 ID: 0x5f46473f
 */
interface AttributeRegistryInterface {
  function hasAttribute(address account, uint256 attributeTypeID) external view returns (bool);
  function getAttributeValue(address account, uint256 attributeTypeID) external view returns (uint256);
  function countAttributeTypes() external view returns (uint256);
  function getAttributeTypeID(uint256 index) external view returns (uint256);
}
```

符合属性注册表 EIP 的合约必须实现上述接口。

作为附加要求，必须包含 ERC-165 接口：
```solidity
/**
 * @title EIP-165 interface. EIP-165 ID: 0x01ffc9a7
 */
interface EIP-165 {
  /**
   * @notice EIP-165 support. Attribute Registry interface ID is 0x5f46473f.
   * @param _interfaceID The interface identifier, as specified in EIP-165
   * @return True for 0x01ffc9a7 & 0x5f46473f, false for unsupported interfaces.
   */
  function supportsInterface(bytes4 _interfaceID) external view returns (bool);
}
```

实现必须遵循以下描述的规范。

### 查看函数
以下详细说明的查看函数必须实现。

#### `hasAttribute` 函数
```solidity
function hasAttribute(address account, uint256 attributeTypeID) external view returns (bool)
```

检查在注册表中是否已将属性分配给给定账户，并且当前有效。

_**注意**_: 此函数必须返回 true 或 false - 即调用此函数不得导致调用者回退。希望在执行此函数期间调用其他合约的实现必须捕获任何 `revert`，并返回 `false`。

_**注意**_: 当使用相同的 `account` 和 `attributeTypeID` 参数执行两次直接连续的函数调用时，此函数必须返回两个相等的值，无论调用者的地址、交易来源或其他带外信息的差异如何。



#### `getAttributeValue` 函数
```solidity
function getAttributeValue(address account, uint256 attributeTypeID) external view returns (uint256)
```

检索在注册表中给定账户上属性的 `uint256` 值，假设该属性当前有效。

_**注意**_: 如果直接前面的或后续的对 `hasAttribute` 的函数调用使用相同的 `account` 和 `attributeTypeID` 参数将返回 false，则此函数必须回退。

_**注意**_: 当使用相同的 `account` 和 `attributeTypeID` 参数执行两次直接连续的函数调用时，此函数必须返回两个相等的值，无论调用者的地址、交易来源或其他带外信息的差异如何。

#### `countAttributeTypes` 函数
```solidity
function countAttributeTypes() external view returns (uint256)
```

检索注册表中定义的有效属性类型的总数。与 `getAttributeTypeID` 一起使用，以确定注册表中可用的所有属性类型。

_**注意**_: 此函数必须返回一个正整数值 - 即调用此函数不得导致调用者回退。

_**注意**_: 此函数必须返回一个值，该值涵盖所有属性类型 ID 的索引，其中对某个地址的 `hasAttribute` 调用与给定索引的属性类型 ID 将返回 `true`。

#### `getAttributeTypeID` 函数
```solidity
function getAttributeTypeID(uint256 index) external view returns (uint256)
```

通过索引检索在注册表中定义的属性类型的 ID。与 `countAttributeTypes` 一起使用，以确定注册表中可用的所有属性类型。

_**注意**_: 如果提供的 `index` 值超出直接前面的或后续的对 `countAttributeTypes` 的函数调用返回的值的范围，则此函数必须回退。如果提供的 `index` 值在该范围内，则不得回退。

_**注意**_: 如果相同的 `attributeTypeID` 值在作为参数传递时会导致对 `hasAttribute` 的给定调用返回 `true`，则此函数必须在 *某个* 索引上返回一个 `attributeTypeID` 值。

## 理由
该标准扩展了元数据分配的适用性，以涵盖那些 EIP-735、EIP-780 或类似提案未能充分表示的用例。即，它强制每个地址每个属性 ID 只有一个属性值的约束，而不是每个地址每个 ID *每个发起者* 只有一个值。

除了规定的属性值外，属性属性在标准中被故意省略。虽然许多属性注册表将在实例和类级别上需要关于属性的额外元数据，但通过强制执行广泛适用的属性基础层，可以更有效地促进高度可变注册表扩展之间的可靠和灵活的互操作性。
## 向后兼容性
没有向后兼容性问题。

## 测试用例
具有 100% 代码覆盖率的目标测试用例可以在 [这个仓库](https://github.com/0age/AttributeRegistry) 中找到。有关实现应用程序注册接口的更复杂合约的测试，请参见 [这里](https://github.com/TPL-protocol/tpl-contracts)。

## 实现
以下基本实现可以在 [这个仓库](https://github.com/0age/AttributeRegistry) 中找到（有关更复杂实现合约的示例，请参见 [这里](https://github.com/TPL-protocol/tpl-contracts/blob/master/contracts/BasicJurisdiction.sol#L399)）：

```solidity
pragma solidity ^0.4.25;

/**
 * @title Attribute Registry interface. EIP-165 ID: 0x5f46473f
 */
interface AttributeRegistryInterface {
  /**
   * @notice Check if an attribute of the type with ID `attributeTypeID` has
   * been assigned to the account at `account` and is currently valid.
   * @param account address The account to check for a valid attribute.
   * @param attributeTypeID uint256 The ID of the attribute type to check for.
   * @return True if the attribute is assigned and valid, false otherwise.
   * @dev This function MUST return either true or false - i.e. calling this
   * function MUST NOT cause the caller to revert.
   */
  function hasAttribute(
    address account,
    uint256 attributeTypeID
  ) external view returns (bool);

  /**
   * @notice Retrieve the value of the attribute of the type with ID
   * `attributeTypeID` on the account at `account`, assuming it is valid.
   * @param account address The account to check for the given attribute value.
   * @param attributeTypeID uint256 The ID of the attribute type to check for.
   * @return The attribute value if the attribute is valid, reverts otherwise.
   * @dev This function MUST revert if a directly preceding or subsequent
   * function call to `hasAttribute` with identical `account` and
   * `attributeTypeID` parameters would return false.
   */
  function getAttributeValue(
    address account,
    uint256 attributeTypeID
  ) external view returns (uint256);

  /**
   * @notice Count the number of attribute types defined by the registry.
   * @return The number of available attribute types.
   * @dev This function MUST return a positive integer value  - i.e. calling
   * this function MUST NOT cause the caller to revert.
   */
  function countAttributeTypes() external view returns (uint256);

  /**
   * @notice Get the ID of the attribute type at index `index`.
   * @param index uint256 The index of the attribute type in question.
   * @return The ID of the attribute type.
   * @dev This function MUST revert if the provided `index` value falls outside
   * of the range of the value returned from a directly preceding or subsequent
   * function call to `countAttributeTypes`. It MUST NOT revert if the provided
   * `index` value falls inside said range.
   */
  function getAttributeTypeID(uint256 index) external view returns (uint256);
}


/**
 * @title A simple example of an Attribute Registry implementation.
 */
contract AttributeRegistry is AttributeRegistryInterface {
  // This particular implementation just defines two attribute types.
  enum Affiliation { Whitehat, Blackhat }

  // Top-level information about attribute types held in a static array.
  uint256[2] private _attributeTypeIDs;

  // The number of attributes currently issued tracked in a static array.
  uint256[2] private _issuedAttributeCounters;

  // Issued attributes held in a nested mapping by account & attribute type.
  mapping(address => mapping(uint256 => bool)) private _issuedAttributes;

  // Issued attribute values held in a nested mapping by account & type.
  mapping(address => mapping(uint256 => uint256)) private _issuedAttributeValues;

  /**
  * @notice The constructor function, defines the two attribute types available
  * on this particular registry.
  */
  constructor() public {
    // Set the attribute type IDs for whitehats (8008) and blackhats (1337).
    _attributeTypeIDs = [8008, 1337];
  }

  /**
   * @notice Assign a "whitehat" attribute type to `msg.sender`.
   * @dev The function may not be called by accounts with a "blackhat" attribute
   * type already assigned. This function is arbitrary and not part of the
   * Attribute Registry specification.
   */
  function joinWhitehats() external {
    // Get the index of the blackhat attribute type on the attribute registry.
    uint256 blackhatIndex = uint256(Affiliation.Blackhat);

    // Get the attribute type ID of the blackhat attribute type.
    uint256 blackhatAttributeTypeID = _attributeTypeIDs[blackhatIndex];

    // Do not allow the whitehat attribute to be set if blackhat is already set.
    require(
      !_issuedAttributes[msg.sender][blackhatAttributeTypeID],
      "no blackhats allowed!"
    );

    // Get the index of the whitehat attribute type on the attribute registry.
    uint256 whitehatIndex = uint256(Affiliation.Whitehat);

    // Get the attribute type ID of the whitehat attribute type.
    uint256 whitehatAttributeTypeID = _attributeTypeIDs[whitehatIndex];

    // Mark the attribute as issued on the given address.
    _issuedAttributes[msg.sender][whitehatAttributeTypeID] = true;

    // Calculate the new number of total whitehat attributes.
    uint256 incrementCounter = _issuedAttributeCounters[whitehatIndex] + 1;

    // Set the attribute value to the new total assigned whitehat attributes.
    _issuedAttributeValues[msg.sender][whitehatAttributeTypeID] = incrementCounter;

    // Update the value of the counter for total whitehat attributes.
    _issuedAttributeCounters[whitehatIndex] = incrementCounter;
  }

  /**
   * @notice Assign a "blackhat" attribute type to `msg.sender`.
   * @dev The function may be called by any account, but assigned "whitehat"
   * attributes will be removed. This function is arbitrary and not part of the
   * Attribute Registry specification.
   */
  function joinBlackhats() external {
    // Get the index of the blackhat attribute type on the attribute registry.
    uint256 blackhatIndex = uint256(Affiliation.Blackhat);

    // Get the attribute type ID of the blackhat attribute type.
    uint256 blackhatAttributeTypeID = _attributeTypeIDs[blackhatIndex];

    // Mark the attribute as issued on the given address.    
    _issuedAttributes[msg.sender][blackhatAttributeTypeID] = true;

    // Calculate the new number of total blackhat attributes.    
    uint256 incrementCounter = _issuedAttributeCounters[blackhatIndex] + 1;

    // Set the attribute value to the new total assigned blackhat attributes.    
    _issuedAttributeValues[msg.sender][blackhatAttributeTypeID] = incrementCounter;

    // Update the value of the counter for total blackhat attributes.    
    _issuedAttributeCounters[blackhatIndex] = incrementCounter;

    // Get the index of the whitehat attribute type on the attribute registry.
    uint256 whitehatIndex = uint256(Affiliation.Whitehat);

    // Get the attribute type ID of the whitehat attribute type.
    uint256 whitehatAttributeTypeID = _attributeTypeIDs[whitehatIndex];

    // Determine if a whitehat attribute type has been assigned.
    if (_issuedAttributes[msg.sender][whitehatAttributeTypeID]) {
      // If so, delete the attribute.
      delete _issuedAttributes[msg.sender][whitehatAttributeTypeID];

      // Delete the attribute value as well.
      delete _issuedAttributeValues[msg.sender][whitehatAttributeTypeID];

      // Set the attribute value to the new total assigned whitehat attributes.      
      uint256 decrementCounter = _issuedAttributeCounters[whitehatIndex] - 1;

      // Update the value of the counter for total whitehat attributes.
      _issuedAttributeCounters[whitehatIndex] = decrementCounter;
    }
  }

  /**
   * @notice Get the total number of assigned whitehat and blackhat attributes.
   * @return Array with counts of assigned whitehat and blackhat attributes.
   * @dev This function is arbitrary and not part of the Attribute Registry
   * specification.
   */
  function totalHats() external view returns (uint256[2]) {
    // Return the array containing counter values.
    return _issuedAttributeCounters;
  }

  /**
   * @notice Check if an attribute of the type with ID `attributeTypeID` has
   * been assigned to the account at `account` and is currently valid.
   * @param account address The account to check for a valid attribute.
   * @param attributeTypeID uint256 The ID of the attribute type to check for.
   * @return True if the attribute is assigned and valid, false otherwise.
   * @dev This function MUST return either true or false - i.e. calling this
   * function MUST NOT cause the caller to revert.
   */
  function hasAttribute(
    address account,
    uint256 attributeTypeID
  ) external view returns (bool) {
    // Return assignment status of attribute by account and attribute type ID
    return _issuedAttributes[account][attributeTypeID];
  }

  /**
   * @notice Retrieve the value of the attribute of the type with ID
   * `attributeTypeID` on the account at `account`, assuming it is valid.
   * @param account address The account to check for the given attribute value.
   * @param attributeTypeID uint256 The ID of the attribute type to check for.
   * @return The attribute value if the attribute is valid, reverts otherwise.
   * @dev This function MUST revert if a directly preceding or subsequent
   * function call to `hasAttribute` with identical `account` and
   * `attributeTypeID` parameters would return false.
   */
  function getAttributeValue(
    address account,
    uint256 attributeTypeID
  ) external view returns (uint256 value) {
    // Revert if attribute with given account & attribute type ID is unassigned
    require(
      _issuedAttributes[account][attributeTypeID],
      "could not find a value with the provided account and attribute type ID"
    );

    // Return the attribute value.
    return _issuedAttributeValues[account][attributeTypeID];
  }

  /**
   * @notice Count the number of attribute types defined by the registry.
   * @return The number of available attribute types.
   * @dev This function MUST return a positive integer value  - i.e. calling
   * this function MUST NOT cause the caller to revert.
   */
  function countAttributeTypes() external view returns (uint256) {
    // Return the length of the attribute type IDs array.
    return _attributeTypeIDs.length;
  }

  /**
   * @notice Get the ID of the attribute type at index `index`.
   * @param index uint256 The index of the attribute type in question.
   * @return The ID of the attribute type.
   * @dev This function MUST revert if the provided `index` value falls outside
   * of the range of the value returned from a directly preceding or subsequent
   * function call to `countAttributeTypes`. It MUST NOT revert if the provided
   * `index` value falls inside said range.
   */
  function getAttributeTypeID(uint256 index) external view returns (uint256) {
    // Revert if the provided index is out of range.
    require(
      index < _attributeTypeIDs.length,
      "provided index is outside of the range of defined attribute type IDs"
    );

    // Return the attribute type ID at the given index in the array.
    return _attributeTypeIDs[index];
  }
}
```

## 版权
通过 [CC0](../LICENSE.md) 放弃版权及相关权利。