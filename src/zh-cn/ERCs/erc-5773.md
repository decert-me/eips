---
eip: 5773
title: 上下文依赖的多资产代币
description: 一种多资产代币的接口，其输出的资产类型依赖于所有者的偏好。
author: Bruno Škvorc (@Swader), Cicada (@CicadaNCR), Steven Pineda (@steven2308), Stevan Bogosavljevic (@stevyhacker), Jan Turk (@ThunderDeliverer)
discussions-to: https://ethereum-magicians.org/t/multiresource-tokens/11326
status: Final
type: Standards Track
category: ERC
created: 2022-10-10
requires: 165, 721
---

## 摘要

多资产 NFT 标准允许构建一种新的原语：每个 NFT 的上下文依赖信息输出。

上下文依赖的信息输出意味着资产以适当的格式显示，具体取决于代币的访问方式。即，如果代币在电子书阅读器中打开，则显示 PDF 资产；如果在市场中打开，则显示 PNG 或 SVG 资产；如果从游戏中访问代币，则访问 3D 模型资产；如果通过物联网 (IoT) 中心访问代币，则访问提供必要地址和规格信息的资产。

一个 NFT 可以有多个资产（输出），这些资产可以是任何类型的文件，供消费者使用，并按优先级排序。它们不必在 mimetype 或 tokenURI 上匹配，也不相互依赖。资产不是独立实体，而应被视为“命名空间 tokenURIs”，可以由 NFT 所有者随意排序，但只有在代币所有者和代币发行者达成一致的情况下，才能修改、更新、添加或删除。

## 动机

随着 NFT 在以太坊生态系统中成为广泛使用的代币形式，并用于各种用例，现在是时候为它们标准化额外的实用性了。将多个资产与单个 NFT 关联可以提供更大的实用性、可用性和向前兼容性。

自 [ERC-721](./eip-721.md) 发布以来的四年中，额外功能的需求导致了无数扩展。此 EIP 在以下几个方面改进了 ERC-721：

- [跨元宇宙兼容性](#cross-metaverse-compatibility)
- [多媒体输出](#multi-media-output)
- [媒体冗余](#media-redundancy)
- [NFT 进化](#nft-evolution)

### 跨元宇宙兼容性

在撰写此提案时，元宇宙仍然是一个新兴的、尚未完全定义的术语。无论元宇宙的定义如何演变，该提案都可以支持任意数量的不同实现。

跨元宇宙兼容性也可以称为跨引擎兼容性。一个例子是游戏 A 的化妆品在游戏 B 中不可用，因为框架不兼容。

这样的 NFT 可以通过新的附加资产获得进一步的实用性：更多的游戏、更多的化妆品项，附加到同一个 NFT 上。因此，作为 NFT 的游戏化妆品项成为一种不断发展的、具有无限实用性的 NFT。

以下是一个更具体的例子。一个资产是游戏 A 的化妆品项，包含化妆品资产的文件。另一个是游戏 B 的化妆品资产文件。第三个是一个通用资产，旨在在目录、市场、投资组合跟踪器或其他通用 NFT 查看器中显示，包含化妆品项的表示、样式缩略图和动画演示/预告片。

此 EIP 增加了一层抽象，允许游戏开发者直接从用户的 NFT 中提取资产数据，而不是硬编码。

### 多媒体输出

电子书的 NFT 可以根据加载它的软件以 PDF、MP3 或其他格式表示。如果加载到电子书阅读器中，则应显示 PDF；如果加载到有声书应用程序中，则应使用 MP3 表示。NFT 中可能存在其他元数据（例如书籍的封面图像），用于在各种市场、搜索引擎结果页面 (SERPs) 或投资组合跟踪器中进行识别。

### 媒体冗余

许多 NFT 在没有考虑最佳实践的情况下匆忙铸造 - 特别是，许多 NFT 的元数据集中在某个服务器上，或者在某些情况下，硬编码的 IPFS 网关也可能会宕机，而不是仅仅使用 IPFS 哈希。

通过将相同的元数据文件作为不同的资产添加，例如，一个资产是元数据及其在 Arweave 上链接的图像，一个资产是相同组合在 Sia 上的，另一个是相同组合在 IPFS 上的，等等，元数据及其引用信息的弹性呈指数级增加，因为所有协议同时宕机的可能性变得更小。

### NFT 进化

许多 NFT，特别是与游戏相关的 NFT，需要进化。这在现代元宇宙中尤其如此，因为没有元宇宙实际上是一个元宇宙 - 它只是一个托管在某人服务器上的多人游戏，用 NFT 余额替代用户名/密码登录。

当服务器宕机或游戏关闭时，玩家最终会一无所获（经验损失）或得到一些无关的东西（与游戏体验无关的资产或配件，充斥钱包，与其他“宇宙”不兼容 - 见上面的 [跨元宇宙](#cross-metaverse-compatibility) 兼容性）。

通过多资产 NFT，铸造者或其他预先批准的实体可以建议一个新资产给 NFT 所有者，后者可以选择接受或拒绝。该资产甚至可以针对要被替换的现有资产。

替换资产在某种程度上可以类似于替换 ERC-721 代币的 URI。当资产被替换时，仍然保留清晰的可追溯性；旧资产仍然可以访问和验证。替换资产的元数据 URI 会模糊这种血统。如果发行者不能随意替换 NFT 的资产，这也会给代币所有者更多的信任。该提案的建议-接受资产替换机制提供了这种保证。

这允许升级机制，一旦收集到足够的经验，用户可以接受升级。升级包括向 NFT 添加新资产，一旦接受，该新资产将替换旧资产。

作为一个具体的例子，想象一下 Pokemon™️ 的进化 - 一旦获得足够的经验，训练师可以选择进化他们的怪物。通过多资产 NFT，不需要对元数据进行集中控制以进行替换，也不需要向用户的钱包空投另一个 NFT - 相反，一个新的 Raichu 资产被铸造到 Pikachu 上，如果被接受，Pikachu 资产就消失，取而代之的是 Raichu，后者现在具有自己的属性、值等。

另一个例子可以是 IoT 设备固件的版本控制。一个资产可以代表其当前固件，一旦更新可用，当前资产可以被包含更新固件的资产替换。

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

```solidity
/// @title ERC-5773 Context-Dependent Multi-Asset Tokens
/// @dev See https://eips.ethereum.org/EIPS/eip-5773
/// @dev Note: the ERC-165 identifier for this interface is 0x06b4329a.

pragma solidity ^0.8.16;

interface IERC5773 /* is ERC165 */ {
    /**
     * @notice Used to notify listeners that an asset object is initialised at `assetId`.
     * @param assetId ID of the asset that was initialised
     */
    event AssetSet(uint64 assetId);

    /**
     * @notice Used to notify listeners that an asset object at `assetId` is added to token's pending asset
     *  array.
     * @param tokenIds An array of IDs of the tokens that received a new pending asset
     * @param assetId ID of the asset that has been added to the token's pending assets array
     * @param replacesId ID of the asset that would be replaced
     */
    event AssetAddedToTokens(
        uint256[] tokenIds,
        uint64 indexed assetId,
        uint64 indexed replacesId
    );

    /**
     * @notice Used to notify listeners that an asset object at `assetId` is accepted by the token and migrated
     *  from token's pending assets array to active assets array of the token.
     * @param tokenId ID of the token that had a new asset accepted
     * @param assetId ID of the asset that was accepted
     * @param replacesId ID of the asset that was replaced
     */
    event AssetAccepted(
        uint256 indexed tokenId,
        uint64 indexed assetId,
        uint64 indexed replacesId
    );

    /**
     * @notice Used to notify listeners that an asset object at `assetId` is rejected from token and is dropped
     *  from the pending assets array of the token.
     * @param tokenId ID of the token that had an asset rejected
     * @param assetId ID of the asset that was rejected
     */
    event AssetRejected(uint256 indexed tokenId, uint64 indexed assetId);

    /**
     * @notice Used to notify listeners that token's priority array is reordered.
     * @param tokenId ID of the token that had the asset priority array updated
     */
    event AssetPrioritySet(uint256 indexed tokenId);

    /**
     * @notice Used to notify listeners that owner has granted an approval to the user to manage the assets of a
     *  given token.
     * @dev Approvals must be cleared on transfer
     * @param owner Address of the account that has granted the approval for all token's assets
     * @param approved Address of the account that has been granted approval to manage the token's assets
     * @param tokenId ID of the token on which the approval was granted
     */
    event ApprovalForAssets(
        address indexed owner,
        address indexed approved,
        uint256 indexed tokenId
    );

    /**
     * @notice Used to notify listeners that owner has granted approval to the user to manage assets of all of their
     *  tokens.
     * @param owner Address of the account that has granted the approval for all assets on all of their tokens
     * @param operator Address of the account that has been granted the approval to manage the token's assets on all of the
     *  tokens
     * @param approved Boolean value signifying whether the permission has been granted (`true`) or revoked (`false`)
     */
    event ApprovalForAllForAssets(
        address indexed owner,
        address indexed operator,
        bool approved
    );

    /**
     * @notice Accepts an asset at from the pending array of given token.
     * @dev Migrates the asset from the token's pending asset array to the token's active asset array.
     * @dev Active assets cannot be removed by anyone, but can be replaced by a new asset.
     * @dev Requirements:
     *
     *  - The caller must own the token or be approved to manage the token's assets
     *  - `tokenId` must exist.
     *  - `index` must be in range of the length of the pending asset array.
     * @dev Emits an {AssetAccepted} event.
     * @param tokenId ID of the token for which to accept the pending asset
     * @param index Index of the asset in the pending array to accept
     * @param assetId Id of the asset expected to be in the index
     */
    function acceptAsset(
        uint256 tokenId,
        uint256 index,
        uint64 assetId
    ) external;

    /**
     * @notice Rejects an asset from the pending array of given token.
     * @dev Removes the asset from the token's pending asset array.
     * @dev Requirements:
     *
     *  - The caller must own the token or be approved to manage the token's assets
     *  - `tokenId` must exist.
     *  - `index` must be in range of the length of the pending asset array.
     * @dev Emits a {AssetRejected} event.
     * @param tokenId ID of the token that the asset is being rejected from
     * @param index Index of the asset in the pending array to be rejected
     * @param assetId Id of the asset expected to be in the index
     */
    function rejectAsset(
        uint256 tokenId,
        uint256 index,
        uint64 assetId
    ) external;

    /**
     * @notice Rejects all assets from the pending array of a given token.
     * @dev Effectively deletes the pending array.
     * @dev Requirements:
     *
     *  - The caller must own the token or be approved to manage the token's assets
     *  - `tokenId` must exist.
     * @dev Emits a {AssetRejected} event with assetId = 0.
     * @param tokenId ID of the token of which to clear the pending array
     * @param maxRejections to prevent from rejecting assets which arrive just before this operation.
     */
    function rejectAllAssets(uint256 tokenId, uint256 maxRejections) external;

    /**
     * @notice Sets a new priority array for a given token.
     * @dev The priority array is a non-sequential list of `uint16`s, where the lowest value is considered highest
     *  priority.
     * @dev Value `0` of a priority is a special case equivalent to uninitialised.
     * @dev Requirements:
     *
     *  - The caller must own the token or be approved to manage the token's assets
     *  - `tokenId` must exist.
     *  - The length of `priorities` must be equal the length of the active assets array.
     * @dev Emits a {AssetPrioritySet} event.
     * @param tokenId ID of the token to set the priorities for
     * @param priorities An array of priorities of active assets. The succession of items in the priorities array
     *  matches that of the succession of items in the active array
     */
    function setPriority(uint256 tokenId, uint64[] calldata priorities)
        external;

    /**
     * @notice Used to retrieve IDs of the active assets of given token.
     * @dev Asset data is stored by reference, in order to access the data corresponding to the ID, call
     *  `getAssetMetadata(tokenId, assetId)`.
     * @dev You can safely get 10k
     * @param tokenId ID of the token to retrieve the IDs of the active assets
     * @return uint64[] An array of active asset IDs of the given token
     */
    function getActiveAssets(uint256 tokenId)
        external
        view
        returns (uint64[] memory);

    /**
     * @notice Used to retrieve IDs of the pending assets of given token.
     * @dev Asset data is stored by reference, in order to access the data corresponding to the ID, call
     *  `getAssetMetadata(tokenId, assetId)`.
     * @param tokenId ID of the token to retrieve the IDs of the pending assets
     * @return uint64[] An array of pending asset IDs of the given token
     */
    function getPendingAssets(uint256 tokenId)
        external
        view
        returns (uint64[] memory);

    /**
     * @notice Used to retrieve the priorities of the active assets of a given token.
     * @dev Asset priorities are a non-sequential array of uint16 values with an array size equal to active asset
     *  priorites.
     * @param tokenId ID of the token for which to retrieve the priorities of the active assets
     * @return uint16[] An array of priorities of the active assets of the given token
     */
    function getActiveAssetPriorities(uint256 tokenId)
        external
        view
        returns (uint64[] memory);

    /**
     * @notice Used to retrieve the asset that will be replaced if a given asset from the token's pending array
     *  is accepted.
     * @dev Asset data is stored by reference, in order to access the data corresponding to the ID, call
     *  `getAssetMetadata(tokenId, assetId)`.
     * @param tokenId ID of the token to check
     * @param newAssetId ID of the pending asset which will be accepted
     * @return uint64 ID of the asset which will be replaced
     */
    function getAssetReplacements(uint256 tokenId, uint64 newAssetId)
        external
        view
        returns (uint64);

    /**
     * @notice Used to fetch the asset metadata of the specified token's active asset with the given index.
     * @dev Can be overridden to implement enumerate, fallback or other custom logic.
     * @param tokenId ID of the token from which to retrieve the asset metadata
     * @param assetId Asset Id, must be in the active assets array
     * @return string The metadata of the asset belonging to the specified index in the token's active assets
     *  array
     */
    function getAssetMetadata(uint256 tokenId, uint64 assetId)
        external
        view
        returns (string memory);

    /**
     * @notice Used to grant permission to the user to manage token's assets.
     * @dev This differs from transfer approvals, as approvals are not cleared when the approved party accepts or
     *  rejects an asset, or sets asset priorities. This approval is cleared on token transfer.
     * @dev Only a single account can be approved at a time, so approving the `0x0` address clears previous approvals.
     * @dev Requirements:
     *
     *  - The caller must own the token or be an approved operator.
     *  - `tokenId` must exist.
     * @dev Emits an {ApprovalForAssets} event.
     * @param to Address of the account to grant the approval to
     * @param tokenId ID of the token for which the approval to manage the assets is granted
     */
    function approveForAssets(address to, uint256 tokenId) external;

    /**
     * @notice Used to retrieve the address of the account approved to manage assets of a given token.
     * @dev Requirements:
     *
     *  - `tokenId` must exist.
     * @param tokenId ID of the token for which to retrieve the approved address
     * @return address Address of the account that is approved to manage the specified token's assets
     */
    function getApprovedForAssets(uint256 tokenId)
        external
        view
        returns (address);

    /**
     * @notice Used to add or remove an operator of assets for the caller.
     * @dev Operators can call {acceptAsset}, {rejectAsset}, {rejectAllAssets} or {setPriority} for any token
     *  owned by the caller.
     * @dev Requirements:
     *
     *  - The `operator` cannot be the caller.
     * @dev Emits an {ApprovalForAllForAssets} event.
     * @param operator Address of the account to which the operator role is granted or revoked from
     * @param approved The boolean value indicating whether the operator role is being granted (`true`) or revoked
     *  (`false`)
     */
    function setApprovalForAllForAssets(address operator, bool approved)
        external;

    /**
     * @notice Used to check whether the address has been granted the operator role by a given address or not.
     * @dev See {setApprovalForAllForAssets}.
     * @param owner Address of the account that we are checking for whether it has granted the operator role
     * @param operator Address of the account that we are checking whether it has the operator role or not
     * @return bool The boolean value indicating whether the account we are checking has been granted the operator role
     */
    function isApprovedForAllForAssets(address owner, address operator)
        external
        view
        returns (bool);
}
```

`getAssetMetadata` 函数返回资产的元数据 URI。资产的元数据 URI 指向的元数据可能包含以下字段的 JSON 响应：

```json
{
  "name": "Asset Name",
  "description": "The description of the token or asset",
  "mediaUri": "ipfs://mediaOfTheAssetOrToken",
  "thumbnailUri": "ipfs://thumbnailOfTheAssetOrToken",
  "externalUri": "https://uriToTheProjectWebsite",
  "license": "License name",
  "licenseUri": "https://uriToTheLicense",
  "tags": ["tags", "used", "to", "help", "marketplaces", "categorize", "the", "asset", "or", "token"],
  "preferThumb": false, // A boolean flag indicating to UIs to prefer thumbnailUri instead of mediaUri wherever applicable
  "attributes": [
    {
      "label": "rarity",
      "type": "string",
      "value": "epic",
      // For backward compatibility
      "trait_type": "rarity"
    },
    {
      "label": "color",
      "type": "string",
      "value": "red",
      // For backward compatibility
      "trait_type": "color"
    },
    {
      "label": "height",
      "type": "float",
      "value": 192.4,
      // For backward compatibility
      "trait_type": "height",
      "display_type": "number"
    }
  ]
}
```

虽然这是资产元数据的建议 JSON 架构，但并不强制执行，可能根据实施者的偏好完全不同。
## 理由

在设计提案时，我们考虑了以下问题：

1. **在提到构成代币的结构时，我们应该使用资产还是资源？**\
最初的想法是将提案称为多资源，但虽然这表示了单个代币可以持有的结构的广泛性，但术语*资产*更能代表它。\
资产被定义为个人、公司或组织所拥有的东西，例如金钱、财产或土地。这是对本提案资产的最佳表述。本提案中的资产可以是多媒体文件、技术信息、土地契约或任何实施者决定作为其实施的代币资产的东西。
2. **为什么不使用 [EIP-712](./eip-712.md) 许可样式签名来管理批准？**\
为了保持一致性。本提案扩展了 ERC-721，后者已经使用 1 个交易来批准代币的操作。如果同时支持签名消息来处理资产操作，这将是不一致的。
3. **为什么使用索引？**\
为了减少 gas 消耗。如果使用资产 ID 来查找要接受或拒绝的资产，则需要对数组进行迭代，操作的成本将取决于活动或待处理资产数组的大小。使用索引时，成本是固定的。每个代币需要维护一组活动和待处理资产数组，因为获取它们的方法是提议接口的一部分。\
为了避免资产索引变化的竞争条件，预期的资产 ID 被包含在需要资产索引的操作中，以验证使用索引访问的资产是否是预期的资产。\
尝试过内部使用映射跟踪索引的实现。将资产添加到代币的平均成本增加了超过 25%，接受和拒绝资产的成本分别增加了 4.6%和 7.1%。我们得出结论，这对本提案来说不是必要的，可以作为愿意接受此成本的用例的扩展来实现。在提供的示例实现中，有几个钩子使这成为可能。
4. **为什么不包括获取所有资产的方法？**\
获取所有资产可能不是所有实施者所需的操作。此外，它可以作为扩展添加，使用钩子实现，或者可以使用索引器进行模拟。
5. **为什么不包括分页？**\
资产 ID 使用`uint64`，测试确认在达到 gas 限制之前可以读取的 ID 限制大约为 30,000。这不被期望是常见的用例，因此不属于接口的一部分。然而，实施者可以根据需要为此用例创建扩展。
6. **本提案与其他试图解决类似问题的提案有何不同？**\
在审查它们后，我们得出结论，每个提案至少包含以下一种限制：
   - 使用单个 URI，随着新资产的需要而被替换，这引入了对代币所有者的信任问题。
   - 仅关注某种类型的资产，而本提案对资产类型不敏感。
   - 为每个新用例拥有不同的代币，这意味着代币不向后兼容。

### 多资产存储模式

资产在代币中作为`uint64`标识符的数组存储。

为了减少链上冗余字符串存储，多资产代币通过内部存储按引用存储资产。存储中的资产条目通过`uint64`映射到资产数据。

资产数组是这些`uint64`资产 ID 引用的数组。

这样的结构允许将通用资产添加到存储中一次，并可以根据需要多次将其引用添加到代币合约中。实施者可以使用字符串连接根据资产中的基本*SRC*和*token ID*程序性生成指向内容地址存档的链接。在新代币中存储资产将仅占用资产数组中每个代币 16 字节的存储空间，适用于重复和`tokenId`相关的资产。

以这种方式构建代币的资产允许通过连接程序性推导出 URI，特别是当它们仅通过`tokenId`不同时。

### 资产添加的提议-提交模式

将资产添加到现有代币中必须以提议-提交模式进行，以允许第三方有限的可变性。当将资产添加到代币时，首先将其放入*"待处理"*数组中，并必须由代币的所有者迁移到*"活动"*数组中。*"待处理"*资产数组应限制为 128 个槽，以防止垃圾邮件和恶意行为。

### 资产管理

包括多个资产管理功能。除了从*"待处理"*迁移到*"活动"*的权限外，代币的所有者还可以从活动和待处理数组中删除资产——还必须包括一个紧急功能，以清除待处理数组中的所有条目。

## 向后兼容性

多资产代币标准已与 [EIP-721](./eip-721.md) 兼容，以利用现有 ERC-721 实现的强大工具，并确保与现有 ERC-721 基础设施的兼容性。

## 测试用例

测试包含在 [`multiasset.ts`](../assets/eip-5773/test/multiasset.ts) 中。

要在终端中运行它们，可以使用以下命令：

```
cd ../assets/eip-5773
npm install
npx hardhat test
```

## 参考实现

请参见 [`MultiAssetToken.sol`](../assets/eip-5773/contracts/MultiAssetToken.sol)。

## 安全考虑

与 [EIP-721](./eip-721.md) 相同的安全考虑适用：任何函数中可能存在隐藏逻辑，包括销毁、添加资产、接受资产等。

处理未审计合同时请谨慎。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。