---
eip: 7720
title: 延迟代币转移
description: 允许用户为提取在指定未来时间的 ERC-20 代币转移进行调度，从而实现延迟支付。
author: 陈辽源 (@chenly) <cly@kip.pro>
discussions-to: https://ethereum-magicians.org/t/erc-7720-deferred-token-transfer/20245
status: 草案
type: 标准跟踪
category: ERC
created: 2024-06-09
requires: 20
---

## 摘要

本标准规定允许用户为受益人存入 [ERC-20](./erc-20.md) 代币。受益人只能在指定的未来时间戳之后提取代币。每笔存款交易都分配一个唯一的 ID，并包括代币地址、发送者、接收者、金额、解锁时间和提取状态等详细信息。

## 动机

在各种场景中，例如归属计划、托管服务或定时奖励，存在延迟支付的需求。该合约提供了一种安全可靠的时间锁定代币转移机制，确保代币只能在达到指定时间戳后转移。通过促进结构化和延迟支付，它为代币转移增加了一层额外的安全性和可预测性。这在支付依赖于时间的场景中特别有用。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

本标准的实现者 **MUST** 具备以下所有功能：

```solidity
pragma solidity ^0.8.0;

interface ITokenTransfer {
    // Event emitted when a transfer is initiated.
    event Transfer(
        uint256 txnId,
        address indexed token,
        address indexed from,
        address indexed to,
        uint256 amount,
        uint40 unlockTime,
        bytes32 referenceNo
    );

    // Event emitted when tokens are withdrawn.
    event Withdraw(
        uint256 txnId,
        address indexed token,
        address indexed from,
        address indexed to,
        uint256 amount
    );

    // Function to initiate a token transfer.
    // Parameters:
    // - _token: Address of the ERC20 token contract.
    // - _from: Address of the sender.
    // - _to: Address of the recipient.
    // - _amount: Amount of tokens to be transferred.
    // - _unlockTime: Time after which the tokens can be withdrawn.
    // - _reference: Reference ID for the transaction.
    // Returns the transaction ID.
    function transferFrom(
        address _token,
        address _from,
        address _to,
        uint256 _amount,
        uint40 _unlockTime,
        bytes32 _reference
    ) external returns (uint256 txnId);

    // Function to withdraw tokens from a transaction.
    // Parameters:
    // - _txnId: ID of the transaction to withdraw from.
    function withdraw(uint256 _txnId) external;

    // Function to get transaction details.
    // Parameters:
    // - _txnId: ID of the transaction.
    // Returns the transaction details.
    function getTransaction(uint256 _txnId)
        external
        view
        returns (
            address token,
            address from,
            address to,
            uint256 amount,
            uint40 unlockTime,
            bytes32 referenceNo,
            bool withdrawn
        );
}

```

## 理由

延迟代币转移合约的设计旨在提供一种简单安全的方法来处理时间锁定的代币转移。在开发过程中考虑了以下因素：

**使用 `uint40` 的解锁时间精度**：我们选择了完整的 `uint40` 作为 `_unlockTime`，因为它提供了足够大的范围以覆盖所有实际的时间锁定场景。这确保合约能够处理需要精确计时的延迟支付，例如归属计划或长期托管。

**从 `transferFrom` 返回 `txnId`**：`transferFrom` 函数为每笔交易返回一个唯一的 `txnId`。这一设计选择旨在便于对每笔交易进行简单和独立的跟踪。通过拥有唯一 ID，用户可以管理和引用特定交易，确保清晰并防止混淆。这种方法允许独立管理每笔交易的状态，从而简化提取过程。

**与现有 ERC-20 代币的兼容性**：该标准被设计为一个独立接口，而不是 ERC-20 的扩展，以确保灵活性和广泛兼容性。通过不直接修改 ERC-20 标准，该提案可以与任何现有的 ERC-20 代币一起使用，而无需更改其合约。这种灵活性使得该标准适用于已经流通的各种代币，增强了其实用性和采用潜力。

## 参考实现

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

contract TokenTransfer {
    using SafeERC20 for IERC20;

    struct Transaction {
        address token;      // Address of the ERC20 token contract.
        address from;       // Address of the sender.
        address to;         // Address of the recipient.
        uint256 amount;     // Amount of tokens to be transferred.
        uint40 unlockTime; // Time after which the tokens can be withdrawn.
        bytes32 referenceNo;  // Reference ID for the transaction.
        bool withdrawn;     // Flag indicating if the tokens have been withdrawn.
    }

    // Mapping from transaction ID to Transaction structure.
    mapping(uint256 => Transaction) public transactions;

    // Variable to keep track of the next transaction ID.
    uint256 public lastTxnId = 0;

    // Event emitted when a transfer is initiated.
    event Transfer(
        uint256 txnId,
        address indexed token,
        address indexed from,
        address indexed to,
        uint256 amount,
        uint40 unlockTime,
        bytes32 referenceNo
    );

    // Event emitted when tokens are withdrawn.
    event Withdraw(
        uint256 txnId,
        address indexed token,
        address indexed from,
        address indexed to,
        uint256 amount
    );

    constructor() {}

    // Function to initiate a token transfer.
    // Parameters:
    // - _token: Address of the ERC20 token contract.
    // - _from: Address of the sender.
    // - _to: Address of the recipient.
    // - _amount: Amount of tokens to be transferred.
    // - _unlockTime: Time after which the tokens can be withdrawn.
    // - _reference: Reference ID for the transaction.
    // Returns the transaction ID.
    function transferFrom(
        address _token,
        address _from,
        address _to,
        uint256 _amount,
        uint40 _unlockTime,
        bytes32 _reference
    ) external returns (uint256 txnId) {
        require(_amount > 0, "Invalid transfer amount");

        // Transfer tokens from sender to this contract.
        IERC20(_token).safeTransferFrom(_from, address(this), _amount);

        lastTxnId++;

        // Store the transaction details.
        transactions[lastTxnId] = Transaction({
            token: _token,
            from: _from,
            to: _to,
            amount: _amount,
            unlockTime: _unlockTime,
            referenceNo: _reference,
            withdrawn: false
        });

        // Emit an event for the transaction creation.
        emit Transfer(lastTxnId, _token, _from, _to, _amount, _unlockTime, _reference);
        return lastTxnId;
    }

    // Function to withdraw tokens from a transaction.
    // Parameters:
    // - _txnId: ID of the transaction to withdraw from.
    function withdraw(uint256 _txnId) external {
        Transaction storage transaction = transactions[_txnId];
        require(transaction.amount > 0, "Invalid transaction ID");
        require(block.timestamp >= transaction.unlockTime, "Current time is before unlock time");
        // require(transaction.to == msg.sender, "Only the recipient can withdraw the tokens");
        require(!transaction.withdrawn, "Tokens already withdrawn");

        IERC20(transaction.token).safeTransfer(transaction.to, transaction.amount);

        transaction.withdrawn = true;

        // Emit an event for the token withdrawal.
        emit Withdraw(_txnId, transaction.token, transaction.from, transaction.to, transaction.amount);
    }

    // Function to get transaction details.
    // Parameters:
    // - _txnId: ID of the transaction.
    // Returns the transaction details.
    function getTransaction(uint256 _txnId)
        external
        view
        returns (
            address token,
            address from,
            address to,
            uint256 amount,
            uint40 unlockTime,
            bytes32 referenceNo,
            bool withdrawn
        )
    {
        Transaction storage transaction = transactions[_txnId];
        require(transaction.amount > 0, "Invalid transaction ID");

        return (
            transaction.token,
            transaction.from,
            transaction.to,
            transaction.amount,
            transaction.unlockTime,
            transaction.referenceNo,
            transaction.withdrawn
        );
    }
}
```

## 安全考虑

**无所有者合约设计**：为了防止存款后代币丢失的风险，合约不应有所有者。这确保合约的代币余额不能转移到除指定受益人以外的任何地址。

**严格的受益人控制**：在提取过程中，合约必须严格确保代币仅转移给存款时指定的受益人。这防止了未经授权的访问，并确保只有预期的接收者可以提取代币。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。