---
eip: 3000
title: 乐观执行治理标准
author: Jorge Izquierdo (@izqui), Fabien Marino (@bonustrack)
discussions-to: https://github.com/ethereum/EIPs/issues/3042
status: 停滞
type: 标准跟踪
category: ERC
created: 2020-09-24
---

## 简单总结

用于调度、执行和挑战基于链下批准的合约执行的接口

## 摘要

ERC-3000 提出了一个基本的链上规范，用于合约乐观地执行链下做出的治理决策。

该标准在定义支持该标准的合约的 6 个入口函数时持有明确的观点。但它允许任何类型的解析机制用于乐观合约特有的挑战/响应游戏。

虽然作者目前认为使用主观预言机来解决挑战是正确的权衡，但该标准的设计使得更改为其他机制成为可能（例如 [Optimism 的 OVM](https://optimism.io) 使用的确定性解析器），甚至允许在同一实时实例中热插拔。

## 规范

### 数据结构

定义了一些数据结构，稍后将在标准接口中使用：

```solidity
library ERC3000Data {
    struct Container {
        Payload payload;
        Config config;
    }

    struct Payload {
        uint256 nonce;
        uint256 executionTime;
        address submitter;
        IERC3000Executor executor;
        Action[] actions;
        bytes proof;
    }

    struct Action {
        address to;
        uint256 value;
        bytes data;
    }

    struct Config {
        uint256 executionDelay;
        Collateral scheduleDeposit;
        Collateral challengeDeposit;
        Collateral vetoDeposit;
        address resolver;
        bytes rules;
    }

    struct Collateral {
        address token;
        uint256 amount;
    }
}
```

### 接口和事件

根据上述数据结构，通过利用 Solidity ABI 编码器 v2，我们定义了四个必需函数和两个可选函数作为合约遵循 ERC-3000 的接口。

所有标准函数在未满足前提条件或发生意外错误时都应回退（是否将错误消息/回退原因作为标准的一部分尚待确定）。成功时，每个函数必须发出其关联事件一次且仅一次。

```solidity
abstract contract IERC3000 {
    /**
     * @notice Schedules an action for execution, allowing for challenges and vetos on a defined time window
     * @param container A Container struct holding both the paylaod being scheduled for execution and
       the current configuration of the system
     */
    function schedule(ERC3000Data.Container memory container) virtual public returns (bytes32 containerHash);
    event Scheduled(bytes32 indexed containerHash, ERC3000Data.Payload payload, ERC3000Data.Collateral collateral);

    /**
     * @notice Executes an action after its execution delayed has passed and its state hasn't been altered by a challenge or veto
     * @param container A ERC3000Data.Container struct holding both the paylaod being scheduled for execution and
       the current configuration of the system
     * should be a MUST payload.executor.exec(payload.actions)
     */
    function execute(ERC3000Data.Container memory container) virtual public returns (bytes[] memory execResults);
    event Executed(bytes32 indexed containerHash, address indexed actor, bytes[] execResults);

    /**
     * @notice Challenge a container in case its scheduling is illegal as per Config.rules. Pulls collateral and dispute fees from sender into contract
     * @param container A ERC3000Data.Container struct holding both the paylaod being scheduled for execution and
       the current configuration of the system
     * @param reason Hint for case reviewers as to why the scheduled container is illegal
     */
    function challenge(ERC3000Data.Container memory container, bytes memory reason) virtual public returns (uint256 resolverId);
    event Challenged(bytes32 indexed containerHash, address indexed actor, bytes reason, uint256 resolverId, ERC3000Data.Collateral collateral);

    /**
     * @notice Apply arbitrator's ruling over a challenge once it has come to a final ruling
     * @param container A ERC3000Data.Container struct holding both the paylaod being scheduled for execution and
       the current configuration of the system
     * @param resolverId disputeId in the arbitrator in which the dispute over the container was created
     */
    function resolve(ERC3000Data.Container memory container, uint256 resolverId) virtual public returns (bytes[] memory execResults);
    event Resolved(bytes32 indexed containerHash, address indexed actor, bool approved);

    /**
     * @dev OPTIONAL
     * @notice Apply arbitrator's ruling over a challenge once it has come to a final ruling
     * @param payloadHash Hash of the payload being vetoed
     * @param config A ERC3000Data.Config struct holding the config attached to the payload being vetoed
     */
    function veto(bytes32 payloadHash, ERC3000Data.Config memory config, bytes memory reason) virtual public;
    event Vetoed(bytes32 indexed containerHash, address indexed actor, bytes reason, ERC3000Data.Collateral collateral);

    /**
     * @dev OPTIONAL: implementer might choose not to implement (initial Configured event MUST be emitted)
     * @notice Apply a new configuration for all *new* containers to be scheduled
     * @param config A ERC3000Data.Config struct holding all the new params that will control the queue
     */
    function configure(ERC3000Data.Config memory config) virtual public returns (bytes32 configHash);
    event Configured(bytes32 indexed containerHash, address indexed actor, ERC3000Data.Config config);
}
```

## 理由

作者认为，这一标准非常重要，因为它允许其他任何解析机制的实现和采用。

这就是为什么许多函数和变量名称被故意留为空白，以便与未来的解析器兼容，而无需更改标准。

ERC-3000 应被视为一个公共利益，公共基础设施将在其之上构建，这比任何特定的实现或特定公司或项目的利益更为重要。

## 安全考虑

该标准允许配置挑战的解析器，甚至可以为共存的调度有效负载设置不同的解析器。选择正确的解析器需要在安全性、最终确定时间、实现复杂性和外部依赖之间做出正确的权衡。

使用主观预言机作为解析器存在风险，因为安全性依赖于系统的加密经济属性。有关 Aragon Court 的加密经济考虑的分析，可以查看 [以下文档](https://github.com/aragon/aragon-court/tree/master/docs/3-cryptoeconomic-considerations)。

另一方面，实现确定性解析器由于其复杂性容易出现危险的错误，并且将依赖于特定版本的链下协议，而该协议可能在标准成熟和被采用的过程中迅速演变。

## 实现

### 1. Aragon Govern

- [ERC-3000 接口 (MIT 许可证)](https://github.com/aragon/govern/blob/master/packages/erc3k)
- [实现 (GPL-3.0 许可证)](https://github.com/aragon/govern/blob/master/packages/govern-core)

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。