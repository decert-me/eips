---
eip: 7417
title: 代币转换器
description: 将一种 ERC 版本的代币转换为另一种的智能合约服务
author: Dexaran (@Dexaran) <dexaran@ethereumclassic.org>
discussions-to: https://ethereum-magicians.org/t/token-standard-converter/15252
status: 草案
type: 标准跟踪
category: ERC
created: 2023-07-27
requires: 20, 165, 223
---

## 摘要

目前以太坊链上有多种代币标准。此 EIP 通过创建一个服务引入跨标准互操作性的概念，允许 [ERC-20](./erc-20.md) 代币随时升级为 [ERC-223](./eip-223.md) 代币。[ERC-223](./eip-223.md) 代币可以在没有任何限制的情况下转换回 [ERC-20](./erc-20.md) 版本，以避免与向后兼容性相关的问题，并允许不同标准共存、互操作和可互换。

为了执行转换，用户必须将一种标准的代币存入转换器合约，系统将自动发送另一种标准的代币回去。

## 动机

该提案引入了一种由专门的智能合约驱动的代币标准升级程序的概念，该程序可以随时将一种标准的代币转换为另一种标准的代币，并创建任何现有旧标准代币的替代版本。

目前，一些代币在不同链上以不同标准存在，例如大多数交易所支持 [ERC-20](./erc-20.md) USDT、TRX USDT、BEP-20 USDT，而所有这些代币实际上都是同一个 USDT 代币。该提案旨在引入一个概念，即在以太坊主网同时存在 [ERC-20](./erc-20.md) USDT 和 [ERC-223](./eip-223.md) USDT，并且两者可以共存。

必须在此处描述已部署的代币转换器的地址，以解决代币开发者的信任问题，并帮助他们找到与转换器交互的适当方式。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

代币转换器系统包括两个主要部分：

- 转换器合约

- 每个 [ERC-20](./erc-20.md) 代币的 [ERC-223](./eip-223.md) 包装合约

转换器合约可以为任何当前没有 [ERC-223](./eip-223.md) 包装的 [ERC-20](./erc-20.md) 代币部署新的 [ERC-223](./eip-223.md) 包装合约。每个 [ERC-20](./erc-20.md) 代币必须有且仅有一个 [ERC-223](./eip-223.md) 包装。

转换器合约必须接受 [ERC-20](./erc-20.md) 代币的存款，并以 1:1 的比例向存款人发送 [ERC-223](./eip-223.md) 代币。在存入 1234 单位的 `ERC-20 token_A` 时，存款人必须收到正好 1234 单位的 `ERC-223 token_A`。这是通过在 [ERC-20](./erc-20.md) 存款时发行新的 [ERC-223](./eip-223.md) 代币来完成的。原始的 [ERC-20](./erc-20.md) 代币必须在转换器合约中被冻结，并可供索回。

转换器合约必须接受 [ERC-223](./eip-223.md) 代币的存款，并以 1:1 的比例向存款人发送 [ERC-20](./erc-20.md) 代币。这是通过在 [ERC-223](./eip-223.md) 存款时释放原始的 [ERC-20](./erc-20.md) 代币来完成的。存入的 [ERC-223](./eip-223.md) 代币必须被销毁。

### 代币转换器

#### 转换合约方法

##### `getERC20WrapperFor`

```solidity
function getERC20WrapperFor(address _token) public view returns (address)
```

返回给定代币地址的 [ERC-20](./erc-20.md) 包装的地址。如果提供的代币地址没有 [ERC-20](./erc-20.md) 版本，则返回 `0x0`。每个给定的 [ERC-223](./eip-223.md) 代币地址只能由代币转换器合约创建一个包装。

##### `getERC223WrapperFor`

```solidity
function getERC223WrapperFor(address _token) public view returns (address)
```

返回给定代币地址的 [ERC-223](./eip-223.md) 包装的地址。如果提供的代币地址没有 [ERC-223](./eip-223.md) 版本，则返回 `0x0`。每个给定的 [ERC-20](./erc-20.md) 代币地址只能由代币转换器合约创建一个 [ERC-223](./eip-223.md) 包装。

##### `getERC20OriginFor`

```solidity
function getERC20OriginFor(address _erc223Token) public view returns (address)
```

返回提供的 [ERC-223](./eip-223.md) 包装的原始 [ERC-20](./erc-20.md) 代币的地址。如果提供的 `_erc223Token` 不是由代币转换器合约创建的任何 [ERC-223](./eip-223.md) 包装的地址，则返回 `0x0`。

##### `getERC223OriginFor`

```solidity
function getERC223OriginFor(address _erc20Token) public view returns (address)
```

返回提供的 [ERC-20](./erc-20.md) 包装的原始 [ERC-223](./eip-223.md) 代币的地址。如果提供的 `_erc20Token` 不是由代币转换器合约创建的任何包装的地址，则返回 `0x0`。

##### `predictWrapperAddress`

```solidity
function predictWrapperAddress(address _token,
                                   bool    _isERC20 // Is the provided _token a ERC-20 or not?
                                                    // If it is set as ERC-20 then we will predict the address of a 
                                                    // ERC-223 wrapper for that token.
                                                    // Otherwise we will predict ERC-20 wrapper address.
                                  ) view external returns (address)
```

包装合约是通过 `CREATE2` 操作码部署的，可以预测尚未部署的包装的地址。包装合约的地址取决于字节码，因此必须指定是预测包装 [ERC-20](./erc-20.md) 还是包装 [ERC-223](./eip-223.md)。

提供 `_token` 地址和 `_isERC20 = false` 将导致预测 [ERC-20](./erc-20.md) 包装地址。

提供 `_token` 地址和 `_isERC20 = true` 将导致预测 [ERC-223](./eip-223.md) 包装地址。

##### `createERC223Wrapper`

```solidity
function createERC223Wrapper(address _erc20Token) public returns (address)
```

为给定的 `_erc20Token` 创建一个新的 [ERC-223](./eip-223.md) 包装，如果尚不存在。若包装已存在，则回滚交易。成功时返回新包装代币合约的地址。如果 `_erc223Token` 是由转换器创建的包装，则回滚。

已部署的合约将是一个标准的 [ERC-223](./eip-223.md) 代币，具有为向后兼容性实现的 `approve` 和 `transferFrom` 函数。

所有由转换器部署的 [ERC-223](./eip-223.md) 包装将实现 `standard() pure returns (bytes32)` 函数，该函数返回 `223`。这为进一步的代币标准自省提供服务，因为在处理识别代币的 `transfer` 函数内部逻辑时， [ERC-165](./eip-165.md) 可能不可靠。

注意：此函数不验证 `_erc20Token` 的标准，因为没有可靠的自省方法可以保证提供的代币实现特定标准。因此，可以为原始的 [ERC-223](./eip-223.md) 代币创建 [ERC-223](./eip-223.md) 包装。

##### `createERC20Wrapper`

```solidity
function createERC20Wrapper(address _erc223Token) public returns (address)
```

为给定的 `_erc223Token` 创建一个新的 [ERC-20](./erc-20.md) 包装，如果尚不存在。若包装已存在，则回滚交易。成功时返回新包装代币合约的地址。如果 `_erc223Token` 是由转换器创建的包装，则回滚。

注意：此函数不验证 `_erc223Token` 的标准，因为没有可靠的自省方法可以保证提供的代币实现特定标准。因此，可以为原始的 [ERC-20](./erc-20.md) 代币创建 [ERC-20](./erc-20.md) 包装。

##### `wrapERC20toERC223`

```solidity
function wrapERC20toERC223(address _ERC20token, uint256 _amount) public returns (bool)
```
提取 `_amount` 的 [ERC-20](./erc-20.md) 代币通过 `transferFrom` 函数从交易发送者处。将 `_amount` 的 [ERC-223](./eip-223.md) 包装代币交付给交易的发送者。将原始代币存储在代币转换器合约的余额中以供将来索赔。成功时返回 `true`。代币转换器必须记录通过 `wrapERC20toERC223` 函数存入的 [ERC-20](./erc-20.md) 代币数量，因为可以通过直接使用 `transfer` 函数将 [ERC-20](./erc-20.md) 代币存入任何合约。

如果 `_ERC20token` 没有 [ERC-223](./eip-223.md) 包装，则通过调用 `createERC223Wrapper(_erc20toke)` 函数创建它。

没有特殊函数将 [ERC-223](./eip-223.md) 包装解包为 [ERC-20](./erc-20.md) 原始代币，因为此逻辑在转换器的 `tokenReceived` 函数中实现。

##### `unwrapERC20toERC223`

```solidity
function unwrapERC20toERC223(address _ERC20token, uint256 _amount) public returns (bool)
```

提取 `_amount` 的 [ERC-20](./erc-20.md) 代币通过 `transferFrom` 函数从交易发送者处。将 `_amount` 的 [ERC-223](./eip-223.md) 包装代币交付给交易的发送者。将原始代币存储在代币转换器合约的余额中以供将来索赔。成功时返回 `true`。代币转换器必须记录通过 `wrapERC20toERC223` 函数存入的 [ERC-20](./erc-20.md) 代币数量，因为可以通过直接使用 `transfer` 函数将 [ERC-20](./erc-20.md) 代币存入任何合约。

如果 `_ERC20token` 没有 [ERC-223](./eip-223.md) 包装，则通过调用 `createERC223Wrapper(_erc20toke)` 函数创建它。

##### `convertERC20`

```solidity
function convertERC20(address _token, uint256 _amount) public returns (bool)
```

自动确定提供的 [ERC-20](./erc-20.md) 代币是否为包装。如果是包装，则执行 `unwrapERC20toERC223` 函数。如果提供的代币是原始代币，则执行 `wrapERC20toERC223` 函数。

此函数的实现旨在显著简化集成同一代币的两个版本并需要通过转换器自动转换代币的服务的工作流程。

##### `isWrapper`

```solidity
function isWrapper(address _token) public view returns (bool)
```

如果提供的 `_token` 地址是由转换器创建的包装地址，则返回 `true`。

注意：此函数不识别 `_token` 的标准。每个由转换器创建的包装只能有一个原始代币。然而，原始代币可以有两个包装，每个标准一个。

##### `tokenReceived`

```solidity
function tokenReceived(address _from, uint _value, bytes memory _data) public override returns (bytes4)
```

这是一个标准的 [ERC-223](./eip-223.md) 交易处理函数，当 `_from` 向 `address(this)` 地址发送 `_value` 的 [ERC-223](./eip-223.md) 代币时，由 [ERC-223](./eip-223.md) 代币合约调用。在此函数的范围内，`msg.sender` 是 [ERC-223](./eip-223.md) 代币合约的地址，`_from` 是代币转移的发送者。

自动确定

如果 `msg.sender` 是由代币转换器创建的 [ERC-223](./eip-223.md) 包装的地址，则必须将 [ERC-20](./erc-20.md) 原始代币的 `_value` 发送到 `_from` 地址。

如果 `msg.sender` 不是代币转换器已知的任何 [ERC-223](./eip-223.md) 包装的地址，则被视为 [ERC-223](./eip-223.md) 原始代币，必须将 `_value` 数量的 [ERC-20](./erc-20.md) 包装代币发送到 `_from` 地址。如果 `msg.sender` 代币的 [ERC-20](./erc-20.md) 包装不存在，则先创建它。

返回 `0x8943ec02`。

##### `extractStuckERC20`

```solidity
function extractStuckERC20(address _token)
```

此函数允许提取直接通过 `transfer` 函数存入合约的 [ERC-20](./erc-20.md) 代币，以防止可能错误发送代币的用户永久丢失其代币。由于代币转换器计算通过 `convertERC20toERC223` 函数合法存入的代币数量，因此始终可以通过从在 [ERC-20](./erc-20.md) 代币合约上调用的 `balanceOf( address(this) )` 函数返回值中减去记录的数量来计算“意外存入代币”的数量。

### 将 [ERC-20](./erc-20.md) 代币转换为 [ERC-223](./eip-223.md)

为了将 [ERC-20](./erc-20.md) 代币转换为 [ERC-223](./eip-223.md)，代币持有者应：

1. 调用 [ERC-20](./erc-20.md) 代币的 `approve` 函数，允许代币转换器通过 `transferFrom` 函数从代币持有者地址提取代币。
2. 等待 `approve` 的交易提交到区块链。
3. 调用代币转换器合约的 `convertERC20toERC223` 函数。

### 将 [ERC-223](./eip-223.md) 代币转换回 [ERC-20](./erc-20.md)

为了将 [ERC-20](./erc-20.md) 代币转换为 [ERC-223](./eip-223.md)，代币持有者应：

1. 通过 [ERC-223](./eip-223.md) 代币合约的 `transfer` 函数将 [ERC-223](./eip-223.md) 代币发送到代币转换器合约的地址。

## 理由

由于以太坊已经建立了代币生态系统，而 [ERC-20](./erc-20.md) 是目前最被广泛采用的标准，缺乏明确的迁移流程可能会成为新标准采用的瓶颈。该提案解决了协调升级过程的问题，并解决了 [ERC-20](./erc-20.md) 和 [ERC-223](./eip-223.md) 代币的向后兼容性问题。

代币转换器旨在允许任何人创建现有代币的替代版本，该版本采用不同的标准。该提案专注于 [ERC-20](./erc-20.md) 和 [ERC-223](./eip-223.md) 标准，并考虑到这些特定代币标准的细节。假设最常见的情况是为现有的 [ERC-20](./erc-20.md) 代币创建 [ERC-223](./eip-223.md) 版本。

该服务的实施是说服每个代币开发者在代币部署时或项目开发阶段选择替代标准的替代方案。通过此服务，不再需要选择一个标准并坚持下去，因为每个代币都可以同时以两种形式存在。

该代币转换器服务的实施预计将是一次性在以太坊主网上部署的合约。其地址将在本提案的文本中提供，以避免任何潜在的信任问题，并确保开发者与之交互的服务正是驱动现有代币转换过程的服务。

所有由代币转换器创建的 [ERC-223](./eip-223.md) 代币在功能上是相同的，它们都实现相同的函数，返回相同的值，没有歧义。这有助于避免在代币标准采用早期阶段部署的代币可能不正确实现的情况，或者标准本身可能存在的歧义，允许开发者以不同方式实现同一标准的代币。

例如，在 [ERC-20](./erc-20.md) 中，开发者可以实现 `transfer` 函数的自定义逻辑并混淆返回值的情况很常见。[ERC-20](./erc-20.md) 规范声明 `transfer` 函数必须返回一个 `bool` 值，但实际上我们有三种不同类型的 [ERC-20](./erc-20.md) 代币，它们彼此不兼容：
1. [ERC-20](./erc-20.md) 代币在成功时返回 `true`，在出错时回退。
2. [ERC-20](./erc-20.md) 代币在成功时返回 `true`，在出错时返回 `false`，且不回退交易。
3. [ERC-20](./erc-20.md) 代币没有返回值，在出错时回退。

从技术上讲，第三类代币与 [ERC-20](./erc-20.md) 标准不兼容。然而，部署在以太坊主网地址 `0xdac17f958d2ee523a2206206994597c13d831ec7` 上的 USDT 代币并未实现返回值，它是使用最广泛的代币之一，因此由于其不当实现标准而拒绝支持 USDT 并不是一个选项。

代币转换器消除了不同开发团队可能以轻微修改实现标准的问题，从而导致在主网上出现同一标准的不同版本的情况。

同时，转换器使得在其他智能合约中支持并发代币成为可能，例如去中心化交易所。转换器可以保证一对代币，其中一个是另一个的包装器，实际上是可以随时从一个标准转换为另一个标准的同一代币。这使得创建流动性池成为可能，其中两个不同的代币被视为一个代币，而实际上这正是一个在不同标准下可用的代币。

## 向后兼容性

该提案旨在消除不同代币标准的向后兼容性问题，使它们可互换和可互操作。

该服务是首创，因此没有任何向后兼容性问题，因为它没有任何前身。

## 参考实现

```solidity

// SPDX-License-Identifier: GPL-3.0

pragma solidity =0.8.19;

library Address {
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }
}

interface IERC20 {
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

interface IERC20Metadata is IERC20 {
    /// @return The name of the token
    function name() external view returns (string memory);

    /// @return The symbol of the token
    function symbol() external view returns (string memory);

    /// @return The number of decimal places the token has
    function decimals() external view returns (uint8);
}

abstract contract IERC223Recipient {
    function tokenReceived(address _from, uint _value, bytes memory _data) public virtual returns (bytes4)
    {
        return 0x8943ec02;
    }
}

abstract contract ERC165 {
    /*
     * bytes4(keccak256('supportsInterface(bytes4)')) == 0x01ffc9a7
     */
    bytes4 private constant _INTERFACE_ID_ERC165 = 0x01ffc9a7;
    mapping(bytes4 => bool) private _supportedInterfaces;

    constructor () {
        // Derived contracts need only register support for their own interfaces,
        // we register support for ERC165 itself here
        _registerInterface(_INTERFACE_ID_ERC165);
    }
    function supportsInterface(bytes4 interfaceId) public view virtual returns (bool) {
        return _supportedInterfaces[interfaceId];
    }
    function _registerInterface(bytes4 interfaceId) internal virtual {
        require(interfaceId != 0xffffffff, "ERC165: invalid interface id");
        _supportedInterfaces[interfaceId] = true;
    }
}

abstract contract IERC223 {
    function name()        public view virtual returns (string memory);
    function symbol()      public view virtual returns (string memory);
    function decimals()    public view virtual returns (uint8);
    function totalSupply() public view virtual returns (uint256);
    function balanceOf(address who) public virtual view returns (uint);
    function transfer(address to, uint value) public virtual returns (bool success);
    function transfer(address to, uint value, bytes calldata data) public payable virtual returns (bool success);
    event Transfer(address indexed from, address indexed to, uint value, bytes data);
}

interface standardERC20
{
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address to, uint256 value) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 value) external returns (bool);
    function transferFrom(address from, address to, uint256 value) external returns (bool);
}

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC223WrapperToken {
    function name()     external view returns (string memory);
    function symbol()   external view returns (string memory);
    function decimals() external view returns (uint8);
    function standard() external view returns (string memory);
    function origin()   external  view returns (address);

    function totalSupply()                                            external view returns (uint256);
    function balanceOf(address account)                               external view returns (uint256);
    function transfer(address to, uint256 value)                      external payable returns (bool);
    function transfer(address to, uint256 value, bytes calldata data) external payable returns (bool);
    function allowance(address owner, address spender)                external view returns (uint256);
    function approve(address spender, uint256 value)                  external returns (bool);
    function transferFrom(address from, address to, uint256 value)    external returns (bool);

    function mint(address _recipient, uint256 _quantity) external;
    function burn(address _recipient, uint256 _quantity) external;
}

interface IERC20WrapperToken {
    function name()     external view returns (string memory);
    function symbol()   external view returns (string memory);
    function decimals() external view returns (uint8);
    function standard() external view returns (string memory);
    function origin()   external  view returns (address);

    function totalSupply()                                         external view returns (uint256);
    function balanceOf(address account)                            external view returns (uint256);
    function transfer(address to, uint256 value)                   external returns (bool);
    function allowance(address owner, address spender)             external view returns (uint256);
    function approve(address spender, uint256 value)               external returns (bool);
    function transferFrom(address from, address to, uint256 value) external returns (bool);

    function mint(address _recipient, uint256 _quantity) external;
    function burn(address _recipient, uint256 _quantity) external;
}

contract ERC20Rescue
{
    // ERC-20 tokens can get stuck on a contracts balance due to lack of error handling.
    //
    // The author of the ERC-7417 can extract ERC-20 tokens if they are mistakenly sent
    // to the wrapper-contracts balance.
    // Contact dexaran@ethereumclassic.org
    address public extractor = 0x01000B5fE61411C466b70631d7fF070187179Bbf;
    
    function safeTransfer(address token, address to, uint value) internal {
        // bytes4(keccak256(bytes('transfer(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0xa9059cbb, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FAILED');
    }

    function rescueERC20(address _token, uint256 _amount) external 
    {
        safeTransfer(_token, extractor, _amount);
    }
}

contract ERC223WrapperToken is IERC223, ERC165, ERC20Rescue
{
    address public creator = msg.sender;
    address private wrapper_for;

    mapping(address account => mapping(address spender => uint256)) private allowances;

    event Transfer(address indexed from, address indexed to, uint256 amount);
    event TransferData(bytes data);
    event Approval(address indexed owner, address indexed spender, uint256 amount);

    function set(address _wrapper_for) external
    {
        require(msg.sender == creator);
        wrapper_for = _wrapper_for;
    }

    uint256 private _totalSupply;

    mapping(address => uint256) private balances; // List of user balances.

    function totalSupply() public view override returns (uint256)             { return _totalSupply; }
    function balanceOf(address _owner) public view override returns (uint256) { return balances[_owner]; }


    /**
     * @dev The ERC165 introspection function.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return
            interfaceId == type(IERC20).interfaceId ||
            interfaceId == type(standardERC20).interfaceId ||
            interfaceId == type(IERC223WrapperToken).interfaceId ||
            interfaceId == type(IERC223).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev Standard ERC-223 transfer function.
     *      Calls _to if it is a contract. Does not transfer tokens to contracts
     *      which do not explicitly declare the tokenReceived function.
     * @param _to    - transfer recipient. Can be contract or EOA.
     * @param _value - the quantity of tokens to transfer.
     * @param _data  - metadata to send alongside the transaction. Can be used to encode subsequent calls in the recipient.
     */
    function transfer(address _to, uint _value, bytes calldata _data) public payable override returns (bool success)
    {
        balances[msg.sender] = balances[msg.sender] - _value;
        balances[_to] = balances[_to] + _value;
        if(Address.isContract(_to)) {
            IERC223Recipient(_to).tokenReceived(msg.sender, _value, _data);
        }
        if (msg.value > 0) payable(_to).transfer(msg.value);
        emit Transfer(msg.sender, _to, _value, _data);
        emit Transfer(msg.sender, _to, _value); // Old ERC-20 compatible event. Added for backwards compatibility reasons.

        return true;
    }

    /**
     * @dev Standard ERC-223 transfer function without _data parameter. It is supported for 
     *      backwards compatibility with ERC-20 services.
     *      Calls _to if it is a contract. Does not transfer tokens to contracts
     *      which do not explicitly declare the tokenReceived function.
     * @param _to    - transfer recipient. Can be contract or EOA.
     * @param _value - the quantity of tokens to transfer.
     */
    function transfer(address _to, uint _value) public override returns (bool success)
    {
        bytes memory _empty = hex"00000000";
        balances[msg.sender] = balances[msg.sender] - _value;
        balances[_to] = balances[_to] + _value;
        if(Address.isContract(_to)) {
            IERC223Recipient(_to).tokenReceived(msg.sender, _value, _empty);
        }
        emit Transfer(msg.sender, _to, _value, _empty);
        emit Transfer(msg.sender, _to, _value); // Old ERC-20 compatible event. Added for backwards compatibility reasons.

        return true;
    }

    function name() public view override returns (string memory)   { return IERC20Metadata(wrapper_for).name(); }
    function symbol() public view override returns (string memory) { return string.concat(IERC20Metadata(wrapper_for).name(), "223"); }
    function decimals() public view override returns (uint8)       { return IERC20Metadata(wrapper_for).decimals(); }
    function standard() public pure returns (uint32)               { return 223; }
    function origin() public view returns (address)                { return wrapper_for; }


    /**
     * @dev Minting function which will only be called by the converter contract.
     * @param _recipient - the address which will receive tokens.
     * @param _quantity  - the number of tokens to create.
     */
    function mint(address _recipient, uint256 _quantity) external
    {
        require(msg.sender == creator, "Wrapper Token: Only the creator contract can mint wrapper tokens.");
        balances[_recipient] += _quantity;
        _totalSupply += _quantity;
    }

    /**
     * @dev Burning function which will only be called by the converter contract.
     * @param _quantity  - the number of tokens to destroy. TokenConverter can only destroy tokens on it's own address.
     *                     Only the token converter is allowed to burn wrapper-tokens.
     */
    function burn(uint256 _quantity) external
    {
        require(msg.sender == creator, "Wrapper Token: Only the creator contract can destroy wrapper tokens.");
        balances[msg.sender] -= _quantity;
        _totalSupply -= _quantity;
    }

    // ERC-20 functions for backwards compatibility.

    function allowance(address owner, address spender) public view virtual returns (uint256) {
        return allowances[owner][spender];
    }

    function approve(address _spender, uint _value) public returns (bool) {

        // Safety checks.
        require(_spender != address(0), "ERC-223: Spender error.");

        allowances[msg.sender][_spender] = _value;
        emit Approval(msg.sender, _spender, _value);

        return true;
    }

    function transferFrom(address _from, address _to, uint _value) public returns (bool) {

        require(allowances[_from][msg.sender] >= _value, "ERC-223: Insufficient allowance.");

        balances[_from] -= _value;
        allowances[_from][msg.sender] -= _value;
        balances[_to] += _value;

        emit Transfer(_from, _to, _value);

        return true;
    }
}

contract ERC20WrapperToken is IERC20, ERC165, ERC20Rescue
{
    address public creator = msg.sender;
    address public wrapper_for;

    mapping(address account => mapping(address spender => uint256)) private allowances;

    function set(address _wrapper_for) external
    {
        require(msg.sender == creator);
        wrapper_for = _wrapper_for;
    }

    uint256 private _totalSupply;
    mapping(address => uint256) private balances; // List of user balances.


    function balanceOf(address _owner) public view override returns (uint256) { return balances[_owner]; }

    function name()        public view  returns (string memory) { return IERC20Metadata(wrapper_for).name(); }
    function symbol()      public view  returns (string memory) { return string.concat(IERC223(wrapper_for).name(), "20"); }
    function decimals()    public view  returns (uint8)         { return IERC20Metadata(wrapper_for).decimals(); }
    function totalSupply() public view override returns (uint256)       { return _totalSupply; }
    function origin()      public view returns (address)                { return wrapper_for; }

    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return
            interfaceId == type(IERC20).interfaceId ||
            interfaceId == type(IERC20WrapperToken).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    function transfer(address _to, uint _value) public override returns (bool success)
    {
        balances[msg.sender] = balances[msg.sender] - _value;
        balances[_to] = balances[_to] + _value;
        emit Transfer(msg.sender, _to, _value);
        return true;
    }

    function mint(address _recipient, uint256 _quantity) external
    {
        require(msg.sender == creator, "Wrapper Token: Only the creator contract can mint wrapper tokens.");
        balances[_recipient] += _quantity;
        _totalSupply += _quantity;
    }

    function burn(address _from, uint256 _quantity) external
    {
        require(msg.sender == creator, "Wrapper Token: Only the creator contract can destroy wrapper tokens.");
        balances[_from] -= _quantity;
        _totalSupply    -= _quantity;
    }

    function allowance(address owner, address spender) public view virtual returns (uint256) {
        return allowances[owner][spender];
    }

    function approve(address _spender, uint _value) public returns (bool) {

        // Safety checks.

        require(_spender != address(0), "ERC-20: Spender error.");

        allowances[msg.sender][_spender] = _value;
        emit Approval(msg.sender, _spender, _value);

        return true;
    }

    function transferFrom(address _from, address _to, uint _value) public returns (bool) {

        require(allowances[_from][msg.sender] >= _value, "ERC-20: Insufficient allowance.");

        balances[_from] -= _value;
        allowances[_from][msg.sender] -= _value;
        balances[_to] += _value;

        emit Transfer(_from, _to, _value);

        return true;
    }
}

contract TokenStandardConverter is IERC223Recipient
{
    event ERC223WrapperCreated(address indexed _token, address indexed _ERC223Wrapper);
    event ERC20WrapperCreated(address indexed _token, address indexed _ERC20Wrapper);

    mapping (address => ERC223WrapperToken) public erc223Wrappers; // A list of token wrappers. First one is ERC-20 origin, second one is ERC-223 version.
    mapping (address => ERC20WrapperToken)  public erc20Wrappers;

    mapping (address => address)            public erc223Origins;
    mapping (address => address)            public erc20Origins;
    mapping (address => uint256)            public erc20Supply; // Token => how much was deposited.

    function getERC20WrapperFor(address _token) public view returns (address)
    {
        return address(erc20Wrappers[_token]);
    }

    function getERC223WrapperFor(address _token) public view returns (address)
    {
        return address(erc223Wrappers[_token]);
    }

    function getERC20OriginFor(address _token) public view returns (address)
    {
        return (address(erc20Origins[_token]));
    }

    function getERC223OriginFor(address _token) public view returns (address)
    {
        return (address(erc223Origins[_token]));
    }

    function predictWrapperAddress(address _token,
                                   bool    _isERC20 // Is the provided _token a ERC-20 or not?
                                                    // If it is set as ERC-20 then we will predict the address of a 
                                                    // ERC-223 wrapper for that token.
                                                    // Otherwise we will predict ERC-20 wrapper address.
                                  ) view external returns (address)
    {
        bytes memory _bytecode;
        if(_isERC20)
        {
            _bytecode= type(ERC223WrapperToken).creationCode;
        }
        else
        {
            _bytecode= type(ERC20WrapperToken).creationCode;
        }

        bytes32 hash = keccak256(
            abi.encodePacked(
                bytes1(0xff), address(this), keccak256(abi.encode(_token)), keccak256(_bytecode)
          )
        );

        return address(uint160(uint(hash)));
    }

    function tokenReceived(address _from, uint _value, bytes memory /* _data */) public override returns (bytes4)
    {
        require(erc223Origins[msg.sender] == address(0), "Error: creating wrapper for a wrapper token.");
        // There are two possible cases:
        // 1. A user deposited ERC-223 origin token to convert it to ERC-20 wrapper
        // 2. A user deposited ERC-223 wrapper token to unwrap it to ERC-20 origin.

        if(erc20Origins[msg.sender] != address(0))
        {
            // Origin for deposited token exists.
            // Unwrap ERC-223 wrapper.

            erc20Supply[erc20Origins[msg.sender]] -= _value;
            safeTransfer(erc20Origins[msg.sender], _from, _value);

            ERC223WrapperToken(msg.sender).burn(_value);

            return this.tokenReceived.selector;
        }
        // Otherwise origin for the sender token doesn't exist
        // There are two possible cases:
        // 1. ERC-20 wrapper for the deposited token exists
        // 2. ERC-20 wrapper for the deposited token doesn't exist and must be created.
        else if(address(erc20Wrappers[msg.sender]) == address(0))
        {
            // Create ERC-20 wrapper if it doesn't exist.
            createERC20Wrapper(msg.sender);
        }

        // Mint ERC-20 wrapper tokens for the deposited ERC-223 token
        // if the ERC-20 wrapper didn't exist then it was just created in the above statement.
        erc20Wrappers[msg.sender].mint(_from, _value);
        return this.tokenReceived.selector;
    }

    function createERC223Wrapper(address _token) public returns (address)
    {
        require(address(erc223Wrappers[_token]) == address(0), "ERROR: Wrapper exists");
        require(!isWrapper(_token), "Error: Creating wrapper for a wrapper token");
        
        ERC223WrapperToken _newERC223Wrapper     = new ERC223WrapperToken{salt: keccak256(abi.encode(_token))}();
        _newERC223Wrapper.set(_token);
        erc223Wrappers[_token]                   = _newERC223Wrapper;
        erc20Origins[address(_newERC223Wrapper)] = _token;

        emit ERC223WrapperCreated(_token, address(_newERC223Wrapper));
        return address(_newERC223Wrapper);
    }

    function createERC20Wrapper(address _token) public returns (address)
    {
        require(address(erc20Wrappers[_token]) == address(0), "ERROR: Wrapper already exists.");
        require(!isWrapper(_token), "Error: Creating wrapper for a wrapper token");

        ERC20WrapperToken _newERC20Wrapper       = new ERC20WrapperToken{salt: keccak256(abi.encode(_token))}();
        _newERC20Wrapper.set(_token);
        erc20Wrappers[_token]                    = _newERC20Wrapper;
        erc223Origins[address(_newERC20Wrapper)] = _token;

        emit ERC20WrapperCreated(_token, address(_newERC20Wrapper));
        return address(_newERC20Wrapper);
    }

    function wrapERC20toERC223(address _ERC20token, uint256 _amount) public returns (bool)
    {
        // If there is no active wrapper for a token that user wants to wrap
        // then create it.
        if(address(erc223Wrappers[_ERC20token]) == address(0))
        {
            createERC223Wrapper(_ERC20token);
        }
        uint256 _converterBalance = IERC20(_ERC20token).balanceOf(address(this)); // Safety variable.
        safeTransferFrom(_ERC20token, msg.sender, address(this), _amount);

        _amount = IERC20(_ERC20token).balanceOf(address(this)) - _converterBalance;
        erc20Supply[_ERC20token] += _amount;

        erc223Wrappers[_ERC20token].mint(msg.sender, _amount);

        return true;
    }

    function unwrapERC20toERC223(address _ERC20token, uint256 _amount) public returns (bool)
    {
        require(IERC20(_ERC20token).balanceOf(msg.sender) >= _amount, "Error: Insufficient balance.");
        require(erc223Origins[_ERC20token] != address(0), "Error: provided token is not a ERC-20 wrapper.");

        ERC20WrapperToken(_ERC20token).burn(msg.sender, _amount);

        safeTransfer(erc223Origins[_ERC20token], msg.sender, _amount);

        return true;
    }

    function convertERC20(address _token, uint256 _amount) public returns (bool)
    {
        if(isWrapper(_token)) return unwrapERC20toERC223(_token, _amount);
        else return wrapERC20toERC223(_token, _amount);
    }

    function isWrapper(address _token) public view returns (bool)
    {
        return erc20Origins[_token] != address(0) || erc223Origins[_token] != address(0);
    }

    function extractStuckERC20(address _token) external 
    {
        require(msg.sender == address(0x01000B5fE61411C466b70631d7fF070187179Bbf));

        safeTransfer(_token, address(0x01000B5fE61411C466b70631d7fF070187179Bbf), IERC20(_token).balanceOf(address(this)) - erc20Supply[_token]);
    }
    
    function safeTransfer(address token, address to, uint value) internal {
        // bytes4(keccak256(bytes('transfer(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0xa9059cbb, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FAILED');
    }

    function safeTransferFrom(address token, address from, address to, uint value) internal {
        // bytes4(keccak256(bytes('transferFrom(address,address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x23b872dd, from, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FROM_FAILED');
    }
}
```

## 安全考虑

1. 虽然可以实现将任何代币标准转换为任何其他标准的服务，但最好将不同标准的转换器分开，因为不同标准可能包含特定逻辑，因此需要不同的转换方法。该提案专注于 [ERC-20](./erc-20.md) 和 [ERC-223](./eip-223.md) 的可升级性。
2. [ERC-20](./erc-20.md) 代币可以直接通过 `transfer` 函数存入任何合约。这可能导致代币的永久损失，因为无法在接收方识别此交易。因此，包装的 ERC-20 代币容易出现此问题，因为它们与 [ERC-20](./erc-20.md) 标准兼容。已实现 `rescueERC20` 函数以解决此问题。
3. 代币转换器依赖于 [ERC-20](./erc-20.md) 的 `approve` 和 `transferFrom` 方法来存入资产。任何相关问题都必须考虑在内。`approve` 和 `transferFrom` 是两个独立的交易，因此必须确保 `approval` 成功后再依赖于 `transferFrom`。
4. 对于 UI 服务，提示用户在任何可能从用户那里提取代币的合约上发出无限制的 `approval` 是一种常见做法。这使用户的资金面临风险，因此不推荐这样做。
5. 没有可靠的代币标准自省方法可以保证某个代币实现特定的代币标准。可以人为构造一个代币，使其假装是实现 `approve & transferFrom` 的 [ERC-20](./erc-20.md) 代币，但同时实现通过 `transfer` 函数转移的 [ERC-223](./eip-223.md) 逻辑。在代币转换器中，可以为这个 [ERC-20](./erc-20.md)-[ERC-223](./eip-223.md) 混合实现创建一个 [ERC-223](./eip-223.md) 包装器。这对代币转换器的工作流程没有威胁，但必须考虑到，如果某个代币在代币转换器中有 [ERC-223](./eip-223.md) 包装器，并不意味着其来源完全兼容 [ERC-20](./erc-20.md) 标准，必须使用自省方法来确定其来源与任何现有标准的兼容性。
6. 代币转换器在被要求为提供的代币创建包装器时不会验证代币的标准，因为缺乏可靠的标准自省方法。可以调用 `createERC20Wrapper` 函数并提供现有 [ERC-20](./erc-20.md) 代币的地址。代币转换器将成功为该 [ERC-20](./erc-20.md) 原始代币创建一个 [ERC-20](./erc-20.md) 包装器。也可以为该原始 [ERC-20](./erc-20.md) 代币创建一个 [ERC-223](./eip-223.md) 包装器。这对转换器的工作流程没有威胁，但必须考虑到，任何代币无论其原始标准如何，可能会由转换器创建最多两个包装器，每个标准一个。任何包装代币必须有且仅有一个来源。无法为包装器创建包装器。
7. 代币转换器仅持有在转换过程中存入的原始代币，并假设代币不会随时间衰减，且转换器的代币余额不会自行减少。如果某个代币实现了销毁逻辑或衰减供应，并可能影响转换器的余额，则不应使用转换器来部署该代币的替代版本，因为它无法保证在任何时候都有足够的代币进行转换。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。