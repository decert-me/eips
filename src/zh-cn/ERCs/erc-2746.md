---
eip: 2746
title: 规则引擎标准
author: Aaron Kendall (@jaerith), Juan Blanco (@juanfranblanco)
discussions-to: https://ethereum-magicians.org/t/eip-2746-rules-engine-interface/4435
status: 停滞
type: 标准跟踪
category: ERC
created: 2020-06-20
---

## 简要总结
一个将智能合约用作规则引擎的接口。一个已部署的合约可以注册一个数据域，创建在该域上执行操作的规则集，然后将其作为原子事务调用。

## 摘要
该标准提出了一个接口，允许创建层次化的规则集（即规则树），可以调用以评估和操作注册的数据域。在本草案撰写时，所有意图在区块链上插入额外功能的操作都需要编码并创建一个新部署的合约。然而，该标准将允许用户仅部署一次合约，从而允许他们在该合约内创建（和调用）命令管道。

## 动机
在本草案撰写时，所有以太坊的开发都需要编写形成智能合约的代码，然后将这些合约部署到以太坊。为了创建一个合适的合约，在设计和实现代码时必须考虑许多因素，特别是在效率（即 gas 成本）和安全性方面。即使是最简单的合约，在部署前后也需要一定程度的警惕和检查。这些要求适用于所有情况，即使是检查一个值和/或更改它的简单情况。

这些技术挑战可能会对许多希望围绕以太坊创建软件的人形成障碍。技术水平较低的公司和用户也可能希望在链上配置和部署简单的功能，而无需了解相关的语言或必要的细节。通过将数据域和预定义的操作（即规则类型）与此接口一起实现，已部署的规则引擎合约实例可以为无代码或少代码的客户提供高效和安全的功能，使各种技术水平的用户能够与以太坊生态系统进行交互。

## 规范
为了澄清术语，属性是数据域内注册的数据点，表示存在于规则引擎合约或其他地方的数据。规则是发生在预定义数据域内单个数据点（即属性）上的预定义操作。例如，规则可以检查属性 'TokenAmt' 的值是否小于 RHL（即右侧值）10。规则集是规则的集合，其集合调用创建一个布尔结果，决定规则集之间的执行流。规则树是组织在层次结构中的规则集的集合，其中规则集可以包含其他规则集。

```solidity
pragma solidity ^0.6.0;

/**
    @title ERC-2746 Rules Engine Standard
    @dev See https://eips.ethereum.org/EIPS/eip-2746
 */
 interface ERCRulesEngine {

    /**
        @dev Should emit when a RuleTree is invoked.
        The `ruler` is the ID and owner of the RuleTree being invoked.  It is also likely msg.sender.
    */
    event CallRuleTree(
        address indexed ruler
    );

    /**
        @dev Should emit when a RuleSet is invoked.
        The `ruler` is the ID and owner of the RuleTree in which the RuleSet is stored.  It is also likely msg.sender.
        The 'ruleSetId' is the ID of the RuleSet being invoked.
    */
    event CallRuleSet(
        address indexed ruler,
        bytes32 indexed tmpRuleSetId
    );

    /**
        @dev Should emit when a Rule is invoked.
        The `ruler` is the ID and owner of the RuleTree in which the RuleSet is stored.  It is also likely msg.sender.
        The 'ruleSetId' is the ID of the RuleSet being invoked.
        The 'ruleId' is the ID of the Rule being invoked.
        The 'ruleType' is the type of the rule being invoked.        
    */
    event CallRule(
        address indexed ruler,
        bytes32 indexed ruleSetId,
        bytes32 indexed ruleId,
        uint ruleType
    );

    /**
        @dev Should emit when a RuleSet fails.
        The `ruler` is the ID and owner of the RuleTree in which the RuleSet is stored.  It is also likely msg.sender.
        The 'ruleSetId' is the ID of the RuleSet being invoked.
        The 'severeFailure' is the indicator of whether or not the RuleSet is a leaf with a 'severe' error flag.
    */
    event RuleSetError (
        address indexed ruler,
        bytes32 indexed ruleSetId,
        bool severeFailure
    );	

    /**
        @notice Adds a new Attribute to the data domain.
        @dev Caller should be the deployer/owner of the rules engine contract.  An Attribute value can be an optional alternative if it's not a string or numeric.
        @param _attrName    Name/ID of the Attribute
        @param _maxLen      Maximum length of the Attribute (if it is a string)
        @param _maxNumVal   Maximum numeric value of the Attribute (if it is numeric)
        @param _defaultVal  The default value for the Attribute (if one is not found from the source)
        @param _isString    Indicator of whether or not the Attribute is a string
        @param _isNumeric   Indicator of whether or not the Attribute is numeric
    */    
    function addAttribute(bytes32 _attrName, uint _maxLen, uint _maxNumVal, string calldata _defaultVal, bool _isString, bool _isNumeric) external;

    /**
        @notice Adds a new RuleTree.
        @param _owner          Owner/ID of the RuleTree
        @param _ruleTreeName   Name of the RuleTree
        @param _desc           Verbose description of the RuleTree's purpose
    */
    function addRuleTree(address _owner, bytes32 _ruleTreeName, string calldata _desc) external;

    /**
        @notice Adds a new RuleSet onto the hierarchy of a RuleTree.
        @dev RuleSets can have child RuleSets, but they will only be called if the parent's Rules execute to create boolean 'true'.
        @param _owner           Owner/ID of the RuleTree
        @param _ruleSetName     ID/Name of the RuleSet
        @param _desc            Verbose description of the RuleSet
        @param _parentRSName    ID/Name of the parent RuleSet, to which this will be added as a child
        @param _severalFailFlag Indicator of whether or not the RuleSet's execution (as failure) will result in a failure of the RuleTree.  (This flag only applies to leaves in the RuleTree.)
        @param _useAndOp        Indicator of whether or not the rules in the RuleSet will execute with 'AND' between them.  (Otherwise, it will be 'OR'.)
        @param _failQuickFlag   Indicator of whether or not the RuleSet's execution (as failure) should immediately stop the RuleTree.
    */    
    function addRuleSet(address _owner, bytes32 _ruleSetName, string calldata _desc, bytes32 _parentRSName, bool _severalFailFlag, bool _useAndOp, bool _failQuickFlag) external;

    /**
        @notice Adds a new Rule into a RuleSet.
        @dev Rule types can be implemented as any type of action (greater than, less than, etc.)
        @param _owner           Owner/ID of the RuleTree
        @param _ruleSetName     ID/Name of the RuleSet to which the Rule will be added
        @param _ruleName        ID/Name of the Rule being added
        @param _attrName        ID/Name of the Attribute upon which the Rule is invoked
        @param _ruleType        ID of the type of Rule
        @param _rightHandValue  The registered value to be used by the Rule when performing its action upon the Attribute
        @param _notFlag         Indicator of whether or not the NOT operator should be performed on this Rule.
    */    
    function addRule(address _owner, bytes32 _ruleSetName, bytes32 _ruleName, bytes32 _attrName, uint _ruleType, string calldata _rightHandValue, bool _notFlag) external;

    /**
        @notice Executes a RuleTree.
        @param _owner           Owner/ID of the RuleTree
    */
    function executeRuleTree(address _owner) external returns (bool);
    
    /**
        @notice Retrieves the properties of a Rule.
        @param _owner           Owner/ID of the RuleTree
        @param _ruleSetName     ID/Name of the RuleSet where the Rule resides
        @param _ruleIdx         Index of the rule in the RuleSet's listing 
        @return bytes32         ID/Name of Rule
        @return uint            Type of Rule
        @return bytes32         Target Attribute of Rule
        @return string          Value mentioned in Rule
        @return bool            Flag for NOT operator in Rule
        @return bytes32[]       Values that should be provided in delegated call (if Rule is custom operator)
    */
    function getRuleProps(address _owner, bytes32 _ruleSetName, uint _ruleIdx) external returns (bytes32, uint, bytes32, string memory, bool, bytes32[] memory);

    /**
        @notice Retrieves the properties of a RuleSet
        @param _owner        Owner/ID of the RuleTree
        @param _ruleSetName  ID/Name of the RuleSet
        @return string       Verbose description of the RuleSet
        @return bool         Flag that indicates whether this RuleSet's failure (if a leaf) will cause the RuleTree to fail
        @return bool         Flag that indicates whether this RuleSet uses the AND operator when executing rules collectively
        @return uint         Indicates the number of rules hosted by this RuleSet
        @return bytes32[]    The list of RuleSets that are children of this RuleSet
    */
    function getRuleSetProps(address _owner, bytes32 _ruleSetName) external returns (string memory, bool, bool, uint, uint, bytes32[] memory);

    /**
        @notice Retrieves the properties of a RuleSet
        @param _owner        Owner/ID of the RuleTree
        @return bytes32      Name of the RuleTree
        @return string       Verbose description of the RuleTree
        @return bytes32      ID/Name of the RuleSet that serves as the root node for the RuleTree
    */
    function getRuleTreeProps(address _owner) external returns (bytes32, string memory, bytes32);
    
    /**
        @notice Removes a RuleTree.
        @param _owner           Owner/ID of the RuleTree
    */
    function removeRuleTree(address _owner) external returns (bool);    
}
```

### 考虑事项

可以提出一个论点，支持接口函数允许规则树的所有者将其他用户包括为规则树的执行者。

另一个论点可以支持接口函数，允许管理员配置属性的来源点，例如属性的值是否来自数据结构（内部于规则引擎合约）或通过调用合约方法（如 [Diamond Standard](https://github.com/ethereum/EIPs/issues/2535) 的实现）。

还有一个论点可以支持接口函数，允许管理员扩展规则引擎提供的功能目录，通过允许其他合约的方法作为规则操作添加。

此外，可以提出一个论点，支持计算和报告调用规则树的潜在成本范围的函数。与正常执行合约方法不同，调用规则树的以太坊交易成本更具动态性，取决于其深度/广度和调用期间的执行流。由于规则树的一般成本在调用时未知，这些函数可以报告交易的最小 gas 量（即规则树中的规则未被调用）和交易的最大 gas 量（即规则树中的所有规则都被调用）。

### 示例

一家公司希望部署一个具有预定义数据点和功能的合约，并且这些功能在管理员的控制之下，旨在构建一个无代码客户端，使技术水平较低的用户能够在规则引擎合约中定义操作。在这个例子中，该公司希望其用户用专有标记语言编写规则，以便计算增值税。为了透明起见，[这些规则](https://ipfs.infura.io/ipfs/QmPrZ9959c7SzzqdLkVgX28xM7ZrqLeT3ydvRAHCaL1Hsn) 被发布到 IPFS，以便审计员和可能的政府官员可以访问。无代码客户端将知道如何解析标记中的规则并与规则引擎合约进行通信，建立规则树，以便稍后由公司的用户或链外程序调用。

为了计算增值税的值，这些提供的规则调用简单的数学运算来执行计算。然而，规则引擎合约的实现可能具有其他由规则调用的功能，这些功能可以执行更复杂的逻辑或调用其他合约的方法。

## 理由

### 属性

数据点被抽象化，以便让实现提供检索/填充数据的机制。数据可以由内部数据结构、其他合约的方法或其他多种选项持有。

### 事件

指定的事件将帮助规则树的调用者在执行后，以便他们可以确定规则集在规则树内的执行流，并了解哪些规则集失败。

### 右侧值

在函数 addRule() 中，右侧值的数据类型为 'string'，因为规则的操作取决于其类型，这意味着值必须以通用形式提供。在执行数值操作的规则的情况下，提供的值可以在存储到规则时转换为数字。

## 实现
- [Wonka](https://github.com/Nethereum/Wonka/tree/master/Solidity/WonkaEngine)
- [Wonka 规则编辑器](https://github.com/jaerith/WonkaRulesBlazorEditor)

Wonka 实现支持该提议的接口，并实现了上述所有额外考虑事项。

## 安全考虑

合约的部署者应为所有者和管理员，允许添加属性和规则树。由于规则树由特定的 EOA（或合约地址）拥有，唯一能够执行规则树的账户应为其所有者或合约的所有者/管理员。如果属性被定义为存在于其他合约中的数据，则实现必须考虑规则树所有者必须具备访问这些合约中数据的安全性。

## 参考

**标准**
- [EIP-2535 Diamond Standard](./eip-2535.md)

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。