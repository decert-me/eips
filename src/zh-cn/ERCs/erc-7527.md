---
eip: 7527
title: 代币绑定函数预言机 AMM
description: 基于嵌入的函数预言机 AMM 的接口，用于将 FT 包装为 NFT 并将 NFT 解包为 FT
author: Elaine Zhang (@lanyinzly) <lz8aj@virginia.edu>, Jerry <jerrymindflow@gmail.com>, Amandafanny <amandafanny200@gmail.com>, Shouhao Wong (@wangshouh) <wongshouhao@outlook.com>, 0xPoet <0xpoets@gmail.com>
discussions-to: https://ethereum-magicians.org/t/eip-7527-token-bound-function-oracle-amm-contract/15950
status: Draft
type: Standards Track
category: ERC
created: 2023-09-03
requires: 165, 721
---


## 摘要

本提案概述了将 [ERC-20](./eip-20.md) 或 ETH 包装为 [ERC-721](./eip-721.md) 的接口，并将 ERC-721 解包为 ERC-20 或 ETH。一个函数预言机根据嵌入的函数预言机自动化做市商（FOAMM）的方程提供铸造/销毁价格，执行和清算 NFT 的铸造和销毁。

## 动机

流动性在去中心化系统中可能是一个重大挑战，尤其是对于独特或不常交易的代币，如 NFT。为了促进一个无信任的 NFT 生态系统，函数预言机自动化做市商（FOAMM）的动机是通过透明的智能合约机制为 NFT 提供自动定价解决方案和流动性。

该 ERC 为以下方面提供了创新解决方案：

- 自动价格发现
- 流动性增强

### 自动价格发现

在 FOAMM 下的交易可以在不需要匹配对手方的情况下进行。当直接与池交互时，FOAMM 会根据预定义的函数自动提供价格。

### 流动性增强

在传统的 DEX 模型中，流动性由外部方提供，称为流动性提供者（LP）。这些 LP 将代币存入流动性池，通过提供流动性来促进交易。这些 LP 的移除或撤回可能会引入显著的波动性，因为这直接影响市场上的可用流动性。

在 FOAMM 系统中，流动性通过 `wrap` 或 `unwrap` 内部添加或移除。FOAMM 减少了对外部 LP 的依赖，并减轻了因其突然撤回而导致的波动风险，因为流动性通过持续的参与者交互不断补充和维护。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

合约接口：

这里包含三个接口：`Agency`、`App` 和 `Factory`。

`Agency` 和 `App` 可以由同一个合约实现，也可以分别实现。如果分别实现，它们必须是相互绑定的，并且在初始化后不可升级。

`Agency` 和 `App` 应实现 `iconstructor` 接口，以初始化合约内的参数并验证配置参数。如果使用工厂来部署 `Agency` 和 `App`，工厂将在部署时自动调用这两个函数。

`App` 必须实现 `onlyAgency()` 修饰符，`mint` 和 `burn` 必须应用 `onlyAgency()` 作为修饰符，这限制了对 `Mint` 和 `Burn` 的调用，仅在通过相应的 `Agency` 调用时才有效。

`Agency` 可选择实现 `onlyApp()`。

`Factory` 接口是可选的。如果 `Agency` 和 `App` 需要重复部署，则此接口最为有用。

函数预言机通过 `getWrapOracle` 和 `getUnwrapOracle` 实现，根据参数和函数中定义的数学方程提供价格。

FOAMM 通过 `wrap` 和 `unwrap` 实现，调用 `getWrapOracle` 和 `getUnwrapOracle` 获取数据并自动清算。要执行 `wrap`，FOAMM 接收溢价并在 `App` 中启动 `mint`。要执行 `unwrap`，FOAMM 转移溢价并在 `App` 中启动 `burn`。

`Agency` 作为所有 `mint` 和 `burn` 转移的单一入口。

### Agency 接口

```
pragma solidity ^0.8.20;

/**
 * @dev The settings of the agency.
 * @param currency The address of the currency. If `currency` is 0, the currency is Ether.
 * @param basePremium The base premium of the currency.
 * @param feeRecipient The address of the fee recipient.
 * @param mintFeePercent The fee of minting.
 * @param burnFeePercent The fee of burning.
 */

struct Asset {
    address currency;
    uint256 basePremium;
    address feeRecipient;
    uint16 mintFeePercent;
    uint16 burnFeePercent;
}

interface IERC7527Agency {
    /**
     * @dev Allows the account to receive Ether
     *
     * Accounts MUST implement a `receive` function.
     *
     * Accounts MAY perform arbitrary logic to restrict conditions
     * under which Ether can be received.
     */
    receive() external payable;

    /**
     * @dev Emitted when `tokenId` token is wrapped.
     * @param to The address of the recipient of the newly created non-fungible token.
     * @param tokenId The identifier of the newly created non-fungible token.
     * @param premium The premium of wrapping.
     * @param fee The fee of wrapping.
     */
    event Wrap(address indexed to, uint256 indexed tokenId, uint256 premium, uint256 fee);

    /**
     * @dev Emitted when `tokenId` token is unwrapped.
     * @param to The address of the recipient of the currency.
     * @param tokenId The identifier of the non-fungible token to unwrap.
     * @param premium The premium of unwrapping.
     * @param fee The fee of unwrapping.
     */
    event Unwrap(address indexed to, uint256 indexed tokenId, uint256 premium, uint256 fee);

    /**
     * @dev Constructor of the instance contract.
     */
    function iconstructor() external;

    /**
     * @dev Wrap some amount of currency into a non-fungible token.
     * @param to The address of the recipient of the newly created non-fungible token.
     * @param data The data to encode into ifself and the newly created non-fungible token.
     * @return The identifier of the newly created non-fungible token.
     */
    function wrap(address to, bytes calldata data) external payable returns (uint256);

    /**
     * @dev Unwrap a non-fungible token into some amount of currency.
     *
     * Todo: event
     *
     * @param to The address of the recipient of the currency.
     * @param tokenId The identifier of the non-fungible token to unwrap.
     * @param data The data to encode into ifself and the non-fungible token with identifier `tokenId`.
     */
    function unwrap(address to, uint256 tokenId, bytes calldata data) external payable;

    /**
     * @dev Returns the strategy of the agency.
     * @return app The address of the app.
     * @return asset The asset of the agency.
     * @return attributeData The attributeData of the agency.
     */
    function getStrategy() external view returns (address app, Asset memory asset, bytes memory attributeData);

    /**
     * @dev Returns the premium and fee of wrapping.
     * @param data The data to encode to calculate the premium and fee of wrapping.
     * @return premium The premium of wrapping.
     * @return fee The fee of wrapping.
     */
    function getWrapOracle(bytes memory data) external view returns (uint256 premium, uint256 fee);

    /**
     * @dev Returns the premium and fee of unwrapping.
     * @param data The data to encode to calculate the premium and fee of unwrapping.
     * @return premium The premium of wrapping.
     * @return fee The fee of wrapping.
     */
    function getUnwrapOracle(bytes memory data) external view returns (uint256 premium, uint256 fee);

    /**
     * @dev OPTIONAL - This method can be used to improve usability and clarity of Agency, but interfaces and other contracts MUST NOT expect these values to be present.
     * @return the description of the agency, such as how `getWrapOracle()` and `getUnwrapOracle()` are calculated.
     */
    function description() public view returns (string);
}
```

### App 接口

`ERC7527App` 必须从接口 `ERC721Metadata` 继承 `name`。

```
pragma solidity ^0.8.20;

interface IERC7527App {
    /**
     * @dev Returns the maximum supply of the non-fungible token.
     */
    function getMaxSupply() external view returns (uint256);

    /**
     * @dev Returns the name of the non-fungible token with identifier `id`.
     * @param id The identifier of the non-fungible token.
     */
    function getName(uint256 id) external view returns (string memory);

    /**
     * @dev Returns the agency of the non-fungible token.
     */
    function getAgency() external view returns (address payable);

    /**
     * @dev Constructor of the instance contract.
     */
    function iconstructor() external;

    /**
     * @dev Sets the agency of the non-fungible token.
     * @param agency The agency of the non-fungible token.
     */
    function setAgency(address payable agency) external;

    /**
     * @dev Mints a non-fungible token to `to`.
     * @param to The address of the recipient of the newly created non-fungible token.
     * @param data The data to encode into the newly created non-fungible token.
     */
    function mint(address to, bytes calldata data) external returns (uint256);

    /**
     * @dev Burns a non-fungible token with identifier `tokenId`.
     * @param tokenId The identifier of the non-fungible token to burn.
     * @param data The data to encode into the non-fungible token with identifier `tokenId`.
     */
    function burn(uint256 tokenId, bytes calldata data) external;
}
```

代币 ID 可以在 `mint` 函数的 `data` 参数中指定。

### Factory 接口

可选 - 此接口可用于部署 App 和 Agency，但接口和其他合约不得期望此接口存在。

如果需要工厂来部署绑定的 App 和 Agency，工厂必须实现以下接口：

```
pragma solidity ^0.8.20;

import {Asset} from "./IERC7527Agency.sol";

/**
 * @dev The settings of the agency.
 * @param implementation The address of the agency implementation.
 * @param asset The parameter of asset of the agency.
 * @param immutableData The immutable data are stored in the code region of the created proxy contract of agencyImplementation.
 * @param initData If init data is not empty, calls proxy contract of agencyImplementation with this data.
 */
struct AgencySettings {
    address payable implementation;
    Asset asset;
    bytes immutableData;
    bytes initData;
}

/**
 * @dev The settings of the app.
 * @param implementation The address of the app implementation.
 * @param immutableData The immutable data are stored in the code region of the created proxy contract of appImplementation.
 * @param initData If init data is not empty, calls proxy contract of appImplementation with this data.
 */
struct AppSettings {
    address implementation;
    bytes immutableData;
    bytes initData;
}

interface IERC7527Factory {
    /**
     * @dev Deploys a new agency and app clone and initializes both.
     * @param agencySettings The settings of the agency.
     * @param appSettings The settings of the app.
     * @param data The data is additional data, it has no specified format and it is sent in call to `factory`.
     * @return appInstance The address of the created proxy contract of appImplementation.
     * @return agencyInstance The address of the created proxy contract of agencyImplementation.
     */
    function deployWrap(AgencySettings calldata agencySettings, AppSettings calldata appSettings, bytes calldata data)
        external
        returns (address, address);
}
```

## 理由

### 先前接口

[ERC-5679](./eip-5679.md) 提出了 `IERC5679Ext721` 接口，以引入一种一致的方式来扩展 [ERC-721](./eip-721.md) 代币标准以进行铸造和销毁。为了确保向后兼容，考虑到一些不实现 `ERC721TokenReceiver` 的合约，`IERC7527App` 使用 `mint` 函数而不是 `safeMint`。为了确保安全性和相互绑定的唯一性，`IERC5679Ext721` 中 `burn` 函数的 `_from` 参数必须是绑定代理的合约地址。因此，`IERC7527App` 中的 `burn` 函数不包含 `_from` 参数。

### 相互绑定

实现 `IERC7527App` 和 `IERC7527Agency` 的合约，使它们彼此成为唯一的所有者。包装过程是检查接收的可替代代币的溢价金额，然后在 App 中铸造非可替代代币。只有非可替代代币的所有者或批准者才能解包它。

### 实现多样性

用户可以在实现 Agency 和 App 接口时自定义函数和费用百分比。

不同的 Agency 实现具有不同的包装、解包函数逻辑和不同的 oracleFunction。用户可以自定义货币、初始价格、费用接收地址、费用率等，以初始化 Agency 合约。

不同的 App 实现适用于各种用例。用户可以初始化 App 合约。

工厂不是必需的。工厂实现是基于需求的。用户可以通过工厂选择不同的 Agency 实现和不同的 App 实现来部署自己的合约，将它们组合以创建各种产品。

### 货币类型

`currency` 在 `IERC7527Agency` 中是可替代代币的地址。`Asset` 只能在系统中定义一种类型的 `currency` 作为可替代代币。`currency` 支持各种可替代代币，包括 ETH 和 [ERC-20](./eip-20.md)。

### 代币 ID

对于每个包装过程，应该生成一个唯一的 `tokenId`。这个 `tokenId` 在解包过程中至关重要，用于验证。它还作为代币的独特凭证。此机制确保合约中资产的安全性。

### 包装和铸造

在实现 Agency 接口时设置 `strategy`，并确保一旦部署后不可升级。

在执行 `wrap` 函数时，将预定的策略参数传递给 `getWrapOracle` 函数以获取当前的溢价和费用。相应的溢价随后转移到 Agency 实例；根据 `mintFeePercent`，费用转移到 `feeRecipient`。随后，App 将 NFT 铸造到用户的地址。

转移到 Agency 的溢价（代币）不能移动，除非通过解包过程。执行包装的行为是铸造过程的唯一触发器。
### 解包和销毁

在执行 `unwrap` 函数时，预定的策略参数被传递到 `getUnwrapOracle` 函数以读取当前的溢价和费用。应用程序销毁 NFT。然后，相应的溢价在根据 `burnFeePercent` 扣除费用后转移到用户地址；费用则转移到 `feeRecipient`。执行 'unwrap' 的行为是 'burn' 过程的唯一触发器。

### 两个接口一起使用

`IERC7527App` 和 `IERC7527Agency` 可以一起实现以确保安全，但它们可以在初始化之前独立实现以提高灵活性。

### 定价

`getWrapOracle` 和 `getUnwrapOracle` 用于获取当前的溢价和费用。它们通过预言机函数实现链上价格获取。它们不仅支持在包装和解包过程中获取溢价和费用，还支持其他合约调用它们以获取溢价和费用，例如借贷合约。

它们可以支持基于链上和链下参数的函数预言机，但建议链上参数仅用于链上现实的共识。

### `initData` 和 `iconstructor`

在工厂部署 `App` 和 `Agency` 时，工厂使用 `initData` 作为 Calldata 来调用 `Agency` 和 `App` 合约，并且还调用 `App` 和 `Agency` 中的 `iconstructor` 函数。

initData 主要用于调用参数化初始化函数，而 `iconstructor` 通常用于验证配置参数和非参数化初始化函数。

## 向后兼容性

未发现向后兼容性问题。

## 参考实现

```
pragma solidity ^0.8.20;

import {
    ERC721Enumerable,
    ERC721,
    IERC721Enumerable
} from "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Address} from "@openzeppelin/contracts/utils/Address.sol";
import {ClonesWithImmutableArgs} from "clones-with-immutable-args/ClonesWithImmutableArgs.sol";
import {IERC7527App} from "./interfaces/IERC7527App.sol";
import {IERC7527Agency, Asset} from "./interfaces/IERC7527Agency.sol";
import {IERC7527Factory, AgencySettings, AppSettings} from "./interfaces/IERC7527Factory.sol";

contract ERC7527Agency is IERC7527Agency {
    using Address for address payable;

    receive() external payable {}

    function iconstructor() external override pure {
        (, Asset memory _asset,) = getStrategy();
        require(_asset.basePremium != 0, "LnModule: zero basePremium");
    }

    function unwrap(address to, uint256 tokenId, bytes calldata data) external payable override {
        (address _app, Asset memory _asset,) = getStrategy();
        require(_isApprovedOrOwner(_app, msg.sender, tokenId), "LnModule: not owner");
        IERC7527App(_app).burn(tokenId, data);
        uint256 _sold = IERC721Enumerable(_app).totalSupply();
        (uint256 premium, uint256 burnFee) = getUnwrapOracle(abi.encode(_sold));
        _transfer(address(0), payable(to), premium - burnFee);
        _transfer(address(0), _asset.feeRecipient, burnFee);
        emit Unwrap(to, tokenId, premium, burnFee);
    }

    function wrap(address to, bytes calldata data) external payable override returns (uint256) {
        (address _app, Asset memory _asset,) = getStrategy();
        uint256 _sold = IERC721Enumerable(_app).totalSupply();
        (uint256 premium, uint256 mintFee) = getWrapOracle(abi.encode(_sold));
        require(msg.value >= premium + mintFee, "ERC7527Agency: insufficient funds");
        _transfer(address(0), _asset.feeRecipient, mintFee);
        if (msg.value > premium + mintFee) {
            _transfer(address(0), payable(msg.sender), msg.value - premium - mintFee);
        }
        uint256 id_ = IERC7527App(_app).mint(to, data);
        require(_sold + 1 == IERC721Enumerable(_app).totalSupply(), "ERC7527Agency: Reentrancy");
        emit Wrap(to, id_, premium, mintFee);
        return id_;
    }

    function getStrategy() public pure override returns (address app, Asset memory asset, bytes memory attributeData) {
        uint256 offset = _getImmutableArgsOffset();
        address currency;
        uint256 basePremium;
        address payable feeRecipient;
        uint16 mintFeePercent;
        uint16 burnFeePercent;
        assembly {
            app := shr(0x60, calldataload(add(offset, 0)))
            currency := shr(0x60, calldataload(add(offset, 20)))
            basePremium := calldataload(add(offset, 40))
            feeRecipient := shr(0x60, calldataload(add(offset, 72)))
            mintFeePercent := shr(0xf0, calldataload(add(offset, 92)))
            burnFeePercent := shr(0xf0, calldataload(add(offset, 94)))
        }
        asset = Asset(currency, basePremium, feeRecipient, mintFeePercent, burnFeePercent);
        attributeData = "";
    }

    function getUnwrapOracle(bytes memory data) public pure override returns (uint256 premium, uint256 fee) {
        uint256 input = abi.decode(data, (uint256));
        (, Asset memory _asset,) = getStrategy();
        premium = _asset.basePremium + input * _asset.basePremium / 100;
        fee = premium * _asset.burnFeePercent / 10000;
    }

    function getWrapOracle(bytes memory data) public pure override returns (uint256 premium, uint256 fee) {
        uint256 input = abi.decode(data, (uint256));
        (, Asset memory _asset,) = getStrategy();
        premium = _asset.basePremium + input * _asset.basePremium / 100;
        fee = premium * _asset.mintFeePercent / 10000;
    }

    function _transfer(address currency, address recipient, uint256 premium) internal {
        if (currency == address(0)) {
            payable(recipient).sendValue(premium);
        } else {
            IERC20(currency).transfer(recipient, premium);
        }
    }

    function _isApprovedOrOwner(address app, address spender, uint256 tokenId) internal view virtual returns (bool) {
        IERC721Enumerable _app = IERC721Enumerable(app);
        address _owner = _app.ownerOf(tokenId);
        return (spender == _owner || _app.isApprovedForAll(_owner, spender) || _app.getApproved(tokenId) == spender);
    }
    /// @return offset The offset of the packed immutable args in calldata

    function _getImmutableArgsOffset() internal pure returns (uint256 offset) {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            offset := sub(calldatasize(), add(shr(240, calldataload(sub(calldatasize(), 2))), 2))
        }
    }
}

contract ERC7527App is ERC721Enumerable, IERC7527App {
    constructor() ERC721("ERC7527App", "EA") {}

    address payable private _oracle;

    modifier onlyAgency() {
        require(msg.sender == _getAgency(), "only agency");
        _;
    }

    function iconstructor() external {}

    function getName(uint256) external pure returns (string memory) {
        return "App";
    }

    function getMaxSupply() public pure override returns (uint256) {
        return 100;
    }

    function getAgency() external view override returns (address payable) {
        return _getAgency();
    }

    function setAgency(address payable oracle) external override {
        require(_getAgency() == address(0), "already set");
        _oracle = oracle;
    }

    function mint(address to, bytes calldata data) external override onlyAgency returns (uint256 tokenId) {
        require(totalSupply() < getMaxSupply(), "max supply reached");
        tokenId = abi.decode(data, (uint256));
        _mint(to, tokenId);
    }

    function burn(uint256 tokenId, bytes calldata) external override onlyAgency {
        _burn(tokenId);
    }

    function _getAgency() internal view returns (address payable) {
        return _oracle;
    }
}

contract ERC7527Factory is IERC7527Factory {
    using ClonesWithImmutableArgs for address;

    function deployWrap(AgencySettings calldata agencySettings, AppSettings calldata appSettings, bytes calldata)
        external
        override
        returns (address appInstance, address agencyInstance)
    {
        appInstance = appSettings.implementation.clone(appSettings.immutableData);
        {
            agencyInstance = address(agencySettings.implementation).clone(
                abi.encodePacked(
                    appInstance,
                    agencySettings.asset.currency,
                    agencySettings.asset.basePremium,
                    agencySettings.asset.feeRecipient,
                    agencySettings.asset.mintFeePercent,
                    agencySettings.asset.burnFeePercent,
                    agencySettings.immutableData
                )
            );
        }

        IERC7527App(appInstance).setAgency(payable(agencyInstance));

        IERC7527Agency(payable(agencyInstance)).iconstructor();
        IERC7527App(appInstance).iconstructor();

        if (agencySettings.initData.length != 0) {
            (bool success, bytes memory result) = agencyInstance.call(agencySettings.initData);

            if (!success) {
                assembly {
                    revert(add(result, 32), mload(result))
                }
            }
        }

        if (appSettings.initData.length != 0) {
            (bool success, bytes memory result) = appInstance.call(appSettings.initData);

            if (!success) {
                assembly {
                    revert(add(result, 32), mload(result))
                }
            }
        }
    }
}
```

## 安全考虑

### 防止欺诈

考虑以下内容以确保合约的安全性：

* 检查修饰符 `onlyAgency()` 和 `onlyApp()` 是否正确实现和应用。

* 检查函数策略。

* 检查合约是否可能受到重入攻击。

* 检查所有非同质化代币是否可以根据 FOAMM 计算的溢价进行解包。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。