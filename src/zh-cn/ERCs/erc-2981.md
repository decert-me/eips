---
eip: 2981
title: NFT 版税标准
author: Zach Burks (@vexycats), James Morgan (@jamesmorgan), Blaine Malone (@blmalone), James Seibel (@seibelj)
discussions-to: https://github.com/ethereum/EIPs/issues/2907
status: Final
type: Standards Track
category: ERC
created: 2020-09-15
requires: 165
---

## 简要总结

一种标准化的方法，用于检索非同质化代币（NFT）的版税支付信息，以便在所有 NFT 市场和生态系统参与者中实现对版税支付的普遍支持。

## 摘要

该标准允许支持 [ERC-721](./eip-721.md) 和 [ERC-1155](./eip-1155.md) 接口的合约，例如 NFT，在每次 NFT 被出售或转售时，向 NFT 创作者或权利持有者发出应支付的版税金额。这是为了支持希望为艺术家和其他 NFT 创作者提供持续资金的 NFT 市场。版税支付必须是自愿的，因为 `transferFrom()` 等转移机制包括钱包之间的 NFT 转移，执行这些操作并不总是意味着发生了销售。市场和个人通过调用 `royaltyInfo()` 来实现该标准，该函数指定在给定销售价格下应支付给哪个地址多少款项。支付和通知接收者的确切机制将在未来的 EIP 中定义。该 ERC 应被视为进一步创新 NFT 版税支付的最小、节省 gas 的构建块。

## 动机
目前有许多 NFT 市场，具有多种独特的版税支付实现，这些实现并不容易与其他市场兼容或使用。就像 ERC-20 代币的早期阶段一样，NFT 市场智能合约因生态系统而异，且没有标准化。该 EIP 使所有市场能够检索给定 NFT 的版税支付信息。这使得无论 NFT 在哪个市场被出售或转售，都能准确支付版税。

许多最大的 NFT 市场已经实施了与其他市场不兼容的定制版税支付解决方案。该标准实现了可以在任何类型的 NFT 市场接受的标准化版税信息检索。这个极简提案仅提供了一种获取版税金额和接收者的机制。实际的资金转移应由市场执行。

该标准允许支持 [ERC-721](./eip-721.md) 和 [ERC-1155](./eip-1155.md) 接口的 NFT 以标准化的方式传达版税信息。更具体地说，这些合约现在可以计算应提供给合法接收者的版税金额。

版税金额始终是销售价格的一个百分比。如果市场选择*不*实施该 EIP，则不会为二次销售支付任何款项。人们相信，NFT 市场生态系统将自愿实施这一版税支付标准，以便为艺术家/创作者提供持续的资金支持。NFT 买家在做出 NFT 购买决策时，会将版税支付视为一个因素。

如果没有达成一致的版税支付标准，NFT 生态系统将缺乏在所有市场收取版税的有效手段，艺术家和其他创作者将无法获得持续的资金支持。这将阻碍 NFT 的增长和采用，并使 NFT 创作者失去铸造新颖代币的动力。

使所有 NFT 市场统一于单一的版税支付标准将使整个 NFT 生态系统受益。

虽然该标准专注于 NFT 及其与 ERC-721 和 ERC-1155 标准的兼容性，但 EIP-2981 并不要求与 ERC-721 和 ERC-1155 标准兼容。任何其他合约都可以集成 EIP-2981 以返回版税支付信息。因此，ERC-2981 是许多资产类型的通用版税标准。

一目了然，以下是一个总结当前 NFT 版税支付的对话示例：

>**艺术家**：“你们的平台支持版税支付吗？”          
>**市场**：“是的，我们有版税支付，但如果你的 NFT 在其他市场出售，我们就无法强制执行这笔支付。”              
>**艺术家**：“其他支持版税的市场呢，你们不分享我的版税信息以使其生效吗？”              
>**市场**：“不，我们不分享版税信息。”

## 规范

本文档中的关键字“MUST”、“MUST NOT”、“REQUIRED”、“SHALL”、“SHALL NOT”、“SHOULD”、“SHOULD NOT”、“RECOMMENDED”、“MAY”和“OPTIONAL”应按 RFC 2119 中的描述进行解释。

**符合 ERC-721 和 ERC-1155 的合约可以实现该 ERC 以提供标准化的版税支付信息指定方法。**

支持该标准的市场**应**实施某种将版税转移给版税接收者的方法。实际转移和通知资金的标准将在未来的 EIP 中指定。

市场**必须**以与传递给 `royaltyInfo()` 的 `_salePrice` 相同的交换单位支付版税。这相当于说，`_salePrice` 参数和 `royaltyAmount` 返回值**必须**以相同的货币单位计价。例如，如果销售价格以 ETH 计价，则版税支付也必须以 ETH 支付；如果销售价格以 USDC 计价，则版税支付也必须以 USDC 支付。

该标准的实施者**必须**在计算版税金额时计算 `_salePrice` 的百分比。后续调用 `royaltyInfo()` **可以**返回不同的 `royaltyAmount`。不过，如果实施者选择在 `royaltyInfo()` 调用之间执行不同的百分比计算，则有一些重要的考虑。

`royaltyInfo()` 函数并不知道销售和版税支付的交换单位。考虑到这一点，实施者**不得**返回固定/常量的 `royaltyAmount`，而忽略 `_salePrice`。出于同样的原因，实施者**不得**根据将 `_salePrice` 与常量数字进行比较来确定 `royaltyAmount`。在这两种情况下，`royaltyInfo()` 函数对交换单位做出了假设，这**必须**避免。

所使用的百分比值必须独立于销售价格，原因如前所述（即如果百分比值为 10%，则 10% **必须**适用于无论 `_salePrice` 是 10、10000 还是 1234567890）。如果版税费用计算结果有余数，实施者**可以**向上或向下舍入到最接近的整数。例如，如果版税费用为 10%，而 `_salePrice` 为 999，实施者可以返回 99 或 100 作为 `royaltyAmount`，两者都是有效的。

实施者**可以**选择根据其他可预测的变量更改百分比值，而不对交换单位做出假设。例如，百分比值可能会随时间线性下降。像这样的做法**不应**基于不可预测的变量，如 `block.timestamp`，而应基于其他更可预测的状态变化。另一种合理的方法**可以**使用 NFT 的转移次数来决定用于计算 `royaltyAmount` 的百分比值。其想法是，百分比值可以在每次 NFT 转移后减少。另一个例子是为每个唯一的 `_tokenId` 使用不同的百分比值。
支持此标准的市场**不应**在返回的 `royaltyAmount` 为 `0` 时发送零值交易。这将浪费 gas，并在此 EIP 中没有任何实际用途。

支持此标准的市场**必须**支付版税，无论销售发生在哪里或使用何种货币，包括链上销售、场外交易（OTC）和拍卖行等线下销售。由于版税支付是自愿的，尊重此 EIP 的实体必须支付，无论销售发生在哪里——在区块链外进行的销售仍然是销售。支付和通知接收者的确切机制将在未来的 EIP 中定义。

此标准的实施者**必须**具备以下所有功能：

```solidity
pragma solidity ^0.6.0;
import "./IERC165.sol";

///
/// @dev Interface for the NFT Royalty Standard
///
interface IERC2981 is IERC165 {
    /// ERC165 bytes to add to interface array - set in parent contract
    /// implementing this standard
    ///
    /// bytes4(keccak256("royaltyInfo(uint256,uint256)")) == 0x2a55205a
    /// bytes4 private constant _INTERFACE_ID_ERC2981 = 0x2a55205a;
    /// _registerInterface(_INTERFACE_ID_ERC2981);

    /// @notice Called with the sale price to determine how much royalty
    //          is owed and to whom.
    /// @param _tokenId - the NFT asset queried for royalty information
    /// @param _salePrice - the sale price of the NFT asset specified by _tokenId
    /// @return receiver - address of who should be sent the royalty payment
    /// @return royaltyAmount - the royalty payment amount for _salePrice
    function royaltyInfo(
        uint256 _tokenId,
        uint256 _salePrice
    ) external view returns (
        address receiver,
        uint256 royaltyAmount
    );
}

interface IERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///  uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

### 示例

在部署期间使用此标准的 ERC-721：

#### 部署 ERC-721 并信号支持 ERC-2981

```solidity
constructor (string memory name, string memory symbol, string memory baseURI) {
        _name = name;
        _symbol = symbol;
        _setBaseURI(baseURI);
        // register the supported interfaces to conform to ERC721 via ERC165
        _registerInterface(_INTERFACE_ID_ERC721);
        _registerInterface(_INTERFACE_ID_ERC721_METADATA);
        _registerInterface(_INTERFACE_ID_ERC721_ENUMERABLE);
        // Royalties interface
        _registerInterface(_INTERFACE_ID_ERC2981);
    }
```

#### 检查在您的市场上出售的 NFT 是否实现了版税

```solidity  
bytes4 private constant _INTERFACE_ID_ERC2981 = 0x2a55205a;

function checkRoyalties(address _contract) internal returns (bool) {
    (bool success) = IERC165(_contract).supportsInterface(_INTERFACE_ID_ERC2981);
    return success;
 }
```

## 理由

### 可选的版税支付

无法知道哪些 NFT 转移是销售的结果，哪些仅仅是钱包在移动或整合其 NFT。因此，我们不能强制每个转移函数，例如 ERC-721 中的 `transferFrom()`，都涉及版税支付，因为并非每个转移都是需要支付的销售。我们相信 NFT 市场生态系统将自愿实施此版税支付标准，以为艺术家/创作者提供持续的资金支持。NFT 买家在做出 NFT 购买决策时会将版税支付作为一个因素进行评估。

### 向单一地址的简单版税支付

此 EIP 并未指定向版税接收者支付的方式。此外，完全了解和高效实施所有可能的版税支付逻辑是不可能的。也就是说，版税支付接收者需要在其自己的接收合约或链下流程中实现所有额外的复杂性和逻辑，例如费用分配、多接收者、税务、会计等。试图将这些作为此标准的一部分进行处理，将大幅增加实施复杂性，增加 gas 成本，并且无法覆盖每个潜在的用例。此 ERC 应被视为一个最小的、节省 gas 的构建块，以便在 NFT 版税支付方面进行进一步创新。未来的 EIP 可以指定有关支付转移和通知的更多细节。

### 版税支付百分比计算

此 EIP 强制采用基于百分比的版税费用模型。最常见的百分比计算情况可能是 `royaltyAmount` 始终从 `_salePrice` 中使用固定百分比计算，即如果版税费用为 10%，则无论 `_salePrice` 是 10、10000 还是 1234567890，均需适用 10% 的版税费用。

如前所述，实施者可以在此基于百分比的计算中发挥创造力，但有一些重要的注意事项。主要是确保 `royaltyInfo()` 函数不知晓交换单位，并且在百分比计算中避免不可预测的变量。为了跟进之前的 `block.timestamp` 示例，如果发生以下事件，则可以突出一些细微差别：

1. 市场出售 NFT。
2. 市场在调用 `royaltyInfo()` 并发送支付之前延迟 `X` 天。
3. 市场收到的 `Y` 作为 `royaltyAmount` 与如果没有延迟发生的情况下 `X` 天前计算的 `royaltyAmount` 有显著不同。
4. 版税接收者对市场的延迟感到不满，因此提出争议。

与其返回一个百分比并让市场根据销售价格计算版税金额，不如返回一个 `royaltyAmount` 值，以便与市场之间就特定销售价格应支付多少没有争议。版税费用支付者必须支付 `royaltyInfo()` 规定的 `royaltyAmount`。

### 所有市场（链上和链下）的无单位版税支付

此 EIP 并未指定用于销售和版税支付的货币或代币。无论使用何种货币或代币进行销售，均需支付相同的基于百分比的版税费用，并以相同的货币或代币支付。这适用于任何地点的销售，包括链上销售、场外交易（OTC）和使用法定货币（例如在拍卖行）进行的线下销售。由于版税支付是自愿的，尊重此 EIP 的实体必须支付，无论销售发生在哪里——在区块链外进行的销售仍然是销售。支付和通知接收者的确切机制将在未来的 EIP 中定义。

### 通用版税支付

尽管专门为 NFT 而设计，但此标准并不要求实现 EIP-2981 的合约与 ERC-721 或 ERC-1155 标准兼容。任何其他合约都可以使用此接口返回版税支付信息，前提是它能够在接口的约束内唯一识别资产。因此，ERC-2981 是许多其他资产类型的通用版税标准。

## 向后兼容性

此标准与当前的 ERC-721 和 ERC-1155 标准兼容。

## 安全考虑

与此标准的实施直接相关的安全考虑没有。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。