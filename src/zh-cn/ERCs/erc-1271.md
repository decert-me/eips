---
eip: 1271
title: 合约的标准签名验证方法
description: 当账户是智能合约时验证签名的标准方法
author: Francisco Giordano (@frangio), Matt Condon (@shrugs), Philippe Castonguay (@PhABC), Amir Bandeali (@abandeali1), Jorge Izquierdo (@izqui), Bertrand Masius (@catageek)
discussions-to: https://github.com/ethereum/EIPs/issues/1271
status: Final
type: Standards Track
category: ERC
created: 2018-07-25
---

## 摘要
外部拥有账户（EOA）可以使用其关联的私钥签署消息，但目前合约无法做到这一点。我们提出了一种标准方法，供任何合约验证代表给定合约的签名是否有效。这可以通过在签名合约上实现 `isValidSignature(hash, signature)` 函数来实现，该函数可以被调用以验证签名。

## 动机

有许多合约希望利用签名消息来验证资产转移的权利或其他目的。为了使这些合约能够支持非外部拥有账户（即合约所有者），我们需要一种标准机制，以便合约可以指示给定签名是否有效。

一个需要提供签名的应用示例是具有离线订单簿的去中心化交易所，其中买卖订单是签名消息。在这些应用中，EOA 签署订单，表示他们希望买卖某个资产，并明确授权交易所智能合约通过签名完成交易。然而，对于合约而言，常规签名是不可能的，因为合约没有私钥，因此提出了此提案。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 [RFC 2119](https://www.ietf.org/rfc/rfc2119.txt) 中的描述进行解释。

```javascript
pragma solidity ^0.5.0;

contract ERC1271 {

  // bytes4(keccak256("isValidSignature(bytes32,bytes)")
  bytes4 constant internal MAGICVALUE = 0x1626ba7e;

  /**
   * @dev Should return whether the signature provided is valid for the provided hash
   * @param _hash      Hash of the data to be signed
   * @param _signature Signature byte array associated with _hash
   *
   * MUST return the bytes4 magic value 0x1626ba7e when function passes.
   * MUST NOT modify state (using STATICCALL for solc < 0.5, view modifier for solc > 0.5)
   * MUST allow external calls
   */ 
  function isValidSignature(
    bytes32 _hash, 
    bytes memory _signature)
    public
    view 
    returns (bytes4 magicValue);
}
```

`isValidSignature` 可以调用任意方法来验证给定签名，这可能是上下文相关的（例如基于时间或状态）、EOA 相关的（例如签署者在智能钱包中的授权级别）、签名方案相关的（例如 ECDSA、多重签名、BLS）等。

此函数应由希望签署消息的合约实现（例如智能合约钱包、DAO、多重签名钱包等）。希望支持合约签名的应用应在签署者是合约时调用此方法。

## 理由
我们认为所提议函数的名称是合适的，因为*授权*签署者为给定数据提供适当的签名时，会将其签名视为签名合约的“有效”签名。因此，只有在签署者被授权代表智能钱包执行给定操作时，签署的操作消息才是有效的。

为了简化将签名与哈希分离，提供了两个参数。使用 bytes32 哈希而不是未哈希的消息是为了简化，因为合约可能期望某种非标准的哈希函数，例如 [EIP-712](./eip-712.md)。

`isValidSignature()` 不应能够修改状态，以防止 `GasToken` 铸造或类似攻击向量。同样，这是为了简化该函数的实现表面，以便更好地标准化并允许离线合约查询。

预期返回特定的返回值而不是布尔值，以便对签名进行更严格和简单的验证。

## 向后兼容性

此 EIP 与之前的签名验证工作向后兼容，因为此方法特定于基于合约的签名，而不是 EOA 签名。

## 参考实现

签名合约的示例实现：

```solidity

  /**
   * @notice Verifies that the signer is the owner of the signing contract.
   */
  function isValidSignature(
    bytes32 _hash,
    bytes calldata _signature
  ) external override view returns (bytes4) {
    // Validate signatures
    if (recoverSigner(_hash, _signature) == owner) {
      return 0x1626ba7e;
    } else {
      return 0xffffffff;
    }
  }

 /**
   * @notice Recover the signer of hash, assuming it's an EOA account
   * @dev Only for EthSign signatures
   * @param _hash       Hash of message that was signed
   * @param _signature  Signature encoded as (bytes32 r, bytes32 s, uint8 v)
   */
  function recoverSigner(
    bytes32 _hash,
    bytes memory _signature
  ) internal pure returns (address signer) {
    require(_signature.length == 65, "SignatureValidator#recoverSigner: invalid signature length");

    // Variables are not scoped in Solidity.
    uint8 v = uint8(_signature[64]);
    bytes32 r = _signature.readBytes32(0);
    bytes32 s = _signature.readBytes32(32);

    // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature
    // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines
    // the valid range for s in (281): 0 < s < secp256k1n ÷ 2 + 1, and for v in (282): v ∈ {27, 28}. Most
    // signatures from current libraries generate a unique signature with an s-value in the lower half order.
    //
    // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value
    // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or
    // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept
    // these malleable signatures as well.
    //
    // Source OpenZeppelin
    // https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/cryptography/ECDSA.sol

    if (uint256(s) > 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0) {
      revert("SignatureValidator#recoverSigner: invalid signature 's' value");
    }

    if (v != 27 && v != 28) {
      revert("SignatureValidator#recoverSigner: invalid signature 'v' value");
    }

    // Recover ECDSA signer
    signer = ecrecover(_hash, v, r, s);
    
    // Prevent signer from being 0x0
    require(
      signer != address(0x0),
      "SignatureValidator#recoverSigner: INVALID_SIGNER"
    );

    return signer;
  }
```

调用外部签名合约上的 isValidSignature() 函数的合约示例实现：

```solidity
  function callERC1271isValidSignature(
    address _addr,
    bytes32 _hash,
    bytes calldata _signature
  ) external view {
    bytes4 result = IERC1271Wallet(_addr).isValidSignature(_hash, _signature);
    require(result == 0x1626ba7e, "INVALID_SIGNATURE");
  }
```

## 安全考虑
由于调用 isValidSignature() 函数时没有预期的 gas 限制，因此某些实现可能会消耗大量 gas。因此，在调用外部合约的此方法时，不应硬编码发送的 gas 数量，因为这可能会阻止某些签名的验证。

还要注意，每个实现此方法的合约都有责任确保传递的签名确实有效，否则将会出现灾难性的后果。

## 版权
版权及相关权利通过 [CC0](../LICENSE.md) 放弃。