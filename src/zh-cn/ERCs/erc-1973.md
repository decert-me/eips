---
eip: 1973
title: 可扩展奖励
author: Lee Raj (@lerajk), Qin Jian (@qinjian)
type: Standards Track
category: ERC
status: Stagnant
created: 2019-04-01
---

## 简单总结

一个可铸造代币奖励接口，每个区块铸造'n'个代币，这些代币在 DAPP 生态系统中的'm'个参与者之间均匀分配。

## 摘要

可铸造代币奖励接口允许 DApps 构建一个代币经济，在这个经济中，代币奖励在活跃参与者之间均匀分配。代币的铸造是基于每个区块的，可以配置（例如，每个区块铸造 10.2356 个代币，每个区块铸造 0.1 个代币，每个区块铸造 1350 个代币），铸造函数可以由任何活跃参与者发起。分配给每个参与者的代币奖励取决于网络中的参与者数量。在网络初期，当参与者数量较少时，每个参与者的代币奖励较高，但随着网络的扩展，代币奖励会动态减少。

## 动机

通过推送系统向大量参与者分配代币由于区块 gas 限制而失败。随着网络中参与者数量增长到数万，跟踪参与者的可迭代注册及其相应奖励在推送系统中变得难以管理。例如，循环遍历 5000 个地址以分配 0.0000001 个奖励代币是极其低效的。此外，这些交易的 gas 费用很高，需要由 DApp 开发者或相关公司承担，从而导致中心化问题。

需要一个拉取系统，以保持应用程序完全去中心化，并避免区块 gas 限制问题。然而，目前尚未提出任何标准解决方案，以通过拉取系统向数万参与者分配可扩展奖励。这正是我们通过 TPP、轮次掩码、参与者掩码等概念在此 EIP 中所提议的。

## 规范

### 定义

`生态系统中每个参与者的代币数量或 TPP（每个参与者的代币）`：TPP = (铸造的代币数量 / 活跃参与者总数)

`roundMask`：代币合约随时间推移的 TPP 累积快照。例如，transactionOne = 10 个代币在 100 个可用参与者中铸造（TPP = 10 / 100），transactionTwo = 12 个代币在 95 个参与者中铸造（TPP = 12 / 95）

roundMask = (10/100) + (12/95)

`participantMask`：用于跟踪`msg.sender`（参与者）随时间变化的奖励。当`msg.sender`加入或离开生态系统时，参与者掩码会更新

participantMask = previous roundMask OR (current roundMask - TPP)

`msg.sender 的奖励`：roundMask - participantMask

例如，假设总共有 6 个交易（智能合约触发或函数调用），有 10 个现有参与者（分母）和每个交易铸造 20 个代币（分子）。在第 2 个交易时，第 11 个参与者加入网络并在第 5 个交易之前退出，第 11 个参与者的余额如下：

``` 
 t1 roundMask = (20/10)
 t2 roundMask = (20/10) + (20/11) 
 t3 roundMask = (20/10) + (20/11) + (20/11)
 t4 roundMask = (20/10) + (20/11) + (20/11) + (20/11)
 t5 roundMask = (20/10) + (20/11) + (20/11) + (20/11)+ (20/10)
 t6 roundMask = (20/10) + (20/11) + (20/11) + (20/11)+ (20/10) + (20/10)
 ``` 

6 个交易中释放的总代币 = 60 个代币

由于参与者在 t2 时加入并在 t5 之前离开，因此该参与者应得 t2 到 t4 之间的奖励。当参与者在 t2 时加入时，'participantMask = (20/10)'，当参与者在 t5 之前离开时，累计应得的奖励代币为：

msg.sender 的奖励：`[t4 roundMask = (20/10) + (20/11)+ (20/11) + (20/11)] - [participantMask = (20/10)] = [rewards = (20/11)+ (20/11) + (20/11)]`

当同一参与者在稍后时间（t27 或 t35）再次加入生态系统时，将给予一个新的'participantMask'，用于计算参与者退出时的新应得奖励代币。这个过程会为每个参与者动态持续进行。

`tokensPerBlock`：每个区块释放的代币数量

`blockFreezeInterval`：在下一个铸造之前需要经过的区块数量。例如，如果设置为 50，并且在区块'b'铸造了'n'个代币，则在'b + 50'个区块经过之前不会铸造下一个'n'个代币

`lastMintedBlockNumber`：最后铸造'n'个代币的区块号

`totalParticipants`：DApp 网络中的参与者总数

`tokencontractAddress`：代币将被铸造的合约地址，默认是 address(this)

```solidity

pragma solidity ^0.5.2;

import "openzeppelin-solidity/contracts/token/ERC20/ERC20Mintable.sol";
import "openzeppelin-solidity/contracts/token/ERC20/ERC20Detailed.sol";

contract Rewards is ERC20Mintable, ERC20Detailed {

using SafeMath for uint256;

uint256 public roundMask;
uint256 public lastMintedBlockNumber;
uint256 public totalParticipants = 0;
uint256 public tokensPerBlock; 
uint256 public blockFreezeInterval; 
address public tokencontractAddress = address(this);
mapping(address => uint256) public participantMask; 

/**
 * @dev constructor, initializes variables.
 * @param _tokensPerBlock The amount of token that will be released per block, entered in wei format (E.g. 1000000000000000000)
 * @param _blockFreezeInterval The amount of blocks that need to pass (E.g. 1, 10, 100) before more tokens are brought into the ecosystem.
 */
 constructor(uint256 _tokensPerBlock, uint256 _blockFreezeInterval) public ERC20Detailed("Simple Token", "SIM", 18){ 
lastMintedBlockNumber = block.number;
tokensPerBlock = _tokensPerBlock;
blockFreezeInterval = _blockFreezeInterval;
}

/**
 * @dev Modifier to check if msg.sender is whitelisted as a minter. 
 */
modifier isAuthorized() {
require(isMinter(msg.sender));
_;
}

/**
 * @dev Function to add participants in the network. 
 * @param _minter The address that will be able to mint tokens.
 * @return A boolean that indicates if the operation was successful.
 */
function addMinters(address _minter) external returns (bool) {
_addMinter(_minter);
totalParticipants = totalParticipants.add(1);
updateParticipantMask(_minter);
return true;
}


/**
 * @dev Function to remove participants in the network. 
 * @param _minter The address that will be unable to mint tokens.
 * @return A boolean that indicates if the operation was successful.
 */
function removeMinters(address _minter) external returns (bool) {
totalParticipants = totalParticipants.sub(1);
_removeMinter(_minter); 
return true;
}


/**
 * @dev Function to introduce new tokens in the network. 
 * @return A boolean that indicates if the operation was successful.
 */
function trigger() external isAuthorized returns (bool) {
bool res = readyToMint();
if(res == false) {
return false;
} else {
mintTokens();
return true;
}
}

/**
 * @dev Function to withdraw rewarded tokens by a participant. 
 * @return A boolean that indicates if the operation was successful.
 */
function withdraw() external isAuthorized returns (bool) {
uint256 amount = calculateRewards();
require(amount >0);
ERC20(tokencontractAddress).transfer(msg.sender, amount);
}

/**
 * @dev Function to check if new tokens are ready to be minted. 
 * @return A boolean that indicates if the operation was successful.
 */
function readyToMint() public view returns (bool) {
uint256 currentBlockNumber = block.number;
uint256 lastBlockNumber = lastMintedBlockNumber;
if(currentBlockNumber > lastBlockNumber + blockFreezeInterval) { 
return true;
} else {
return false;
}
}

/**
 * @dev Function to calculate current rewards for a participant. 
 * @return A uint that returns the calculated rewards amount.
 */
function calculateRewards() private returns (uint256) {
uint256 playerMask = participantMask[msg.sender];
uint256 rewards = roundMask.sub(playerMask);
updateParticipantMask(msg.sender);
return rewards;
}

/**
 * @dev Function to mint new tokens into the economy. 
 * @return A boolean that indicates if the operation was successful.
 */
function mintTokens() private returns (bool) {
uint256 currentBlockNumber = block.number;
uint256 tokenReleaseAmount = (currentBlockNumber.sub(lastMintedBlockNumber)).mul(tokensPerBlock);
lastMintedBlockNumber = currentBlockNumber;
mint(tokencontractAddress, tokenReleaseAmount);
calculateTPP(tokenReleaseAmount);
return true;
}

 /**
* @dev Function to calculate TPP (token amount per participant).
* @return A boolean that indicates if the operation was successful.
*/
function calculateTPP(uint256 tokens) private returns (bool) {
uint256 tpp = tokens.div(totalParticipants);
updateRoundMask(tpp);
return true;
}

 /**
* @dev Function to update round mask. 
* @return A boolean that indicates if the operation was successful.
*/
function updateRoundMask(uint256 tpp) private returns (bool) {
roundMask = roundMask.add(tpp);
return true;
}

 /**
* @dev Function to update participant mask (store the previous round mask)
* @return A boolean that indicates if the operation was successful.
*/
function updateParticipantMask(address participant) private returns (bool) {
uint256 previousRoundMask = roundMask;
participantMask[participant] = previousRoundMask;
return true;
}

}
``` 

## 理由

目前，没有可扩展奖励分配机制的标准。为了在 DAPP 中创建可持续的加密经济环境，激励机制起着重要作用。然而，如果没有可扩展的方式向数万参与者分配奖励，大多数 DAPP 缺乏良好的激励结构。那些拥有可持续加密经济环境的 DAPP 在很大程度上依赖于集中服务器或一组选择性节点来触发智能合约。但是，为了保持应用程序真正去中心化，奖励分配机制必须依赖于活跃参与者本身，并随着参与者数量的增长而扩展。这正是此 EIP 所要实现的目标。

## 向后兼容性

不适用。

## 测试用例

正在进行中，将会添加。

## 实现

正在进行中，稍后将添加适当的实现。以下是一个示例：

`etherscan 奖励合约`： https://ropsten.etherscan.io/address/0x8b0abfc541ab7558857816a67e186221adf887bc#tokentxns

`步骤 1`：使用以下参数部署奖励合约_tokensPerBlock = 1e18，_blockFreezeInterval = 1

`步骤 2`：将 Alice(0x123)和 Bob(0x456)添加为铸造者，addMinters(address _minter)

`步骤 3`：从 Alice / Bob 的账户调用 trigger()。经过 65 个区块，因此铸造了 65 个 SIM 代币。RM 为 32500000000000000000

`步骤 4`：Alice 提取并收到 32.5 个 SIM 代币（65 个代币 / 2 个参与者），她的 PM = 32500000000000000000

`步骤 5`：将 Satoshi(0x321)和 Vitalik(0x654)添加为铸造者，addMinters(address _minter)

`步骤 6`：从 Alice / Bob / Satoshi / Vitalik 的账户调用 trigger()。经过 101 个区块，因此铸造了 101 个 SIM 代币。RM 为 57750000000000000000

`步骤 7`：Alice 提取并收到 25.25 个 SIM 代币（101 个代币 / 4 个参与者），她的 PM = 57750000000000000000

`步骤 8`：Bob 提取并收到 57.75 个 SIM 代币（(65 个代币 / 2 个参与者) + (101 个代币 / 4 个参与者)）。Bob 的 PM = 57750000000000000000

## 版权

版权及相关权利通过 CC0 放弃。

## 参考文献

1. Bogdan Batog, Lucian Boca 和 Nick Johnson 的《以太坊区块链上的可扩展奖励分配》

2. Fomo3d DApp，https://fomo3d.hostedwiki.co/