---
eip: 7196
title: 简单代币，简化的 ERC-20
description: 设计用于智能合约钱包，这个标准移除了 ERC-20 代币中的 transferFrom、approve 和 allowance 函数。
author: Xiang (@wenzhenxiang), Ben77 (@ben2077), Mingshi S. (@newnewsms)
discussions-to: https://ethereum-magicians.org/t/simple-token-designed-for-smart-contract-wallet-aa/14757
status: 草案
type: 标准跟踪
category: ERC
created: 2023-06-21
requires: 20
---

## 摘要

该 ERC 是基于用户合约钱包（包括账户抽象）设计的新资产，并与 [ERC-20](./erc-20.md) 向前兼容。为了保持代币资产的简单性，该 ERC 移除了 ERC-20 的 `transferFrom`、`approve` 和 `allowance` 函数。

## 动机

[ERC-20](./erc-20.md) 定义了基于以太坊的标准代币，可以进行交易和转移，但 ERC-20 的本质是基于外部拥有账户（EOA）钱包设计。EOA 钱包没有状态和代码存储，而智能合约钱包则不同。

几乎所有与代币相关的 ERC 都添加了功能，但我们的观点正好相反。我们认为代币合约应该更简单，更多的功能由智能合约钱包来处理。

我们的提案是基于智能合约钱包设计一个更简单的代币资产。

它旨在实现以下目标：

1. 保持资产合约简单：仅负责 `transfer` 函数。
2. `approve` 和 `allowance` 函数不由代币合约管理，而是在用户层面管理，提供更大的灵活性和控制权。这一变化不仅增强了用户的自主性，还减轻了与 ERC-20 合约实现这些功能相关的某些风险。
3. 移除 `transferFrom` 函数。调用对方的代币资产的更好方式是访问对方自己的合约，而不是直接访问代币资产合约。
4. 与 ERC-20 的向前兼容意味着所有可替代代币都可以与该提案兼容。

## 规范

本文档中的关键字 "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY" 和 "OPTIONAL" 应按 RFC 2119 和 RFC 8174 中的描述进行解释。

合规合约必须实现以下接口：

```solidity
pragma solidity ^0.8.20;

/**
 * @title ERC7196 Simple token interface 
 * @dev See https://ercs.ethereum.org/ERCS/erc-7196
 */
interface IERC7196 {
    /**
     * @notice Used to notify transfer tokens.
     * @param from Address of the from
     * @param to Address of the receive
     * @param value The transaction amount 
     */
    event Transfer(
        address indexed from,
        address indexed to,
        uint256 value
    );
	
    /**
     * @notice Get the total supply
     * @return total The total supply amount
     */
    function totalSupply() 
        external  
        view
        returns (uint256 total);
	  
    /**
     * @notice get the balance of owenr address
     * @param owner Address of the owner
     * @return balance The balance of the owenr address
     */
    function balanceOf(address owner) 
        external
        view
        returns (uint256 balance);

    /**
     * @notice Transfer token
     * @param to Address of the to
     * @param value The transaction amount 
     * @return success The bool value returns whether the transfer is successful
     */
    function transfer(address to, uint256 value)
        external
        returns (bool success);

}
```

## 理由

该提案旨在通过移除 `transferFrom`、`approve` 和 `allowance` 函数来简化代币标准。这种简化旨在增强安全性、减少复杂性并提高效率，使标准更适合智能合约钱包环境，同时保持基本功能。

## 向后兼容性

如前所述，该 ERC 与 [ERC-20](./erc-20.md) 向前兼容，ERC-20 与该 ERC 向后兼容。

## 参考实现

**与 [ERC-20](./erc-20.md) 向前兼容**

```solidity
pragma solidity ^0.8.20;

import "./IERC7196.sol";
import "../../math/SafeMath.sol";

/**
 * @title Standard ERC7196 token
 * @dev Note: the ERC-165 identifier for this interface is 0xc1b31357
 * @dev Implementation of the basic standard token.
 */
contract ERC7196 is IERC7196 {
    using SafeMath for uint256;

    mapping (address => uint256) private _balances;

    uint256 private _totalSupply;

    function totalSupply() external view returns (uint256) {
        return _totalSupply;
    }

    function balanceOf(address owner) external view returns (uint256) {
        return _balances[owner];
    }

    function transfer(address to, uint256 value) external returns (bool) {
        require(value <= _balances[msg.sender]);
        require(to != address(0));

        _balances[msg.sender] = _balances[msg.sender].sub(value);
        _balances[to] = _balances[to].add(value);
        emit Transfer(msg.sender, to, value);
        return true;
    }

}
```

## 安全考虑

需要注意的是，该 ERC 与 [ERC-20](./erc-20.md) 不向后兼容，因此与现有的 dapp 会存在不兼容性。

## 版权

版权及相关权利通过 [CC0](../LICENSE.md) 放弃。